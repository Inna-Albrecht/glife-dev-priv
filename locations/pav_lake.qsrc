# pav_lake

CLOSE ALL
if sound = 0:PLAY 'sound/lakeshore1.mp3', 30
$loc = 'pav_lake'
$loc_arg = ''
$region = 'pav_lake'
$menu_loc = 'pav_lake'
$menu_arg = ''
$locclass = 'beach'
menu_off = 0
$location_type = 'public_outdoors'
gs'stat'

! --- meynold event --- !. edit to month 5~10
if PCloswimwear = 0 and $clothingworntype ! 'nude' and temper >= 5 and sunWeather = 1 and month >= 5 and month <= 10 and hour < 20 and ((hour >= 10 and week >= 6) or (hour >= 14 and week < 6)) and mey_vika['mey_vika_qw'] >= 18 and mey_vika['mey_vika_qw'] < 20 and mey_vika['qw_day'] ! daystart: gt 'mey_vika_events', 'beach_event1'

'<center><H4>Lake</H4></center>'

! to memorize were cowards or not
if month >= 3 and month <= 5:
	if hour >= 6 and hour <= 17:
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/lake.spring.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/lake.spring.night.jpg"></center>'
	end
	'<center>A small lake, located not far from Pavlovsk. You''re not sure whether Pavlovsk was built near the lake on purpose or not. When it''s warm enough, people often come to sunbathe or go swimming.</center>'
	*nl
elseif month >= 6 and month <= 8:
	if hour >= 6 and hour <= 19:
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/lake.summer.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/lake.summer.night.jpg"></center>'
	end
	'<center>A small lake, located not far from Pavlovsk. You''re not sure whether Pavlovsk was built near the lake on purpose or not. People often come to sunbathe or go swimming during the summer.</center>'
	*nl
elseif month >= 9 and month <= 11:
	if hour >= 6 and hour <= 18:
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/lake.autumn.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/lake.autumn.night.jpg"></center>'
	end
	'<center>A small lake, located not far from Pavlovsk. You''re not sure whether Pavlovsk was built near the lake on purpose or not. When it''s warm enough, people often come to sunbathe or go swimming.</center>'
	*nl
elseif month < 3 or month = 12:
	if hour >= 7 and hour <= 16:
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/lake.winter.jpg"></center>'
	'<center>A small lake, located not far from Pavlovsk. You''re not sure whether Pavlovsk was built near the lake on purpose or not.</center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/lake.winter.night.jpg"></center>'
	end
	'<center>A small lake, located not far from Pavlovsk. You''re not sure whether Pavlovsk was built near the lake on purpose or not. The lake has been frozen over ever since winter arrived and it''s currently being used as a skating rink.</center>'
	*nl
	if pcs_icesktng>0: 'Your ice skating skill is <<pcs_icesktng>>.'
end

if storyline = 1 and (month <= 2 or month = 12):
	if week >= 6 and hour >= 8 and hour <= 11:
		'Looking out over the frozen lake, you see a figure in the distance figure skating and doing all types of crazy moves. Stepping onto the ice and skating out, you notice that it''s <a href="exec:gt ''gschool_socialchg1'', ''tVeronika''">Veronika</a>.'
	end
end

if StoryLine = 1 and SchoolAtestat = 0 and ((month >= 3 and month <= 5) or (month >= 9 and month <= 11)) and week <= 5 and hour >= 15 and hour <= 20 and (christinaQW['dompath'] >= 6 or christinaQW['subpath'] = 1) and socialchg['tChristina_day'] ! daystart:
	if christinaQW['subpath'] = 1:
		'<a href="exec:gt ''gschool_socialchg1'', ''chris_sub''">Christina</a> is standing by the beach looking out over the lake as Lina stands next to her, breathing heavily.'
	else
		'Christina is standing by the beach looking out over the lake as <a href="exec:gt ''gschool_socialchg1'', ''chris_dom''">Lina</a> stands next to her, breathing heavily.'
	end
end

gs 'camera', 'check_location'

act'Return to Pavlovsk':
	*clr & cla
	if konki_cloth = 1:
		'You need to remove your skates first.'
		act'Go back':gt'pav_lake'
	elseif PCloswimwear = 1:
		'You need to get dressed into your regular clothing first.'
		act'Return':gt'pav_lake'
	elseif $clothingworntype ! 'nude':
		minut += 5
		gt'pav_residential'
	else
		'You need to put some clothes on first.'
		act'Return':gt'pav_lake'
	end
end

if month >= 5 and month <= 9:

	if func('changingroom','count_swim_item') > 0:
		act 'Use changing room':gt 'changingroom', 'view_swim_list'
	else
		*nl
		'You should buy some swimwear if you intend on spending time at the beach.'
		*nl
	end

	if $clothingworntype ! 'nude' and (pcs_inhib > 30 or (exhibitionist_lvl > 1 and pcs_horny >= 60)):
		gs 'willpower', 'exhib', 'self', 'hard'
		if will_cost <= pcs_willpwr:
			act 'Strip naked (<<will_cost>> Willpower)':
				*clr & cla
				gs 'willpower', 'pay', 'self'
				menu_off = 1
				minut += 5
				sauna_stripped = 0
				if pcs_inhib < 60: inhib_exp += rand(1,3)
				gs 'clothing', 'strip_all'
				'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/event/strip.jpg"></center>'
				'Without hesitation and despite the fact that other people are around, you strip from your clothes and neatly fold them before putting them down in a pile. Without your clothes holding you back, you can go for a swim in the lake or do some sunbathing.'
				act'Continue':gt'pav_lake'
			end
		else
			act 'Strip naked (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
		end
	elseif $clothingworntype = 'nude' and sauna_stripped = 0:
		act 'Put your clothes back on':
			*clr & cla
			menu_off = 1
			minut+=5
			nudelake = rand(1,20)
			if nudelake <= 3 and home_owned[2] = 1:
				minut += 5
				pcs_mood -= 5
				inhib_exp += rand(1,3)
				'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/event/noclothes.jpg"></center>'
				'You return to the place where put your clothes to get dressed... except you can''t find them. You desperately search the beach for a while, a few passers-by giving you amused stares. Despite your efforts, you can''t find them. You panic and, without thinking, start running home naked.'
				act 'Run home naked': gt 'pav_lake', 'nude_home'
			elseif nudelake = 4 and home_owned[2] = 1:
				minut += 5
				pcs_mood -= 5
				'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/event/nakedcar.jpg"></center>'
				'You return to the place where you put your clothes to get dressed... except you can''t find them. You desperately search the beach for a while, a few passers-by giving you amused stares. A man eventually approaches you, his eyes are darting over your body as he speaks. "Is there a problem, sweetheart? You seem upset..."'
				if bag = 1:
					'Luckily, your bag is sitting where you left it.'
				else
					money = 0
				end
				'You''re seriously panicking now. "I can''t find my clothes! I left them right here and now they''re gone! I don''t know what to do!" you blurt out and he tries to calm you down.'
				'"Don''t worry girl, you''re going to be fine. Come on, I happen to have some sportswear in my car."'
				'You profusely thank him as he guides you to his car and are relieved to see that he wasn''t lying. He pulls out a set of brand new sportswear that looks look like they would fit you. He doesn''t hand them over though. "Of course, I can''t just give these away for free... They''re brand new! They cost me 3000 <b>₽</b>, so if you can give me that they''re all yours."'
				if money >= 3000:
					act 'Pay 3000 <b>₽</b>':
						*clr & cla
						money -= 3000
						danilovich_outfits[1] = 1
						danilovich_outfits_s[1] = 0
						danilovich_outfits_h[1] = 100
						gs 'clothing', 'wear', 'danilovich_outfits', 1
						'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/event/nakedcar.jpg"></center>'
						'There''s the catch. He looks at your naked body as you mull over his proposition. You realize that you don''t really have any other choice. You silently nod and give him the money, quickly putting the clothes on when he hands them over. You feel relieved that you won''t have to go home naked and hope that your clothes turn up later.'
						act'Continue':gt'pav_lake'
					end
				else
					act 'Tell him you don''t have enough money':
						*clr & cla
						'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/event/nakedcar.jpg"></center>'
						'There''s the catch. He looks over your naked body as you mull over your predicament. "I don''t have that kind of money. Do you have a towel or something?"'
						'He shakes his head and you can see a slight bulge forming in his pants. "No sorry, but I might be willing to trade the clothes..." He trails off suggestively and with the way he''s looking at you, you have no doubt what he has in mind.'
						'"What did you have in mind?" you ask rhetorically and he grins.'
						'"Well, how about we get in my car and you let me fuck you? Then you can have the clothes."'
						gs 'willpower', 'prostitution', 'resist'
						if will_cost <= pcs_willpwr:
							act 'Refuse and run home naked (<<will_cost>> Willpower)':
								gs 'willpower', 'pay', 'resist'
								gt 'pav_lake', 'nude_home'
							end
						else
							act 'Refuse and run home naked (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
						end
						act 'Agree to have sex': gt 'pav_lake', 'car_sex'
					end
				end
				gs 'willpower', 'prostitution', 'self'
				if will_cost <= pcs_willpwr:
					act 'Suggest alternative payment (<<will_cost>> Willpower)':
						*clr & cla
						gs 'willpower', 'pay', 'self'
						'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/event/nakedcar.jpg"></center>'
						'This is not how you planned for your day to go! Too afraid to look him in the eye, you feebly make a suggestion. "Is there... is there some other way I can pay you? 3000 <b>₽</b> is a lot of money."'
						'He''s been gawking at your naked body the whole time you''ve been talking, and your suggestion seems to be exactly what he wanted to hear as he starts grinning. "I can think of one way..."'
						'The growing bulge in his pants does an excellent job of telling exactly what he''s thinking.'
						act 'Agree to have sex': gt 'pav_lake', 'car_sex'
						if money >= 3000:
							act 'Just pay the 3000 <b>₽</b>':
								*clr & cla
								money -= 3000
								danilovich_outfits[1] = 1
								danilovich_outfits_s[1] = 0
								danilovich_outfits_h[1] = 100
								gs 'clothing', 'wear', 'danilovich_outfits', 1
								'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/event/nakedcar.jpg"></center>'
								'You don''t want to have sex with him, but you also don''t want to go home naked. Not seeing any other option, you decide to pay him the 3000 <b>₽</b>. You quickly put on the clothes as he hands them over. You feel relieved that you won''t have to go home naked and hope that your clothes turn up later.'
								act 'Continue': gt 'pav_lake'
							end
						end
					end
				else
					act 'Suggest alternative payment (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
				end
			else
				gs 'clothing', 'wear_last_worn'
				gs 'underwear', 'wear'
				'You return to the place where you left your clothes, and put them back on.'
				act'Continue':gt'pav_lake'
			end
		end
	end
end

if temper >= 15 and month>=5 and month<=9 and hour>=6 and hour<=20:
	act 'Take a walk along the beach':
		*clr & cla
		menu_off = 1
		minut += 30
		pcs_mood += 5
		gs'stat'

		if (PCloswimwear = 1) and pcs_hips<=90:
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/lake.walk.bikini0,'+rand(0,3)+'.jpg"></center>'
		elseif (PCloswimwear = 1) and pcs_hips >= 90 and pcs_hips<=110:
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/lake.walk.bikini.fat0,0.jpg"></center>'
		elseif (PCloswimwear = 1) and pcs_hips>=110:
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/lake.walk.bikini.fat0,1.jpg"></center>'
			if pcs_inhib < 50:inhib_exp += rand(1,2)
		elseif $clothingworntype = 'nude':
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/walknude.jpg"></center>'
			inhib_exp += rand(1,3)
		else
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/lake.walk0,'+rand(0,2)+'.jpg"></center>'
		end

		'You walk along the beach, enjoying the feeling of the sun on your body.'
		act 'Continue':gt'pav_lake'
	end
	
	if PCloswimwear = 1 or $clothingworntype = 'nude':
		act 'Go for a swim in the lake':
			*clr & cla
			menu_off = 1
			pcs_makeup = 1
			cumspclnt = 4
			gs 'cum_cleanup'
			minut += 30
			pcs_mood += 5
			if pcs_sweat > 10: pcs_sweat = 10 + rand(0,4)
			if pcs_stren < 30: stren_exp += 1
			fat -= 1
			if $clothingworntype = 'nude':
				'<center><img <<$set_imgh>> src="images/locations/shared/lake/swim_nude.jpg"></center>'
				inhib_exp += rand(1,2)
			else
				'<center><img <<$set_imgh>> src="images/locations/shared/lake/swim_swimsuit.jpg"></center>'
				if pcs_inhib < 50: inhib_exp += rand(1,2)
			end
			'You swim and play in the lake, feeling refreshed and invigorated by the water.'
			if $clothingworntype = 'nude' and rand(0,1): pcs_mood -= 5 & 'After a few minutes, you feel the piercing gaze of several nearby women on you. They''re making an effort of whispering to one another, yet loud enough so you can hear them. "Look at that <<$pcs_lastname>> slut! She has no decency whatsoever, shamelessly flaunting her naked body like that!" The men at the lake are much more appreciative, and openly stare at your attractive body. You can see bulges forming in the shorts of some of them.'
			if deodorant_on = 1: gs 'sweat', 'remove_deo' & '<br>Your deodorant gets washed away in the water.'
			act 'Get out of the water':gt'pav_lake'
		end
		
		if sunWeather = 1:
			act 'Sunbathe':
				*clr & cla
				minut += 30
				if pcs_sweat < 35 : gs 'sweat', 'add', 5
				pcs_mood += 5
				gs'stat'
				if PCloswimwear = 1:
					'<center><img <<$set_imgh>> src="images/locations/shared/lake/tanning_swimsuit.jpg"></center>'
					if pcs_inhib < 50:inhib_exp += rand(1,2)
				else
					'<center><img <<$set_imgh>> src="images/locations/shared/lake/tanning_nude.jpg"></center>'
					inhib_exp += rand(1,3)
				end

				if krem = 0:
					'You lie down on your towel and enjoy the warm rays of the sun on your body.'
					pcs_tan = pcs_tan + 1
				else
					krem = krem - 1
					'You apply sunscreen to yourself and lie down on the towel to work on your tan. You enjoy the warm rays of the sun on your body.'
					pcs_tan = pcs_tan + 3
				end

				if pcs_apprnc >= rand(1,400):
					tipelakeboyrand = rand(1,100)
					if tipelakeboyrand <= 5:
						if npc_QW['A113'] = 1:
							'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/boy/fedor/fedorev/strela/vadimbely.jpg"></center>'
							'While you''re sunbathing, you see Vadim Bely and his brother sitting not far from you. The brother points at you, and seconds later Vadim walks over and sits down next to you. He idly scratches his tattoo covered belly as he speaks to you. "Hello <<$pcs_nickname>>, want to go for a ride with me?"'
							'The greedy look in his eyes tells you that when he says ''ride'', he doesn''t just mean in his car.'
							if $clothingworntype = 'nude':
								act 'Get dressed and go with him':
									gs 'clothing', 'wear_last_worn'
									if PCloswimwear = 1:gs 'clothing', 'wear', $normalclothingworntype, normalclothingwornno
									gt 'LakeBoyDy', 'ride'
								end
							else
								act 'Get dressed and go with him':
									gs 'clothing', 'wear', $normalclothingworntype, normalclothingwornno
									gt 'LakeBoyDy', 'ride'
								end
							end
							exit
						elseif $clothingworntype ! 'nude' and npc_QW['A113'] < 1:
							*clr & cla
							gs'stat'
							'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/boy/fedor/fedorev/strela/vadimbely.jpg"></center>'
							'While sunbathing, you can''t help but notice a man walking towards you. He looks truly intimidating, his entire body covered in criminal gang tattoos. A bit further down the beach you see a younger man with similar tattoos, keeping an eye on the man approaching you.'
							'His head is mostly bald, and a large golden cross dangles on a thick chain from his neck. He sits his fat body down next to you before you can protest, idly scratching his belly as he begins to chat. "Hello! I''m not interrupting anything, am I?"'
							act'Annoyed':
								*clr & cla
								minut += 5
								gs'stat'
								'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/boy/fedor/fedorev/strela/vadimbely.jpg"></center>'
								'"Erm, excuse me!? I was here first!" you exclaim in an annoyed tone.'
								'The man is not impressed in the slightest, obviously having played this game before, and immediately challenges your claim. "Oh, excuse me princess! Is this your spot? Do you own it?"'

								act'Yes':
									*clr & cla
									minut += 5
									gs'stat'
									'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/boy/fedor/fedorev/strela/vadimbely.jpg"></center>'
									'"Yes, this is my spot! I was here first, that''s how it works!" you respond indignantly.'
									'You suddenly hear a deep voice behind you. "This beach is only used by prostitutes and bus drivers, and I don''t see any fucking buses nearby!"'
									'Startled, you look behind you and see the tattooed younger man behind you. He has tattoos over his whole body as well, most notably several tombs and skulls on his chest. He stares you down with a stone cold look on his face.'
									'The bald man smiles and puts his hand on your chest as if he owns it, roughly squeezing your breast through your swimsuit.'
									'"So... how much?"'
									act 'Resist': gs 'LakeBoyDy', 'slap'
									act 'Move away': gt 'LakeBoyDy', 'go'

								end
							end

							act'Ignore them':
								*clr & cla
								minut+=5
								gs'stat'
								'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/boy/fedor/fedorev/strela/vadimbely.jpg"></center>'
								'Feeling intimidated and not wanting any trouble, you ignore them and pretend they''re not there, lying on the beach with your eyes closed. However, you feel a strange tickling your stomach few seconds later and open your eyes. You see the fat man running a blade of grass over your stomach with a smug grin on his face.'

								act'Keep ignoring them':
									*clr & cla
									minut+=5
									gs'stat'
									'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/boy/fedor/fedorev/strela/vadimbely.jpg"></center>'
									'You try to ignore them and close your eyes again, but you feel the blade of grass continuously teasing your body, and after a few minutes his sweaty hand is pawing at your breasts through your swimsuit.'
									'"Come with me, baby and I''ll make you feel so good! You won''t regret it." His groping becomes more insistent and he''s roughly pinching your nipples now, the outline of them slowly but surely becomes visible through your swimsuit.'
									act 'Resist': gs 'LakeBoyDy', 'slap'
								end
							end

							act 'Walk away':gt 'pav_lake'
							exit
						else
							*clr & cla
							'Three men sit down near you as you sunbathe. While you notice them looking at you and talking about your body, they don''t come over.'
							act'Stop sunbathing':gt'pav_lake'
						end
					elseif tipelakeboyrand <= 30:
						!! {below is for when this event is fixed. Generates a boy[0], boy[1], and boy[2]}
						!! killvar '$boy'
						!! i = rand((age - 5),(age + 5))
						!! if i < 16: i = 16
						!! gs 'npcgeneratec', 0, 'Guy', rand((i - 1),(i + 1))
						!! gs 'boyStat', $npclastgenerated
						!! gs 'npcgeneratec', 0, 'Guy', rand((i - 1),(i + 1))
						!! gs 'boyStat', $npclastgenerated, 1
						!! gs 'npcgeneratec', 0, 'Guy', rand((i - 1),(i + 1))
						!! gs 'boyStat', $npclastgenerated, 2
						'Three men sit down near you as you sunbathe. While you notice them looking at you and talking about you, they don''t come over.'

						act 'Continue':gt'pav_lake'
						exit
					elseif tipelakeboyrand <= 80:
						i = age - 5
						if i < 16: i = 16
						gs 'npcgeneratec', 0, 'Guy', rand((i),(age + 5))
						gs 'boyStat', $npclastgenerated
						$boydesc = $npc_firstname[$npclastgenerated]
						gs'LakeBoyZ'
						'A rather cute looking guy sits down next to you as you sunbathe.'
						'"Hey there beautiful, I''m <<$boydesc>>. What''s your name?" he asks in an attempt to chat you up.'

						act 'Get away from him':gt'pav_lake'
						dynamic $gL_boy_z
						exit
					else
						'Three men sit down near you as you sunbathe. While you notice them looking at you and talking about your body, they don''t come over.'
						act 'Stop sunbathing':gt'pav_lake'
						exit
					end
				else
					act'Stop sunbathing':gt'pav_lake'
				end
			end
		end
	end
end

act'Go to the secluded beach':minut += 10 & gt'pav_lake_nude'

if month = 12 or month < 3:
	if konki_cloth = 1:
		act'Take off your skates':
			*clr & cla
			menu_off = 1
			minut += 5
			konki_cloth=0
			gs 'stat'
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/konki0,1.jpg"></center>'
			'You take off your skates and put your shoes back on. It takes you a while to adjust to walking on your feet again.'
			act'Continue':gt'pav_lake'
		end
	elseif konki_cloth ! 1 and konki > 0 and hour >= 8 and hour <= 17:
		act'Put your skates on':
			*clr & cla
			menu_off = 1
			minut += 5
			konki_cloth = 1
			gs 'stat'
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/konki0,0.jpg"></center>'
			'You take your shoes off and put your skates on.'
			act 'Continue': gt'pav_lake'
		end
	end
	if konki_cloth = 1: act'Skate on the ice rink': gt 'pav_lake', 'skating_start'
end

if $ARGS[0] = 'car_sex': 
	*clr & cla
	danilovich_outfits[1] = 1
	danilovich_outfits_s[1] = 0
	danilovich_outfits_h[1] = 100
	gs 'clothing', 'wear', 'danilovich_outfits', 1
	'<center><img <<$set_imgh>> src="images/locations/shared/sex/car/sexcar1.jpg"></center>'
	'He wastes no time and immediately throws you down on the back seat of his car when you agree. He pulls down his pants and, after putting a condom on, quickly guides his cock to your pussy. He obviously doesn''t plan on spending a lot of time with you and sinks it inside you in one firm thrust before he starts fucking you with rapid, short thrusts. He quickly shoots his load in the condom, ties a knot in it and throws it away. He hands you the clothes as promised, kicks you out of the car and drives off.'
	*nl
	'It wasn''t the best sex you''ve had, but at least you have some clothes to wear now. With a sigh of relief, you straighten the clothes and feel ready to take the day on again, hoping your own clothes will turn up at some point.'
	gs 'arousal', 'vaginal', 10, 'sub'
	gs 'arousal', 'end'
	gs 'stat'
	act 'Continue': gt 'pav_lake'
end

if $ARGS[0] = 'nude_home':
	*clr & cla
	minut += 10
	menu_off = 1
	inhib_exp += rand(1,3)
	gs'stat'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/homenude.jpg"></center>'
	'You cover yourself up as best as you can and dash into the woods, using them as cover to get as close as you can to your apartment. Luckily, no one spots you, but now you''re at the edge of the woods and have the difficult task of actually making it to the building without being seen.'
	
	gs 'willpower', 'exhib', 'self'
	if will_cost <= pcs_willpwr:
		act 'Run for it (<<will_cost>> Willpower)':
			*clr & cla
			gs 'willpower', 'pay', 'self'
			minut += 7
			gs 'fame', 'pav', 'sex', 5
			inhib_exp += rand(2,4)
			if month < 3 or month = 12:
				'<center><img <<$set_imgh>> src="images/locations/pavlovsk/naked/runhomenakedw.jpg"></center>'
			else
				'<center><img <<$set_imgh>> src="images/locations/pavlovsk/naked/runhomenaked.jpg"></center>'
			end
			'You decide to just get this over with and make a run for it. You use your hands to cover yourself as best as you can and look away from people, but you sometimes have to use your hands to cover your face so no one recognizes you, exposing yourself. Several people gasp and more than a few people, mostly older women, chastise you. Some men whistle and more than a few people take your picture; you really hope none of them show your face. After what seems like an eternity, you finally make it to the apartment complex.'
			gs 'arousal', 'flash', 3, 'exhibitionism'
			gs 'stat'
			act 'Continue to complex': gt 'pav_aptcourtev', 'naked_aptcomplex'
		end
	else
		act 'Run for it (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
	end
	
	act 'Sneak home':
		*clr & cla
		minut += 20
		gs 'fame', 'pav', 'sex', 2
		inhib_exp += rand(1,3)
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/naked/sneakhomenaked.jpg"></center>'
		'You decide it would be better to take your time and sneak home, doing your best to not be seen. You use your hands to cover yourself as best as you can and hide behind trees and parked cars, duck down alleyways and dart across open stretches of ground when nobody is around. Once or twice you think someone might have seen you and hope that you weren''t recognized. After what seems like an eternity, you finally make it to the apartment complex.'
		gs 'arousal', 'flash', 10, 'exhibitionism'
		gs 'stat'
		act 'Continue to complex': gt 'pav_aptcourtev', 'naked_aptcomplex'
	end
end

if $ARGS[0] = 'skating_start':
	*clr & cla
	minut += 3
	menu_off = 1
	gs'stat'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/katok0,'+rand(0,3)+'.jpg"></center>'
	'You slowly make your way over to the ice rink where a lot of people are already skating. Some of them are really good and fly around the lake, while others carefully shuffle around, doing their best to just stay on their feet without falling.'
	act'Leave the rink':gt'pav_lake'
	act'Skate on the rink': gt 'pav_lake', 'skating'
end

if $ARGS[0] = 'skating':
	*clr & cla
	menu_off = 1
	fat -= 1
	gs 'sweat', 'add', 10
	stren_exp += 1
	agil_exp += 1
	react_exp += 1
	icesktng_exp += 1
	minut += 30
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/katok_do0,'+rand(0,7)+'.jpg"></center>'
	if pcs_icesktng <= 10:
		pcs_mood -= 10
		'You do your best to ride your skates, but you''re not very good at it. You fall flat on your ass several times.'
		'Some of the friendlier skaters are cheering you on and try to give you some tips, while others laugh when you fall on your ass yet again.'
	elseif pcs_icesktng <= 30:
		pcs_mood -= 5
		'Your persistent attempts to learn how to skate are starting to pay off. You don''t fall as often any more, even though it still happens on occasion. Ice skating is a lot more fun when you''re not falling on your ass all the time!'
		if rand(1,3) = 1:
			icesktng_exp += 1
			'While you''re trying some new things, you suddenly figure out how to do a certain move properly. It definitely improves your skating skills.'
		end
	elseif pcs_icesktng <= 50:
		pcs_mood += 5
		'At long last, you finally manage to skate without falling once. Some of the other regulars who have seen you struggle before notice how much better you''re doing, and congratulate you on your progress.'
		'You become a bit more daring as you continue, almost never falling any more.'
		if rand(1,3) = 1:
			icesktng_exp += 2
			'You fall, but one of the more experienced skaters notices and helps you up. Once you''re up on your skates again, he explains what you did wrong and you feel like you''re a better skater for it. You thank him as you skate off, trying to apply the trick he just taught you.'
		end
	elseif pcs_icesktng<=70:
		pcs_mood += 10
		'You''re getting better at ice skating and basically never fall anymore. You enjoy the flow of the wind on your face as you dare to go faster and faster.'
		if rand(1,3) = 1:
			icesktng_exp += 3
			'You watch the more experienced skaters, trying to pick up new tricks. You try to copy some of the things they are doing, and notice that those moves are indeed more efficient than how you were doing it before.'
		end
	else
		pcs_mood += 15
		'You''ve pretty much mastered ice skating now, and won''t get any better without professional training. You occasionally do some tricks while you skate around the rink, and others admire your skills as you fly past them.'
	end

	gs'stat'
	
	if hour <= 17:
		act 'Leave the rink': gt'pav_lake'
		act 'Skate some more': gt 'pav_lake', 'skating'
	else
		*clr & cla
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/katok0,'+rand(0,3)+'.jpg"></center>'
		'It''s too dark to go ice skating now. The rink is closed.'
		act 'Leave': gt'pav_lake'
	end
end


--- pav_lake ---------------------------------

