# booty_call_sex

if $ARGS[0] = 'sex_start':
	if booty_call['no_sex'] = 0:
		if stat['think_virgin'] = 1 and booty_call['virgin'] = 0 and booty_call['fuck_count'] = 0: 
			act'Give him your virginity':gt'booty_call_virgin', 'start1'
		elseif booty_call['fuck_count'] = 0:
			act'Fuck <<$npc_firstname[$boy]>>':booty_call['must_fuck'] = 1 & booty_call['fuck_now'] = 1 & gs 'booty_call_sex', 'sex_pre'
		else
			act'Fuck again':
				if booty_call['cum_count'] >= npc_sexdrive[$boy] + booty_call['force_harden']:
					booty_call['fuck_again'] = 1
					gs 'booty_call_after', 'fucked_out'
				else
					cla
					gs 'booty_call_sex', 'reset'
					gs 'booty_call_condoms', 'condoms'
					gs 'booty_call_sex', 'position_change'
				end
			end
		end
	end
end

if $ARGS[0] = 'sex_pre':
	cla & *nl
	if booty_call['fuck_count'] = 0:
		if booty_call['about_time'] = 1:
			'"It''s about fucking time," you say, scooting backwards onto the bed.'
		elseif booty_call['fuck_now'] = 1:
			booty_call['must_fuck'] = 1
			'"Enough of the foreplay," you say with a lusty growl. "It''s time to fuck."'
		elseif booty_call['not_today'] = 1:
			booty_call['not_today'] = 2
			'"I thought you didn''t want to fuck today," <<$npc_firstname[$boy]>> says with a wry smile.'
			'"Changed my mind," you grin back.'
		elseif booty_call['position_choose'] = 0:
			'"I want you inside me," you smile seductively.'
		end
	elseif booty_call['fuck_count'] > 0:
		if booty_call['get_hard'] = 1:
			minut += rand(1,2)
			booty_call['get_hard'] = 0
			'After a minute or so, he starts to stiffen up again.'
			'<i>Finally...</i>'
		else
			'"I haven''t had enough yet," you say, seductively, pulling <<$npc_firstname[$boy]>> towards you.'
		end
	end

	if know_virgin[$boy] = 1:
		know_virgin[$boy] = 2
		'"I thought you said you were saving your virginity," <<$npc_firstname[$boy]>> says, looking a little confused but extremely excited.'
		'"I accidentally lost it somewhere," you grin. "Now hurry up and fuck me."'
	end
	booty_call['position_choose'] = 1
	gs 'stat'
	if booty_call['condom'] ! 0 or booty_call['no_condom'] ! 0:
		gs 'booty_call_sex', 'position_choose'
	end
	gs 'booty_call_condoms', 'condoms'
end



if $ARGS[0] = 'position_choose':
	if booty_call['virgin'] = 1: 
		gs 'booty_call_virgin', 'start_options'
	!! initiative roll
	elseif booty_call['no_sex'] = 0:
		booty_call['initative'] = rand(0,1)
		if (booty_call['initative'] = 1 and booty_call['force_initiative'] ! 2) or booty_call['force_initiative'] = 1:
			!! player wins initiative
			if $booty_call['pos_speed'] = '':
				act'Let him choose':
					cla & *nl
					'"Take me however you want," you smile.'
					booty_call['must_fuck'] = 1
					if $npc_fav_pos[$boy] = 'missionary':
						gs 'booty_call_miss', 'missionary_start'
					elseif $npc_fav_pos[$boy] = 'doggy':
						gs 'booty_call_doggy', 'doggy_start'
					elseif $npc_fav_pos[$boy] = 'cowgirl':
						gs 'booty_call_cowgirl', 'cowgirl_start'
					else
						gs 'booty_call_sex', 'rand_position'
					end
	!!				if npc_condom_conscious[$boy] = 1 or booty_call['condom'] = 1:
				end
			end
			gs 'booty_call_miss', 'missionary_start'
			gs 'booty_call_doggy', 'doggy_start'
			gs 'booty_call_cowgirl', 'cowgirl_start'
		else
			!! boy wins initiative
			if rand(0,1) = 1:
				if $npc_fav_pos[$boy] = 'missionary':
					gs 'booty_call_miss', 'missionary_start'
				elseif $npc_fav_pos[$boy] = 'doggy':
					gs 'booty_call_doggy', 'doggy_start'
				elseif $npc_fav_pos[$boy] = 'cowgirl':
					gs 'booty_call_cowgirl', 'cowgirl_start'
				else
					gs 'booty_call_sex', 'rand_position'
				end
			else
				gs 'booty_call_sex', 'rand_position'
			end
		end
	end
end

if $ARGS[0] = 'he_choose_position':
	if $booty_call['position'] = 'kuni' or $booty_call['position'] = 'blowjob':booty_call['must_fuck'] = 1
	*nl
	if $npc_lovername[$boy] = '':
		'"Do whatever you want to me babe," you smile.'
	else
		'"Do whatever you want to me <<$npc_lovername[$boy]>>," you smile.'
	end
	
	if rand(0,1) = 1:
		if $npc_fav_pos[$boy] = 'missionary':
			gs 'booty_call_miss', 'missionary_start'
		elseif $npc_fav_pos[$boy] = 'doggy':
			gs 'booty_call_doggy', 'doggy_start'
		elseif $npc_fav_pos[$boy] = 'cowgirl':
			gs 'booty_call_cowgirl', 'cowgirl_start'
		elseif $npc_fav_pos[$boy] = 'blowjob' and booty_call['must_fuck'] ! 1:
			gs 'booty_call_start', 'bj_ask'
		elseif npc_pussyeater[$boy] = 1 and booty_call['must_fuck'] ! 1:
			*nl
			'<<$npc_firstname[$boy]>> starts leading a trail of kisses down your stomach towards your pussy.'
			gs 'booty_call_start', 'kuni_start'
		else
			gs 'booty_call_sex', 'rand_position'
		end
	else
		gs 'booty_call_sex', 'rand_position'
	end
end

if $ARGS[0] = 'rand_position':
	if booty_call['must_fuck'] = 1:
		booty_call['position_rand'] = rand(1,3)
	else
		booty_call['position_rand'] = rand(1,5)
	end
	if booty_call['position_rand'] = 1:
		!! missionary
		gs 'booty_call_miss', 'missionary_start'
	elseif booty_call['position_rand'] = 2:
		!! doggy
		gs 'booty_call_doggy', 'doggy_start'
	elseif booty_call['position_rand'] = 3:
		!! cowgirl
		gs 'booty_call_cowgirl', 'cowgirl_start'
	elseif booty_call['position_rand'] = 4 and booty_call['must_fuck'] ! 1:
		!! blowjob
		gs 'booty_call_start', 'bj_ask'
	elseif booty_call['position_rand'] = 5 and booty_call['must_fuck'] ! 1:
		*nl
		'<<$npc_firstname[$boy]>> starts leading a trail of kisses down your stomach towards your pussy.'
		gs 'booty_call_start', 'kuni_start'
	else
		!! failsafe: default to missionary
		gs 'booty_call_miss', 'missionary_start'
	end
end

if $ARGS[0] = 'position_change':
	booty_call['bored'] = 0
	if $booty_call['position'] ! 'miss': gs 'booty_call_miss', 'missionary_start'
	if $booty_call['position'] ! 'doggy': gs 'booty_call_doggy', 'doggy_start'
	if $booty_call['position'] ! 'cowgirl': gs 'booty_call_cowgirl', 'cowgirl_start'
end

if $ARGS[0] = 'girl_cum_pre':
	*nl
	'It isn''t long before you feel right at the edge of climax. Your body tenses up, pressure builds inside you, straining against your skin which feels hot and tingly. You feel like you''re going to explode any second now.'
end

if $ARGS[0] = 'sex_end':
	if booty_call['mad'] = 1:
		gs'booty_call_after', 'bad_sex_end'
	else
		gs'booty_call_after', 'stop_here'
	end
	if booty_call['sex_stop'] = 0:
		gs'booty_call_after', 'keep_going'
		gs'booty_call_sex', 'harden_hand'
		gs'booty_call_sex', 'harden_mouth'
		gs'booty_call_sex', 'sex_start'
	end
	if booty_call['cleanup'] = 0:gs'booty_call_after', 'cum_clean'
	if booty_call['react'] = 0:
		gs'booty_call_after', 'facial_mouth_clean'
		gs'booty_call_after', 'cum_kiss_cock'
		gs'booty_call_talk', 'cum_react'
	end
	gs'booty_call_after', 'tap_out'
end

if $ARGS[0] = 'speed_select':
	if booty_call['too_rough'] = 1:
		booty_call['speed'] = rand(1,2)
	else
		booty_call['speed'] = npc_sex_speed[$boy] + rand(-1,1)
		if booty_call['speed'] <= 0: booty_call['speed'] = 1
		if booty_call['speed'] > 3: booty_call['speed'] = 3
	end
end
if $ARGS[0] = 'reset':
	$booty_call['cum_choice'] = ''
	$booty_call['last_cum'] = ''
	booty_call['orgasm'] = orgasm
	booty_call['kiss'] = 0
	booty_call['bored'] = 0
	booty_call['he_ask'] = 0
	$orgasm_or = ''
	booty_call['cock_remove'] = 0
	booty_call['not_inside'] = 0
	booty_call['pullout_ask'] = 0
	booty_call['creampie_exit'] = 0
	booty_call['no_cum'] = 0
	$booty_call['position'] = ''
!!	$booty_call['pos_speed'] = ''
	booty_call['cleanup'] = 0
	if booty_call['cum_together'] > 0:
		booty_call['cum_together'] = 0
		booty_call['creampie_orgasm'] = 1
		
	end
	booty_call['cum_together_fake'] = 0
	booty_call['cum_react'] = 0
	if booty_call['mad'] = 1:booty_call['mad'] = 2
	booty_call['boy_cum'] = 0
	booty_call['consecutive_cuni_orgasm'] = 0
!!	booty_call['reluctant_bj'] = 0
!!	booty_call['cum_mouth'] = 0
!!	booty_call['cum_mouth_force'] = 0
	booty_call['about_time'] = 0
	booty_call['his_choice'] = 0
	booty_call['cum_warn'] = 0
	booty_call['cum_where'] = 0
	booty_call['he_ask'] = 0
	booty_call['he_ask_creampie'] = 0
	booty_call['he_ask_creampie_yes'] = 0
	booty_call['he_ask_mouth'] = 0
	booty_call['he_ask_facial'] = 0
	booty_call['he_ask_tits'] = 0
	booty_call['cum_wherever'] = 0
	booty_call['swallow'] = 0
	booty_call['spit'] = 0
	booty_call['gag'] = 0
	booty_call['react'] = 0
	booty_call['fuck_again'] = 0
	booty_call['must_fuck'] = 0
end

if $ARGS[0] = 'harden_hand':
	if $booty_call['last_cum'] ! '':
		act'Try to get him hard again (hand)':
			cla & *clr
			if booty_call['cum_count'] > npc_sexdrive[$boy]:
				!! IMAGE HERE
				$npc_apt_bedroom[$boy]
				if $booty_call['cum_choice'] = 'creampie' or $booty_call['cum_choice'] = 'creampie_surprise':
					'You pull away, pussy trailing semen as you get down in front of <<$npc_firstname[$boy]>> and grab his cock. You work his shaft furiously, trying to keep him hard. But despite your best efforts, his dick softens in your grasp and you can''t get it to rise again. '
				else
					'Taking hold of his cock, you work his shaft furiously, trying to keep him hard. But despite your best efforts, his dick softens in your grasp and you can''t get it to rise again.'
				end
				if booty_call['not_done'] = 1:
					'"Goddammit!" you growl. "I still wanted more out of you!"'
				end
				gs 'booty_call_after', 'after_sex1'
			else
				'<center><video autoplay loop src="images/shared/sex/handjob/hj3.mp4"></video></center>'
				if $booty_call['cum_choice'] = 'creampie' or $booty_call['cum_choice'] = 'creampie_surprise':
					'You pull away, pussy trailing semen as you get down in front of <<$npc_firstname[$boy]>> and grab his cock. You work his shaft furiously, trying to keep him hard. It doesn''t take long, only a minute or two, and before you know it <<$npc_firstname[$boy]>> is standing at attention once again.'
				else
					'Taking hold of his cock, you work his shaft furiously, trying to keep him hard. It doesn''t take long, only a minute or two, and before you know it <<$npc_firstname[$boy]>> is standing at attention once again.'
				end
				if booty_call['not_done'] = 1:
					'"Yeah, that''s right...  Just like that little guy..."'
				end
				booty_call['not_done'] = 0
				$booty_call['foreplay'] = ''
				gs'booty_call_sex', 'reset'
				gs 'booty_call_start', 'handjob_dom1'
				gs 'booty_call_start', 'foreplay_choose'
				if booty_call['fuck_count'] > 0:
					act'Back to sex':cla & gs'booty_call_sex', 'position_change' & gs 'booty_call_condoms', 'condoms'
				else
					gs'booty_call_sex', 'sex_start'
				end
			end
		end
	end
end

if $ARGS[0] = 'harden_mouth':
	if $booty_call['last_cum'] ! '':
		act'Try to get him hard again (mouth)':
			cla & *clr
			if booty_call['cum_count'] > npc_sexdrive[$boy]:
				!! IMAGE HERE
				$npc_apt_bedroom[$boy]
				if ($booty_call['last_cum'] = 'creampie' or $booty_call['cum_choice'] = 'creampie_surprise') and booty_call['cock_remove'] ! 1:
					'You pull away, pussy trailing semen as you get down in front of <<$npc_firstname[$boy]>>. You take him in your mouth, tasting of cum and your own juices, and suck gently on its head, licking, teasing, playing, with his cock in an attempt to keep him hard. But try as you might, you can feel his dick softening between your lips.'
				elseif $booty_call['last_cum'] = 'condom':
					'You pull away and get down in front of <<$npc_firstname[$boy]>>. You slip the condom from his cock and take him in your mouth, you suck gently on its head, licking, teasing, playing, cleaning the cum from his shaft while trying to keep him hard. But try as you might, you can feel his dick softening between your lips.'
				else
					'You pull away and get down in front of <<$npc_firstname[$boy]>>. Taking his cock in your mouth, you suck gently on its head, licking, teasing, playing, trying to keep him hard. But try as you might, you can feel his dick softening between your lips.'
				end
				if booty_call['not_done'] = 1:
					'"Goddammit!" you growl. "I still wanted more out of you!"'
				end
				gs 'booty_call_after', 'after_sex1'
			else
				!! REPLACE
				!! IMAGE HERE
				 '<center><video autoplay loop src="images/shared/sex/blowjob/play1.mp4"></video></center>'
				if ($booty_call['cum_choice'] = 'creampie' or $booty_call['cum_choice'] = 'creampie_surprise') and booty_call['cock_remove'] ! 1:
					'You pull away, pussy trailing semen as you get down in front of <<$npc_firstname[$boy]>>. You take him in your mouth, tasting of cum and your own juices, and suck gently on its head, licking, teasing, playing, trying to keep him hard. As you do, the dick between your lips noticeably stiffens up and is ready to ride again.'
				elseif $booty_call['last_cum'] = 'condom':
					'You pull away and get down in front of <<$npc_firstname[$boy]>>. You slip the condom from his cock and take him in your mouth, you suck gently on its head, licking, teasing, playing, cleaning the cum from his shaft. As you do, the dick between your lips noticeably stiffens up and ready to ride again.'
				else
					'Taking his cock in your mouth, you suck gently on its head, licking, teasing, playing, trying to keep him hard. After a few moments of softness, the dick between your lips noticeably stiffens up and is ready to ride again.'
				end
				if booty_call['not_done'] = 1:
					'"Yeah, that''s right...  Just like that little guy..."'
				end
				booty_call['not_done'] = 0
				$booty_call['foreplay'] = ''
				gs'booty_call_sex', 'reset'
				act'Keep blowing him':cla & *clr & gs 'booty_call_start', 'bj_dom2'
				gs 'booty_call_start', 'foreplay_choose'
				if booty_call['fuck_count'] > 0:
					act'Back to sex':cla & gs'booty_call_sex', 'position_change' & gs 'booty_call_condoms', 'condoms'
				else
					gs'booty_call_sex', 'sex_start'
				end
			end
		end
	end
end

if $ARGS[0] = 'no_sex_only':
	if booty_call['no_sex'] = 0:
		act'We could do other stuff':
			booty_call['no_sex'] = 1
			cla & *clr
			$npc_apt_bedroom[$boy]
			'"We could do other stuff?" you suggest.'
			if npc_no_kiss[$boy] ! 1:
				act'We could make out':
					cla & *clr
					$npc_apt_bedroom[$boy]
					'"We could do other stuff?" you suggest. "We could just make out?"'
					'"Okay," <<$npc_firstname[$boy]>> nods and you happily move to kiss him.'
					gs 'booty_call_start', 'makeout_dom1'
				end
			end
			
			act'I could blow you':
				cla & *clr
				$npc_apt_bedroom[$boy]
				'"We could do other stuff?" you suggest. "I could just blow you?"'
				if $npc_fav_pos[$npclastgenerated] = 'blowjob':
					'"I would love that," <<$npc_firstname[$boy]>> grins.'
				else
					'"I could get behind that..." <<$npc_firstname[$boy]>> smiles.'
				end
				act'Blow him':gt 'booty_call_start', 'bj_dom2'
			end
			
			if booty_call['cuni_orgasm'] < 5:
				act'You could eat me out':
					cla & *clr
					$npc_apt_bedroom[$boy]
					'"We could do other stuff?" you suggest. "I wouldn''t mind if you ate my pussy for a little while..."'
					if npc_pussyeater[$boy] = 1:
						'"I think I''d like that too," <<$npc_firstname[$boy]>> says with a dangerous glint in his eye.'
						act'Get eaten out':gs 'booty_call_start', 'pussy_lick'
					elseif npc_selfish[$boy] = 1:
						'"How about you blow me instead?" he asks.'
						act'That sounds fun':
							cla & *nl
							'"That sounds fun," you smile, moving down between his legs.'
							act'Blow him':gt 'booty_call_start', 'bj_sub1'
						end
						
						act'I guess I could':
							cla & *nl
							'"I guess I could do that," you say reluctantly, moving down between his legs.'
							act'Blow him':gt 'booty_call_start', 'bj_sub1'
						end
					end
				end
			end
		end
	end
end

if $ARGS[0] = 'pain':
	if booty_call['speed'] ! 3:
		if $npc_dick_desc[$boy] = 'lengthy':
			if pcs_vag <= 20:
				gs 'pain', 2, 'cervix', 'hit'
			else
				gs 'pain', 1, 'cervix', 'hit'
			end
		elseif $npc_dick_desc[$boy] = 'gigantic':
			if pcs_vag <= 20:
				gs 'pain', 2, 'cervix', 'hit'
				gs 'pain', 1, 'vaginal', 'stretch'
			else
				gs 'pain', 1, 'cervix', 'hit'
			end
		elseif $npc_dick_desc[$boy] = 'monster':
			if pcs_vag <= 20:
				gs 'pain', 2, 'cervix', 'hit'
				gs 'pain', 2, 'vaginal', 'stretch'
			else
				gs 'pain', 1, 'cervix', 'hit'
				gs 'pain', 1, 'vaginal', 'stretch'
			end
		end
	else
		if $npc_dick_desc[$boy] = 'lengthy':
			if pcs_vag <= 20:
				gs 'pain', 3, 'cervix', 'hit'
			else
				gs 'pain', 2, 'cervix', 'hit'
			end
		elseif $npc_dick_desc[$boy] = 'gigantic':
			if pcs_vag <= 20:
				gs 'pain', 3, 'cervix', 'hit'
				gs 'pain', 2, 'vaginal', 'stretch'
			else
				gs 'pain', 2, 'cervix', 'hit'
			end
		elseif $npc_dick_desc[$boy] = 'monster':
			if pcs_vag <= 20:
				gs 'pain', 3, 'cervix', 'hit'
				gs 'pain', 2, 'vaginal', 'stretch'
			else
				gs 'pain', 2, 'cervix', 'hit'
				gs 'pain', 2, 'vaginal', 'stretch'
			end
		end
	end
end

--- booty_call_sex ---------------------------------
