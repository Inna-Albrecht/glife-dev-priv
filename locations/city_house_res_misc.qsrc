# city_house_res_misc


!!Small frontyard
if $ARGS[0] = 'fronty':
	$loc = 'city_house_res_misc'
	$loc_arg = 'fronty'
	$menu_loc = 'city_house_res_misc'
	$menu_arg = 'fronty'
	$location_type = 'public_outdoors'
	killvar '$locclass'
	
	menu_off = 0
	minut += 1
	gs 'stat'

	if sound = 0:
		if month >=11 and month <= 12 or month >=1 and month <=3:
			if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street7.mp3',30 & PLAY 'sound/zima.mp3',30
			if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
		else
			if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street7.mp3',30 & PLAY 'sound/street_leto.mp3',30
			if hour >= 0 and hour <= 7: PLAY 'sound/street_night_leto.mp3' & PLAY 'sound/street_night.mp3',30
		end
	end

	*clr & cla
	'<center><b><font color="maroon">Living Room</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/house/crh_fronty.jpg"></center>'
	'Your small two story house sits on the edge of the residential area. Despite that, your neighbors are pretty close and the sounds of the street fill the air.'
	*nl
	if car > 0 and cardrive = 8: '<a href="exec:GS ''carF'', ''start''">Your <<$car>></a> is in the driveway.'

	act 'Go inside': gt 'city_house_res_misc', 'hallw'
	act 'Go to the city':
		minut += 10
		gt 'city_residential'
	end
	if curr_home ! 15:
		act 'Set this house as your main home':gs 'set_home', 15 & gt 'city_house_res_misc', 'fronty'
	end

end

if $ARGS[0] = 'hallw':
	CLOSE ALL
	$loc = 'city_house_res_misc'
	$loc_arg = 'hallw'
	$menu_loc = 'city_house_res_misc'
	$menu_arg = 'hallw'
	$locM = 'city_house_res_misc'
	$locM_arg = 'hallw'
	$location_type = 'private'
	killvar '$locclass'

	menu_off = 0
	minut += 1
	gs 'stat'
	gs 'themes', 'indoors'
	if frost > 0: frost = 0

	*clr & cla
	'<center><b><font color="maroon">Hallway</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/house/crh_hallway.jpg"></center>'
	
	if courtletter_date <= daystart and courtletter_date ! 0: gt 'sentence', 'letter'

	act 'Go out front':
		if $clothingworntype ! 'nude':
			minut += 1
			gt 'city_house_res_misc', 'fronty'
		else
			msg'<b><font color = red>You need to get dressed.</font></b>'
			gt 'city_house_res_misc', 'hallw'
		end
	end

	'The main hallway of your house. You can visit the different rooms from here.'
	'There''s a <a href="exec:gt ''mirror'', ''start''">mirror</a> hanging on the wall.'
	
	if umbrella > 0:'There''s an umbrella hanging on a hook by the door.'
	
	act 'Go to your bedroom': gt 'city_house_res_bedr', 'bedro'
	act 'Go to your bathroom': gt 'city_house_res_bathr', 'bathr'
	act 'Go to your kitchen': gt 'city_house_res_kitch', 'kitch'
	act 'Go to your living room': gt 'city_house_res_misc', 'livroom'
	act 'Go to your backyard': gt 'city_house_res_misc', 'backy'
	
end

!!Small backyard for outdoor working out or maybe gardening?
if $ARGS[0] = 'backy':
	$loc = 'city_house_res_misc'
	$loc_arg = 'backy'
	$menu_loc = 'city_house_res_misc'
	$menu_arg = 'backy'
	$location_type = 'secluded'
	
	menu_off = 0
	minut += 1
	gs 'stat'
	
	*clr & cla
	'<center><b><font color="maroon">Living Room</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/house/crh_backy.jpg"></center>'
	'Your small backyard is fenced in, with a few plots intended for gardening.'

	act 'Go inside': gt 'city_house_res_misc', 'hallw'

	if sunWeather = 1:
		if pcs_stam >=15 or (pcs_stam >= 10 and bookYog + obruch > 0):
			if $clothingworntype = 'danilovich_outfits' and $shoeworntype = 'danilovich':
				*nl
				'You can do some exercises here.'
				act 'Exercise': gt 'exercise', 'workout'
			else
				*nl
				'You could do some exercises here if you were wearing exercise clothes.'
			end
		end
	end

end

if $ARGS[0] = 'livroom':
	$loc = 'city_house_res_misc'
	$loc_arg = 'livroom'
	$menu_loc = 'city_house_res_misc'
	$menu_arg = 'livroom'
	$location_type = 'private'
	$locclass = 'livingr'
	
	menu_off = 0
	minut += 1
	gs 'stat'
	
	*clr & cla
	'<center><b><font color="maroon">Living Room</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/house/crh_living.jpg"></center>'

	act 'Leave the room': gt 'city_house_res_misc', 'hallw'

	'A place where you can watch <a href="exec:gt ''TV'',''start''">TV</a>, or relax on your <a href="exec:gt ''divan'', ''start''">sofa</a>.'

	if komp = 1:
		'There''s a <a href="exec:gt ''Komp'', ''start''">computer</a> sitting on your <a href="exec:gt ''stol'', ''start''">desk</a>.'
	else
		'A small <a href="exec:gt ''stol'', ''start''">desk</a> is against the wall.'
	end

	'There is enough space to workout.'
	gs 'exercise', 'start'

	gs 'subkid'

	if ml_guitar['carried'] = 1:
		act 'Place the guitar next to your desk':
			ml_guitar['carried'] = 0
			$ml_guitar['location'] = $curloc
			gt $loc, $loc_arg
		end
	elseif $ml_guitar['location'] = $curloc:
		act 'Pick up the guitar':
			ml_guitar['carried'] = 1
			$ml_guitar['location'] = ''
			gt $loc, $loc_arg
		end
	end

	if ml_online['account'] = 0 and internet > 0:
		act 'Set up an online music account': gt 'music_onlinemusic', 'setupAccount'
	end

	!!Uploading recorded music if there are any not uploaded yet
	if ml_uploadablemusic > 0 and internet > 0 and ml_online['account'] = 1:
		if pcs_inhib < 30:
			if pcs_inhib < 10:
				$diff = 'hard'
			elseif pcs_inhib < 20:
				$diff = 'medium'
			else
				$diff = 'easy'
			end
			gs 'willpower', 'skill', 'self', $diff
			will_cost = (will_cost * 2) - (pcs_perform)
			if will_cost <= pcs_willpwr:
				act 'Upload music (<<will_cost>> Willpower)': gt 'music_onlinemusic', 'uploadmusic'
			else
				act 'Upload music (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
			end
		end
		act 'Upload music': gt 'music_onlinemusic', 'uploadallmusic'
	end

	if ml_uploadablemusic > 0:
		act 'Delete old recordings': gt 'music_onlinemusic', 'deleteoldmusic'
	end

	if hour > 8 and hour < 22:
		if ml_guitar['hasguitar'] = 1:
			act 'Play something on the guitar':
				pcs_mood += 10
				minut += 10
				gs 'stat'
				'You pick up your guitar and start to play some songs you know, just for your own entertainment.'
				'You hum along to the tune, relaxing into the music and enjoying it. When you look up, 10 minutes has passed and you are in a much better mood.'
			end
		end

		if ml_guitar['hasguitar'] = 1 and (ml_guitar['chordbook'] = 1 or ml_guitarlesson['lessoncount'] > 0) and ml_performance['performed_minutes'] < ml_performance['max_perform_minutes'] and alko < 5:
			if pcs_inhib < 30:
				if pcs_inhib < 10:
					$diff = 'hard'
				elseif pcs_inhib < 20:
					$diff = 'medium'
				else
					$diff = 'easy'
				end
				gs 'willpower', 'skill', 'self', $diff
				will_cost = (will_cost * 3) - (pcs_perform)
				if will_cost <= pcs_willpwr:
					act 'Practice guitar (<<will_cost>> Willpower)':
						inhib_exp += rand(1,2)
						gs 'willpower', 'pay', 'self'
						gs 'stat'
						gt 'music_bedroomPractice', 'guitar'
					end
				else
					act 'Practice guitar (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You feel too embarrassed to pick up your guitar in front of people</font>'
				end
			else
				act 'Practice guitar': gt 'music_bedroomPractice', 'guitar'
			end
		end

		!! Streaming music
		if ml_guitar['hasguitar'] = 1 and ml_online['account'] = 1 and internet > 0 and ml_performance['performed_minutes'] < ml_performance['max_perform_minutes'] and ml_performance['max_perform_minutes'] >= 15 and alko < 5 and ml_streaming['lastday'] ! daystart:
			ml_streamtime =  min((ml_performance['max_perform_minutes']-ml_performance['performed_minutes']), 60)
			if pcs_inhib < 30:
				if pcs_inhib < 10:
					$diff = 'hard'
				elseif pcs_inhib < 20:
					$diff = 'medium'
				else
					$diff = 'easy'
				end
				gs 'willpower', 'skill', 'self', $diff
				will_cost = (will_cost * 3) - (pcs_perform)
				if will_cost <= pcs_willpwr:
					act 'Live stream (<<ml_streamtime>> minutes) (<<will_cost>> Willpower)':
						inhib_exp += rand(1,3)
						gs 'willpower', 'pay', 'self'
						gs 'stat'
						gt 'music_onlinemusic', 'liveStream'
					end
				else
					act 'Live stream (<<ml_streamtime>> minutes) (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
				end
			else
				act 'Live stream (<<ml_streamtime>> minutes)': gt 'music_onlinemusic', 'liveStream'
			end
		end

		!! Recording music
		if ml_guitar['hasguitar'] = 1 and ml_performance['performed_minutes'] < ml_performance['max_perform_minutes'] and alko < 5:
			if pcs_inhib < 20:
				if pcs_inhib < 10:
					$diff = 'medium'
				elseif pcs_inhib < 20:
					$diff = 'easy'
				end
				gs 'willpower', 'skill', 'self', $diff

				if will_cost <= pcs_willpwr:
					act 'Record a song with your phone (<<will_cost>> Willpower)':
						inhib_exp += rand(1,2)
						gs 'willpower', 'pay', 'self'
						gs 'stat'
						gt 'music_onlinemusic', 'recordSong'
					end
					if komp = 1 and webcamera = 1 and ml_studio['scarlet-3rd-gen'] = 1:
						act 'Record and edit a song (<<will_cost>> Willpower)':
							inhib_exp += rand(1,2)
							gs 'willpower', 'pay', 'self'
							gs 'stat'
							gt 'music_onlinemusic', 'recordAndEditSong'
						end
					end
				else
					act 'Record a song (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
					act 'Record and edit a song (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
				end
			else
				act 'Record a song with your phone': gt 'music_onlinemusic', 'recordSong'
				if komp = 1 and webcamera = 1 and ml_studio['scarlet-3rd-gen'] = 1:
					act 'Record and edit a song': gt 'music_onlinemusic', 'recordAndEditSong'
				end
			end
		end
	end

	if sewing_kit = 1:
		'Your <a href="exec:gt ''sewing'',''start''">sewing kit</a> is stored next to your favorite chair.'
		*nl
		if gobelen > 0:
			'You have completed <<gobelen>> tapestries.'
			*nl
		end
		if tkan <= 0:
			'You do not have any fabric.'
		else
			if pcs_sewng < 40:
				'You have <<tkan>> pieces of sewing fabric left.'
			elseif pcs_sewng < 80:
				'You have enough fabric for <<tkan>> lessons.'
			else
				if newgobelen = 0:
					act'Start a new tapestry (00:15)':
						*clr & cla
						menu_off = 1
						tkan -= 1
						newgobelen = 1
						minut += 15
						gs'stat'
						'You spend 15 minutes at the embroidery frame, preparing the fabric and outlining the pattern for the tapestry.'
						act'Set the tapestry aside':gt $curloc
					end
				end
			end
		end	
		if newgobelen >= 1:
			'Your tapestry is <<newgobelen/10>> percent finished.'
			act'Work on the tapestry (01:00)':
				*clr & cla
				menu_off = 1
				minut += 60
				sewng_exp += rand(pcs_intel/10, pcs_intel/5)
				gobramax = (pcs_sewng - 70) * 6
				gobramin = (pcs_sewng - 70) * 2
				newgobelen += rand(gobramin,gobramax)
				gs'stat'
				if newgobelen < 1000:'You spend an hour working on your tapestry, which is now <<newgobelen/10>> percent finished.'
					if newgobelen >= 1000:
					newgobelen = 0
					gobelen += 1
					'Your work on the tapestry is done.'
				end
				act'Set the tapestry aside':gt $curloc
			end
		end
	end

end

--- city_house_res_misc ---------------------------------

