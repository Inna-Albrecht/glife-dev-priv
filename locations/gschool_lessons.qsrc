# gschool_lessons
!2021/05/02

if $ARGS[0] = 'morning':
	*clr & cla
	$menu_loc = 'gschool_lessons'
	$menu_arg = 'morning'
	$location_type = 'public_indoors'
	killvar '$locclass'
	menu_off = 0
	
	if school_check_day ! daystart:
		school_check_day = daystart
		schoolprogul -= 1
		school_lunch = 0
		if missing_class > 9:
			missing_class -= 9
		elseif missing_class > 6:
			missing_class -= 6
		end
		lernSkill += 1
		lernHome += 1
		if SchoolSorev = 1: SchoolSorev = 0
		gs 'gschool_socialchg', 'test'
	end
		
	if $breaktype ! 'first':	
		if detention_set = 1 and week = 1:
			detention_set = 2
			'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big134.jpg"></center>'
			'The principal is waiting for you in the hall, and with a simple curl of a finger, she summons you to follow her to her office.'
			*nl
			gs 'gschool_lessons', 'uniform_check'

			act 'Follow': gt 'gschool_office', 'principal'
		elseif late = 0 and cheerleadingQW['skipped_ev'] + cheerleadingQW['skipped_g_ev'] > 0:
			gs 'gschool_cheerleading', 'skipped_ev'
			act 'Continue down the hall': gt 'gschool_lessons', 'morning'
		elseif week < 4 and class['first_grade_check'] = 0:
			gt 'gschool_lessons', 'grade_check'
		elseif grupTipe = 3 and nerd_game['invite_day'] < daystart - week:
			gt 'nerd_game_night', 'invite', 'morning'
		else
			'<center><b><font color="maroon">School Hallway</font></b></center>'
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/building/gschool_hall0<<Rand(0,2)>>.jpg"></center>'
			'Walking down the halls, you barely notice the poor state of the school anymore, having grown used to it. Approaching your locker, you see several of your classmates who you wave to.'
			gs 'gschool_gossip', 'morning'
			*nl			
			gs 'gschool_lessons', 'uniform_check'
			gs 'gschool_schedule'
			act 'Go to first period': school_period = 0 & gt 'gschool_lessons', 'schedule'
			act 'Skip first period': school_period = 1 & gt 'gschool_lessons', 'skip'

			act 'Use water cooler': $breaktype = 'first' & gt 'gschool_break', 'watercooler'
			act 'Go to the girls restroom': $breaktype = 'first' & gt 'gschool_break', 'girls_restroom'
			act 'Go to the boys restroom': $breaktype = 'first' & gt 'gschool_break', 'boys_restroom'
			if week = 1 and class['grade_check_day'] ! daystart:
				act 'Check you grades': gt 'gschool_lessons', 'grade_check'
			end
		end
	else
		'<center><b><font color="maroon">School Hallway</font></b></center>'
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/building/gschool_hall0<<Rand(0,2)>>.jpg"></center>'
		'Walking down the halls, you barely notice the poor state of the school anymore, having grown used to it. Approaching your locker, you see several of your classmates and wave at them.'
		act 'Go to first period': school_period = 0 & gt 'gschool_lessons', 'schedule'
		act 'Skip first period': school_period = 1 & gt 'gschool_lessons', 'skip'
	end
end

if $ARGS[0] = 'uniform_check':
	if PCloInhibit <= 10:
		grupvalue[3] += 1
		grupvalue[5] += 1
		grupvalue[1] -= 1
		grupvalue[2] -= 1
		'<font color="blue">This uniform is very conservative.</font>'
	elseif PCloSkirt < 5 and PCloThinness < 5 and PCloBra = 0:
		if PCloQuality >= 4:
			grupvalue[1] += 1
			grupvalue[2] += 1
			'<font color="blue">This uniform complies with the regulations and is of good quality.</font>'
		else
			'<font color="blue">This uniform complies with the school regulations on skirt length.</font>'
		end
	elseif PCloSkirt = 5 and PCloThinness < 5 and PCloBra = 0:
		if PCloQuality >= 4:
			grupvalue[1] += 1
			grupvalue[2] += 1
			grupvalue[4] += 1
			'<font color="blue">This uniform slightly breaches the school regulations on skirt length and is of good quality.</font>'
		else
			grupvalue[4] += 1
			'<font color="blue">This uniform slightly breaches the school regulations on skirt length.</font>'
		end
	else
		grupvalue[1] -= 1
		grupvalue[2] -= 1
		grupvalue[3] -= 1
		grupvalue[5] -= 1
		demerit += 5
		gs 'fame', 'pav', 'sex', 1
		'<font color="red">This uniform makes a mockery of the school uniform regulations.</font>'
	end
end

if $ARGS[0] = 'short_break':
	killvar '$breaktype'
	killvar '$locclass'
	menu_off = 1
	gs 'stat'
	if school_period = 1:
		if hour = 6:
			minut += 165-minut
		elseif hour = 7:
			minut += 105-minut
		elseif hour = 8 and minut < 45:
			minut += 45-minut
		elseif hour = 8 and minut < 50:
			'Time to go to your next class.'
		else
			school_period = 2
			gt 'gschool_lessons', 'short_break'
		end
	elseif school_period = 2:
		if hour = 8:
			minut += 95-minut
		elseif hour = 9 and minut < 35:
			minut += 35-minut
		elseif hour = 9 and minut < 55:
			'Time to go to your next class.'
		else
			school_period = 4
			gt 'gschool_lessons', 'short_break'
		end
	elseif school_period = 3:
		if $ARGS[1] = '':
			if hour = 9 and minut < 55:
				minut += 55-minut
				'Time to go to your next class.'
			else
				school_period = 4
				gt 'gschool_lessons', 'short_break'
			end
		else
			if hour = 9 and minut < 46:
				minut += 46-minut
			end
		end
	elseif school_period = 4:
		if hour = 9:
			minut += 100-minut
		elseif hour = 10 and minut < 40:
			minut += 40-minut
		elseif hour = 10 and minut < 45:
			'Time to go to your next class.'
		else
			school_period = 5
			gt 'gschool_lessons', 'short_break'
		end
	elseif school_period = 5:
		if hour = 10:
			minut += 90-minut
		elseif hour = 11 and minut < 30:
			minut += 30-minut
		elseif hour = 12 and minut < 25 and minut > 20:
			school_period = 6
			gt 'gschool_lessons', 'short_break'
		elseif (hour = 12 and minut > 25 ) or hour > 12:
			school_period = 6
			gt 'gschool_lessons', 'short_break'
		end
		if NikoVolkovQW = 10 and NikoEv = 12: school_lunch = 1 & school_period = 6 & gt 'NikoDreams', 'Classroom Dream'
	elseif school_period = 6:
		if $ARGS[1] = '':
			if hour = 10:
				minut += 145-minut
			elseif hour = 11:
				minut += 85-minut
			elseif hour = 12 and minut < 25:
				minut += 25-minut
			else
				school_period = 7
				gt 'gschool_lessons', 'short_break'
			end
		end
	elseif school_period = 7:
		if hour = 11:
			minut += 130-minut
		elseif hour = 12:
			minut += 70-minut
		elseif hour = 13 and minut < 10:
			minut += 10-minut
		elseif hour = 13 and minut < 15:
			'Time to go to your next class.'
		else
			school_period = 8
			gt 'gschool_lessons', 'short_break'
		end
	elseif school_period = 8:
		if hour = 13: minut += 60-minut
	end
	gs 'stat'
	if school_period = 2:
		if $ARGS[1] ! 'nopict':
			'<center><b><font color="maroon">School Hallway</font></b></center>'
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/building/gschool_hall0<<Rand(0,2)>>.jpg"></center>'
			nl
			gs 'gschool_schedule'
		end
		act 'Time for morning break': gt 'gschool_lessons', 'schedule'
	elseif school_period = 5:
		if $ARGS[1] ! 'nopict':
			'<center><b><font color="maroon">School Hallway</font></b></center>'
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/building/gschool_hall0<<Rand(0,2)>>.jpg"></center>'
			nl
			gs 'gschool_schedule'
		end
		act 'Time for lunch break': gt 'gschool_lessons', 'schedule'
	elseif (school_period < 8):
		if $ARGS[1] ! 'nopict':
			'<center><b><font color="maroon">School Hallway</font></b></center>'
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/building/gschool_hall0<<Rand(0,2)>>.jpg"></center>'
			gs 'gschool_gossip', 'gossip'
			nl
			gs 'gschool_schedule'
		end
		
		if christinaphotos > 4: gt 'gschool_events', 'christinaphotos'
		
		act 'Go to next class': gt 'gschool_lessons', 'schedule'

		act 'Skip next class': school_period += 1 & gt 'gschool_lessons', 'skip'
		
		if cheerleadingQW['skipped_ev'] + cheerleadingQW['skipped_g_ev'] > 0:
			gs 'gschool_cheerleading', 'skipped_ev'
			act 'Continue down the hall': gt 'gschool_lessons', 'short_break'
		elseif grupTipe = 3 and nerd_game['invite_day'] < daystart - week:
			gt 'nerd_game_night', 'invite', 'short_break'
		end
	elseif Kuznetsov_punishment = 1:
		'<center><b><font color="maroon">School Hallway</font></b></center>'
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/building/gschool_hall0<<Rand(0,2)>>.jpg"></center>'
		act 'Go to Kuznetsov''s classroom': gt 'gschool_lessonsev2','shop_punishment'
		act 'Leave the school': gt 'gschool_lessons', 'schedule'
	else
		gt 'gschool_lessons', 'schedule'
	end
end

if $ARGS[0] = 'skip':
	*clr & cla
	$menu_loc = 'gschool_lessons'
	$menu_arg = 'skip'
	$location_type = 'public_indoors'
	menu_off = 0
	killvar '$breaktype'
	gs 'stat'
	'<center><b><font color="maroon">School Hallway</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/building/gschool_hall0<<rand(0,2)>>.jpg"></center>'
	'You don''t feel like attending all of your classes this morning. Surely no one will miss you if you skip one of your three morning classes.'

	act 'Go to girl''s bathroom': missing_class += 1 & gt 'gschool_girls_bathroom', 'start'
	act 'Go to boy''s bathroom': missing_class += 1 & gt 'gschool_boys_bathroom', 'start'
!!	act 'Go to old school building': gt
end

if $ARGS[0] = 'schedule':
	killvar '$breaktype'
	gs 'stat'
	if school_period = 0:
		school_period = 1
		if hour >= 8:
			if week = 1: gt 'gschool_lessons4', 'math_late'
			if week = 2: gt 'gschool_lessons3', 'literature_late'
			if week = 3: gt 'gschool_lessons4', 'math_late'
			if week = 4: gt 'gschool_lessons3', 'literature_late'
			if week = 5: gt 'gschool_lessons4', 'math_late'
		else
			if hour = 7: minut += 60 - minut
			if hour = 6: minut += 120 - minut
			if week = 1: gt 'gschool_lessons4', 'math'
			if week = 2: gt 'gschool_lessons3', 'literature'
			if week = 3: gt 'gschool_lessons4', 'math'
			if week = 4: gt 'gschool_lessons3', 'literature'
			if week = 5: gt 'gschool_lessons4', 'math'
		end
	elseif school_period = 1:
		school_period = 2
		if hour = 8 and minut < 50: minut += 50 - minut
		if week = 1: gt 'gschool_lessons1', 'russian'
		if week = 2: gt 'gschool_lessons1', 'geography'
		if week = 3: gt 'gschool_lessons1', 'russian'
		if week = 4: gt 'gschool_lessons1', 'geography'
		if week = 5: gt 'gschool_lessons1', 'russian'
	elseif school_period = 2:
		school_period = 3
		gt 'gschool_lunch', 'break'
	elseif school_period = 3:
		school_period = 4
		if hour = 9 and minut < 55: minut += 55 - minut
		if week = 1: gt 'gschool_lessons4', 'english'
		if week = 2: gt 'gschool_lessons2', 'science'
		if week = 3: gt 'gschool_lessons2', 'biology'
		if week = 4: gt 'gschool_lessons2', 'science'
		if week = 5: gt 'gschool_lessons4', 'english'
	elseif school_period = 4:
		school_period = 5
		if hour = 10 and minut < 45: minut += 45 - minut
		if week = 1: gt 'gschool_lessons3', 'art'
		if week = 2: gt 'gschool_lessons3', 'shop'
		if week = 3: gt 'gschool_lessons1', 'history'
		if week = 4: gt 'gschool_lessons3', 'shop'
		if week = 5: gt 'gschool_lessons3', 'art'
	elseif school_period = 5:
		school_period = 6
		gt 'gschool_lunch', 'lunch'
	elseif school_period = 6:
		school_period = 7
		if hour = 12 and minut < 25: minut += 25 - minut
		if week = 1: gt 'gschool_lessons2', 'biology'
		if week = 2: gt 'gschool_lessons2', 'computer'
		if week = 3: gt 'gschool_lessons2', 'computer'
		if week = 4: gt 'gschool_lessons2', 'computer'
		if week = 5: gt 'gschool_lessons1', 'history'
	elseif school_period = 7:
		school_period = 8
		if hour = 13 and minut < 15: minut += 15 - minut
		if week = 1: gt 'gschool_lessons4', 'pe'
		if week = 2: gt 'gschool_lessons2', 'music'
		if week = 3: gt 'gschool_lessons4', 'pe'
		if week = 4: gt 'gschool_lessons2', 'music'
		if week = 5: gt 'gschool_lessons4', 'pe'
	else
		school_period = 0
		gt 'gschool_grounds', 'main'
	end
end

if $ARGS[0] = 'grade_check':
	*clr & cla
	menu_off = 0
	class['grade_check_day'] = daystart
	'<center><b><font color="maroon">School Hallway</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/school/building/grade_results<<rand(1,2)>>.jpg"></center>'
	if class['first_grade_check'] = 0:
		class['first_grade_check'] = 1
		'You walk to the wall where the grades are posted and check your grades. You see that your final grade from last year is still there. Grades are updated every Monday, but you note your grade down in your journal so you can always check it.'
	else
		'You walk over to the wall where the grades are posted and check your grades to see what has changed since last week. You note your grade down in your journal so you can check it later.'
	end
	*nl
	gs 'grades', 'assing_grade_description', 'school'
	
	'Math: <<$class[''school_math_grade'']>> (<<class[''school_math_grade'']>>) Russian: <<$class[''school_rus_grade'']>> (<<class[''school_rus_grade'']>>) Literature: <<$class[''school_lit_grade'']>> (<<class[''school_lit_grade'']>>) Art: <<$class[''school_art_grade'']>> (<<class[''school_art_grade'']>>) Biology: <<$class[''school_bio_grade'']>> (<<class[''school_bio_grade'']>>) English: <<$class[''school_eng_grade'']>> (<<class[''school_eng_grade'']>>) Geography: <<$class[''school_geo_grade'']>> (<<class[''school_geo_grade'']>>) Science: <<$class[''school_sci_grade'']>> (<<class[''school_sci_grade'']>>) History: <<$class[''school_his_grade'']>> (<<class[''school_his_grade'']>>) Shop: <<$class[''school_shop_grade'']>> (<<class[''school_shop_grade'']>>) Computer: <<$class[''school_comp_grade'']>> (<<class[''school_comp_grade'']>>) Music: <<$class[''school_mus_grade'']>> (<<class[''school_mus_grade'']>>) P.E.: <<$class[''school_pe_grade'']>> (<<class[''school_pe_grade'']>>)' 
	*nl
	'A grade of 1 means "Very Poor" and is generally only used for absence. 2 means "Unsatisfactory" and is also a falling grade. 3 means "Satisfactory" and is a passing grade. 4 means "Good". 5 means "Excellent" and is only given to the best students. (The number in the parenthesis is the variable grade, which is changed by your in-class actions and translated into the actual grade)'
	*nl
	
	gs 'grades', 'grade above', 'school', 90
	gs 'grades', 'grade above', 'school', 70
	gs 'grades', 'grade above', 'school', 50
	gs 'grades', 'grade above', 'school', 40
	
	if class['school_grades_above_90'] = 13:
		'You''re getting top marks in all your classes. You''re one of the best, if not the best student in the entire school.'
	elseif class['school_grades_above_90'] > 6 and class['school_grades_above_40'] = 13: 
		'You''re getting top marks in more than half your classes and aren''t failing any of the others. You''re one of the best students in the school.'
	elseif class['school_grades_above_90'] > 6: 
		'Even if you''re failing some of your classes, you''re getting top marks in more than half of them and are still one of the best students in the school. You should have no trouble getting into university.'
	elseif class['school_grades_above_90'] > 0 and class['school_grades_above_70'] = 13: 
		'You''re only getting top marks in a few of your classes, but the rest of your grades are still good. You should have no trouble getting into university.'
	elseif class['school_grades_above_90'] > 0 and class['school_grades_above_70'] > 10 and class['school_grades_above_40'] =13: 
		'You''re only getting top marks in some of your classes, but only a few of your grades are bad and you''re not failing any classes. You should have no trouble getting into university, but improving your grades wouldn''t hurt.'
	elseif class['school_grades_above_90'] > 0 and class['school_grades_above_70'] > 10: 
		'You''re only getting top marks in a few of your classes and are failing others. You should improve your grades if you want to get into university.'
	elseif class['school_grades_above_90'] > 0 and class['school_grades_above_40'] > 6: 
		'You''re getting top marks in a few of your classes, but are failing a lot of the others. You should improve your grades if you want to get into university.'
	elseif class['school_grades_above_90'] > 0: 
		'Even a top mark or two won''t help when you''re failing more than half of your classes. You''ll have to put a lot of work in if you want to go to university.'
	elseif class['school_grades_above_70'] = 13: 
		'You''re getting good grades in all of your classes, but nothing impressive. You should have no trouble getting into university, but improving your grades wouldn''t hurt.'
	elseif class['school_grades_above_70'] > 6 and class['school_grades_above_40'] =13: 
		'You''re getting good grades in over half of your classes, but nothing impressive. You''re not failing any classes, but improving your grades wouldn''t hurt.'
	elseif class['school_grades_above_70'] > 6 and class['school_grades_above_40'] >10: 
		'You''re getting good grades in over half of your classes, but are failing others. You should improve your grades if you want to get into university.'
	elseif class['school_grades_above_70'] > 0 and class['school_grades_above_40'] = 13: 
		'You''re getting good grades in a few of your classes, but nothing impressive. You''re not failing any classes, but improving your grades wouldn''t hurt.'
	elseif class['school_grades_above_70'] > 0 and class['school_grades_above_40'] >10: 
		'You''re getting good grades in a few of your classes, but are failing others. You should improve your grades if you want to get into university.'
	elseif class['school_grades_above_70'] > 0 and class['school_grades_above_40'] >6: 
		'You''re getting good grades in a few of your classes, but are failing a lot of the others. You should improve your grades if you want to get into university.'
	elseif class['school_grades_above_70'] > 0: 
		'A good grade or two isn''t enough when you''re failing more than half of your classes. It will require a lot of work for you to turn this around.'
	elseif class['school_grades_above_40'] = 13: 
		'You are not getting any good grades, but at least you are not failing any classes. It will require a lot of work if you want to go to university.'
	elseif class['school_grades_above_40']  > 10: 
		'Your grades are terrible and you''re in danger of not graduating high school.'
	elseif class['school_grades_above_40']  > 6: 
		'You''re failing a lot of classes and barely passing the rest. You won''t graduate at this rate unless you get your grades up.'
	else
		'You''re failing high school and need to do something about it right now if you want to graduate.'
	end
	
	act 'Continue': gt 'gschool_lessons', 'morning'
end
--- gschool_lessons ---------------------------------