# kuhrPar
CLOSE ALL
if fullmorrout = 1:
	minut += 2
	act '<font color = maroon><b>Leave the apartment and go outside</b></font>':
		minut +=1
		killvar 'fullmorrout'
		gs 'korrPar'
		gt'pavResidential'
		
	end
end

	act 'Return to the hallway':
		killvar 'fullmorrout'
		gt 'korrPar'
		
	end


$metka = $ARGS[0]
$loc = $CURLOC
clr
minut += 1
gs 'stat'
if night_mode = 1:
	fcolor = rgb(255, 255, 255)
	bcolor = rgb(0, 0, 0)
	lcolor = rgb(106, 90, 205)
else
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)
end
'<center><b><font color="maroon">Kitchen</font></b></center>'
'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/kuhrpar.jpg"></center>'
if hour = 7 and indorf = 0:
	!r125
	*nl '<a href="exec:gt''father''">Your stepfather</a> and <a href="exec:gt ''brother'', ''start''">Kolka</a>  are sitting at the table eating Breakfast. <a href="exec:gt''mother''">Your mother</a> is hard at work to make sure everybody has enough to eat for the rest of the day.'
	! on weekdays Anka here too
	if week <= 5: *P ' <a href="exec:gt''sister''">Anya</a> is trying to lose some weight so she is only having a very light breakfast.'
else
	'The kitchen is not very impressive, but it has everything your family needs. Since everyone has the habit of eating at different times, Mom usually pre-cooks the meals and stores them in the fridge. Of course, you could also just have a snack if you''re not that hungry.'
end
gs 'family'

act 'Eat a meal (0:30)':gs 'food', 'm_meal'

act 'Have a snack (0:15)':gs 'food', 'snack'

act 'Drink some water (0:05)':gs 'beverage', 'bev_wat'

act 'Have a cup of tea (0:05)':
	cls
	minut += 5
	if pcs_hydra >= 100:
		'You don''t need any more tea.'
	else
		pcs_hydra += 80
		cumspclnt = 2
		gs 'cum_cleanup'
		pcs_breath = 0
		pcs_mood += rand(3, 6)
		'You make a refreshing cup of tea and everything seems a little better.'
	end
	
	gs 'stat'

	act 'Get up from the table':gt $loc, $metka
end

if pranik > 0:
	'You have <<pranik>> cookies.'

	act 'Drink tea with cookies (0:05)':
		cla
		*clr
		minut += 5
		pranik -= 1
		pcs_health += 30
		pcs_mood += 20
		pcs_energy += 20
		if pcs_hydra >= 100:
			pcs_hydra += 30
		else
			pcs_hydra += 60
		end
		cumspclnt = 2
		gs 'cum_cleanup'
		pcs_breath = 0
		fat += 4
		frost = 0
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/shared/food/food.jpg"></center>'
		'You drank tea and enjoyed the cookies. You know they''re too sweet and can''t be good for your figure, but they taste so good!'

		act 'Get up from the table':gt $curloc
	end
end

if edaD > 0:
	act 'Eat diet food (0:30)':
		cla
		*clr
		frost = 0
		minut += 30
		pcs_health += 10
		pcs_mood += 20
		edaD -= 1
		'<center><img <<$set_imgh>> src="images/shared/food/food.jpg"></center>'
		if pcs_energy >= 100:
			'You prepare a diet meal and try to eat it, but you''re really not hungry at all. You can''t finish the meal.'
		elseif pcs_energy >= 80:
			pcs_energy += 40
			'You try to enjoy the diet meal, but half-way in you realize you weren''t all that hungry yet. You manage to finish the meal, but feel like it was kind of wasted.'
		else
			pcs_energy += 80
			'The diet meal is tasty, and you enjoy eating it.'
		end

		if pcs_hydra >= 80:
			'You don''t need any more tea.'
		else
			pcs_hydra += 100
			cumspclnt = 2
			gs 'cum_cleanup'
			pcs_breath = 0
			'You have a cup of tea, quenching your thirst.'
		end

		act 'Get up from the table':gt $curloc
	end
end

if fatdel > 0:
	act 'Consume fat burners':
		cla
		*clr
		fatdel -= 1
		if pcs_hydra >= 100:
			pcs_hydra += 25
		else
			pcs_hydra += 50
		end
		salo -= 20
		if salo < 11: salo = 11
		gs 'body_shape', 'softreset'

		'You throw the capsule of fat burners in a glass, add some water and drink it. You''re amazed at how fast the capsule works, feeling your body slim down almost immediately.'

		act 'Get up from the table':gt $curloc
	end
end

if lekarstvo > 0:
	'You have <<lekarstvo>> tablet(s).'

	if sick > 0 and lekarday ! daystart:
		act 'Take a pill (0:05)':
			cla
			*clr
			minut += 5
			lekarday = daystart
			lekarstvo -= 1
			sick -= sick * 20 / 100
			if pcs_hydra >= 100:
				pcs_hydra += 25
			else
				pcs_hydra += 50
			end
			picrand = rand(1,9)
			'<center><img <<$set_imgh>> src="images/locations/shared/apartment/event/sick/lekr<<picrand>>.jpg"></center>'
			'You swallow the pill, and shortly after you feel a bit better.'

			act 'Continue':gt $curloc
		end
	end
end

if vitamin > 0:
	'You have <<vitamin>> vitamin pills.'

	if vitaminday ! daystart:
		act 'Take vitamins (0:05)':
			cla
			*clr

			minut += 5
			vitaminday = daystart
			vitamin -= 1
			frost = 0
			if pcs_hydra >= 100:
				pcs_hydra += 25
			else
				pcs_hydra += 50
			end
			if KandidozOnce = 1:Kandidoz -= 2
			if GerpesOnce = 1:Gerpes -= 2
			if SifacOnce = 1 and Sifilis >= 10:Sifilis -= rand(0, 1)

			pcs_skin += 1

			'You take some vitamins to stay healthy.'

			act 'Continue':gt $curloc
		end
	end
end

--- kuhrPar ---------------------------------

