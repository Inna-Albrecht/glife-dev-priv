# kuhrPar
CLOSE ALL
*clr & cla
if fullmorrout = 1:
	minut += 2
	act '<font color = maroon><b>Leave the apartment and go outside</b></font>':
		minut +=1
		killvar 'fullmorrout'
		gs 'korrPar'
		gt'pavResidential'		
	end
end

act 'Return to the hallway':
	killvar 'fullmorrout'
	gt 'korrPar'	
end

$loc = 'kuhrPar'
$metka = ''
$locclass = 'kitr'
$menu_loc = 'kuhrPar'
$menu_arg = ''
menu_off = 0
minut += 1
gs 'stat'
gs 'kit_din'
gs 'themes', 'indoors'
'<center><b><font color="maroon">Kitchen</font></b></center>'
'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/kuhrpar.jpg"></center>'
if hour = 7 and indorf = 0:
	*nl '<a href="exec:gt''father''">Your stepfather</a> and <a href="exec:gt ''brother'', ''start''">Kolka</a> are sitting at the table eating Breakfast. <a href="exec:gt''mother''">Your mother</a> is hard at work to make sure everybody has enough to eat for the rest of the day.'
	if week <= 5: *P ' <a href="exec:gt ''sister_chat'', ''sister_chat''">Anya</a> is trying to lose some weight so she is only having a very light breakfast.'
else
	'The kitchen is not very impressive, but it has everything your family needs. Since everyone has the habit of eating at different times, <<$npc_nickname[''A29'']>> usually pre-cooks the meals and stores them in the fridge. Of course, you could also just have a snack if you''re not that hungry.'
end
gs 'family'

if week < 6 and hour = 7 and breakfast_day ! daystart: 
	act 'Eat breakfast with your family (0:20)': 
		*clr & cla
		frost = 0
		minut += 20
		pcs_health += 10
		pcs_mood += 10
		fat += 6
		pcs_energy += 60
		if pcs_hydra >= 100:
			pcs_hydra += 30
		else
			pcs_hydra += 60
		end
		cumspclnt = 2
		gs 'cum_cleanup'
		pcs_breath = 0
		npc_rel['A29'] += 1
		breakfast_day = daystart
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/shared/food/breakfast_'+rand(0,1)+'.jpg"></center>'
		'You thoroughly enjoy your breakfast with your family.'

		act 'Get up from the table': gt $loc, $metka
	end
else
	act 'Eat a full meal (0:30)':gs 'food', 'm_meal'
	act 'Eat a light meal (0:25)':gs 'food', 's_meal'
	act 'Have a snack (0:15)':gs 'food', 'snack'
end

dynamic $sandwich
dynamic $driwater
dynamic $dritea
dynamic $fill_bottle
dynamic $edaD
dynamic $pranik
dynamic $fatdel
dynamic $lekarstvo
dynamic $vitamin

--- kuhrPar ---------------------------------

