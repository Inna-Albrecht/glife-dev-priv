# sleep

if $ARGS[0] = 'simple':
!! to avoid sleeping the whole day I decide to calculate the duration of the sleep in advance
!! the quality of the sleep depends on many factors, so the time of sleep can be modify later, especially the part when the time of sleep is shortened

	varsleep['time_to_full'] = (100 - pcs_sleep) * 6
!! fully exhausted Sveta can recover in 420 minutes = 7 hours of sleep
!! healthy Sveta can hardly sleep more then 10 hours = 600 minutes
!! so we can add about 3 hours to time to full as a limit
	varsleep['time_to_full'] += 150 + rand(0,60)

	varsleep['time_now'] = daystart*1440 + hour * 60 + minut
	varsleep['alarm_time_end'] = daystart*1440 + timerEnd * 60 + timerEndM
	if varsleep['time_now'] >= varsleep['alarm_time_end']: varsleep['alarm_time_end'] += 1440
	varsleep['alarm_time'] = daystart*1440 + timer * 60 + timerM
	if varsleep['time_now'] >= varsleep['alarm_time']: varsleep['alarm_time'] += 1440
	if budilnikOn = 1: 
		varsleep['minutes to wakeup'] = min(varsleep['time_to_full'], varsleep['alarm_time'] - varsleep['time_now'])
		varsleep['minutes to wakeup end'] = min(varsleep['time_to_full'], varsleep['alarm_time_end'] - varsleep['time_now'])
	else 
		varsleep['minutes to wakeup'] = varsleep['time_to_full']
		varsleep['minutes to wakeup end'] = varsleep['time_to_full']
	end

	InSleep = 1
	:loopson_sleep
	minut += 1
	stime += 1
	if recuperation = 0:pcs_health += 5
	
	if stime >= 60:
		stime = 0
		pcs_sleep += 15
		gs 'stat'
	end
	
	varsleep['minutes to wakeup'] -= 1
	varsleep['minutes to wakeup end'] -= 1

	if alarm_holiday = 1 and kanikuli > 0:
	!! use weekend time alarm
		varsleep['alarm_time'] = daystart*1440 + timerEnd * 60 + timerEndM
		if varsleep['minutes to wakeup end'] > 0:jump 'loopson_sleep'
	elseif week > 5:
	!! use weeken time alarm 
		varsleep['alarm_time'] = daystart*1440 + timerEnd * 60 + timerEndM
		if varsleep['minutes to wakeup end'] > 0:jump 'loopson_sleep'
	else 
		varsleep['alarm_time'] = daystart*1440 + timer * 60 + timerM
		if varsleep['minutes to wakeup'] > 0:jump 'loopson_sleep'
	end
	varsleep['time_now'] = daystart*1440 + hour * 60 + minut
	InSleep = 0
	gs'stat'
end

if $ARGS[0] = 'full':
	if succubusQW = 2:
		act '<<$sleep_txt>>':
			if clo_flag['bed'] = 0:
				clo_flag['bed'] = 1
				$lastwornclothingtype['bed'] = $clothingworntype
				lastwornclothingnumber['bed'] = clothingwornnumber
				$lastwornpantytype['bed'] = $pantyworntype
				lastwornpantynumber['bed'] = pantywornnumber
				$lastwornbratype['bed'] = $braworntype
				lastwornbranumber['bed'] = brawornnumber
			end
			menu_off = 1
			if bedPanty = 2:
				gs 'underwear', 'remove'
			elseif bedPanty = 1:
				gs 'underwear', 'wear'
			end
			if $clothingworntype ! 'nude':
				strip_here = 1
				gs 'clothing', 'strip'
			end
			gt 'sleep', 'succhoice'
		end
	else
		act '<<$sleep_txt>>':
			if clo_flag['bed'] = 0:
				clo_flag['bed'] = 1
				$lastwornclothingtype['bed'] = $clothingworntype
				lastwornclothingnumber['bed'] = clothingwornnumber
				$lastwornpantytype['bed'] = $pantyworntype
				lastwornpantynumber['bed'] = pantywornnumber
				$lastwornbratype['bed'] = $braworntype
				lastwornbranumber['bed'] = brawornnumber
			end
			menu_off = 1
			if bedPanty = 2:
				gs 'underwear', 'remove'
			elseif bedPanty = 1:
				gs 'underwear', 'wear'
			end
			if disable_autosave = 0:
				if mid($start_type,1,4) = 'city':
					savegame 'autosave_city.sav'
				elseif mid($start_type,1,3) = 'uni':
					savegame 'autosave_uni.sav'
				else
					savegame 'autosave_sg.sav'
				end
			end
			if $clothingworntype ! 'nude':
				strip_here = 1
				gs 'clothing', 'strip'
			end
			if magf2bdo = 2:
				gt 'sleep', 'magbstchoice'
			else
				gt 'sleep', 'sleep'
			end
		end
		killvar '$sleep_txt'
	end
end


if $ARGS[0] = 'nap_bed':
	act 'Take a nap (1:00)':
		if pcs_sleep >= 10 and pcs_sleep <= 90:
			cla
			pcs_sleep += 10
			pcs_health += 5
			minut += 60
			gs 'stat'
			*nl
			'You sleep about an hour.'
			act 'Get up': gt $loc, $loc_arg
		elseif pcs_sleep < 10:
			gt 'sleep', 'sleep'
		else
			cla
			minut += 5
			gs 'stat'
			*nl
			'You are not tired enough to sleep, even for a short nap.'
			act 'Get up': gt $loc, $loc_arg
		end
	end
	gs 'events', 'read'
end

if $ARGS[0] = 'sleep':
	*clr & cla
	menu_off = 1
	if $clothingworntype ! 'nude':
		'<center><img <<$set_imgh>> src="images/shared/home/bed.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/shared/home/bedroom/son.jpg"></center>'
	end
	gs 'stat'
	*nl
	if budilnikOn = 1 and ((hour => timer and week + 1 < 6) or (hour < timer and week < 6)):
		'You run through your regular nightly routine, setting your alarm for <b>'+iif(timer < 10,'0<<timer>>','<<timer>>')+':'+iif(timerM < 10,'0<<timerM>>','<<timerM>>')+'</b>, you undress, and prepare for sleep:'
	elseif budilnikOn = 1 and alarm_holiday = 1 and (week > 5 or kanikuli > 0):
		'You run through your regular nightly routine, setting your alarm for <b>'+iif(timerEnd < 10,'0<<timerEnd>>','<<timerEnd>>')+':'+iif(timerEndM < 10,'0<<timerEndM>>','<<timerEndM>>')+'</b>, you undress, and prepare for sleep:'
	elseif budilnikOn = 1 and (week > 5):
		'You run through your regular nightly routine, setting your alarm for <b>'+iif(timerEnd < 10,'0<<timerEnd>>','<<timerEnd>>')+':'+iif(timerEndM < 10,'0<<timerEndM>>','<<timerEndM>>')+'</b>, you undress, and prepare for sleep:'
	elseif budilnikOn = 1:
		'You run through your regular nightly routine, setting your alarm for <b>'+iif(timer < 10,'0<<timer>>','<<timer>>')+':'+iif(timerM < 10,'0<<timerM>>','<<timerM>>')+'</b>, you undress, and prepare for sleep:'
	else
		'You run through your regular nightly routine, not bothering to setting your alarm, you undress, and prepare for sleep:'
	end
	pcs_hairbsh = 0
	pain['killer'] = 0
	if mid($start_type,1,2) = 'sg' and $loc ! 'bedrPar' and SchoolAtestat = 0:
		notathomesleep += 1
	else
		notathomesleep = 0
	end


	if succubusflag = 1 and sucwalkday <= daystart and (succhungry < (0 - 1 - succublvl) or pcs_willpwr < ((succublvl * willpowermax) / 13) or sucpowzeroed >= 3): gs 'succubus', 'sucwalkinginit'

	if analplugIN = 1 and vibratorIN = 1:
		if bedAnal = 1 and bedVibrator = 1:
			analplugIN = 0
			vibratorIN = 0
			gs 'stat'
			*nl
			'You remove your butt plug and your vibrator.'
		elseif bedAnal = 0 and bedVibrator = 0:
			*nl
			'You decide to leave your butt plug and vibrator in.'
		elseif bedAnal = 1 and bedVibrator = 0:
			vibratorIN = 0
			gs 'stat'
			*nl
			'You decide to leave your butt plug in, but remove your vibrator.'
		elseif bedAnal = 0 and bedVibrator = 1:
			analplugIN = 0
			gs 'stat'
			*nl
			'You decide to remove your butt plug, but leave your vibrator in.'
		end
	elseif analplugIN = 1 and vibratorIN = 0:
		if bedAnal = 1:
			analplugIN = 0
			gs 'stat'
			*nl
			'You remove your butt plug.'
		elseif bedAnal = 0:
			*nl
			'You decide to leave your butt plug in.'
		end
	elseif analplugIN = 0 and vibratorIN = 1:
		if bedVibrator = 1:
			vibratorIN = 0
			*nl
			'You remove your vibrator.'
		elseif bedVibrator = 0:
			*nl
			'You decide to leave your vibrator in.'
		end
	end
	if $shoeworntype = 'none':
		$bed['shoetype'] = 'none'
	else
		$bed['shoetype'] = $shoeworntype
		bed['shoenumber'] = shoewornnumber
!!		gs 'shoes', 'strip'
	end
	if bedPanty = 1:
		if $pantyworntype = 'none' and $braworntype = 'none':
			*nl
			'You put on your bra and panties.'
		elseif $pantyworntype ! 'none' and $braworntype = 'none':
			*nl
			'You put on your bra.'
		elseif $pantyworntype ! 'none' and $braworntype ! 'none':
			*nl
			'You put on your panties.'
		else
			*nl
			'You change your bra and panties.'
		end
	elseif bedPanty = 2:
		if $pantyworntype ! 'none' or $braworntype ! 'none':
			*nl
			'You remove your bra and panties.'
		elseif $pantyworntype ! 'none' and $braworntype = 'none':
			*nl
			'You remove your panties.'
		elseif $braworntype ! 'none':
			*nl
			'You remove your bra.'
		end
	else
		*nl
		'You were too tired to worry about your underwear, so for tonight you left it as is.'
	end

	if $hairstyle ! '' :
		*nl
		$hairstyle
	else
		*nl
		'Your hair is loose and free.'
	end

	if pcs_makeup > 1 and cosmetic_tattoo = 0:
		pcs_makeup = 0
		vidageday -= 1
		gs 'body', 'SkinLoss', 'MakeUpSleep'
		*nl
		'You went to bed wearing your makeup, which will smear your face as you sleep; your skin will age faster from this!'
	end
	if fat > 5 and stringimplant = 1: silicone += 1 & fat -= 5
	act 'Sleep...':
	!!flag to stop autosave breaking my awesome clothing thingy
		clo_flag['bed'] = 0
		gt 'sleep', 'dream'
	end
end

if $ARGS[0] = 'dream':

	if $loc = 'nichBedroomServant':
		gs 'nichBedroomServant', 'sleepEvents'
	end

	*clr & cla
	menu_off = 1
	InSleep = 1
	gs 'stat'
	gs 'dreams', 'start'
	wake_up = 0
!! to avoid sleeping the whole day I decide to calcululate the duration of the sleep in advance
!! the quality of the sleep depends on many factors, so the time of sleep can be modify later, especially the part when the time of sleep is shortened

	varsleep['time_to_full'] = (100 - pcs_sleep) * 6
!! fully exhausted Sveta can recover in 420 minutes = 7 hours of sleep
!! healthy Sveta can hardly sleep more then 10 hours = 600 minutes
!! so we can add about 3 hours to time to full as a limit
	varsleep['time_to_full'] += 150 + rand(0,60)

	varsleep['time_now'] = daystart*1440 + hour * 60 + minut
	varsleep['alarm_time_end'] = daystart*1440 + timerEnd * 60 + timerEndM
	if varsleep['time_now'] >= varsleep['alarm_time_end']: varsleep['alarm_time_end'] += 1440
	varsleep['alarm_time'] = daystart*1440 + timer * 60 + timerM
	if varsleep['time_now'] >= varsleep['alarm_time']: varsleep['alarm_time'] += 1440
	if budilnikOn = 1: 
		varsleep['minutes to wakeup'] = min(varsleep['time_to_full'], varsleep['alarm_time'] - varsleep['time_now'])
		varsleep['minutes to wakeup end'] = min(varsleep['time_to_full'], varsleep['alarm_time_end'] - varsleep['time_now'])
	else 
		varsleep['minutes to wakeup'] = varsleep['time_to_full']
		varsleep['minutes to wakeup end'] = varsleep['time_to_full']
	end
	:loopson_bed
		minut += 1
		stime += 1
		if recuperation = 0:pcs_health += 5

		if vibratorIN = 1:
			vtime += 1
			if vtime >= 5:
				pcs_horny +=1
				vtime = 0
			end

			if pcs_horny = 100:
				*clr & cla
				pcs_horny = 0
				orgasm += 1
				minut += 5
				gs 'stat'
				'<center><img <<$set_imgh>> src="images/shared/sex/oface/o_face.jpg"></center>'
				*nl
				'You abruptly wake up in the throes of an orgasm. As you recover, you check the time, <b><<$mid(100+hour,2,2)>>:<<$mid(100+minut,2,2)>></b>.'

				act 'Go back to sleep':
					*clr & cla
					gt 'sleep', 'dream'
				end

				act 'Give up trying to sleep':
					*clr & cla
					'<center><img <<$set_imgh>> src="images/shared/sex/oface/kotovfirst1.jpg"></center>'
					*nl
					'Maybe trying to sleep with a vibrator in your pussy wasn''t such a good idea.'
					gs 'sleep', 'wake'
				end

				exit
			end
		!! Exclusions for succubus dreams as they set horny to 100 and will always get overridden otherwise
		elseif pcs_horny > 80 and pcs_sleep >= 30 and rand (1,300) = 300 and succubusQW ! 1 and succubusQW ! 2:
			gs 'stat'
			$waketext = 'You glance at your alarm clock. It reads <b><<$mid(100+hour,2,2)>>:<<$mid(100+minut,2,2)>></b>.'
			gt 'sleep', 'wake_horny'
		end

		if stime >= 60:
			stime = 0
			pcs_sleep += 15
			if pcs_sleep >= 100:
				pcs_condition['lack_of_sleep'] = 0
			elseif pcs_condition['lack_of_sleep'] > 0:
				pcs_condition['lack_of_sleep'] -= 1
			end
			gs 'stat'
		end

		
		varsleep['minutes to wakeup'] -= 1
		varsleep['minutes to wakeup end'] -= 1

		if alarm_holiday = 1 and kanikuli > 0:
		!! use weekend time alarm
			varsleep['alarm_time'] = daystart*1440 + timerEnd * 60 + timerEndM
			if varsleep['minutes to wakeup end'] = 0: wake_up = 1
		elseif week > 5:
		!! use weeken time alarm 
			varsleep['alarm_time'] = daystart*1440 + timerEnd * 60 + timerEndM
			if varsleep['minutes to wakeup end'] = 0: wake_up = 1
		else 
			varsleep['alarm_time'] = daystart*1440 + timer * 60 + timerM
			if varsleep['minutes to wakeup'] = 0: wake_up = 1
		end

		varsleep['time_now'] = daystart*1440 + hour * 60 + minut

	!! Ensuring single exit point instead of multiple ones.
	if wake_up = 0: jump 'loopson_bed'

	if stime < 300:
	!!Worsening of skin quality if you do not sleep enough.
		skinDailyPenalty += 2
	elseif stime < 360:
		skinDailyPenalty += 1
	elseif stime < 420:
		skinDailyPenalty += 0
	else
		skinDailyPenalty -= 1
	end

	gs 'stat'
	if slept_in = 1:
		$waketext = 'You wake up at <b><<$mid(100+hour,2,2)>>:<<$mid(100+minut,2,2)>></b>, after sleeping-in a little longer than planned, but at least you''ve had plenty of sleep.'
	elseif pcs_sleep >= 100 and budilnikOn = 1 and varsleep['time_now'] >= varsleep['alarm_time']:
		$waketext = 'You wake up at <b><<$mid(100+hour,2,2)>>:<<$mid(100+minut,2,2)>></b>, just before your alarm goes off. You''ve had plenty of sleep.'
	elseif pcs_sleep >= 100:
		$waketext = 'You wake up at <b><<$mid(100+hour,2,2)>>:<<$mid(100+minut,2,2)>></b>, no longer tired and ready to start the day with plenty of sleep.'
	elseif budilnikOn = 1 and varsleep['time_now'] = varsleep['alarm_time']:
		pcs_mood -= 10
		$waketext = 'Your alarm goes off at <b><<$mid(100+hour,2,2)>>:<<$mid(100+minut,2,2)>></b>, '+iif(pcs_sleep < 90, 'but you could do with some extra sleep.', 'and you''ve had plenty of sleep.')+''
		gs 'sleep', 'extra'
	else
		!! I do not think that it could go here
		$waketext = ''
	end

	gs 'sleep', 'wake'
	*nl
	'<<$waketext>>'

	killvar 'wake_up'
end

if $ARGS[0] = 'wake_horny':
	cla
	menu_off = 1
	InSleep = 1
	SleepHorny += 1
	gs 'stat'
	if $pantyworntype = 'none':
		if SleepHorny = 1:
			'<center><img <<$set_imgh>> src="images/shared/home/sleep_horny.jpg"></center>'
		elseif SleepHorny = 2:
			'<center><img <<$set_imgh>> src="images/shared/sex/mast/klit.jpg"></center>'
		elseif SleepHorny >= 3:
			'<center><img <<$set_imgh>> src="images/shared/sex/oface/kotovfirst1.jpg"></center>'
		end
	else
		if SleepHorny = 1:
			'<center><img <<$set_imgh>> src="images/shared/sex/mast/start.jpg"></center>'
		elseif SleepHorny = 2:
			'<center><img <<$set_imgh>> src="images/shared/sex/undress/kiska.jpg"></center>'
		elseif SleepHorny >= 3:
			'<center><img <<$set_imgh>> src="images/shared/sex/oface/kotovfirst1.jpg"></center>'
		end
	end
	*nl
	'<<$waketext>>'
	*nl
	if SleepHorny = 1:
		'Your wet dream and overall horniness rouse you from your sleep. From one moment to the next, you are wide awake and very much aware that your pussy is hot and wet.'
		*nl
		'You could try and go back to sleep, of course, but it''s doubtful that you would find this kind of sleep very restful; not to mention the frustration that comes with ignoring your needs, but masturbating is an option, too, and not the worst one in this situation. It might take some time, but you''d be able to sleep like a baby after.'
		*nl
		'Of course, you could always just give up on trying to sleep and just get up.'
	elseif SleepHorny = 2:
		'You''re so horny that you''ve woken a second time. Your pussy is still wet and wanting attention.'
		*nl
		'You could try and get back to sleep again, but it''s doubtful that you will get much sleep, and continuously ignoring your needs won''t help; maybe you should masturbate, and make the most of the time that''s left.'
		*nl
		'Soon you will have to get up one way or another.'
	elseif SleepHorny >= 3:
		'Once again your horniness has roused you from your sleep. This is getting ridiculous! You are finding it very difficult to get any sleep at all.'
		*nl
		'Going back to sleep almost feels like a waste of time at this point;  you won''t get much rest at all, at least until you fix the problem.'
		*nl
		if $pantyworntype ! 'none':
			gs 'underwear', 'remove'
			'You''ve removed  your underwear, as your panties were soaked through.'
			*nl
		end
		'Maybe you should just get up.'
	end
	if $loc = 'bedrPar' and sisterLesb = 6 and hour < 6:
		*nl
		'Or... you could wake up Anya and see if your dearest sister would be willing to help you with that.'
	elseif $loc = 'FedorEv4':
		*nl
		'Or... you could wake up Fedor and see if your boyfriend can''t help you with that.'
	elseif $loc = 'bedr' and husband > 0:
		*nl
		'Or... you could wake up <<$husName>> and see if your husband can''t help you with that.'
	end

	act 'Try and go back to sleep':
		pcs_mood -= 10
		pcs_sleep -= 5
		gt 'sleep', 'dream'
	end

	act 'Masturbate': InSleep = 0 & gt 'selfplay', 'start'

	act 'Get out of bed (0:10)':
		minut += 10
		gs 'sleep', 'end'
	end
	if strip_here = 1:
		act 'Get out of bed and get dressed (0:15)':
			minut += 15
			gs 'clothing', 'wear', $lastwornclothingtype['bed'], lastwornclothingnumber['bed']
			gs 'panties', 'wear', $lastwornpantytype['bed'], lastwornpantynumber['bed']
			gs 'bras', 'wear', $lastwornbratype['bed'], lastwornbranumber['bed']
			if bed['shoetype'] ! 'none':
				gs 'shoes', 'wear', $bed['shoetype'], bed['shoenumber']
			end
			gs 'sleep', 'end'
		end
	end

	if $loc = 'bedrPar' and sisterLesb = 6 and hour < 6:
		act 'Wake Anya and see if she''s in the mood': strip_here = 0 & InSleep = 0 & gt 'sistersleep', 'sister_sleep'
	elseif $loc = 'FedorEv4':
		act 'Wake Fedor and see if he''s in the mood': strip_here = 0 & InSleep = 0 & gt 'FedorEv4', 'Bedroom 1'
		!!Intro text doesn''t quite fit event (but doesn''t quite fit - try for baby - scenario either) - custom scene from VK?
	elseif ($loc = 'bedr' or $loc = 'bedr2x') and husband > 0:
		act 'Wake your husband and see if he''s in the mood': strip_here = 0 & InSleep = 0 & gt 'husbSex', 'variant'
		!!Should be custom scene - maybe boyfriend/husband overhaul can cover this?
	end
end

if $ARGS[0] = 'wake':
	if mid($start_type,1,2) = 'sg' and locat['Fam_inGad'] = 0 and motherQW['bathroom_dildos'] > 0 and MarishaQW["marisha_sleepover"] = 0:gt'mother_sextalk','dildo_wakeup1'
	menu_off = 1
	InSleep = 1
	gs 'stat'
	if MarishaQW["marisha_sleepover"] = 1: gs 'marisha_ev', 'morning'
	if daystart < 10 and $start_type[1] = 'tg':'The first thing you think as you wake up is, "Damn! It''s is not a dream, I really am a woman now."'
	if daystart >= 10 and daystart < 30 and $start_type[1] = 'tg':'Looking at your female body, you are still a little surprised it''s actually you.'
	if slept_in = 1 and kanikuli = 0 and mid($start_type,1,2) = 'sg' and SchoolAtestat = 0 and SchoolBlock = 0 and week < 6 and hour >= 9 and hour < 14 and suspended['on'] ! 1:
		if schoolprogul > 2 and schoolPredupr = 2:
			pcs_mood -= 35
			gs 'stat'
			msg '<center>Damn it''s <b><<$mid(100+hour,2,2)>>:<<$mid(100+minut,2,2)>></b>. It''s too late to go to school now!<br><<$npc_nickname[''A29'']>> will be furious. I don''t know what she might do...</center>'
		elseif schoolprogul > 2 and schoolPredupr = 1:
			pcs_mood -= 30
			gs 'stat'
			msg '<center>Damn it''s <b><<$mid(100+hour,2,2)>>:<<$mid(100+minut,2,2)>></b>. It''s too late to go to school now!<br><<$npc_nickname[''A29'']>> will be very angry if I miss another day of school.</center>'
		elseif schoolprogul > 2 and schoolPredupr = 0:
			pcs_mood -= 25
			gs 'stat'
			msg '<center>Damn it''s <b><<$mid(100+hour,2,2)>>:<<$mid(100+minut,2,2)>></b>. It''s too late to go to school now!<br><<$npc_nickname[''A29'']>> will be angry to find out I missed a day of school.</center>'
		else
			pcs_mood -= 20
			gs 'stat'
			msg '<center>Damn it''s <b><<$mid(100+hour,2,2)>>:<<$mid(100+minut,2,2)>></b>. It''s too late to go to school now!<br><<$npc_nickname[''A29'']>> will be very upset if I''m not at school every day.</center>'
		end
	end
	if kanikuli = 0 and mid($start_type,1,2) = 'sg' and SchoolAtestat = 0 and SchoolBlock = 0 and week < 6 and hour >= 4 and hour < 9:
		if slept_in = 1 and (hour = 7 or hour = 8) and suspended['on'] ! 1:
			pcs_mood -= 10
			gs 'stat'
			msg '<center>Damn it''s <b><<$mid(100+hour,2,2)>>:<<$mid(100+minut,2,2)>></b>, you''ve slept too long! You need to hurry or you''re going to be late for school!</center>'
		end
		act 'Get out of bed and get dressed for school (0:15)':
			minut += 15
			gs 'wardrobe', 'school_outfit'
			gs 'sleep', 'end'
		end
	end

	if strip_here = 1:
		act 'Get out of bed and get dressed (0:15)':
			minut += 15
			gs 'clothing', 'wear', $lastwornclothingtype['bed'], lastwornclothingnumber['bed']
			gs 'panties', 'wear', $lastwornpantytype['bed'], lastwornpantynumber['bed']
			gs 'bras', 'wear', $lastwornbratype['bed'], lastwornbranumber['bed']
			if bed['shoetype'] ! 'none':
				gs 'shoes', 'wear', $bed['shoetype'], bed['shoenumber']
			end
			gs 'sleep', 'end'
		end
	end
	if enfullmorrout = 1 and hour >= 4 and hour < 12:
		act 'Full morning routine (01:00)':
			minut += 15
			fullmorrout = 1
			gs 'clothing', 'wear', $lastwornclothingtype['bed'], lastwornclothingnumber['bed']
			gs 'panties', 'wear', $lastwornpantytype['bed'], lastwornpantynumber['bed']
			gs 'bras', 'wear', $lastwornbratype['bed'], lastwornbranumber['bed']
			if bed['shoetype'] ! 'none':
				gs 'shoes', 'wear', $bed['shoetype'], bed['shoenumber']
			end
			gs 'sleep', 'end'
		end
		if kanikuli = 0 and mid($start_type,1,2) = 'sg' and SchoolAtestat = 0 and SchoolBlock = 0 and week < 6 and hour >= 4 and hour < 9 and suspended['on'] ! 1:
			act 'Full morning routine for School (01:00)':
				fullmorrout = 1
				minut += 15
				gs 'wardrobe', 'school_outfit'
				gs 'sleep', 'end'
			end
		end
	end

	act 'Get out of bed (0:10)':
		minut += 10
		slept_in = 0
		gs 'sleep', 'end'
	end
end

if $ARGS[0] = 'extra':
	act '"Snooze" a few minutes more...':
		menu_off = 1
		slept_in = 1
		gs 'stat'
		gt 'sleep', 'dream'
	end
end

if $ARGS[0] = 'end':
	menu_off = 1
	minut -= 1
	InSleep = 0
	slept_in = 0
	strip_here = 0
	SleepHorny = 0
	if fullmorrout = 1 and $loc = 'bedrPar':
		gt 'vanrPar'
	elseif fullmorrout = 1 and $loc = 'bedr':
		gt 'vanr'
	else
		gt $loc, $loc_arg
	end
end

if $ARGS[0] = 'succhoice':
	menu_off = 1
	minut += 15
	gs 'stat'
	cla
	if $clothingworntype ! 'nude':
		'<center><img <<$set_imgh>> src="images/shared/home/bed.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/shared/home/bedroom/son.jpg"></center>'
	end
	''
	'You toss and turn for a while, but the energy that has been buzzing under your skin since that dream just won''t let you sleep.'
	''
	'Thinking about it you realize that you could push the energy out or pull it in!'
	''
	'<font color="red"><b>You also feel that both are <i>permanent</i> choices.</b></font>'
	act 'Try to ignore it and go to sleep':
		if disable_autosave = 0:
			if mid($start_type,1,4) = 'city':
				savegame 'autosave_city.sav'
			elseif mid($start_type,1,3) = 'uni':
				savegame 'autosave_uni.sav'
			else
				savegame 'autosave_sg.sav'
			end
		end
		gt 'sleep', 'sleep'
	end
	act 'Push the Energy out for all time': gt 'sleep', 'succhoiceNO'
	act 'Pull the Energy in and let it change you permanently': gt 'sleep', 'succhoiceYES'
end

if $ARGS[0] = 'succhoiceNO':
	cla
	menu_off = 1
	if $clothingworntype ! 'nude':
		'<center><img <<$set_imgh>> src="images/shared/home/bed.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/shared/home/bedroom/son.jpg"></center>'
	end
	''
	'You push the energy out of you and it dissipates.'
	succubusQW = 3
	pcs_horny = 0
	minut +=5
	gs 'stat'
	act 'Go to sleep':
		if disable_autosave = 0:
			if mid($start_type,1,4) = 'city':
				savegame 'autosave_city.sav'
			elseif mid($start_type,1,3) = 'uni':
				savegame 'autosave_uni.sav'
			else
				savegame 'autosave_sg.sav'
			end
		end
		gt 'sleep', 'sleep'
	end
end

if $ARGS[0] = 'succhoiceYES':
	cla
	menu_off = 1
	if $clothingworntype ! 'nude':
		'<center><img <<$set_imgh>> src="images/shared/home/bed.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/shared/home/bedroom/son.jpg"></center>'
	end
	succubusQW = 4
	''
	'You pull the energy deeper into yourself and hold it there.'
	''
	'Then you feel pressure building...'
	wait 450
	minut += 5
	gs 'stat'
	'BUILDING...'
	wait 450
	minut += 5
	gs 'stat'
	'<b>BUILDING...</b>'
	wait 450
	minut += 5
	gs 'stat'
	'Power bursts through your body causing you to hop out of bed.'
	act 'Continue': gt 'sleep', 'succubinit'
end

if $ARGS[0] = 'succubinit':
	*clr & cla
	menu_off = 1
	minut += 10
	gs 'stat'
	!!Sets the succubus flag, succubus level (succublvl), Initial lesbian sex count (suclezsnapshot), initial Succubus hunger (succhungry),
	!! & the succubus descriptor ($sucself1). The -2 on succhungry is to give th PC 2 days to get in the Succubus grove.
	succubusflag = 1
	succublvl = 1
	suclezsnapshot = lesbian
	succhungry = -2
	sucwalkday = daystart + 2 + rand(0,5)
	$sucself1 = 'that feeling'
	pcs_horny = 100
	InSleep = 0
	slept_in = 0
	strip_here = 0
	'<center><img <<$set_imgh>> src="images/pc/body/succubusself.jpg"></center>'
	*nl
	'<font color="red">You feel the power flowing around your body for several minutes, leaking through your skin and making changes all throughout you, before fading to a presence deep in your core leaving you looking the same as before... but feeling so different! </font>'

	if preg = 1:
		gs 'medical_din', 'remove_preg'
		RecovH = 0		
	end

	act 'Finish': gt $loc, $loc_arg
end

if $ARGS[0] = 'magbstchoice':
	*clr & cla
	menu_off = 1
	tittmp = tits + 1
	'<center><img <<$set_imgh>> src="images/pc/body/tits/t<<tittmp>>.jpg"></center>'
	killvar 'tittmp'
	'You have an odd dream where you are looking at your naked chest in a mirror and your breasts seem larger...'
	'As the dream starts to drift away, you find yourself gazing at the reflection of your breasts and think:'
	act 'Nice, I wish my breasts really would grow.':
		magf2bdo = 1
		magtarcup += 1
		killvar 'mgf2bnocnt'
		gt 'sleep', 'sleep'
	end
	act 'Eh, I don''t like them like that.': magf2bdo = 0 & mgf2bnocnt += 1 & gt 'sleep', 'dream'
end

--- sleep ---------------------------------
