# bedr
*clr & cla
menu_off = 0
minut += 1
elektro += 1
gs 'shortgs', 'setloc', 'bedr', $ARGS[0]
$locclass = 'bedr'
$location_type = 'private'
gs 'themes', 'indoors'
gs 'stat'

if pcs_magik >= 6 and spellbefshild = 1 and tobiQW = 0:gt 'tobiQW', 'start'
'<center><b><font color="maroon">Bedroom</font></b></center>'
if rembedr = 0:
	'<center><img <<$set_imgh>> src="images/locations/city/residential/apartment/bedr.jpg"></center>'
	*nl
	'A tight, little room with a <a href="exec:gt ''bed'', ''start''">double bed</a>.'
	*nl
	
	'Your <a href="exec:gt ''budilnik'', ''start''">alarm clock</a> is set to <b>'+iif(timer < 10,'0<<timer>>','<<timer>>')+':'+iif(timerM < 10,'0<<timerM>>','<<timerM>>')+'</b> during the week and <b>'+iif(timerEnd < 10,'0<<timerEnd>>','<<timerEnd>>')+':'+iif(timerEndM < 10,'0<<timerEndM>>','<<timerEndM>>')+'</b> during the weekend. It is currently turned '+iif(budilnikOn = 0, '<a href="exec: budilnikOn = 1 & gt $loc">OFF</a>.', '<a href="exec: budilnikOn = 0 & gt $loc">ON</a>.')
	*nl
	'An old wooden <a href="exec:gt ''wardrobe'', ''start''">wardrobe</a> stands near the door to the corridor. The windows are closed with faded, heavy curtains.'
else
	'<center><img <<$set_imgh>> src="images/locations/city/residential/apartment/bedr2.jpg"></center>'
	*nl
	'A bright and beautiful room, with a large, kingsized <a href="exec:gt ''bed'', ''start''">double bed</a>.'
	*nl
	if budilnikOn = 0:
		'There is an <a href="exec:gt ''budilnik'', ''start''">alarm clock</a> on the bedside table, and it is set to '+iif(timer < 10,'0<<timer>>','<<timer>>')+':'+iif(timerM < 10,'0<<timerM>>','<<timerM>>')+' and is currently turned <a href="exec: budilnikOn = 1 & gt ''<<$loc>>''">off</a>.'
	else
		'There is an <a href="exec:gt ''budilnik'', ''start''">alarm clock</a> on the bedside table, and it is set to '+iif(timer < 10,'0<<timer>>','<<timer>>')+':'+iif(timerM < 10,'0<<timerM>>','<<timerM>>')+' and is currently turned <a href="exec: budilnikOn = 0 & gt ''<<$loc>>''">on</a>.'
	end
	*nl
	'A modern <a href="exec:gt ''wardrobe'', ''start''">wardrobe</a> stands near the door to the corridor. Against the wall stands a make-up table with a <a href="exec:gt ''mirror'', ''start''">mirror</a> on it.'
end
if $ml_guitar['location'] = $loc: 'Your guitar rests on its stand next to your bed.'
*nl
act 'Go to the corridor': gt 'korr'

if husbandrinkday = daystart:
	if husbanstrapon = 1:'Your drunken husband sleeps on the bed with a butt plug up his ass.'
	if husbanstrapon = 2:'Your drunken husband sleeps on the bed after you put a dildo up his ass.'
	if husbanstrapon = 3:'Your drunken husband sleeps on the bed after you fucked him with a strap-on.'
	gs 'music_actions', 'no_music'
end
if husband > 0 and husbandrink ! 10 and husbandrinkday ! daystart:
	if hour > 22 or hour < 7:'Your <a href="exec:gt ''husb'', ''start''">husband</a> is sleeping on the bed.'
	gs 'music_actions', 'no_music'
end

if husbandrink = 11 and husbanstrapon > 0 and husbandrinkday ! daystart:
	cla
	'Your husband approaches you. "Honey, my ass hurts. Like a lot. Do you know why?"'

	act 'Lie':
		cla
		menu_off = 1
		husbanstrapon = 0
		'"No dear, I don''t know anything about that," you answer, trying to look as innocent as possible before he shrugs and walks away.'

		act 'Finish': gt 'bedr'
	end

	act 'Confess':
		cla
		menu_off = 1

		if husbanstrapon = 1:'"Honey, you were so drunk last night that I decided to teach you a lesson and put a butt plug up your ass."'
		if husbanstrapon = 2:'"I wanted to make you happy, so I started to give you a blowjob, but you didn''t cum, so I inserted a finger into your asshole. When you still didn''t cum, I took a small dildo and some lube and put in it your ass; I''ve never seen you cum like that before. I even gave you a secong blowjob. Forgive me, but you seemed to enjoy it so much that I could''t stop. I just wanted to make you happy," you lie to him.'
		if husbanstrapon = 3:'"I wanted to make you happy, so I started to give you a blowjob, but you didn''t cum, so I inserted a finger into your asshole. When you still didn''t cum, I put in a second finger you began to move your ass towards my fingers, moaning heavily. I thought that you liked it, so I put on a strap-on. You enjoyed it so much that you finished over and over again, and I continued until I was tired. I then licked all the cum off your body and went to sleep. I just wanted to make you happy and you were enjoying it," you lie him.'

		if harakHusb = 2:
			husbizvradd = 0
			husbharmin = 0
			husbandMark = 0
			husband = 0
			divorced += 1
			fingal += 1
			minut += 120
			'The next thing you feel is a powerful slap to the face. When you come back to your senses, your husband and his things are gone. He left a note telling you he''s going to file for divorce.'
		elseif harakHusb = 1:
			if husbanstrapon = 1:
				'"Don''t do that again!" he snarls and storms off.'
			elseif husbanstrapon = 2:
				husbizvradd += 1
				'"Don''t do that again! I''m a man and I''m not supposed to get fucked in the ass!" he snarls and storms off.'

			elseif husbanstrapon = 3:
				if pcs_intel > 40 or izvratH = 1:
					husbharmin += 1
					'"Are you serious?! I''m not some kind of bitch that wants to get fucked up the ass!" he yells at you. You start to cry and say that you only wanted to please him and he looks at you and sighs. "Don''t cry, sweetie. I believe you, just don''t do it again."'
				elseif pcs_intel <= 40:
					husbizvradd = 0
					husbharmin = 0
					husbandMark = 0
					husband = 0
					divorced += 1
					fingal += 1
					minut += 120
					'The next thing you feel is a powerful slap to the face. When you come back to your senses, your husband and his things are gone. He left a note telling you he''s going to file for divorce.'
				end
			end
		elseif harakHusb = 0:
			if husbanstrapon = 1:
				'"I''m sorry. I won''t get that drunk again," he says and leaves the room.'
			elseif husbanstrapon = 2:
				husbizvradd += 1
				'"You''re so thoughtful. Instead of scolding me, you still wanted to make me happy. Thank you, dear," he says and leaves the room.'
			elseif husbanstrapon = 3:
				husbizvradd += 1
				'"Did I really like it?" he asks and you nod.'
				'"Well then, thank you dear. Ask me next time though," he says and leaves the room.'
			end
		end

		act 'Finish':husbanstrapon = 0 & gt 'bedr'
	end
end

if husbandrink = 10 and hour > 20 and hour < 23 and husbanstrapon = 0:
	'<<$husName>> came home drunk and fell asleep on the bed.'
	'<i>That pig</i>! you think when an idea springs to mind. <i>Maybe I should teach him a lesson so he won''t get drunk again</i>.'

	if analplug = 1:
		act 'Insert a butt plug in his ass':
			*clr & cla
			menu_off = 1
			husbandrinkday = daystart
			husbandrink = 11
			husbanstrapon = 1
			minut += 10
			'<center><img <<$set_imgh>> src="images/characters/city/husband/sex/h0.jpg"></center>'
			if analplugin = 1: 'You pull the plug out of you and shove it up his ass.'
			if analplugin = 0: 'You take an anal plug and put it into his ass.'

			'<<$husName>> moves, but he doesn''t wake up.'

			if dildo > 0:
				act 'Insert a dildo into his ass':
					*clr & cla
					menu_off = 1
					husbanstrapon = 2
					gs 'boyStat', $HusID
					gs 'cum_call', 'mouth_swallow', $boy, 1
					'<center><img <<$set_imgh>> src="images/characters/city/husband/sex/h1.jpg"></center>'
					'You pull out the plug and replace with it with a dildo. <<$husName>> doesn''t even budge as you slowly move the dildo in and out, trying not to wake him up. You notice his cock starts to get rock hard, and he starts to moan softly in his sleep. His dick starts to throb, like he''s going to cum. You take his dick in your mouth and don''t need to suck on it for long. You swallow his cum before carefully putting his underpants back on.'
					gs 'arousal', 'bj', 20
					gs 'arousal', 'anal_dildo_give', 20
					minut -= 20
					gs 'arousal', 'end'
					gs 'stat'

					act 'Finish':gt 'bedr'
				end
			end

			if strapon > 0:
				act 'Use the strap-on':
					*clr & cla
					menu_off = 1
					minut += 20
					husbanstrapon = 3
					gs 'boyStat', $HusID
					gs 'cum_call', 'mouth_swallow', $boy, 1
					pcs_horny += 30
					'<center><img <<$set_imgh>> src="images/characters/city/husband/sex/h2.jpg"></center>'
					'You put on the strapon and pull out the butt plug. You gently move the shaft of the strap-on inside his ass and <<$husName>> moves, but doesn''t wake up. You continue to slowly move it in and out, trying not to wake him. You notice his cock is getting rock hard and he starts to moan softly in his sleep. Hoping that he won''t wake up, you increase your speed and push the entire length of the strap-on into his ass as you start to stroke his dick. Realizing he''s not far from cumming, you pull out and finish him by giving him a blowjob. He cums in your mouth and you swallow before carefully putting his underpants back on.'
					gs 'arousal', 'bj', 20
					gs 'arousal', 'anal_strap_give', 20
					minut -= 20
					gs 'arousal', 'end'
					gs 'stat'

					act 'Finish':gt 'bedr'
				end
			end

			act 'Finish':gt 'bedr'
		end
	end
end

if sick >= 1:
	act 'Go to sleep (Sick)':
		*clr & cla
		menu_off = 1
		minut += 240
		pcs_sleep += 40
		sick -= 1
		if sick < 0:sick = 0
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/locations/shared/apartment/event/sick/sick<<rand(11, 17)>>.jpg"></center>'
		'You''re lying in bed coughing, sneezing and blowing your nose.'

		act 'Check your temperature': gt 'bedr'
	end
end

dynamic $palelady

if pcs_horny >= 50: act 'Masturbate': gt 'selfplay', 'start'

if pornMagazine > 0:
	'You hide a porn magazine under your mattress.'
	gs 'events', 'read_porn'
end
gs 'subkid'

act 'Relax on your bed': gt 'bed', 'start'
act 'Open wardrobe': gt 'wardrobe', 'start'
gs 'prostitution_functions', 'prostitute_outfit_at_home'
gs 'music_actions', 'music_icon'

--- bedr ---------------------------------

