# buklinik

if $ARGS[0] = 'start':
	$menu_loc = 'buklinik'
	$menu_arg = 'start'
	menu_off = 0
	*clr & cla
	minut += 5	
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b>Mercy clinic</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/mercyclinic/bukli.jpg"></center>'
	'A well-known homeless shelter in a run-down building. Looking above the entrance you can see a sign saying, Mercy Clinic.'
	'There is a note by the entrance.'
	'Working hours: 09:00 to 17:00'
	temp = rand(1, 10)
	if temp = 9:'A crowd of homeless people have huddled together trying to see if there are any available beds...'
	if temp = 2:'A homeless person is trying to get into the shelter...'
	if temp = 3:'An older man is standing outside the shelter...'
	if temp = 4:'There is a woman standing outside the shelter trying to console her crying child...'
	if temp = 5:'A bruised woman stands near the entrance having a smoke...'
	if temp = 1:'A man is walking up and down a path, screaming profanities...'
	if temp = 8:'A young woman is sitting on a bench right next to the shelter with tears in her eyes...'
	if temp = 7:'A couple are loudly arguing if they want to stay at the shelter tonight...'
	if temp = 6:'A woman is crying, arguing on the phone, probably with her husband...'
	if temp = 10:'Surprisingly, there is no one around the shelter...'

	act 'Leave':gt 'city_industrial'
	
	if hour >= 9 and hour <= 17: act 'Enter the shelter':gt 'buklinik', 'holl'
end

if $ARGS[0] = 'holl':
	$menu_loc = 'buklinik'
	$menu_arg = 'holl'
	menu_off = 0
	*clr & cla
	minut += 5
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b>Entrance</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/mercyclinic/bukli_holl.jpg"></center>'
	'The interior is even worse than the exterior, the paint on the wall is falling off and hasn''t been repainted for quite a while. There are some lights working but not all of them and there is this weird smell that you can''t really pinpoint what it is.'
	if mid($start_type,1,2) = 'sg' and SchoolAtestat = 0 and kanikuli = 0 and week < 6 and bumtolik >= 5:
		'You can volunteer here on the weekends.'
	else
		if bumtolik > 4:'The <a href="exec:gt ''buklinik'', ''sklad''">storage</a> room is locked.'
		if bumtolik > 5:'The <a href="exec:gt ''buklinik'', ''registr3''">office</a> door is right by the entrance.'
		if bumtolik > 5:'Opposite the office door you see the <a href="exec:gt ''buklinik'', ''olga2''">accountants''</a> office door sign.'
		if bumtolik > 5:'Furthest down the corridor, are the <a href="exec:gt ''buklinik'', ''shower''">showers</a>.'
		if bumtolik = 6:'There are several doors leading into the <a href="exec:gt ''buklinik'', ''spal''">dormitory</a>.'
		if bumtolik > 6:'I shouldn''t interact with the people staying there right now...'
		if bumtolik > 5:'The <a href="exec:gt ''buklinik'', ''stol''">canteen</a> is located opposite the dormitories.'
		if bumtolik > 5:'Behind a solid wooden door is the <a href="exec:gt ''buklinik'', ''sec''">Managers''</a> office.'
		if bumtolik > 5:'The <a href="exec:gt ''buklinik'', ''dir''">coordinators''</a> office is next to the canteen.'
	end

	act 'Leave':gt 'city_industrial'
	if (SchoolAtestat = 0 and bumtolik = 0) or bumtolik = 4: act 'Volunteer':gt 'buklinik', 'registr'
end

if $ARGS[0] = 'registr':
	*clr & cla
	menu_off = 1
	minut += 15
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b>Counter</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/mercyclinic/bukli_reg.jpg"></center>'
	'As you approach the counter there are two women chatting with each other, and laughing.'

	if mid($start_type,1,2) = 'sg' and SchoolAtestat = 0 and bumtolik < 5:
		!r125
		'You clear you throat, "Hello, my name is <<$pcs_firstname>>, I found a note that you were looking for volunteers..."'
		'One of the women cheerfully replies, "Welcome to our little humble shelter. As you''ve probably noticed the place is run down and we''re really in need of some volunteers." They point further down the corridor, "What you see here is just a small part of the operation that we provide. We also help out with the elderly and the less fortunate outside the shelter."'
		'Nodding, "I''m not afraid to get dirty and I''m sure that I''ll manage fine..."'
		'The woman behind the counter continues, "In addition, since I assume that you are still in high school, the law states that you can only volunteer outside school hours, or during the weekends."'
	else
		'You clear your throat, "Hello, my name is <<$pcs_firstname>>, I met a girl named Olga who said that you were in need of volunteers. I would like to sign up..."'
		'The woman rushes over, "Hello! We are very pleased to have you here. Just be aware, the work is hard and ungrateful, what you see here is just a small part of it, also help out with the elderly and the less fortunate outside the shelter."'
	end

	act 'I have to think about it':gt 'city_industrial'
	act 'Accept':gt 'buklinik', 'registr2'
end

if $ARGS[0] = 'registr2':
	*clr & cla
	menu_off = 1
	minut += 15
	bumtolik = 5
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b>Counter</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/mercyclinic/bukli_reg.jpg"></center>'
	'You smile, "I think I can handle it. What''s next?'
	'The woman lets out a sigh of relief, "I am happy to hear that. Fill out this form. We give all our volunteers documentation that they''ve worked here in case of you need it as a future reference.'
	'You take some time to fill out the form and hand it back to the woman.'
	'The woman skims through it, "Great, you see the the first door on the right down the corridor, you should find Olga there, tell her that the reception sent you and she''ll show you around.'

	act 'Continue down the corridor':gt 'buklinik', 'holl'
end

if $ARGS[0] = 'registr3':
	*clr & cla
	menu_off = 1
	minut += 15
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b>Counter</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/mercyclinic/bukli_reg.jpg"></center>'
	'You usually find either Karina or Regina sitting by the counter, welcoming either the volunteers or people that are in need of help.'

	act 'Continue down the corridor':gt 'buklinik', 'holl'
end

if $ARGS[0] = 'sklad':
	*clr & cla
	menu_off = 1
	minut += 5
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b>Storage room</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/mercyclinic/bukli_sklad.jpg"></center>'
	'The storage room is filled with all sort of things. From clothing, food, to children toys and even a medicine box in case of need. There is a note next to the medicine box, <i>keep locked at all times!!!</i>'
	'You see several people sorting through the stuff, trying to make sense what goes where.'

	if bumtolik = 5:act 'Look for Olga':gt 'buklinik', 'olga'

	act 'Head back to the corridor':gt 'buklinik', 'holl'
end

if $ARGS[0] = 'olga':
	*clr & cla
	menu_off = 1
	minut += 5
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b>Olga</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/mercyclinic/bukli_olga.jpg"></center>'
	
	if bumtolik = 5:
		'You manage to find Olga and walk up to her, "Hi Olga! I don''t know if you remember me, it''s <<$pcs_nickname>>. You told me about this place and that you were looking volunteers and I decided to see if I could help out. The women at the reception told me that I would find you here."'
		'Olga looks at you, "<<$pcs_nickname>>..., good afternoon! Glad to have you on board! Well as you can see we are swamped so any help is very welcome!" Smiling, she continues, "Since you got sent here that means I''ll be your mentor, feel free to ask me whenever you have a question."'
		'You nod, "I will do that!"'
		'Olga, still smiling, "Good, lets start off with a tour. If we start off here, the storage room. Every donation we receive ends up here and gets sorted depending on where they need to end up. Next up the managers'' office!"'
		
		act 'Visit the managers office':gt 'buklinik', 'sec'
	
	else
		'You look around, "Is there someone named Olga here?!"'
		'A woman looks up, "Right here, honey! And who might you be?"'
		'"<<$pcs_nickname>>" you answer, "The woman at the counter told me to find you."'
		'Olga crosses her arms, "Is that so?! They are keen on sending everyone to me. Well, nothing to worry about, and since you were looking specifically for me, you''re a new volunteer?"'
		'You nod.'
		'Olga smiles, "Good, well, lets start off with a tour. If we start off here, the storage room. Every donation we receive ends up here and gets sorted depending on where they need to end up. Next up the managers'' office!"'
		
		act 'Visit the managers office':gt 'buklinik', 'sec'
	end
end

if $ARGS[0] = 'sec':
	*clr & cla
	menu_off = 1
	minut += 15
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b>Managers'' office</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/mercyclinic/bukli_sec.jpg"></center>'

	if bumtolik = 5:
		'Olga knocks on the door and walks right in, "<<$pcs_nickname>>, meet our office manager Jeanne. She is responsible for everything operational here. If you run into any issues take it up with her and she''ll sort it out. Jeanne, this is <<$pcs_nickname>> and is our new volunteer."'
		'Jeanne gives you a warm smile, "Welcome, I hope you won''t mind the state of our shelter, it''s a bit on the run down side. And I hope that Olga doesn''t end up being a bad influence..." she teasingly looks at Olga.'
		'Before you''re able to say anything Olga chimes in, "See, <<$pcs_nickname>> this is the thanks I get for recruiting volunteers. It''s clear we''re not welcome here so let us move on..."'
		'You manage to give a friendly nod before the two of you leave the office, "Next stop, the coordinators'' office!"'
		act 'Head to the coordinator':gt 'buklinik', 'dir'
	elseif bumtolik > 5:
		'"Hello, <<$pcs_nickname>>! Is there anything I can help you with?"'
		act 'Head back to the corridor':gt 'buklinik', 'holl'
	end
end

if $ARGS[0] = 'dir':
	*clr & cla
	menu_off = 1
	minut += 15
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b>Coordinators'' office</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/mercyclinic/bukli_dir.jpg"></center>'
	
	if bumtolik = 5:
		'Olga barges in without even knocking, "And this is our head coordinator Vera..."'
		'Vera sighs, "Again Olga?! Haven''t I told you to knock when you''re about to enter the and I told you, just call me Vera."'
		'Olga brushes her off, "Yeah, yeah..." turning to you, "Vera is not only the head coordinator, but also the mastermind behind everything. She is one of the founders of the organization."'
		'You politely greet her, "My name is <<$pcs_nickname>> and I''ll do my best to help everyone out!"'
		'Vera smiles, "Glad to hear that you''re excited! You''ve probably heard that we could use all the hands we can get hold of."'
		'The three of you chat for a while and after a while Olga pushes you to continue on with the tour...'
		act 'Continue the tour':gt 'buklinik', 'olga2'
	elseif bumtolik > 5:
		'"Hello, <<$pcs_nickname>>! Is there anything I can help you with?"'
		act 'Head back to the corridor':gt 'buklinik', 'holl'
	end
end

if $ARGS[0] = 'shower':
	*clr & cla
	menu_off = 1
	minut += 15
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b>Showers</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/mercyclinic/bukli_show.jpg"></center>'
	'The showers are mixed. There''s a note put up by the staff:'
	'Men: Every even hour. Females stay out!!!'
	'Women: Every odd hour. Males stay out breaking this rule will get you kicked out!!!'
	'Note: Staff might enter during any hours!'
	*nl
	'You shortly stay here as some people might take offense to you being there.'

	act 'Head back to the corridor':gt 'buklinik', 'holl'
end

if $ARGS[0] = 'stol':
	*clr & cla
	menu_off = 1
	minut += 15
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b>Canteen</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/mercyclinic/bukli_stol.jpg"></center>'
	'There''s a large canteen with lots of tables and chairs with loads of space. This is probably the biggest room in the building.'
	*nl
	'There is a sign by the entrance:'
	'Breakfast: 08:00 - 09:30'
	'Dinner: 17:00 - 19:00'

	act 'Leave the canteen':gt 'buklinik', 'holl'
end

if $ARGS[0] = 'spal':
	*clr & cla
	menu_off = 1
	minut += 15
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b>Dormitory</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/mercyclinic/bukli_spal.jpg"></center>'
	'A shared dormitory with plenty of beds filled with people from all walks of life. There are plenty of homeless alcoholics, vagrants and drug addicts. Mixed in with them are women with their children that have ran away from their abusive relationships.'
	'Within the dormitory there is a small unwritten divide as male population stick to one side while the women keep to the other.'

	if bumtolik = 6: act 'Introduce yourself':gt 'buklinik_event', 'pos1'

	act 'Leave the dormitory':gt 'buklinik', 'holl'
end

if $ARGS[0] = 'olga2':
	*clr & cla
	menu_off = 1
	minut += 15
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b>Olga</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/mercyclinic/bukli_olga.jpg"></center>'

	if bumtolik > 5:'"Hello, <<$pcs_nickname>>! Is there anything I can help you with?"'
	if buklinikDay = daystart:'"All the assignments are done for today. We can finally let our guard down..."'
	if bumtolik >= 7 and buklinikDay ! daystart:'"<<$pcs_nickname>>, I need your help in the <a href="exec:gt ''buklinik_event'', ''pos6''">canteen</a> today."'
	if bumtolik >= 8 and buklinikDay ! daystart: '"<<$pcs_nickname>>, here is the address to one the <a href="exec:gt ''buklinik_event'',''pos8''">elderly gentlemen</a> we''re helping out. Would you be so kind and help him out with some housework?'
	if bumtolik = 9 and buklinikDay ! daystart: '"<<$pcs_nickname>>, I''ll need some help to persuade an alcoholic to stop wandering the streets, and stay <a href="exec:gt ''buklinik_event'',''pos9''">here</a>."'
	if bumtolik = 10 and buklinikDay ! daystart: '"We need a babysitter for one very <a href="exec:gt ''buklinik_event'',''event0''">weird</a> man. Don''t worry, he''s totally harmless."'
	if bumtolik = 11 and buklinikDay ! daystart: '"<<$pcs_nickname>>, would you be kind and visit <a href="exec:gt ''buklinik_event'',''entrance'',0">Michael</a>?"'
	if bumtolik = 12 and buklinikDay ! daystart: '"<a href="exec:gt ''buklinik_event'',''entrance'',1">Michael</a> is asking for you, <<$pcs_nickname>>. You must''ve made some impression."'
	if bumtolik = 13 and buklinikDay ! daystart: '"<<$pcs_nickname>>, <a href="exec:gt ''buklinik_event'',''entrance'',2">Michael</a> won''t stop nagging us about you... Could you please visit him?"'
	if bumtolik = 14 and buklinikDay ! daystart: '"<<$pcs_nickname>>, <a href="exec:gt ''buklinik_event'',''entrance'',3">Michael</a> is expecting you."'
	if bumtolik = 15 and buklinikDay ! daystart: '"Why the hell is <a href="exec:gt ''buklinik_event'',''entrance'',4">Michael</a> always asking for you?'
	if bumtolik = 16 and week = 1 and buklinikDay ! daystart: '"Guess who is asking for you... You guessed it, <a href="exec:gt ''buklinik_event'',''entrance'',5">Michael</a>."'
	!bumtolik = 3000 if michael events stoped.
	if bumtolik = 5:
		bumtolik = 6
		'Olga keeps showing you around, "Here we have the beds, showers and the canteen where clients eat for free. I think that would be all, ask away if I forgot something to show you. Oh yeah, you''ve already met them, but the ones sitting at the reception are Regina and Karina. You''ll run across other volunteers, but I''ll let yourself get acquainted."'
		'You thank Olga for the tour as she quickly heads back to the storage room.'
	end

	act 'Head back to the corridor':gt 'buklinik', 'holl'
end

--- buklinik ---------------------------------

