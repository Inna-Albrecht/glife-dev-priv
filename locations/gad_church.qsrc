# gad_church
!2021/05/04
if $ARGS[0] = 'start':
	CLOSE ALL
	if sound = 0:
		if week = 7:
			if hour >= 8 and hour <= 12: PLAY 'sound/street2.mp3',30 & PLAY 'sound/church/ivironKampan01.wav',30
			if hour >= 13 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
		elseif week = 6:
			if hour >= 19 and hour <= 23: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/church/ivironKampan01.wav',30
			if hour >= 13 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
		else
			if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street_leto.mp3',30
			if hour >= 0 and hour <= 7:PLAY 'sound/street_night.mp3',30
		end
	end
	$loc = 'gad_church'
	$loc_arg = 'start'
	$menu_loc = 'gad_church'
	$menu_arg = 'start'
	$location_type = 'public_indoors'
	menu_off = 0
	gs 'themes', 'outdoors'
	gs 'stat'
	'<center><b><font color="maroon">Local Church</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/gadukino/church/church.jpg"></center>'
	'The local church is small and relatively old, it is not in the best shape, but it appears well-loved.'
	*nl
	!!'You can attend confession every weekday between 07:00 and 09:00 or 16:00 and 18:00, and on the weekend between 16:00 and 18:00.'
	'The vigil is held on Saturdays at 19:00.'
	'The Divine Liturgy is on Sundays between 09:00 and 12:00.'
	act 'Go to the village square': minut += 3 & gt 'gadukino'
	if week = 6 and hour >=19 and hour <= 22 and churchday ! daystart:
		act 'Attend vigil': gt 'gad_church', 'vigil'
	elseif week = 7 and hour >= 9 and hour < 12 and churchday ! daystart:
		act 'Attend Divine Liturgy': gt 'gad_church' , 'liturgy'
	else
		act 'Enter the church': gt 'gad_church' , 'Narthex'
	end
end

if $ARGS[0] = 'Sermon':
	cla & *clr
	minut += 20
	church_moral += 1
	gs'stat'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/church'+ rand(1,5) +'.jpg"></center>'
	'The candles are dimmed as the sermon begins. You listen intently to the priest as he preaches the word of God. You study His teachings, feeling enlightened until you reach the end of the sermon.'
	act 'Stand up':gt 'gad_church', 'Narthex'
end

if $ARGS[0] = 'vigil':
	CLOSE ALL
	*clr & cla
	menu_off = 1
	churchday = daystart
	pcs_mood += 10
	temprand = rand(0,5)
	if rand(0,1) = 1:
		pcs_horny += temprand
	else
		pcs_horny -= temprand
	end
	minut += 30
	gs 'themes', 'indoors'
	church_moral += 1
	PLAY 'sound/church/Blessing.mp3',30
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/church'+ rand(1,5) +'.jpg"></center>'
	'You attend the vigil and reflect on the recent events in your life. The harmonies as the hymns and psalms are sung sweep aside some of the worries in your heart; although you know they will return once you leave this holy place, you still enjoy the rest. The sweet scent of incense tickles your nose.'
	gs 'stat'
	act 'Return to Narthex': gt 'gad_church', 'Narthex'
	act 'Stay for the Rite of Mutual Forgiveness':
		*clr & cla
		church_moral += 1
		pcs_mood += 10
		pcs_horny -= 20
		minut += 10
		gs 'stat'
		gs 'fame', 'church_reduction', 'village', -2
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/church'+ rand(9,10) +'.jpg"></center>'
		'You decide to stay for the Rite of Mutual Forgiveness and feel much better afterwards.'
		act 'Return to Narthex': gt 'gad_church', 'Narthex'
	end
end

if $ARGS[0] = 'liturgy':
	*clr & cla
	CLOSE ALL
	churchday = daystart
	menu_off = 1
	minut += 60
	pcs_mood += 20
	church_moral += 1
	gs 'themes', 'indoors'
	gs 'fame', 'church_reduction', 'village', -1
	PLAY 'sound/church/t' + rand(1,19) + '.mp3',30
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/church'+ rand(1,8) +'.jpg"></center>'
	'You join the congregation for the Divine Liturgy. You see mostly older people around, but some people around your age are also present. You participate in the singing, but that''s about it. Your presence seems to go largely unnoticed, and you have the chance to rest. You feel a lot better.'
    gs 'exp_gain', 'vokal', rand(0,3)
    gs 'stat'
	act 'Leave the church': gt 'gad_church' , 'start'
end

if $ARGS[0] = 'Narthex':
	CLOSE ALL
	$menu_loc = 'gad_church'
	$menu_arg = 'Narthex'
	menu_off = 0
	*clr & cla
	gs 'themes', 'indoors'
	gs 'stat'
	PLAY 'sound/church/76139__dkustic__090227-01-people-church-concert.wav',30
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/enterchurch.jpg"></center>'
	'You enter the church, and the scent of incense and beeswax fills your lungs. Your eyes are drawn past the Narthex into the Nave with its walls covered in lovingly painted icons while a thousand points of light dance in joy throughout God''s house from the magnificent chandelier that hangs from the ceiling.'
	'To your left is a counter with many candles, icons and prayer books, a small jar resting next to it with a few rubles and donations for taking a candle.'
	act 'Leave the church': gt 'gad_church' , 'start'
	!!if ((week <= 5 and hour >= 7 and hour < 9) or (hour >= 16 and hour < 18)) and confessday ! daystart: act 'Go to confession': confessday = daystart & gt 'gad_church2', 'confession'
	act 'Light a candle':gt 'gad_church', 'candle'
	act 'Donate for a candle':gt 'gad_church', 'candle1'
	act 'Pray':gt 'gad_church', 'pray'
	!!act 'Talk to the Priest': gt 'gad_church' , 'talk_priest'
!!the stuff commented out is things we can add in later, I didn''t feel like completely making a new priest NPC at this time.
end

if $ARGS[0] = 'candle':
	*clr & cla
	minut += 10
	menu_off = 1
	gs 'themes', 'indoors'
	gs 'stat'
	gs 'fame', 'church_reduction', 'village', -1
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/ch_candle.jpg"></center>'
	'You approach the counter and take one of the candles, lighting it without giving a donation.'
	act 'Return to Narthex': gt 'gad_church', 'Narthex'
end

if $ARGS[0] = 'candle1':
	*clr & cla
	minut += 10
	menu_off = 1
	church_moral += 1
	money -= 10
	gs 'themes', 'indoors'
	gs 'stat'
	gs 'fame', 'church_reduction', 'village', -1
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/ch_candle.jpg"></center>'
	'You approach the counter and put <b>â‚½</b>10 in the jar for a candle. Then, you take one of the candles and light it.'
	act 'Return to Narthex': gt 'gad_church', 'Narthex'
end

if $ARGS[0] = 'pray':
	*clr & cla
	menu_off = 1
	minut += 1
	gs 'themes', 'indoors'
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/ch_praying.jpg"></center>'
	'You approach the prayer bench and kneel down to pray.'
	act 'Pray':
		*clr & cla
		menu_off = 1
		minut += 10
		gs 'fame', 'church_reduction', 'village', -1
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/ch_praying.jpg"></center>'
		if hour > 6 and hour < 20 and church_moral < 30:
			pcs_horny -= 10
			pcs_mood += 20
			church_moral += 1
			'You decide to pray for a while. You''re not overly religious, but the peace and quiet give you the time to think.'
		elseif hour > 6 and hour < 20 and church_moral >= 30 and church_moral < 60:
			pcs_horny -= 20
			pcs_mood += 30
			church_moral += 1
			minut += 15
			'You decide to pray for a while. While you don''t consider yourself deeply religious, you feel a solid connection to God when you look at the icons around you. You allow yourself to look deeply into your soul as you contemplate your life''s direction.'
		elseif hour >6 and hour < 20 and church_moral >= 60:
			pcs_horny -= 30
			pcs_mood += 40
			church_moral += 1
			minut += rand(30,60)
			'You decide to pray. The beauty and glory of being in the body of God overwhelm you, and you lose yourself in prayer. When you finally become aware of your surroundings again, you''re unsure how much time has passed, but it doesn''t matter as you are at peace with God and the world.'
		elseif (hour >= 20 or hour <= 6) and church_moral < 30:
			pcs_horny -= 40
			pcs_mood += 50
			church_moral += 1
			minut += rand(30,60)
			'The Nave is lit only with small candles, leaving your surroundings in darkness. You start to get a strange vibe you can''t quite shake. You suddenly get the urge to pray very intensely, and before you know it, an hour has passed, and you feel reborn.'
		elseif (hour >= 20 or hour <= 6) and church_moral >= 30 and church_moral < 60:
			pcs_horny = 0
			pcs_mood += 50
			church_moral += 1
			minut += rand(30,60)
			if nark > 1: nark = nark/2
			'The Nave is lit only with small candles, leaving your surroundings in darkness, but you find it comforting. You get the urge to pray very intensely, and before you know it, an hour has passed, and you feel cleansed.'
		else
			pcs_horny = 0
			pcs_mood = 100
			church_moral += 1
			minut += 60
			if nark > 1: nark = ((nark/4)*3)
			'The Nave is lit only with candles and small presence lights, and the darkness feels very comforting, like being in the womb. You get the urge to pray very intensely, and before you know it, more than an hour has passed, and you feel reborn and cleansed.'
		end
		gs 'stat'
		act 'Return to Narthex': gt 'gad_church', 'Narthex'
	end

	if exhibitionist_lvl > 0:
		act 'Be naughty':
			*clr & cla
			menu_off = 1
			gs 'fame', 'city', 'sex', 2
			gs 'flash', 'butt', 'indoors', 2, 1
			gs 'stat'
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/ch_prayingflash.jpg"></center>'
			'While you''re praying, you get a devious idea. With a sly smile, you expose your ass, giving anyone that looks your way a naughty sight as you lower your head and pray in silence. You occasionally hear whispers behind you, causing you to smile.'
			act 'Return to Narthex': gt 'gad_church', 'Narthex'
		end
	end
end

--- gad_church ---------------------------------

