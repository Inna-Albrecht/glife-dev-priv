# intro_uni

gs 'themes', 'indoors'

if $ARGS[0] = 'start':
	killvar '$start_type'
	$start_type = 'uni'
	$start_type[1] = 'nomagic'
	!! Intro to starting uni
	$loc = 'intro_uni'
	$loc_arg = 'custom'
	*clr & cla
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/uni_day.jpg"></center>'
	'The big day has finally arrived! Your family piled into your stepfather''s Gazelle pickup and you all traveled from your hometown, Pavlovsk, to St. Petersburg. Today is the day you move into the university dorms to start this new phase of your life.'
	'As you approach the city, you think back to your school days and recall what type of kid you were...'

	act 'Continue': gt 'intro_city_select', 'start'
end

if $ARGS[0] = 'custom':
	$loc = 'intro_uni'
	$loc_arg = 'custom'
	*clr & cla
!!	Just hair and eye color here for now; the dialog will be update as more options are added
	'<center><b><font color = maroon>CHARACTER CUSTOMIZATION</font></b></center>'
	'<center><b>Allows you to alter hair and eyes, use to match fixed profile picture if in use.</b></center>'
	'<center><img <<$set_imgh>> src="images/system/1_openings/shared/character_creation_2.jpg"></center>'
	'As you look in the mirror, your face comes into focus. While some aspects of your appearance are already set, you can define the others now.'
	'You are fairly tall.'
	*nl
	'<center>Customize your:</center>'
	'<center><a href="exec:gs ''intro_customization'', ''hair''"><img src="images/system/1_openings/shared/icon_hair.png"></a> <a href="exec:gs ''intro_customization'', ''eyes''"><img src="images/system/1_openings/shared/icon_eye.png"></a></center>'
	
	act 'Done': gt 'intro_uni', 'vlad_intro'
end

if $ARGS[0] = 'vlad_intro':
	!! Intro to your stepfather
	*clr & cla
	'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big28.jpg"></center>'
	!!basic desc of Vlad, maybe a preset is used somewhere in game already? If not already in the intro (I think it is.)
	'Having spent almost an hour in the cramped truck, you jump out as soon as you arrive. Vladimir gets out and stretches before telling you to take your sister and find out where you''re staying while the rest of the family unloads your stuff.'
	'Looking around, you see many people doing the same.'

	act 'Check Kolka': gt 'intro_uni', 'kolka_intro'
end

if $ARGS[0] = 'kolka_intro':
	!! Intro to Kolka
	*clr & cla
	'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big34.jpg"></center>'
	!!basic desc of Kolka, maybe a preset is used somewhere in game already? If not already in the intro (I think it is.)
	'Your brother, caught up in the excitement of opening day, tries to slide past Vlad unnoticed but is called back to help unload. He sighs and shuffles his feet before turning around to help. You feel bad for him, but he''ll be able to get a better look around next year when he graduates secondary school and starts here.'

	act 'Head inside': gt 'intro_uni', 'anya_intro'
end

if $ARGS[0] = 'anya_intro':
	!! Intro to Anya
	*clr & cla
	'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big33.jpg"></center>'
	!!basic desc of Anya, maybe a preset is used somewhere in game already? If not already in the intro (I think it is.)
	'Your sister comes up to you, links her arm with yours, and pulls you forward. "Come on, <<$pcs_nickname>>! I want to see this place."'
	'Already knowing the answer, you ask anyway. "Why aren''t you enrolled then?"'
	'"I''m comfortable where I am right now. When I''m ready, maybe I will."'
	'Rolling your eyes, you let her keep pulling on your arm.'

	act 'Enter the university': gt 'intro_uni', 'diane_intro'
end

if $ARGS[0] = 'diane_intro':
	!! Going into Uni quad to get escort - meeting Diane(Rex older sister)
	*clr & cla
	'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big253.jpg"></center>'
	'Joining the crowd heading inside, you head into the main courtyard where the new students are being greeted by the older students.'
	'A (color undecided)-haired girl comes up and introduces herself. "Welcome, I am Diane Borisyuk... Anya?"'
	'Anya squeals with joy before running to her and giving her a hug. "Diane! I haven''t seen you in a long time. <<$pcs_nickname>>, this is Diane, Rex''s sister. She used to buy us booze for our parties when we were younger."'
	'Diane laughs. "That <b>was</b> a long time ago. So this is your sister? Nice to meet you. You''re going to have a lot of fun here. Let''s get you your dorm keys and get you set up!"'
	'"By the way, what are you studying?"'

	act 'Get your key': gt 'intro_uni', 'degree_choice'
end

if $ARGS[0] = 'degree_choice':
	!! Choose your degree here.
	!!variables for attending UNI
	university['prep_counter'] = 0
	university['prep_enrolled'] = 0
	university['student'] = 1
	university['enrolled_in_semester'] = 1
	home_owned[11] = 1
	gs 'set_home', 11
	*clr & cla
	'<center><img <<$set_imgh>> src="images/system/1_openings/6_uni/degree_choice.jpg"></center>'
	'This is where you decide what degree you wish to study. For now, Teaching is the only option, but more will be added in the future.'
	
	act 'Teaching':
		!!variables for teaching degree
		$university['enrolled_in']= 'teaching_studies'
		gs 'grades', 'createclass', 'uni_teaching_studies_semester_1', 'general education 101', 3, 2, 'no', 'no', 1, 12
		gs 'grades', 'createclass', 'uni_teaching_studies_semester_1', 'teaching methods 101', 3, 2, 'no', 'no', 1, 12
		gs 'grades', 'createclass', 'uni_teaching_studies_semester_1', 'learning theories 101', 3, 2, 'no', 'no', 1, 12
		gs 'grades', 'grade_award', 'uni_teaching_studies_semester_1', 'general education 101', 35
		gs 'grades', 'grade_award', 'uni_teaching_studies_semester_1', 'teaching methods 101', 35
		gs 'grades', 'grade_award', 'uni_teaching_studies_semester_1', 'learning theories 101', 35
		!!gt 'intro_uni', 'elective_choice'
		gt 'intro_uni', 'dorm_intro'
	end
end

if $ARGS[0] = 'dorm_intro':
	!! Going to the dorm to get the keys
	*clr & cla
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm.jpg"></center>'
	'You follow Diane into the dorm building where an older woman with a very unpleasant and judgmental look on her face sits in a room just off the main hallway.'
	'Diane walks up to her. "I need <<$pcs_firstname>> <<$pcs_lastname>>''s room key, please."'
	'The woman looks your name up on her computer before finding your dorm key and handing it to you.'
	'"Come on <<$pcs_nickname>>, I want to see your room." Anya drags you towards the elevators, with a smiling Diane following you.'

	act 'Check out your room': gt 'intro_uni', 'vika_intro'
end

if $ARGS[0] = 'vika_intro':
	!! Visiting dorm room - meeting Vika
	*clr & cla
	'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big220.jpg"></center>'
	'Arriving on the tenth floor, you check the number on your key and approach the matching door. You find a blonde haired girl inside sitting on one of the two beds in the room. Looking around, you see there is a desk for each of you and a large wardrobe for your clothes.'
	'The girl gets up and comes to greet you. "Hi, I''m Vika Kirilova. I guess one of you is my new roommate?" She looks between you and Anya, not sure who it is.'
	'"Hello, Vika. I''m <<$pcs_firstname>> <<$pcs_lastname>>, but you can call me <<$pcs_nickname>>. I''m your roommate and this is my sister, Anya." Anya gives her a smile, but is mostly occupied with checking out your room.'
	'Diane stays in the doorway, though you do notice the strange look she gives Vika. "We should let your parents know which room you''re in so they can drop off your stuff while I show you around the campus."'
	'Once you get back out into the hallway, you ask why she doesn''t like your roommate. "There are a lot of nasty rumors about her. I''m not exactly a prude or anything, but if any of those rumors are true, then she makes me look like a virgin that''s been living under a rock my whole life."'

	act 'Find your parents': gt 'intro_uni', 'mom_intro'
end

if $ARGS[0] = 'mom_intro':
	!! Back outside Intro to Mom - answer sets relationship level
	*clr & cla
	'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big29.jpg"></center>'
	'Once you get back outside, you find your family standing around the piles of boxes with your stuff, surrounded by people coming and going. You tell your father which room you''re in and give him the key before your <<$npc_nickname[''A29'']>> pulls you to one side. Anya and Diane step away to catch up and give you some privacy.'
	'"Big day! Are you excited? I know I am." She gives you a proud look.'
	'"Yeah, <<$npc_nickname[''A29'']>>! There''s so much to do and see here."'
	'"Well don''t forget to stay focused on your studies, and stay away from boys for now. There will be plenty of time after you''ve finished studying to think about that stuff."'
	
	if $start_type[1] ! 'tg':
		act 'Tell her what she wants to hear':
			gs 'npc_relationship', 'set', 'A29', '50'
			*clr & cla
			'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big29.jpg"></center>'
			'"Everything will be fine, <<$npc_nickname[''A29'']>>. You got me here, I will do the rest."'
			'She gives you a hug before going to stand over at your pile of stuff to keep an eye on it.'
			
			act 'Go on tour': gt 'intro_uni', 'tour_intro'
		end
		act 'Snap at her':
			gs 'npc_relationship', 'set', 'A29', '30'
			*clr & cla
			'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big29.jpg"></center>'
			'"Just stop <<$npc_nickname[''A29'']>>! I''m an adult now, and I''ll do whatever I want with whoever I want!"'
			'Her eyes flash in anger, but she doesn''t say anything. She goes and stands next to your pile to keep an eye on it.'
			
			act 'Go on tour': gt 'intro_uni', 'tour_intro'
		end
	end
	act 'Appreciate her concern':
		gs 'npc_relationship', 'set', 'A29', '70'
		*clr & cla
		'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big29.jpg"></center>'
		'"I know, I know. Don''t worry so much <<$npc_nickname[''A29'']>>. You got me here. I''ll do the rest."'
		'She gives you a hug before going to stand over your pile of stuff to keep an eye on it.'

		act 'Go on a tour': gt 'intro_uni', 'tour_intro'
	end
end

if $ARGS[0] = 'tour_intro':
	!! Taking a short tour - can be expanded as more buildings are added - sports arena/pool etc
	*clr & cla
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/uni_day.jpg"></center>'
	'You rejoin Anya and Diane, who begins to point out the main areas of the university.'
	'"Right now you''re in the main courtyard. It''s basically the main hub of the university. You already know where the dorms are, and I imagine you''ve been to the admin building already. It has all the main offices, so any paperwork or trouble you get into will be handled there."'
	*nl
	'She takes you to the front of what must be the largest building on campus. "This is the main building, where most of your classes will be held. Finding your way around inside is pretty easy despite its size. It''s signposted like crazy in there."'
	*nl
	'She takes you up to another large building. "This is the library. Most students without a computer come here to study. You can find me here almost all the time and I''m always willing to help if you need it. I''m studying to be a teacher, so offering you assistance would help me as much as it helps you."'
	*nl
	'"Well, that''s the basic tour. The rest you will learn as you go."'

	act 'End the tour': gt 'intro_uni', 'tour_end'
end

if $ARGS[0] = 'tour_end':
	!! End tour
	*clr & cla
	'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big253.jpg"></center>'
	'The three of you walk back to the dorms. You take in the sights while Anya and Diane chat about Rex and the old days.'
	'Once you get there, Diane reminds you to look for her in the library if you need anything before hugging Anya goodbye and heading back into the crowd of new students.'
	'Not seeing your <<$npc_nickname[''A29'']>> or your pile of stuff, you head up to your room.'

	act 'Say goodbye to your family': gt 'intro_uni', 'family_goodbye'
end

if $ARGS[0] = 'family_goodbye':
	!! Say goodbye to your family
	*clr & cla
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm_room/room.jpg"></center>'
	'Returing to your room, you find your stuff piled on your bed and your <<$npc_nickname[''A29'']>> putting the last of your clothes into the wardrobe. You don''t see Vika, and assume she left to give you privacy or to avoid meeting your family.'
	'Anya gives a hug and congratulates you on starting university while your brother gives you an awkward hug and whispers to text him what it''s like.'
	'Vladimir comes over and hugs you too, telling you how proud he is to be your stepfather and to call if you need anything.'
	if npc_rel['A29'] >= 60:
		'Your <<$npc_nickname[''A29'']>> finishes what she''s doing and comes over, tears in her eyes. Wrapping you in a tight hug, she tells you how proud she is of you for enrolling in university and again reminds you to stay away from boys before joining the rest of the family by the door.'
	elseif npc_rel['A29'] <= 30:
		'Your <<$npc_nickname[''A29'']>> finishes what she''s doing and gives you an awkward hug, apologising for fighting with you on your big day before joining the rest of the family by the door.'
	else
		'Your <<$npc_nickname[''A29'']>> finishes what she''s doing and comes over and gives you a hug, telling how happy she is that you enrolled before joining the rest of the family by the door.'
	end
	*nl
	'They all wave at you before leaving. You spend some time putting the rest of your stuff away or stashing it under your bed before deciding to explore the grounds.'

	if $start_type[1] = 'tg':
		act 'Head outside': gt 'intro_uni_tg', 'welcome_intro'
	else
		act 'Head outside': gt 'intro_uni', 'welcome_intro'
	end
end

if $ARGS[0] = 'welcome_intro':
	year = 2016
	month = 5
	week = 1
	hour = 7
	day = 25
	gs 'intro_initialization_city'
	!! Welcome to Girl Life speech
	showstat 1
	showobjs 1
	gs 'obj_din', 'old'
	*clr & cla
	'<center><img <<$set_imgh>> src="images/system/1_openings/6_uni/uni_start.jpg"></center>'
	'Welcome to Girl Life. Guide <<$pcs_nickname>> through the many challenges of life at university. Manage coursework, a job and relationships with the people you meet on your journey.'

	act 'Start the game': gt 'uni_grounds', 'main'
end


--- intro_uni ---------------------------------

