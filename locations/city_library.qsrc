# city_library

$loc_arg = $ARGS[0]
$loc = 'city_library'
$menu_loc = 'city_library'
$menu_arg = ''
menu_off = 0
$location_type = 'public_indoors'
frost = 0
gs 'stat'
gs 'themes', 'indoors'

! Stuff to do on Studying a Book
!	$ARGS[0] = Skill to Check
$downlibrary['BookStudy'] = {
	$downlibrary['Skill'] = $ARGS[0]
	*clr & cla
	menu_off = 1
	if blizoruk = 500 or glassqw = 1:
		glassqw = 1
		msg'	The text blurs across the page. It seems you have poor eyesight. Maybe you should visit an ophthalmologists?'&gt $curloc
	end
	if pcs_nerd > 0:
		lastread = totminut
		lastreadday = daystart
		pcs_nerd += 1
	end
	minut += 60
	if $downlibrary['Skill'] ! '':
		dynamic "
			if pcs_<<$downlibrary['Skill']>> >= 50:
				<<$downlibrary['Skill']>>_exp += rand(0,1)
			elseif pcs_<<$downlibrary['Skill']>> >= 25:
				<<$downlibrary['Skill']>>_exp += rand(1,2)
			else
				<<$downlibrary['Skill']>>_exp += rand(1,3)
			end"
	end

	blizoruk += 1
	gs'stat'
}

'<center><b><font color="maroon">National Library of Russia</font></b></center>'
'<center><img <<$set_imgh>> src="images/locations/city/citycenter/library/downlibint.jpg"></center>'
'A huge library with thousands upon thousands of books. You could spend years in here and probably still wouldn''t be able to read it all.'

act 'Leave': minut += 5 & gt 'city_center'

if hour >= 8 and hour <= 18:
	act 'Read a science book':
		gs 'exp_gain', 'intel', 4 + (mentats_dose - rand(0,mentats_dose))

		dynamic $downlibrary['BookStudy'], ''

		'You read a science book for an hour, trying to make sense of and understand the subject matter.'

		act 'Put the book down':gt $curloc
	end

	act 'Read an entertaining book':
		pcs_mood = 100

		dynamic $downlibrary['BookStudy'], ''

		'You read an adventure book for an hour.'

		act 'Put the book down':gt $curloc
	end

	act 'Read the collected works of Leo Tolstoy':
	
		dynamic $downlibrary['BookStudy'], 'sprt'
      
		'You read the book for an hour. Tolstoy is certainly a classic writer, but his works are certainly exhaustive.'

		act 'Put the book down':gt $curloc
	end
	if MagicLibrary = 1:
		act 'Read magic books':
			dynamic $downlibrary['BookStudy'], 'splcstng'

			'You pick out one of the magic books you can actually read and sit down with it.'
			'Not only is the subject matter complex and mostly a mystery to you, but the descriptions and explanations are unnecessarily wordy, as if the writer had something to prove.'
			'After about an hour, you decide to take a break from it and instead test your memory. You don''t remember much, but you understand magic a little better than before.'

			! Find Researchable Spells
			if func('spellList','numAvailableSpells','librarySpells') > 0:
				if rand(1,10) = 10:
					gs 'spellList','addAvailableSpells','librarySpells'
					*nl
					*pl 'You have come across a lead on a new spell.'
				end
			else
				'There doesn''t seem to be any more spells to learn here, but you can still improve your skills in the ones that you''ve already found.'
			end

			!!Adding to the Actions opportunity to practice spells MC found
			gs 'spellList', 'teacherActions', '$librarySpells', 'city_library', ''

			act 'Put the book down':gt $curloc
		end
		!!Adding to the Actions opportunity to practice spells MC found
		gs 'spellList', 'teacherActions', '$librarySpells', 'city_library', ''
	end

else
	'The library is closed.'
end
gs 'camera', 'check_location'


--- city_library ---------------------------------

