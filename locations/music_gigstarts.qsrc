# music_gigstarts

!!	This is where all the gig introduction meetings will go (pub owners, cafe managers, etc.)

if $ARGS[0] = 'zariyah_passing_by':
	gs 'music_checks', 'zariyah_approach'
	if ml_approach:
		gt 'music_gigstarts', 'delparco_start'
	else
		gs 'music_actions', 'finish'
	end
	killvar 'ml_approach'
end

if $ARGS[0] = 'delparco_start':
	rules = 0

	!! Version 2 - Sveta tipsy: 
	!! * Ask her to be sober when she comes to the meeting 
	if ml_delparcoQW['Drinking Rule'] = 1:
		$ruletext = 'be sober'
		rules = 1
	end 

	!! Version 2 - Sveta high: 
	!! * Ask her to skip the pot when she comes to the meeting 
	if ml_delparcoQW['Weed Rule'] = 1:
		if $ruletext <> '' and ml_delparcoQW['Outfit Rule'] = 0:
			$ruletext += 'and don''t be high'
		elseif $ruletext <> '' and ml_delparcoQW['Outfit Rule'] = 1:
			$ruletext += ', don''t be high'
		else
			$ruletext = 'don''t be high'
		end
		rules = 1
	end

	!! Version 3 - Sveta outfit: 
	!! * Ask her to dress more appropriately when she comes to the office
	if ml_delparcoQW['Outfit Rule'] = 1:
		if $ruletext <> '':
			$ruletext += 'and wear appropriate clothing.'
		else
			$ruletext = 'wear and appropriate outfit.'
		end
		rules = 1 
	end

	'<center><img <<$set_imgh>> src="images/pc/activities/music/busking.jpg"></center>'
	if ml_delparcoQW['Stage'] = 99:
		'As you finish your set, a tall woman steps up to you with a smile. "Hi, you are really good at this." she looks at you thoughtfully for a brief second'
		'"I''m Zariyah, I''m managing Del Parco and we are planning to run some live music evenings and I''m looking for musicians. I wouldn''t want to drag you into this right now,"'
		'she says, looking at your swollen belly "but I was thinking that maybe when you gave birth and recovered a little, you would be interested." She reaches into her purse,'
		'and pulls out a business card "Here is my number and obviously, you can come to the cafe if that''s better. I''m there every weekday." '

		'You take the card a bit hesitantly "Hi, I''m Sveta and thank you... I will think about it." you answer a bit confused, looking at the smiling woman "Splendid," she says '
		'"I''m looking forward to it and please rest and don''t tire yourself out. And I really, really would like to see you again and meet the baby too... oh, damn " she looks at her watch'
		'"I must run to work, but it was lovely to meet you Sveta, I hope I will run into you soon and looking forward to your answer!" she gives you a last smile and hurries off.' 

		killvar 'rules'
		gs 'music_checks', 'reset_checks'

		act 'Leave': gs 'music_actions', 'finish'

	else
		if rules = 1:
			'As you finish your set, you notice a tall woman watching you carefully, her head tilted to the side.'
			'She bites her lip thoughtfully, hesitating for a moment then steps up to you with a smile.' 
			'"Hi, you are really good at this." she looks at you thoughtfully, then nods. "I''m Zariyah, I''m managing Del Parco and we are planning to run some live music evenings.'
			'Would you be interested in playing in a more formal setting?"'
		else  
			'As you finish your set, a tall woman steps up to you with a smile. "Hi, you are really good at this."'  
			'"I''m Zariyah, I''m managing Del Parco and we are planning to run some live music evenings. Would you be interested in playing in a more formal setting?"'
		end
		gs 'music_actions', 'willpower_cost'
		if ml_willcost = 0:
			act 'Accept (<<will_cost>> Willpower)': gt 'music_gigstarts', 'delparco_accept'
		elseif ml_willcost <= pcs_willpwr:
			act 'Accept (<<will_cost>> Willpower)': gt 'music_gigstarts', 'delparco_accept'
		else	
			act 'Accept (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
		end
		act 'Decline': gt 'music_gigstarts', 'delparco_decline'
	end
	
end


if $ARGS[0] = 'delparco_accept':

	*clr & cla
	ml_venues['del_parco'] = 1
	ml_delparcoQW['Stage'] = 2
	ml_delparcoQW['Next Date'] = daystart + (12 - week)
	
	'<center><img <<$set_imgh>> src="images/pc/activities/music/zariyah.jpg"></center>'
	'You look at the woman a bit surprised, then you smile back "Of course! I mean... you mean like a proper stage performance?"'	
	if rules = 0:
		'She nods "Well, it''s a small cafe so you wouldn''t be playing on a stage, per se, but yes, in front of people. You would sing and play your music." she looks at her watch "I will have to go, but please come to the cafe before next Friday, during the week, and we can discuss the details. Poka!" she gives a small wave and hurries off.'
	else
		'She nods "Well, it''s a small cafe so you wouldn''t be playing on a stage, per se, but yes, in front of people. You would sing and play your music." she looks at her watch "I will have to go, but please come to the cafe before next Friday, during the week, and we can discuss the details. Poka!" she gives a small wave but then turns around'
		'"Oh, just one thing, please, when you visit me and whenever you play at the café<<$ruletext>>" she smiles at you and hurries off.'  
	end
	
	killvar 'rules'
	gs 'music_checks', 'reset_checks'

	act 'Leave': gs 'music_actions', 'finish'
end

if $ARGS[0] = 'delparco_decline':

	*clr & cla
	ml_venues['del_parco'] = 1	
	ml_delparcoQW['Stage'] = 1

	'<center><img <<$set_imgh>> src="images/pc/activities/music/zariyah.jpg"></center>'
	'You look at the woman a bit surprised - "I''m... that''s really nice but... I''m not sure... "'
	if rules = 0:
		'She looks at you for a long moment then shakes her head - "You need to be a bit more confident if you want do anything with music. Listen, " - she looks at her watch - " I have to run, but please, think about it and if you change your mind, come to the cafe on any weekday to talk. You are good, and you should not miss out on opportunities." - she says, giving you an encouraging smile before heading off.'
	else
		if ml_delparcoQW['Drinking Rule'] = 0 and ml_delparcoQW['Weed Rule'] = 0:
			'She looks at you for a long moment then shakes her head "You need to be a bit more confident if you want do anything with music. Listen, " - she looks at her watch - " I have to run, but if you change your mind, come to the cafe on any weekday to talk. You are good, and you should not miss out on opportunities." she says, smiling at you.'
		else
			'She looks at you for a long moment then shakes her head "You need to be a bit more confident if you want do anything with music. Listen, " - she looks at her watch - " I have to run, but I would like you to think about it when you are... calmer... and if you change your mind, come to the cafe on a weekday and we can talk. You are good, and you should not miss out on opportunities." - she says, smiling at you.'
		end
		'"Oh, just one thing, please, when you visit me and if you decide to play at the café<<$ruletext>>" - she waves good bye and hurries off.'
	end

	killvar 'rules'
	gs 'music_checks', 'reset_checks'

	act 'Leave': gs 'music_actions', 'finish'

end


--- music_gigstarts ---------------------------------