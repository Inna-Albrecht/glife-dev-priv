# metro

if $ARGS[0] = 'start':
	CLOSE ALL
	$menu_loc = 'metro'
	$menu_arg = 'start'
	menu_off = 0
	if sound = 0: PLAY 'sound/metro' + rand(1,4) + '.mp3', 30
	'<center><b><font color="maroon">Metro station</font></b></center>'
	if $loc = 'city_residential':
		'<center><b><font color="maroon">Residential Area</font></b></center>'
		'<center><img <<$set_imgh>> src="images/locations/city/shared/metro/platresident.jpg"></center>'
	elseif $loc = 'city_center':
		'<center><b><font color="maroon">City Center</font></b></center>'
		'<center><img <<$set_imgh>> src="images/locations/city/shared/metro/platcenter.jpg"></center>'
	elseif $loc = 'city_industrial':
		'<center><b><font color="maroon">City Industrial Region</font></b></center>'
		'<center><img <<$set_imgh>> src="images/locations/city/shared/metro/platindust.jpg"></center>'
	elseif $loc = 'city_suburbs':
		'<center><b><font color="maroon">City Suburbs</font></b></center>'
		'<center><img <<$set_imgh>> src="images/locations/city/shared/metro/platsuburb.jpg"></center>'
	elseif $loc = 'city_island': 
		'<center><b><font color="maroon">Vasilyevsky Island</font></b></center>'
		'<center><img <<$set_imgh>> src="images/locations/city/shared/metro/platisland.jpg"></center>'
	end

	'The metro fare costs 16 <b>₽</b>.'
	clr
	gs 'stat'
	gs 'themes', 'indoors'

	if workDisk = 3 and konvert = 1:
		badrand = rand(0, 100)
		if badrand >= 80:konvert = 0
	end

	act 'Leave the Metro station':
		if $loc = 'city_suburbs':
			minut += 20
		else
			minut += 5
		end
		gt $loc, $loc_arg
	end
	
	if money >= 16:
		if $loc ! 'city_residential':
			act 'Go to the Residential Area (16 <b>₽</b>)':
				$loc = 'city_residential'
				$loc_arg = ''
				money -= 16
				minut += 10
				gt 'metro', 'vagon'
			end
		end

		if $loc ! 'city_center':
			act 'Go to the City Center (16 <b>₽</b>)':
				$loc = 'city_center'
				$loc_arg = ''
				money -= 16
				minut += 10
				gt 'metro', 'vagon'
			end
		end

		if $loc ! 'city_industrial':
			act 'Go to the City Industrial Region (16 <b>₽</b>)':
				$loc = 'city_industrial'
				$loc_arg = ''
				money -= 16
				minut += 10
				gt 'metro', 'vagon'
			end
		end

		if $loc ! 'city_suburbs':
			act 'Go to the City Suburbs (16 <b>₽</b>)':
				$loc = 'city_suburbs'
				$loc_arg = 'start'
				money -= 16
				minut += 10
				gt 'metro', 'vagon'
			end
		end

		if $loc ! 'city_island':
			act 'Go to Vasilyevsky Island (16 <b>₽</b>)':
				$loc = 'city_island'
				$loc_arg = ''
				money -= 16
				minut += 10
				gt 'metro', 'vagon'
			end
		end
	else
		'You don''t have enough money to pay for the metro.'
	end
	
	if pusher = 1 and dealer = 0:
		dealer = 1
		'You see a tall, emaciated man leaning against the wall next to the tracks, looking very shady. He notices you looking and beckons you to come closer.'
		act 'Talk to the man':gt 'metro', 'dealer'
	elseif dealer ! 0:
		'You see the dealer lurking around the station.'
		act 'Buy drugs':gt 'metro', 'shop'
	end
end

if $ARGS[0] = 'vagon':
	*clr & cla
	CLOSE ALL
	if sound = 0:PLAY 'sound/metro1.mp3',30
	gs 'stat'
	gs 'themes', 'indoors'
	menu_off = 1

	'<center><b><font color="maroon">Metro car</font></b></center>'
	if hour < 7 or hour >= 22:
		if rand(0, 100) >= 70: gt 'metro_events', 'frot'
		metro_busy = rand(0,1)
	elseif (hour >= 9 and hour < 16) or (hour >= 18 and hour < 22):
		if rand(0, 100) >= 80: gt 'metro_events', 'frot'
		metro_busy = rand(1,2)
	else
		if rand(0, 100) >= 90: gt 'metro_events', 'frot'
		metro_busy = rand(2,3)
	end

	if metro_busy = 0:
		'<center><img <<$set_imgh>> src="images/locations/city/shared/metro/metroempty.jpg"></center>'
		'The car is almost completely empty, with only one or two other passengers in the car with you. It seems a bit odd and a little creepy seeing the car this empty. Having practically the entire car to yourself, you pick the cleanest spot to sit.'
	elseif metro_busy = 1:
		'<center><img <<$set_imgh>> src="images/locations/city/shared/metro/metro.jpg"></center>'
		'The car is mostly empty, leaving you a choice of seats. You easily find a place for yourself, leaving you plenty of room to get comfortable. The few other people on the car mind their own business, leaving you to your own thoughts.'
	elseif metro_busy = 2:
		'<center><img <<$set_imgh>> src="images/locations/city/shared/metro/metrofull<<rand(1,5)>>.jpg"></center>'
		'The car is full of people. All the seats are taken, but only a few people are forced to stand. You easily find a place for yourself, leaving you plenty of room to get comfortable.'
	else
		'<center><img <<$set_imgh>> src="images/locations/city/shared/metro/metropacked.jpg"></center>'
		'The car is packed with people, most of them being forced to stand since there aren''t enough seats. It''s so tightly packed that people are crammed up tightly against each other, but you manage to squeeze yourself in. You can feel other bodies pressed up tightly against yours, making for a very uncomfortable journey.'
	end

	if (cumloc[6] = 1 or cumloc[7] = 1) or cumloc[11] = 1:
		if hour >= 7 and hour <= 21:
			pcs_mood -= 10
			'Several passengers are shocked, noticing the cum smears on you.'
		end
	end

	act 'Get out of the car':
		if hour < 7 or hour >= 22:
			if rand(0, 100) >= 70: gt 'metro_events', 'station'
		elseif (hour >= 9 and hour < 16) or (hour >= 18 and hour < 22):
			if rand(1,100) >= 80: gt 'metro_events', 'station'
		else
			if rand(1,100) >= 90: gt 'metro_events', 'station'
		end			
		gt 'metro', 'start'
	end
end

if $ARGS[0] = 'shop':
	*clr & cla
	menu_off = 1
	gs 'stat'
	'He shows you a small box filled with red and blue pills. There are about two dozen of each pill inside.'
	act 'Leave':gt 'metro','start'
	if money >= 1000:
		'<a href="exec:money -= 1000 & mentats_have += 20 & gt ''metro'', ''shop''">Buy neuroboosters</a> 1000 <b>₽</b>'
	else
		'Buy neuroboosters 1000 <b>₽</b> (You do not have enough money)'
	end
	'Uses left: <<mentats_have>>'
	'A pillbox with 10 orange pills inside, the description is removed from the box.'
	if money >= 500:
		'<a href="exec:money -= 500 & steroid_have += 10 & gt ''metro'', ''shop''">Buy steroids</a> 500 <b>₽</b>'
	else
		'Buy steroids 500 <b>₽</b> (You do not have enough money)'
	end
	'Uses left: <<steroid_have>>'
	'Paste tube with two pictures of a woman. In the latter one, she sports an enormous bust.'
	if money >= 500:
		'<a href="exec:money -= 500 & bcream_have += 5 & gt ''metro'', ''shop''">Buy breast cream</a> 500 <b>₽</b>'
	else
		'Buy breast cream 500 <b>₽</b> (You do not have enough money)'
	end
	'Uses left: <<bcream_have>>'
	if $start_type[1] ! 'nomagic':
		'Packet of pink chewing gum. There are 10 in one package.'
		if money >= 200:
			'<a href="exec:money -= 200 & aphrodisiac_have += 10 & gt ''metro'', ''shop''">Buy aphrodisiacs</a> 200 <b>₽</b>'
		else
			'Buy aphrodisiacs 200 <b>₽</b> (You do not have enough money)'
		end
		'Uses left: <<aphrodisiac_have>>'
	end
	if money >= 250:
		'Buy enough <a href="exec:money -= 250 & joint += 5 & gt ''metro'', ''shop''">weed</a> for 5 joints for 250 <b>₽</b>'
		if money >= 500: 'Buy enough <a href="exec:money -= 500 & joint += 10 & gt ''metro'', ''shop''">weed</a> for 10 joints for 500 <b>₽</b>'
	else
		'Buy enough weed for 5 joints for 250 <b>₽</b> (You do not have enough money)'
	end
end

if $ARGS[0] = 'dealer':
	*clr & cla
	menu_off = 1
	'He sizes you up with suspicion. "Hey there. Saw you buying Lady in the park." He clears his throat and spits onto the tracks. "That shit will mess you up."' 
	'He opens his coat and you flinch, thinking he''s going to flash you, but instead see that he''s wearing a business suit underneath.' 
	'He gestures at numerous pockets lining the insides of his coat, filled to bursting with various packages, pills and ampules. "Here, my products are of a much better quality. You''ll have a great time, and I guarantee they won''t kill you."'
	act 'Continue': gt 'metro','shop'
end

--- metro ---------------------------------

