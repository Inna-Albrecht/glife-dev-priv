# obroom

if $ARGS[0] = 'start' or $ARGS[0] = '':
	$sexloc = 'obroom'
	CLOSE ALL
	$loc_arg = 'start'
	$loc = 'obroom'
	$locM_arg = 'start'
	$locM = 'obroom'
    $location_type = 'private'
	$menu_loc = 'obroom'
	$menu_arg = 'start'
	menu_off = 0
	cla
	*clr
	odkomp = 1
	gs 'stat'
	'<center><b>Your room</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/obroom.jpg"></center>'
	'This is your room which you share with another girl named Vika Kirilova.'
	'At the right of the entrance, you find an old <a href="exec:gt ''wardrobe'', ''start''">wardrobe</a> (where you can choose outfits and organize your clothing).'
	'There''s a <a href="exec:gt ''mirror'',''start''">mirror</a> hanging on the wall.'
	'Your <a href="exec:gt ''bed'',''start''">bed</a> is positioned in one of the corners of the room.'
	if komp = 1:'There''s a <a href="exec:gt ''Komp'',''start''">laptop</a> lying on your bed.'
	*nl
	'You can access your <a href="exec: quick_dress = 1 & gt ''wardrobe'', ''default''">default clothing</a> options directly.'

	gs 'exercise', 'start'
	''
	if hour > 14 and hour < func('bordel', 'open_hour'):
		vika_sleep = 0
		if npc_rel['A220'] >= 60 and birthday = day and birthmonth = month and vikaslut = 1 and b_vika_present_Day ! daystart:
			'<a href="exec:gt''Vika''">Vika</a> seems excited when she notices you, trying to hide a box behind her back.'
		else
			'<a href="exec:gt''Vika''">Vika</a> is lying on her bed.'
		end
	elseif hour > func('bordel', 'close_hour') and hour < 8:
		vika_sleep = 1
		if vikaslut = 0:
			'<a href="exec:gt''Vika''">Vika</a> is sleeping in her bed after being out the whole night.'
		elseif vikaslut = 1:
			'<a href="exec:gt''Vika''">Vika</a> is sleeping in her bed after working hard in the brothel all night.'
		end
	end

	act 'Walk out in the corridor':
		if $clothingworntype ! 'nude':
			odkomp = 0
			minut += 5
			gt 'dorm', 'korr'
		else
			'You need to get dressed.'
		end
	end

	act 'Read the course literature (2:00)':
		cla
		menu_off = 1
		minut += 120
		intel_exp += 1 + (mentats_dose - rand(0,mentats_dose))
		gs 'stat'
		'You spend two hours reading the course literature. You feel like you''ve picked up some stuff from the book that will come in handy in time for the examination.'

		act 'Finish':gt 'obroom', 'start'
	end

	gs 'unicoursework', 'start', $loc, $loc_arg

dynamic $wearpan
dynamic $removepan
end

--- obroom ---------------------------------

