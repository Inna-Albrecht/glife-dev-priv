# bedrPar2

if $ARGS[0] = '':
	CLOSE ALL
	*clr & cla
	$loc = 'bedrPar2'
	$metka = ''
	$locclass = 'bedr'
	$location_type = 'private'
	$menu_loc = 'bedrPar2'
	$menu_arg = ''
	menu_off = 0
	minut += 1
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><h1><font color="maroon">Master bedroom</font></h1></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/bedrpar2.jpg"></center>'
	*nl
	'The room in which your parents live. Their large bed, <a href="exec: gt ''bedrPar2'',''parents_wardrobe''">wardrobe</a> and other closets take up most of the space.'
	gs 'family'

	act 'Return to the hallway':gt 'korrPar'

	if indorf = 1 or (indorf = 0 and hour > 6 and hour < 21):
		if kamasutra_day ! daystart:
			'You notice a <a href="exec: gt ''bedrPar2'',''kamasutra''">book laying on the bed</a>.'
		end
		if prezikday ! daystart:
			if prezikProver >= 3:
				prezikday = daystart - 4
				act 'Steal condoms from your parents'' stash':
					*clr & cla
					menu_off = 1
					minut+= rand(5,7)
					gs 'stat'
					if preziktype = 0: prezik += rand(1,3)
					if preziktype = 1 or preziktype = 2: prezikcount += rand(1,3)
					prezikday = daystart
					prezikProver = 0
					'<center><img <<$set_imgh>> src="images/shared/accessories/birthcontrol/condoms_steal.jpg"></center>'
					'After you make sure you''re not seen, you quickly search your parents'' room for their condom stash and take a few. Now you have '+iif(preziktype = 0, '<<prezik>>', '<<prezikcount>>')+' condoms.'
					act 'Continue': gt 'bedrPar2'
				end
			end
		end
	end

	if hour = 21 and parsexrand = 5 and parentsexday ! daystart:gt 'seeparentsex'
end

! book
if $ARGS[0] = 'kamasutra':
	*clr & cla
	menu_off = 1
	minut += 1
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/book.jpg"></center>'
	'The cover says "Kamasutra"'
	act 'Leave': gt $loc, $metka
	act 'Read': kamasutra_page = 0 & kamasutra_day = daystart & gt 'bedrPar2', 'read_book'
end

! read book
if $args = 'read_book':
	*clr & cla
	menu_off = 1
	pcs_horny += 2
	minut += 3
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/shared/accessories/books/kamasutra/ik' + kamasutra_page + '.jpg"></td></tr><tr><td align=center>'+iif(kamasutra_page < 1, '', '<a href="exec: kamasutra_page-=1 & gt''bedrPar2'',''read_book''"><br>Previous page</a>')+'</td><td align=center>'+iif(kamasutra_page > 45, '', '<a href="exec: kamasutra_page+=1 & gt''bedrPar2'',''read_book''"><br>Next page</a>')+'</center>'
	act 'Close the book': gt $loc, $metka
	if pcs_horny >= 60 and week = 6 and family_trip = 1: act 'Masturbate': gt 'selfplay', 'start'
end

! parent''s wardrobe
if $ARGS[0] = 'parents_wardrobe':
	*clr & cla
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/wardrobe.jpg"></center>'
	'An old looking wardrobe, probably from the Soviet times.'
	act 'Leave ': gt $loc, $metka
	act 'Rummage': gt 'bedrPar2', 'wardrobe_search'
end

if $ARGS[0] = 'wardrobe_search':
	*clr & cla
	if (hour >= 21 or hour < 6) and indorf = 0:
		'What are you thinking? You can''t go through the wardrobe while your parents are sleeping right there!'
		act 'Leave ': gt $loc, $metka
		exit
	end
	if rand(1,100) > 75:
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/wardrobesearch'+rand(1,4)+'.jpg"></center>'
		$failwords[0]='You cannot find anything interesting.'
		$failwords[1]='You find some clothes and underwear.'
		$failwords[2]='You dig through your mother''s closet, but find nothing interesting except underwear.'
		*pl $failwords[rand(0,2)]
		killvar '$failwords'
	else
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/wardrobetoy.jpg"></center>'
		$findmomtoy[0]='Digging through the closet you find a <a href="exec: gt ''bedrPar2'',''momtoy_play''">Dildo</a>'
		$findmomtoy[1]='Rummaging through your mother''s lingerie you find a small <a href="exec: gt ''bedrPar2'',''momtoy_play''">Dildo</a>'
		$findmomtoy[2]='You find your mother''s <a href="exec: gt ''bedrPar2'',''momtoy_play''">toy</a>'
		*pl $findmomtoy[rand(0,2)]
		killvar '$findmomtoy'
	end
	act 'Close the Cabinet': gt $loc, $metka
end

if $ARGS[0] = 'momtoy_play':
	*clr & cla
	menu_off = 1
	dildo = 1
	dildohand = 10
	selfmomtoyplay = 1
	'<center><img <<$set_imgh>> src="images/shared/sex/mast/start.jpg"></center>'
	if pcs_horny < 70:
		act 'Put it back': gt $loc, $metka
	else
		act 'Play with it': gt 'selfplay', 'start'
	end
end

--- bedrPar2 ---------------------------------

