# agentned

if $ARGS[0] = 'start':
	CLOSE ALL
	
	*clr & cla
	$loc = 'agentned'
	$loc_arg = $ARGS[0]
	$location_type = 'indoors'
	$menu_loc = 'agentned'
	$menu_arg = 'start'
	menu_off = 0
	
	gs'stat'
	gs'dina'
	gs 'housing', 'rent'
	'<center><b><font color = maroon>Kirsanova Real Estate Agency</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/realestate/agenstvo.jpg"></center>'
	*nl
	'This is the head office of the largest real estate agency in the Leningrad Oblast.'
	*nl
	'At the reception, you notice a large print banner:'
	'"Please be aware that all sales transactions are made exclusively through your bank account. Rent payments are excluded from this policy."'

	cityaptdays = func('homes_properties', 'get_rent_days', 'city_apartment')
	oldtowndays = func('homes_properties', 'get_rent_days', 'old_town_apartment')
	if owned_property['city_apartment'] = 1 and cityaptdays > 0:
		*nl
		'You have <B><<cityaptdays>> days</B> remaining on your St. Petersburg apartment lease, and your monthly rent is <<$prop_rent[''city_apartment'']>> <b>₽</b>. '+iif(money >= prop_rent['city_apartment'],'You can make a rent <a href="exec:money -= prop_rent[''city_apartment''] & gs ''homes_properties'', ''add_rent_days'', ''city_apartment'' & gt ''agentned'', ''start''">payment</a> in cash to extend it.','You can''t afford to pay your rent at the moment')+''
	end
	if owned_property['old_town_apartment'] = 1 and oldtowndays > 0:
		*nl
		gs 'homes_properties', 'get_rent', 'old_town_apartment'
		'You have <B><<oldtowndays>> days</B> remaining on your Pushkin apartment lease, and your monthly rent is <<$prop_rent[''old_town_apartment'']>> <b>₽</b>. '+iif(money >= prop_rent['old_town_apartment'],'You can make a rent <a href="exec:money -= prop_rent[''old_town_apartment''] & gs ''homes_properties'', ''add_rent_days'', ''old_town_apartment'' & gt ''agentned'', ''start''">payment</a> in cash to extend it.','You can''t afford to your pay rent at the moment')+''
	end
	killvar 'cityaptdays'
	killvar 'oldtowndays'

	count = ARRSIZE('owned_property') 
	if count > 0:
		$text_to_print += ''
		i = 1
		:rent_loop
			if owned_property[i] = 2 or owned_property[i] = 3:	
				print = 1			
				gs 'homes_properties', 'get_property_attr', $owned_property[i]
				if owned_property[i] = 3:
					$text_to_print += '<<$property[''agency_display'']>> which is currently occupied by your tenants. <br/>'
				elseif owned_property[i] = 2:
					'<<$property[''agency_display'']>> which is currently occupied by your tenants. <br/>'
				end
				killvar '$property'
			end
			i += 1
		if i < count: jump 'rent_loop'
	end
	if print = 1:
		*nl
		'<b>You own the following property</b>'
		*nl
		$text_to_print
		*nl
		'We are at your service if you decide to <a href="exec:gt ''agentned'',''sell''">sell</a>.'
	end
	killvar 'count'
	killvar 'i'
	killvar 'print'
	killvar '$text_to_print'

	act 'Leave': minut += 5 & gt 'city_center'

	act 'View rentals': gt 'agentned', 'rent'
	act 'View properties for sale': gt 'agentned', 'buy'

	if owned_property['city_apartment'] = 1:
		act 'Cancel the lease of the city apartment':
			cla & *nl
			'Attention: You won''t be able to return to your apartment when you cancel the lease.'

			act 'Return': gt'agentned', 'start'
			act 'Cancel it': owned_property['city_apartment'] = 0 & gt'agentned', 'start'
		end
	end

	if owned_property['old_town_apartment'] = 1:
		act 'Cancel the lease of the apartment in Pushkin':
			cla & *nl
			'Attention: You won''t be able to return to your apartment when you cancel the lease.'

			act 'Return': gt'agentned', 'start'
			act 'Cancel it': owned_property['old_town_apartment'] = 0 & gt'agentned', 'start'
		end
	end
end

if $ARGS[0] = 'buy':
	*clr & cla
	$loc = 'agentned'
	$loc_arg = $ARGS[0]
	$location_type = 'indoors'
	$menu_loc = 'agentned'
	$menu_arg = 'buy'
	menu_off = 0
	gs 'housing', 'sale'
	'<center><b><font color = maroon>Real Estate Agency</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/realestate/agenstvo.jpg"></center>'
	*nl
	minut += 5
	gs 'stat'
	
	act 'Return':gt'agentned', 'start'
	
	if property_owned['city_apartment'] = 0 or property_owned['village_cottage'] = 0 or owned_property['matryona_mansion'] = 0 or owned_property['city_house'] = 0:
		if property_owned['city_apartment'] = 0:
			'There is an apartment in the city residential area available for <<$prop_price[''city_apartment'']>> <b>₽</b>.'
			*nl
			if karta + bankDebtLimit >= $prop_price['city_apartment']: act 'Buy the city residential apartment': gt 'agentned', 'buy_property', 'city_apartment'
		elseif property_owned['city_apartment'] = 1:
			'You could buy your current rental apartment for <<$prop_price[''city_apartment'']>> <b>₽</b>.'
			*nl
			if karta + bankDebtLimit >= prop_price['city_apartment']: act 'Buy the city residential apartment': gt 'agentned', 'buy_property', 'city_apartment'
		end

		if property_owned['village_cottage'] = 0:
			'There is a holiday cottage with an adjacent allotment in the village for <<$prop_price[''village_cottage'']>> <b>₽</b>.'
			*nl
			if karta + bankDebtLimit >= prop_price['village_cottage']: act 'Buy the cottage in the village': gt 'agentned','buy_property', 'village_cottage'
		end

		if onwed_property['matryona_mansion'] = 0:
			'There is a plot of land in the suburbs with planning permission for a mansion for <<$prop_price[''matryona_mansion'']>> <b>₽</b>.'
			*nl
			if karta + bankDebtLimit >= prop_price['matryona_mansion']: act 'Buy the land plot in the suburbs': gt 'agentned','buy_property', 'matryona_mansion'
		end
		
		if owned_property['city_house'] = 0:
			'There is a small house on the edge of the city residential area for <<$prop_price[''city_house'']>> <b>₽</b>.'
			*nl
			if karta + bankDebtLimit >= prop_price['city_house']: act 'Buy the house on the edge of the city residential area': gt 'agentned','buy_property', 'city_house'
		end
	else
		'We currently have no properties listed for sale.'
	end
end


!! TODO: This needs a bit of thinking because Sveta can buy the sold property again, for the old price, but will get all the renovation
!! bonuses because those variables are not cleared. 
!! Two options:
!!   1. Sold properties can''t be bought back
!!   2. The sales value is increased based on what Sveta sold it for plus the rand() range
if $ARGS[0] = 'sell':
	*clr & cla
	$loc = 'agentned'
	$loc_arg = $ARGS[0]
	$location_type = 'indoors'
	$menu_loc = 'agentned'
	$menu_arg = 'sell'
	menu_off = 0
	gs 'housing', 'sale'
	'<center><b><font color = maroon>Real Estate Agency</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/realestate/agenstvo.jpg"></center>'
	*nl
	
	act 'Return':gt'agentned', 'start'
	
	if owned_property['city_apartment'] = 2:
		if rembedr = 1 and remsitr = 1 and remkorr = 1 and remvanr = 1 and remkuhr = 1:
			!Renovated city residential apartment
			offer1 = ((prop_price['city_apartment'] + 250000) + rand(-100000, 100000)) 
		else
			offer1 = (prop_price['city_apartment'] + rand(-100000, 100000))
		end
		'We have found a buyer for your apartment in the city residential area. They will offer <<offer1>> <b>₽</b>, minus 5% for fees and taxes, bringing the total to <<offer1/100*95>>'
		act 'Sell the city apartment':
			*clr
			menu_off = 1
			'<center><b><font color = maroon>Real Estate Agency</font></b></center>'
			'<center><img <<$set_imgh>> src="images/locations/city/citycenter/realestate/agenstvo_paperwork.jpg"></center>'
			minut += 30
			'You spend thirty minutes filling out the paperwork for the sale of your renovated apartment. You will suffer a loss of around 5% for fees and taxes. <<offer1/100*95>> <b>₽</b> has been paid into your bank account.'
			gs 'homes_properties', 'set_home', 'city_apartment', 0
			if bankaccount = 1:
				karta += offer1/100*95
			else
				money += offer1/100*95
			end
			delact 'Sell the city apartment'
			gs 'stat'
		end
	end
	
	if property_owned['village_cottage'] = 2:
		if func('homes_properties', 'property_renovated', 'village_cottage') = 1: 
			!Renovated communal cottage
			offer2 = ((prop_price['village_cottage'] + 200000) + rand(-10000, 10000))
		else
			!Communal cottage
			offer2 = (prop_price['village_cottage'] + rand(-5000, 5000))
		end
		'We have found a buyer for your small communal cottage. They will offer <<offer2>> <b>₽</b>, minus 5% for fees and taxes, bringing the total to <<offer2/100*95>>'
		act 'Sell the cottage':
			*clr
			menu_off = 1
			'<center><b><font color = maroon>Real Estate Agency</font></b></center>'
			'<center><img <<$set_imgh>> src="images/locations/city/citycenter/realestate/agenstvo_paperwork.jpg"></center>'
			minut += 30
			'You spend thirty minutes filling out the paperwork for the sale of your small cottage. You will suffer a loss of around 5% for fees and taxes. <<offer2/100*95>> <b>₽</b> has been paid into your bank account.'
			gs 'homes_properties', 'set_home', 'village_cottage', 0
			if bankaccount = 1:
				karta += offer/100*95
			else
				money += offer/100*95
			end
			delact 'Sell the cottage'
			gs 'stat'
		end
	end
	
	if owned_property['matryona_mansion'] = 2:
		if func('homes_properties', 'property_construction_status', 'matryona_mansion') = 0:
			!Suburban land only - currently too many variables for selling upgraded mansion - might do later - 3xpurt.
			offer3 = (prop_price['matryona_mansion'] + rand(-50000, 50000))
			'We have found a buyer for your vacant plot of land in the suburbs. They will offer <<offer3>> <b>₽</b>, minus 5% for fees and taxes, bringing the total to <<offer3/100*95>>'
			act 'Sell the vacant plot':
				*clr
				menu_off = 1
				'<center><b><font color = maroon>Real Estate Agency</font></b></center>'
				'<center><img <<$set_imgh>> src="images/locations/city/citycenter/realestate/agenstvo_paperwork.jpg"></center>'
				minut += 30
				'You spend thirty minutes filling out the paperwork for the vacant plot. You will suffer a loss of around 5% for fees and taxes. <<offer3/100*95>> <b>₽</b> has been paid into your bank account.'
				gs 'homes_properties', 'set_home', 'matryona_mansion', 0
				if bankaccount = 1:
					karta += offer3/100*95
				else
					money += offer3/100*95
				end
				delact 'Sell the vacant plot'
				gs 'stat'
			end
		else 
			!Suburban land only - currently too many variables for selling upgraded mansion - might do later - 3xpurt.
			'Sorry, we are unable to find a buyer for your property at this time.'
		end
	end
	
	if owned_property['city_house'] = 2:
		!! - might do later - Alaratt.
		'Sorry, we are unable to find a buyer for your house at this time.'
	end

	if property_owned['city_apartment'] = 0 and property_owned['village_cottage'] = 0 and owned_property['city_house'] = 0 and owned_property['matryona_mansion'] = 0:
		'You don''t currently own any property.'
	end
end

if $ARGS[0] = 'rent':
	*clr & cla
	$loc = 'agentned'
	$loc_arg = $ARGS[0]
	$location_type = 'indoors'
	$menu_loc = 'agentned'
	$menu_arg = 'rent'
	menu_off = 0
	'<center><b><font color = maroon>Real Estate Agency</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/realestate/agenstvo.jpg"></center>'
	*nl
	minut += 5
	gs 'stat'
	
	act 'Return':gt'agentned', 'start'
	
	if property_owned['city_apartment'] > 0 and property_owned['old_town_apartment'] > 0:
		'We currently have no properties listed for rent.'
	elseif property_owned['city_apartment'] = 0 and property_owned['old_town_apartment'] = 0:
		*nl
		'There are two apartments available for rent. They are located in the city residential area and in Pushkin.'
		act 'View the city apartment details': gt 'agentned', 'rent_cityres'
		act 'View the Pushkin apartment details': gt 'agentned', 'rent_pushkin'
	elseif property_owned['city_apartment'] = 0:
		'There is a property for rent in the city residential area'
		act 'View the city apartment details': gt 'agentned', 'rent_cityres'
	elseif property_owned['old_town_apartment'] = 0:
		'There is a property for rent in the old town of Pushkin'
		act 'View the Pushkin apartment details': gt 'agentned', 'rent_pushkin'
	end
end

if $ARGS[0] = 'rent_cityres':
	*clr & cla
	menu_off = 1
	'<center><b><font color = maroon>Real Estate Agency</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/realestate/agenstvo.jpg"></center>'
	*nl
	'"We have a flat available right now in the St. Petersburg residential area." The receptionist says as he starts pulling out the paperwork, "Rent is <<$prop_rent[''city_apartment'']>> ₽ and leases have to be renewed every 30 days. Payment will be automatically deducted from your account or you can come into the office to pay. When you sign for the lease you must make your first month''s rental payment upfront."'
	'<br>He pulls out a pen and slides the paperwork onto the counter. "Interested? You can move in right away," he calmly says.'

	if money >= prop_rent['city_apartment'] or karta >= prop_rent['city_apartment']:
		act 'Agree and pay':
			*clr & cla
			cls
			minut += 30
			if money >= prop_rent['city_apartment']: 
				money -= prop_rent['city_apartment']
			else
				karta -= prop_rent['city_apartment']
			end
			gs 'homes_properties', 'set_ownership', 'city_apartment', 1
			if func( 'homes_properties', 'get_accessible_count') = 1:
				gs 'homes_properties', 'set_home', 'city_apartment'
			else
				$set_homeyn = input("Would you like to set this as your new home? (yes/no)")
				if $set_homeyn = 'yes': gs 'homes_properties', 'set_home', 'city_apartment'
				killvar '$set_homeyn'
			end
			gs'stat'
			'<center><b><font color = maroon>Real Estate Agency</font></b></center>'
			'<center><img <<$set_imgh>> src="images/locations/city/citycenter/realestate/agenstvo_paperwork.jpg"></center>'
			*nl
			'You take the pen then start filling out the paperwork. After writing for half an hour, you hand over the money and receive the keys to your new apartment.'

			act 'Return':gt'agentned', 'start'
		end
	else
		*nl
		'You do not have enough money with you or in your bank account to pay the rent.'
	end
	
	act 'Decline':
		*clr & cla
		minut += 5
		'<center><b><font color = maroon>Real Estate Agency</font></b></center>'
		'<center><img <<$set_imgh>> src="images/locations/city/citycenter/realestate/agenstvo.jpg"></center>'
		*nl
		'You shake your head, "Actually, I need more time to think about this."'
		'<br>The receptionist seems a bit miffed, but says nothing as he takes back the paperwork and stuffs it back into his desk drawer.'

		act 'Return':gt'agentned', 'start'
	end
end

if $ARGS[0] = 'rent_pushkin':
	*clr & cla
	menu_off = 1
	'<center><b><font color = maroon>Real Estate Agency</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/realestate/agenstvo.jpg"></center>'
	*nl
	'"We have a small apartment available right now that is in Pushkin’s city center. It’s a little bit pricey, but it’s been recently renovated to be more modern. It looks quite nice, so you won’t find a place like that outside of Pushkin." The receptionist says as he starts pulling out the paperwork. "Rent is <<$home_rent_txt[4]>> ₽ and leases have to be renewed every 30 days, which can be taken automatically from your account or you can come in and pay directly. Last, but not least, you have to make your first payment up front at signing."'
	'<br>He pulls out a pen and slides the paperwork onto the counter. "Interested? You can move in right away," he calmly says.'
	
	if money >= property_owned['old_town_apartment'] or karta >= property_owned['old_town_apartment']:
		act 'Agree and pay':
			*clr & cla
			minut += 30
			if money >= property_owned['old_town_apartment']: 
				money -= property_owned['old_town_apartment']
			else
				karta -= property_owned['old_town_apartment']
			end
			gs 'homes_properties', 'set_ownership', 'old_town_apartment', 1
			if func( 'homes_properties', 'get_accessible_count') = 1:
				gs 'homes_properties', 'set_home', 'old_town_apartment'
			else
				$set_homeyn = input("Would you like to set this as your new home? (yes/no)")
				if $set_homeyn = 'yes': gs 'homes_properties', 'set_home', 'old_town_apartment'
				killvar '$set_homeyn'
			end
			gs'stat'
			'<center><b><font color = maroon>Real Estate Agency</font></b></center>'
			'<center><img <<$set_imgh>> src="images/locations/city/citycenter/realestate/agenstvo_paperwork.jpg"></center>'
			*nl
			'You take the pen and start filling out the paperwork. After writing for an hour, you hand over the money and receive the keys to your new apartment.'

			act 'Return':gt'agentned', 'start'
		end
	else
		*nl
		'You do not have enough money with you or in your bank account to rent this.'
	end
	
	act 'Decline':
		*clr & cla
		minut += 5
		gs 'stat'
		'<center><b><font color = maroon>Real Estate Agency</font></b></center>'
		'<center><img <<$set_imgh>> src="images/locations/city/citycenter/realestate/agenstvo.jpg"></center>'
		*nl
		'You shake your head, "Actually, I need more time to think about this."'
		'<br>The receptionist seems a bit miffed, but says nothing as he takes back the paperwork and stuffs it back into his desk drawer.'

		act 'Return':gt'agentned', 'start'
	end
end

if $ARGS[0] = 'buy_property':
	*clr & cla
	menu_off = 1
	gs 'housing', 'sale'
	gs 'homes_properties', 'get_property_attr', $ARGS[1]
	minut += 30
	karta -= $property['price']
	gs 'homes_properties', 'set_ownership', $ARGS[1], 2

	$set_homeyn = input("Would you like to set this as your new home? (yes/no)")
	if $set_homeyn = 'yes': gs 'homes_properties', 'set_home', $ARGS[1]
	
	killvar '$set_homeyn'

	!!TODO: this etoexhib will need some rethinking I get what it means by now, but some better name would be better.
	if $ARGS[1] = 'village_cottage': etoexhib = 0
	gs'stat'
	'<center><b><font color = maroon>Real Estate Agency</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/realestate/agenstvo_paperwork.jpg"></center>'
	*nl
	'You spend half an hour filling in the paperwork to buy the <<$property[''name'']>>. <<$property[''price'']>> <b>₽</b> has been removed from your bank account.'

	act 'Return':gt'agentned', 'start'
end


--- agentned ---------------------------------

