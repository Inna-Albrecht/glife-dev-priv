# homes_properties

if ARRSIZE('home_name') < 1:
	!! The list goes like this:
	!!    1. $home_name['property code']
	!!    2. $home_name['property id']  - for backward compatibility only, nothing else and the number is a string index
	!!    2. $home_name['enctrance location'] 
	!!    3.- N.  $home_name['other locations']

	!! CITY APARTMENT
	$home_name['city_apartment'] = 'city_apartment'
	$home_name['1'] = 'city_apartment'
	$home_name['korr'] = 'city_apartment'
	$home_name['bedr'] = 'city_apartment'
	$home_name['sitr'] = 'city_apartment'
	$home_name['kuhr'] = 'city_apartment'
	$home_name['vanr'] = 'city_apartment'

	!! PARENTS HOME
	$home_name['parents_home'] = 'parents_home'
	$home_name['2'] = 'parents_home'
	$home_name['code'] = 'parents_home'
	$home_name['korrPar'] = 'parents_home'
	$home_name['bedrPar'] = 'parents_home'
	$home_name['sitrPar'] = 'parents_home'
	$home_name['kuhrPar'] = 'parents_home'
	$home_name['vanrPar'] = 'parents_home'

	!! VILLAGE COTTAGE
	$home_name['village_cottage'] = 'village_cottage'
	$home_name['3'] = 'village_cottage'
	$home_name['dachamy'] = 'village_cottage'
	$home_name['dachain'] = 'village_cottage'
	$home_name['dachaogr'] = 'village_cottage'
	$home_name['dachaogr0'] = 'village_cottage'

	!! OLD TOWN (PUSHKIN) APARTMENTS
	$home_name['old_town_apartment'] = 'old_town_apartment'
	$home_name['4'] = 'old_town_apartment'
	$home_name['korr2x'] = 'old_town_apartment'
	$home_name['bedr2x'] = 'old_town_apartment'
	$home_name['sitr2x'] = 'old_town_apartment'
	$home_name['kuhr2x'] = 'old_town_apartment'
	$home_name['vanr2x'] = 'old_town_apartment'

	!! MATRYONA MANSION
	$home_name['matryona_mansion'] = 'matryona_mansion'
	$home_name['5'] = 'matryona_mansion'
	$home_name['city_mansion_entrance'] = 'matryona_mansion'
	$home_name['city_mansion_residence_1'] = 'matryona_mansion'
	$home_name['city_mansion_residence_2'] = 'matryona_mansion'
	$home_name['city_mansion_residence_3'] = 'matryona_mansion'

	!! PAVLOVSK HOTEL
	$home_name['pavlovsk_hotel'] = 'pavlovsk_hotel'
	$home_name['6'] = 'pavlovsk_hotel'
	$home_name['pav_hotel'] = 'pavlovsk_hotel'
	$home_name['HotelRoom'] = 'pavlovsk_hotel'

	!! LIVE IN MAID BEDROOM
	$home_name['maid_bedroom'] = 'maid_bedroom'
	$home_name['7'] = 'maid_bedroom'
	$home_name['nichBedroomServant'] = 'maid_bedroom'

	!! NIKO''S APARTMENT
	$home_name['niko_apartment'] = 'niko_apartment'
	$home_name['8'] = 'niko_apartment'
	!! This home actually doesn''t exist yet, but it was defined,
	!! has an ID and all so, I am keeping it here

	!! SHULGA APARTMENT
	$home_name['shulga_apartment'] = 'shulga_apartment'
	$home_name['9'] = 'shulga_apartment'
	$home_name['shulgahall'] = 'shulga_apartment'
	$home_name['shulgabath'] = 'shulga_apartment'
	$home_name['shulgabed'] = 'shulga_apartment'
	$home_name['shulgakuh'] = 'shulga_apartment'
	
	!! LYCEUM DORM
	$home_name['lyceum_dorm'] = 'lyceum_dorm'
	$home_name['10'] = 'lyceum_dorm'
	$home_name['dom_gor'] = 'lyceum_dorm'

	!! UNIVERSITY DORM
	$home_name['university_dorm'] = 'university_dorm'
	$home_name['11'] = 'university_dorm'
	$home_name['uni_grounds'] = 'university_dorm'
	$home_name['uni_dorm'] = 'university_dorm'
	
	!! HUNTERS LODGE
	$home_name['hunters_lodge'] = 'hunters_lodge'
	$home_name['12'] = 'hunters_lodge'
	$home_name['swamphouse'] = 'hunters_lodge'
	$home_name['swamp_yard'] = 'hunters_lodge'


	!! SHARED APARTMENT - PAVLOVSK
	$home_name['shared_apartment'] = 'shared_apartment'
	$home_name['13'] = 'shared_apartment'
	$home_name['pav_shared_apt'] = 'shared_apartment'

	!! GRANDPARENTS HOUSE
	$home_name['grandparents_house'] = 'grandparents_house'
	$home_name['14'] = 'grandparents_house'
	$home_name['gaddvor'] = 'grandparents_house'
	$home_name['gadgarden'] = 'grandparents_house'
	$home_name['gadbana'] = 'grandparents_house'
	$home_name['gadgarden'] = 'grandparents_house'
	$home_name['gadhouse'] = 'grandparents_house'


	!! CITY HOUSE
	$home_name['city_house'] = 'city_house'
	$home_name['15'] = 'city_house'
	$home_name['city_house_res_misc'] = 'city_house'
	$home_name['city_house_res_bath'] = 'city_house'
	$home_name['city_house_re_bedr'] = 'city_house'
	$home_name['city_house-res_kitch'] = 'city_house'


	!! MEYNODLD HOUSEHOLD
	$home_name['meynold_household'] = 'meynold_household'
	$home_name['16'] = 'meynold_household'
	$home_name['mey_home'] = 'meynold_household'
end

!!{ All the existing properties should be added here. 
	id:			The old numerical id - for the nostalgia
	code:		The code of the property. Example: 'city_apartment', 'village_cottage'
	name:		General name to display
	display: 	The string to use in the middle of a sentence
	agency_display: The string the agency location uses 
	town:		The town/city/village the property can be found
	location:	The region within the town
	entrance:	The location where the charactes enter
	type:		The type of the property - it is used by some checks right now, dating app and such. 
				In the future this could be expanded to things like business properties, offices or other whatever we 
				want to simplify some checks.
	price:		The sales value of the property. Both as a number and as a string. !! NOTE: Maybe we don''t need it, 
				the numeric value can be displayed too and formatting can be done otherwise
	rent:		The monthly rent of the property. Both as a number and as a string. !! NOTE: Maybe we don''t need it, 
				the numeric value can be displayed too and formatting can be done otherwise
	rental:		Whether the property can be rented by Sveta or not. 
				1: Can be rented
				0: Can''t be rented // Examples are: Parents home, Shulga apartment, Hotel room in Pavlovsk, University dorm.
				Possible values are:
				1: rented via the agency, monthly rent, utility bills are paid, can be furnished, maybe renovated,
				   definitely needs an internet subscription
				4: not really a rented apartment, it is a "can live here" property. No utility is paid, 
				   Sveta can''t change anything
				   Examples are: Parents home, Shulga apartment, Hotel room in Pavlovsk, University dorm.

	free_internet: if the property comes with free internet (uses subscription['free']) then the value is `1`
				   Right now there are two potential homes with this: Meynold Household, University Dorm	

	internet_enabled: If the value is 1, then Sveta can buy, cancel, transfer an internet subscription here


	Property Types
	!! IMPORTANT: 
	!! NOTE: The types are not 100% clear and decided yet, need some more thinking. Also new ones can be added.

	permanent_residence:	This is a home that can be used for everything. Certain things don''t allow the more "transient"
							homes, like the dating app in Uni.
	temporary_residence:	This is a home that some events/actions won''t consider.

	New ones in the future could be for porperties that Sveta can own, rent, used, is not a home but can be changed/customised, etc. 
	For example a music studio/practice room, or a gallery, or a newspaper kiosk

	To make life easier, if is 0 or ''  for a property, then I didn''t include it. 
	Undefined strings will always evaluate to '' and undefined integers have a value of 0.
}

!! Always call this function with gs and use killvar'property' after finished with the data.
!!
!! ARGS[1] - the proprty code - a string identifier of the property. 
if $ARGS[0] = 'get_property_attr':
	if $ARGS[1] = 'city_apartment':
		property['id'] = 1
		$property['code'] = 'city_apartment'
		$property['name'] = 'City residential apartment'
		$property['display'] = 'apartment in St. Petersburg''s residential area'
		$property['agency_display'] = 'An apartment in St. Petersburg''s residential area'
		$property['town'] = 'city'
		$property['location'] = 'city_residential'
		$property['location_arg'] = ''
		$property['entrance'] = 'korr'
		$property['entrance_arg'] = ''
		$property['type'] = 'permanent_residence'
		$property['price'] = '800,000'
		$property['rent'] = '15,000'		
		property['price'] = 800000
		property['rent'] = 15000
		property['rental'] = 1
		property['internet_enabled'] = 1
	elseif $ARGS[1] = 'parents_home':
		property['id'] = 2
		$property['code'] = 'parents_home'
		$property['name'] = 'Parent''s home in Pavlovsk'
		$property['display'] = 'parent''s home in Pavlovsk'
		$property['town'] = 'pavlovsk'
		$property['location'] = 'pav_complex'
		$property['location_arg'] = 'start'
 		$property['entrance'] = 'korrPar'
		$property['entrance_arg'] = 'start'
		$property['type'] = 'permanent_residence'
	elseif $ARGS[1] = 'village_cottage':
		property['id'] = 3
		$property['code'] = $ARGS[1]
		$property['name'] = 'My cottage in the cooperative village'
		$property['display'] = 'cottage in the cooperative village'
		$property['agency_display'] = 'A cottage located in the cooperative village'
		$property['town'] = 'city'
		$property['location'] = 'dachamy'
		$property['location_arg'] = ''
		$property['entrance'] = 'dachamy'
		$property['entrance_arg'] = ''
		$property['type'] = 'permanent_residence'
		$property['price'] = '150,000'
		property['price'] = 150000
		!! Currently no cable or internet can be installed at this property, maybe later.
		property['internet_enabled'] = 0
	elseif $ARGS[1] = 'old_town_apartment':
		property['id'] = 4
		$property['code'] = $ARGS[1]
		$property['name'] = 'Old town apartment'
		$property['display'] = 'old town apartment'
		$property['angecy-display'] = 'An apartment in the old town of Puskin.'
		$property['town'] = 'oldtown'
		$property['location'] = 'liames'
		$property['location_arg'] = ''
		$property['entrance'] = 'korr2x'
		$property['entrance_arg'] = ''
		$property['type'] = 'permanent_residence'
		$property['rent'] = '10,000'		
		property['price'] = 10000
		property['rental'] = 1
		property['internet_enabled'] = 1
	elseif $ARGS[1] = 'matryona_mansion':
		property['id'] = 5
		$property['code'] = $ARGS[1]
		$property['name'] = 'Matryona mansion'
		$property['display'] = 'Matryona mansion'
		$property['agency_display'] = 'A plot of land in the St. Petersburg''s suburbs'
		$property['town'] = 'city'
		$property['location'] = 'city_mansion_entrance'
		$property['location_arg'] = ''
		$property['entrance'] = 'city_mansion_residence_1'
		$property['entrance_arg'] = 'yfoyer'
		$property['type'] = 'permanent_residence'
		$property['price'] = '1,500,000'
		property['price'] = 1500000
		property['internet_enabled'] = 1
		property['contstruction_needed'] = 1
	elseif $ARGS[1] = 'pavlovsk_hotel':
		property['id'] = 6
		$property['code'] = $ARGS[1]
		$property['name'] = 'My hotel room in Pavlovsk'
		$property['display'] = 'hotel room in Pavlovsk'
		$property['town'] = 'pavlovsk'
		$property['location'] = 'pav_market'
		$property['location_arg'] = ''
		$property['entrance'] = 'pav_hotel'
		$property['entrance_arg'] = ''
		$property['type'] = 'temporary_residence'
	elseif $ARGS[1] = 'maid_bedroom':
		property['id'] = 7
		$property['code'] = $ARGS[1]
		$property['name'] = 'Nicholas'' Apartment'
		$property['display'] = 'maid bedroom in Nicholas'' apartment'
		$property['town'] = 'city'
		$property['location'] = 'city_center'
		$property['location_arg'] = ''
		$property['entrance'] = 'nichBedroomServant'
		$property['entrance_arg'] = 'start'
		$property['type'] = 'permanent_residence'
	elseif $ARGS[1] = 'niko_apartment':
		property['id'] = 8
		$property['code'] = $ARGS[1]
		$property['name'] = 'Niko''s apartment'
		$property['display'] = 'Niko''s apartment'
		$property['town'] = 'pavlovsk'
		$property['location'] = 'pav_residential'
		$property['location_arg'] = ''
		$property['entrance'] = 'pav_residential'
		$property['entrance_arg'] = ''
		$property['type'] = 'temporary_residence'
	elseif $ARGS[1] = 'shulga_apartment':
		property['id'] = 9
		$property['code'] = $ARGS[1]	
		$property['name'] = 'Uncle Sergey''s apartment'
		$property['display'] = 'Uncle Sergey''s apartment'
		$property['town'] = 'pavlovsk'
		$property['location'] = 'pav_complex'
		$property['location_arg'] = 'start'
		$property['entrance'] = 'shulgahall'
		$property['entrance_arg'] = ''
		$property['type'] = 'temporary_residence'
	elseif $ARGS[1] = 'lyceum_dorm':
		property['id'] = 10
		$property['code'] = $ARGS[1]	
		$property['name'] = 'Lyceum school'
		$property['display'] = 'room in the Lyceum dorm'
		$property['town'] = 'pavlovsk'
		$property['location'] = 'pav_commercial'
		$property['location_arg'] = ''
		$property['entrance'] = 'etoexhib'
		$property['entrance_arg'] = 'pos40'
		$property['type'] = 'permanent_residence'
	elseif $ARGS[1] = 'university_dorm':
		property['id'] = 11
		$property['code'] = $ARGS[1]
		$property['name'] = 'University halls'
		$property['display'] = 'room in the university dorm'
		$property['town'] = 'city'
		$property['location'] = 'city_island'
		$property['location_arg'] = ''
		$property['entrance'] = 'uni_dorm'
		$property['entrance_arg'] = 'dorm_room'
		$property['free-internet'] = 1
		$property['type'] = 'permanent_residence'
	elseif $ARGS[1] = 'hunters_lodge':
		property['id'] = 12
		$property['code'] = $ARGS[1]
		$property['name'] = 'Hunter''s lodge in Gadukino'
		$property['display'] = 'hunter''s lodge in Gadukino'
		$property['town'] = 'gadukino'
		$property['location'] = 'swamp_yard'
		$property['location_arg'] = 'start'
		$property['entrance'] = 'swamphouse'
		$property['entrance_arg'] = 'start'
		$property['type'] = 'temporary_residence'
	elseif $ARGS[1] = 'shared_apartment':
		property['id'] = 13
		$property['code'] = $ARGS[1]	
		$property['name'] = 'Shared apartment in Pavlovsk'
		$property['display'] = 'shared apartment in Pavlovsk'
		$property['town'] = 'pavlovsk'
		$property['location'] = 'pav_complex'
		$property['location_arg'] = 'start'
		$property['entrance'] = 'pav_shared_apt'
		$property['entrance_arg'] = 'start'
		$property['type'] = 'temporary_residence'
	elseif $ARGS[1] = 'grandparents_house':
		property['id'] = 14
		$property['code'] = $ARGS[1]
		$property['name'] = 'Grandparent''s house in Gadukino'
		$property['display'] = 'grandparent''s house in Gadukino'
		$property['town'] = 'gadukino'
		$property['location'] = 'gaddvor'
		$property['location_arg'] = ''
		$property['entrance'] = 'gaddvor'
		$property['entrance_arg'] = ''
		$property['type'] = 'temporary_residence'
	elseif $ARGS[1] = 'city_house':
		property['id'] = 15
		$property['code'] = $ARGS[1]
		$property['name'] = 'City residential house'
		$property['display'] = 'house in the St. Petersburg''s residential area'
		$property['agency_display'] = 'A house in the St. Petersburg''s residential area'
		$property['town'] = 'city'
		$property['location'] = 'city_residential'
		$property['location_arg'] = ''
		$property['entrance'] = 'city_house_res_misc'
		$property['entrance_arg'] = 'fronty'		
		$property['type'] = 'permanent_residence'
		$property['price'] = '100,000'
		property['price'] = 100000
		property['internet_enabled'] = 1
	elseif $ARGS[1] = 'meynold_household':
		property['id'] = 16
		$property['code'] = $ARGS[1]
		$property['name'] = 'Meynold Household'
		$property['display'] = 'the Meynold Household'
		$property['town'] = 'pavlovsk'
		$property['location'] = 'pav_residential'
		$property['location_arg'] = ''
		$property['entrance'] = 'mey_home'
		$property['entrance_arg'] = 'door'
		$property['type'] = 'temporary_residence'
		property['free_internet'] = 1
	else
		gs 'homes_properties', 'mod_property_query_hook'
	end
end

!!-----------------------------------------------------------------------------------------------------
!!
!!   General calls 
!!
!!-----------------------------------------------------------------------------------------------------

!! Set the current home for Sveta based on either the property code or the id - for backward compatibility or some mods
!! $ARGS[1] - a string to identify the property.
!!			  Possible values:
!!			  A property code - 'city_apartment', 'village_cottage', etc.
!!			  A location name - $loc passed
!! 			  An empty string - in this case ARGS[2] is mandatory
!!			  A number as str - really should remove this 	
!!
if $ARGS[0] = 'set_home':
	if $ARGS[1] = '' and $ARGS[2] = 0:
		!! WARNING: No way to identify home, missing arguments
	elseif $ARGS = '':
		gs 'homes_properties', 'get_property_attr', $home_name[$loc]
	else
		gs 'homes_properties', 'get_property_attr', $home_name[$ARGS[1]]
	end

	COPYARR 'home','property'
	$home['current'] = $property['code']
	killvar 'property'
end

!! $ARGS[1]: the location for which the access is set
!!			Accepted values
!!			- property code   -> 'city_apartment', 'parents_home'
!!			- a location      ->  'bedrPar, 'koo2x' - A value that can shop up in $loc
!!			- empty strying   ->  '' - will work as if a location was passed, but will use the actual value of $loc
!!			- A number as str -> '1', '2' - this should be the id of the property 
!!			  					NOTE: only added in case someone really want to use the old ids
!! ARGS[2]: ownership type
!!			0: no acces  -> 'no access'
!!			1: not owned but can live her - a rented property or living at some else''s place
!!			2: owned     -> 'owned'
!!			3: owned and rented out -> 'tenants'
!! ARGS[3]: construction status - optional
!!			0: land only 
!!			1: foundation, frames built, utilities are connected
!!			2: fully built
!! ARGS[4]:renovation status - optional
!!			0: not renovated
!!			1: fully renovated
if $ARGS[0] = 'set_access':
	if $ARGS[1] = '':
		$propcode = $home_name[$loc]
	else
		$propcode = $home_name[$ARGS[1]]
	end
	orig_status_code = accessible_property[$propcode]
	new_status_code = ARGS[2]

	if orig_status_code ! new_status_code:
		if $accessible_property[$propcode] = '' or accessible_property['<<$propcode>>-display'] = '' or accessible_property['<<$propcode>>-name']:
			gs 'homes_properties', 'get_property_attr', $propcode		
			if ARGS[2] = 1 and property['rental'] = 0: new_status_code = 4

			accessible_property[$propcode] = new_status_code			
			$accessible_property[$propcode] = $propcode
			$accessible_property['<<$propcode>>-display'] = $property['display']
			$accessible_property['<<$propcode>>-name'] = $property['name']
			accessible_property['<<$propcode>>-rent'] = property['rent']
			
			if $property['type'] = 'permanent_residence' or $property['type'] = 'temporary_residence': accessible_property['<<$propcode>>-is-home'] = 1
		end

		if $property['contstruction_needed'] = 1:
			accessible_property[$propcode+'-construction-status'] = ARGS[3]
		else
			accessible_property[$propcode+'-construction-status'] = 2
		end
		accessible_property[$propcode+'-renovated'] = ARGS[4]

		!! Setting status dependent variables
		!! Property is unaccessible
		if accessible_property[$propcode] = 0:
			!! If this was the current home then killing it
			if $home['current'] = $propcode: killvar '$home'
			
			!! Updating home counts
			!! NOTE: Valid transitions: 1 -> 0, 2 -> 0, 4 -> 0
			if accessible_property['<<$propcode>>-is-home'] = 1:
				if orig_status_code = 1: accessible_property['rental_home_count'] -= 1
				if orig_status_code = 2: accessible_property['owned_home_count'] -= 1
				if accessible_property['<<$propcode>>-construction-status'] = 2: accessible_property['furnishable_home_count'] -= 1
				accessible_property['accessible_home_count'] -= 1
			end
		!! Property is rented
		elseif accessible_property[$propcode] = 1:
			!! Updating home counts
			!! NOTE: Only valid transition is 0 -> 1
			if accessible_property['<<$propcode>>-is-home'] = 1:
				accessible_property['rental_home_count'] += 1
				accessible_property['accessible_home_count'] += 1
				if accessible_property['<<$propcode>>-construction-status'] = 2: accessible_property['furnishable_home_count'] +=1 
			end
		!! Property is owned
		elseif accessible_property[$propcode] = 2:
			!! Updating home counts
			!! NOTE: Valid transitions: 0 -> 2, 1 -> 2, 3-> 2
			if accessible_property['<<$propcode>>-is-home'] = 1:
				if orig_status_code = 0: accessible_property['accessible_home_count'] += 1
				if accessible_property['<<$propcode>>-construction-status'] = 2: accessible_property['furnishable_home_count'] += 1
				if orig_status_code = 1: accessible_property['rental_home_count'] -= 1
				if orig_status_code = 3: 
					accessible_property['tenant_home_count'] -= 1 
					accessible_property['<<$propcode>>-tenant-day'] = -1
					accessible_property['<<$propcode>>-tenant-month'] = -1	
					if accessible_property['<<$propcode>>-construction-status'] = 2: accessible_property['furnishable_home_count'] += 1
				end
				accessible_property['owned_home_count'] += 1
			end
		!! Property has tenants
		elseif accessible_property[$propcode] = 3:
			!! Updating home counts
			!! NOTE: Valid transitions: 2 -> 3
			if accessible_property['<<$propcode>>-is-home'] = 1:
				accessible_property['tenant_home_count'] += 1
				accessible_property['owned_home_count'] -= 1
				accessible_property['accessible_home_count'] -= 1
				if accessible_property['<<$propcode>>-construction-status'] = 2: accessible_property['furnishable_home_count'] -= 1
			end
		!! Property is just accessible
		elseif accessible_property[$propcode] = 4:
			!! Updating home counts
			!! NOTE: Valid transitions: 0 -> 4
			if accessible_property['<<$propcode>>-is-home'] = 1:
				accessible_property['accessible_home_count'] += 1
			end
		end
		killvar 'property'
	end
	killvar 'propcode' && killvar 'orig_status_code' && killvar 'new_status_code'
end

!! $ARGS[1]: the location for which the access is set
!!			Accepted values
!!			- property code   -> 'city_apartment', 'parents_home'
!!			- a location      ->  'bedrPar, 'koo2x' - A value that can shop up in $loc
!!			- empty strying   ->  '' - will work as if a location was passed, but will use the actual value of $loc
!!			- A number as str -> '1', '2' - this should be the id of the property 
!!			  					NOTE: only added in case someone really want to use the old ids
if $ARGS[0] = 'can_live_here':
	if $ARGS[1] = '':
		result = accessible_property[$home_name[$loc]] > 0 and func('homes_properties', 'property_construction_status', $home_name[$loc]) = 2
	else
		result = accessible_property[$home_name[$ARGS[1]]] > 0 and func('homes_properties', 'property_construction_status', $home_name[$loc]) = 2
	end
end

!! $ARGS[1]: the location for which the access is set
!!			Accepted values
!!			- property code   -> 'city_apartment', 'parents_home'
!!			- a location      ->  'bedrPar, 'koo2x' - A value that can shop up in $loc
!!			- empty strying   ->  '' - will work as if a location was passed, but will use the actual value of $loc
!!			- A number as str -> '1', '2' - this should be the id of the property 
!!			  					NOTE: only added in case someone really want to use the old ids
if $ARGS[0] = 'is_current_home':
	if $ARGS[1] = '': 
		result = $home['current'] = $home_name[$loc]
	elseif $home_name[$ARGS[1]] ! '':
		result = $home_name[$ARGS[1]]
	else
		result = 0
	end
end

!! $ARGS[1]: the location for which the access is set
!!			Accepted values
!!			- property code   -> 'city_apartment', 'parents_home'
!!			- a location      ->  'bedrPar, 'koo2x' - A value that can shop up in $loc
!!			- empty strying   ->  '' - will work as if a location was passed, but will use the actual value of $loc
!!			- A number as str -> '1', '2' - this should be the id of the property 
!!			  					NOTE: only added in case someone really want to use the old ids
if $ARGS[0] = 'get_home_name':
	if $ARGS[1] = '': 
		$result = $home_name[$loc]
	elseif $home_name[$ARGS[1]] ! '':
		$result = $home_name[$ARGS[1]]
	else
		$result = ''
	end
end

!!---------------------------------------------------------
!!  Home and Property Counts
!!---------------------------------------------------------

!! Returns the count of rented properties
!!
!! ARGS[1] - property type filter: 'home', 'business', 'all' or '' or missing
if $ARGS[0] = 'get_rented_property_count':
	if $ARGS[1] = 'home': 
		result = accessible_property['rental_home_count']
	elseif $ARGS[1] = 'business': 
		result = accessible_property['rental_business_count']
	elseif $ARGS[1] = 'all' or $ARGS[1] = '': 
		result = accessible_property['rental_home_count'] + accessible_property['rental_business_count']
	end
end

!! Returns the count of rented properties
!!
!! ARGS[1] - property type filter: 'home', 'business', 'all' or '' or missing
if $ARGS[0] = 'get_accessible_property_count':
	if $ARGS[1] = 'home': 
		result = accessible_property['accessible_home_count']
	elseif $ARGS[1] = 'business': 
		result = accessible_property['accessible_business_count']
	elseif $ARGS[1] = 'all' or $ARGS[1] = '': 
		result = accessible_property['accessible_home_count'] + accessible_property['accessible_business_count']
	end
end


!!---------------------------------------------------------
!!  Get Properties and their values
!!---------------------------------------------------------

!! Use it as `gs`, returns the rent related information for all rented homes
!!
!! Returns: rental_amount, rental_days, $rental_name, $rental_code
if $ARGS[0] = 'get_rented_homes':
	i = 0
	j = 0
	count = ARRSIZE('accessible_property')
	if count > 0: 
		:countloop
			if $home_name[$accessible_property[i]] ! '' and accessible_property[i] = 1:
				$rental_code[j] = $accessible_property[i]
				$rental_name[j] = $accessible_property['<<$accessible_property[i]>>-display']
				rental_days[j] = accessible_property['<<$accessible_property[i]>>-days-left']
				rental_amount[j] = accessible_property['<<$accessible_property[i]>>-rent']
				j += 1
			end
			i += 1
		if i < count: jump 'countloop'
	end
	killvar 'count' && killvar 'i' && killvar 'j'
end

!!---------------------------------------------------------
!!  Give and block access to properties - use these if the 
!!  property is not bought or rented.
!!  Parents home, Meynold household, Grandparents house, etc.
!!---------------------------------------------------------

!! $ARGS[1]: the location for which the access is set
!!			Accepted values
!!			- property code   -> 'city_apartment', 'parents_home'
!!			- a location      ->  'bedrPar, 'koo2x' - A value that can shop up in $loc
!!			- empty strying   ->  '' - will work as if a location was passed, but will use the actual value of $loc
!!			- A number as str -> '1', '2' - this should be the id of the property 
!!			  					NOTE: only added in case someone really want to use the old ids
if $ARGS[0] = 'give_access':
	if $ARGS[1] = '':
		gs 'homes_properties', 'set_access', $home_name[$loc], 1
	else
		gs 'homes_properties', 'set_access', $home_name[$ARGS[1]], 1
	end
end

!! $ARGS[1]: the location for which the access is set
!!			Accepted values
!!			- property code   -> 'city_apartment', 'parents_home'
!!			- a location      ->  'bedrPar, 'koo2x' - A value that can shop up in $loc
!!			- empty strying   ->  '' - will work as if a location was passed, but will use the actual value of $loc
!!			- A number as str -> '1', '2' - this should be the id of the property 
!!			  					NOTE: only added in case someone really want to use the old ids
if $ARGS[0] = 'block_access':
	if $ARGS[1] = '':
		gs 'homes_properties', 'set_access', $home_name[$loc], 0
	else
		gs 'homes_properties', 'set_access', $home_name[$ARGS[1]], 0
	end
end

!!---------------------------------------------------------
!!  Buy, rent out and sell properties
!!---------------------------------------------------------

!! $ARGS[1] = the code name of the property, e.g. 'city_apartment'
if $ARGS[0] = 'buy_property':
	gs 'homes_properties', 'set_access',$ARGS[1], 2
end

!! $ARGS[1] = the code name of the property, e.g. 'city_apartment'
if $ARGS[0] = 'sell_property':
	gs 'homes_properties', 'set_access',$ARGS[1], 0
end

!! $ARGS[1] - the property code of the home that is rented to tenants
!! ARGS[2] - the tenancy end day
!! ARGS[3] - the tenancy end month
if $ARGS[0] = 'tenants_move_in':
	gs 'homes_properties', 'set_ownership', $ARGS[1], 3
	owned_property['<<$ARGS[1]>>-tenant-day'] = ARGS[2]
	owned_property['<<$ARGS[1]>>-tenant-month'] = ARGS[3]
end

!!---------------------------------------------------------
!!  Create, Update and Cancel Rents and related values
!!---------------------------------------------------------

!! $ARGS[1] = the code name of the property, e.g. 'city_apartment'
!! ARGS[2] = the number of days the rent is paid for, optional
if $ARGS[0] = 'rent_property':
	gs 'homes_properties', 'set_access', $ARGS[1], 1
	gs 'homes_properties', 'add_rent_days', $ARGS[1], $ARGS[2]
end

!! $ARGS[1] = the code name of the property, e.g. 'city_apartment'
if $ARGS[0] = 'cancel_rent':
	gs 'homes_properties', 'set_access', $ARGS[1], 0
	gs 'homes_properties', 'set_rent_days', $ARGS[1], -1
end

!! $ARGS[1] = the code name of the property, e.g. 'city_apartment'
!! ARGS[2] - the number of days to add.
!!           if the value is 0 then the code will add the number of days that is there in the month
if $ARGS[0] = 'add_rent_days':
	if ARGS[2] = 0:
		accessible_property['<<$ARGS[1]>>-days-left'] += func( 'homes_properties', 'rent_due_date')
	else
		accessible_property['<<$ARGS[1]>>-days-left'] += ARGS[2]
	end
end

!! $ARGS[1] = the code name of the property, e.g. 'city_apartment'
!! ARGS[2] - the number of days to set to - it's an absolute value
if $ARGS[0] = 'set_rent_days':
	accessible_property['<<$ARGS[1]>>-days-left'] = ARGS[2]
end


!! Call it as a func()
!! $ARGS[1] = the code name of the property, e.g. 'city_apartment'
if $ARGS[0] = 'get_rent_days':
	result = accessible_property['<<$ARGS[1]>>-days-left']
end

!! Call it as a func()
!! $ARGS[1] = the code name of the property, e.g. 'city_apartment'
if $ARGS[0] = 'get_rent_amount':
	result = accessible_property['<<$ARGS[1]>>-rent']
end


!! Returns the rent due date for the given month. Call as a func()
if $ARGS[0] = 'rent_due_date':
	if month = 1 or month = 3 or month = 5 or month = 7 or month = 8 or month = 10 or month = 12:
		result = 31
	elseif month = 2:
		if ((year mod 4 = 0) and (year mod 100 ! 0)) or (year mod 400 = 0):
			result = 29
		else
			result = 28
		end
	else
		result = 30
	end
end

!!---------------------------------------------------------
!!  Go to properties, home, etc.
!!---------------------------------------------------------


!! Takes Sveta to the location where her home can be found
if $ARGS[0] = 'go_home':
	gt $home['location'], $home['location_arg']
end

!! Takes Sveta to the entracne of her home
if $ARGS[0] = 'go_straight_home':
	gt $home['entrance'], $home['entrance_arg']
end


--- homes_properties ---------------------------------