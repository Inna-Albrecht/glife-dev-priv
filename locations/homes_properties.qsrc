# homes_properties

!! The initial rental values for different properties
if ARRSIZE('rent') < 2:
	prop_rent['city_apartment'] = 15000
	$prop_rent['city_apartment'] = '15,000'
	prop_rent['old_town_apartment'] = 10000
	$prop_rent['old_town_apartment'] = '10,000'
end

!! The initial rental values for different properties
if ARRSIZE('price')  < 4:
	prop_price['city_apartment'] = 800000
	$prop_price['city_apartment'] = '800,000'
	prop_price['village_cottage'] = 150000
	$prop_price['village_cottage'] = '150,000'
	prop_price['matryona_mansion'] = 1500000
	$prop_price['matryona_mansion'] = '1,500,000'
	prop_price['city_house'] = 100000
	$prop_price['city_house'] = '100,000'
end

if ARRSIZE('home_name') < 1:
	!! The list goes like this:
	!!    1. $home_name['property code']
	!!    2. $home_name['property id']  - for backward compatibility only, nothing else and the number is a string index
	!!    2. $home_name['enctrance location'] 
	!!    3.- N.  $home_name['other locations']

	!! CITY APARTMENT
	$home_name['city_apartment'] = 'city_apartment'
	$home_name['1'] = 'city_apartment'
	$home_name['korr'] = 'city_apartment'
	$home_name['bedr'] = 'city_apartment'
	$home_name['sitr'] = 'city_apartment'
	$home_name['kuhr'] = 'city_apartment'
	$home_name['vanr'] = 'city_apartment'

	!! PARENTS HOME
	$home_name['parents_home'] = 'parents_home'
	$home_name['2'] = 'parents_home'
	$home_name['code'] = 'parents_home'
	$home_name['korrPar'] = 'parents_home'
	$home_name['bedrPar'] = 'parents_home'
	$home_name['sitrPar'] = 'parents_home'
	$home_name['kuhrPar'] = 'parents_home'
	$home_name['vanrPar'] = 'parents_home'

	!! VILLAGE COTTAGE
	$home_name['village_cottage'] = 'village_cottage'
	$home_name['3'] = 'village_cottage'
	$home_name['dachamy'] = 'village_cottage'
	$home_name['dachain'] = 'village_cottage'
	$home_name['dachaogr'] = 'village_cottage'
	$home_name['dachaogr0'] = 'village_cottage'

	!! OLD TOWN (PUSHKIN) APARTMENTS
	$home_name['old_town_apartment'] = 'old_town_apartment'
	$home_name['4'] = 'old_town_apartment'
	$home_name['korr2x'] = 'old_town_apartment'
	$home_name['bedr2x'] = 'old_town_apartment'
	$home_name['sitr2x'] = 'old_town_apartment'
	$home_name['kuhr2x'] = 'old_town_apartment'
	$home_name['vanr2x'] = 'old_town_apartment'

	!! MATRYONA MANSION
	$home_name['matryona_mansion'] = 'matryona_mansion'
	$home_name['5'] = 'matryona_mansion'
	$home_name['city_mansion_entrance'] = 'matryona_mansion'
	$home_name['city_mansion_residence_1'] = 'matryona_mansion'
	$home_name['city_mansion_residence_2'] = 'matryona_mansion'
	$home_name['city_mansion_residence_3'] = 'matryona_mansion'

	!! PAVLOVSK HOTEL
	$home_name['pavlovsk_hotel'] = 'pavlovsk_hotel'
	$home_name['6'] = 'pavlovsk_hotel'
	$home_name['pav_hotel'] = 'pavlovsk_hotel'
	pav_hotel['HotelRoom'] = 'pavlovsk_hotel'

	!! LIVE IN MAID BEDROOM
	$home_name['maid_bedroom'] = 'maid_bedroom'
	$home_name['7'] = 'maid_bedroom'
	$home_name['nichBedroomServant'] = 'maid_bedroom'

	!! NIKO''S APARTMENT
	$home_name['niko_apartment'] = 'niko_apartment'
	$home_name['8'] = 'niko_apartment'
	!! This home actually doesn''t exist yet, but it was defined,
	!! has an ID and all so, I am keeping it here

	!! SHULGA APARTMENT
	$home_name['shulga_apartment'] = 'shulga_apartment'
	$home_name['9'] = 'shulga_apartment'
	$home_name['shulgahall'] = 'shulga_apartment'
	$home_name['shulgabath'] = 'shulga_apartment'
	$home_name['shulgabed'] = 'shulga_apartment'
	$home_name['shulgakuh'] = 'shulga_apartment'
	
	!! LYCEUM DORM
	$home_name['lyceum_dorm'] = 'lyceum_dorm'
	$home_name['10'] = 'lyceum_dorm'
	$home_name['dom_gor'] = 'lyceum_dorm'

	!! UNIVERSITY DORM
	$home_name['university_dorm'] = 'university_dorm'
	$home_name['11'] = 'university_dorm'
	$home_name['uni_grounds'] = 'university_dorm'
	$home_name['uni_dorm'] = 'university_dorm'
	
	!! HUNTERS LODGE
	$home_name['hunters_lodge'] = 'hunters_lodge'
	$home_name['12'] = 'hunters_lodge'
	$home_name['swamphouse'] = 'hunters_lodge'
	$home_name['swamp_yard'] = 'hunters_lodge'


	!! SHARED APARTMENT - PAVLOVSK
	$home_name['shared_apartment'] = 'shared_apartment'
	$home_name['13'] = 'shared_apartment'
	$home_name['pav_shared_apt'] = 'shared_apartment'

	!! GRANDPARENTS HOUSE
	$home_name['grandparents_house'] = 'grandparents_house'
	$home_name['14'] = 'grandparents_house'
	$home_name['gaddvor'] = 'grandparents_house'
	$home_name['gadgarden'] = 'grandparents_house'
	$home_name['gadbana'] = 'grandparents_house'
	$home_name['gadgarden'] = 'grandparents_house'
	$home_name['gadhouse'] = 'grandparents_house'


	!! CITY HOUSE
	$home_name['city_house'] = 'city_house'
	$home_name['15'] = 'city_house'
	$home_name['city_house_res_misc'] = 'city_house'
	$home_name['city_house_res_bath'] = 'city_house'
	$home_name['city_house_re_bedr'] = 'city_house'
	$home_name['city_house-res_kitch'] = 'city_house'


	!! MEYNODLD HOUSEHOLD
	$home_name['meynold_household'] = 'meynold_household'
	$home_name['16'] = 'meynold_household'
	$hoome_name['mey_home'] = 'meynold_household'
end

!!{ All the existing properties should be added here. 
	id:			The old numerical id - for the nostalgia
	code:		The code of the property. Example: 'city_apartment', 'village_cottage'
	name:		General name to display
	display: 	The string to use in the middle of a sentence
	agency_display: The string the agency location uses 
	town:		The town/city/village the property can be found
	location:	The region within the town
	entrance:	The location where the charactes enter
	type:		The type of the property - it is used by some checks right now, dating app and such. 
				In the future this could be expanded to things like business properties, offices or other whatever we 
				want to simplify some checks.
	price:		The sales value of the property. Both as a number and as a string. !! NOTE: Maybe we don''t need it, 
				the numeric value can be displayed too and formatting can be done otherwise
	rent:		The monthly rent of the property. Both as a number and as a string. !! NOTE: Maybe we don''t need it, 
				the numeric value can be displayed too and formatting can be done otherwise
	rental:		Whether the property can be rented by Sveta or not. 
				1: Can be rented
				0: Can''t be rented // Examples are: Parents home, Shulga apartment, Hotel room in Pavlovsk, University dorm.
				Possible values are:
				1: rented via the agency, monthly rent, utility bills are paid, can be furnished, maybe renovated,
				   definitely needs an internet subscription
				4: not really a rented apartment, it is a "can live here" property. No utility is paid, 
				   Sveta can''t change anything
				   Examples are: Parents home, Shulga apartment, Hotel room in Pavlovsk, University dorm.

	free_internet: if the property comes with free internet (uses subscription['free']) then the value is `1`
				   Right now there are two potential homes with this: Meynold Household, University Dorm	

	internet_enabled: If the value is 1, then Sveta can buy, cancel, transfer an internet subscription here


	Property Types
	!! IMPORTANT: 
	!! NOTE: The types are not 100% clear and decided yet, need some more thinking. Also new ones can be added.

	permanent_residence:	This is a home that can be used for everything. Certain things don''t allow the more "transient"
							homes, like the dating app in Uni.
	temporary_residence:	This is a home that some events/actions won''t consider.

	New ones in the future could be for porperties that Sveta can own, rent, used, is not a home but can be changed/customised, etc. 
	For example a music studio/practice room, or a gallery, or a newspaper kiosk

	To make life easier, if is 0 or ''  for a property, then I didn''t include it. 
	Undefined strings will always evaluate to '' and undefined integers have a value of 0.
}

if ARRSIZE('rent') < 2:
	prop_rent['city_apartment'] = 15000
	$prop_rent['city_apartment'] = '15,000'
	prop_rent['old_town_apartment'] = 10000
	$prop_rent['old_town_apartment'] = '10,000'
end

if ARRSIZE('price')  < 4:
	prop_price['city_apartment'] = 800000
	$prop_price['city_apartment'] = '800,000'
	prop_price['village_cottage'] = 150000
	$prop_price['village_cottage'] = '150,000'
	prop_price['matryona_mansion'] = 1500000
	$prop_price['matryona_mansion'] = '1,500,000'
	prop_price['city_house'] = 100000
	$prop_price['city_house'] = '100,000'
end

!! Always call this function with gs and use killvar'property' after finished with the data.
!! ARGS[1] - the string identifier of the property. If it is empty (`' '`) then use `id` in ARGS[2]
!! ARG[2] - the id of the property, only for backward compatibility and for some mods maybe
if $ARGS[0] = 'get_property_attr':
	if $ARGS[1] = 'city_apartment' or ($ARGS[1] = '' and ARGS[2] = 1):
		property['id'] = 1
		$property['code'] = 'city_apartment'
		$property['name'] = 'City residential apartment'
		$property['display'] = 'apartment in St. Petersburg''s residential area'
		$property['agency_display'] = 'An apartment in St. Petersburg''s residential area'
		$property['town'] = 'city'
		$property['location'] = 'city_residential'
		$property['entrance'] = 'korr'
		$property['type'] = 'permanent_residence'
		$property['price'] = $prop_price['city_apartment']
		$property['rent'] = $prop_rent['city_apartment']		
		property['price'] = prop_price['city_apartment']
		property['rent'] = prop_rent['city_apartment']
		property['rental'] = 1
		property['internet_enabled'] = 1
	elseif $ARGS[1] = 'parents_home' or ($ARGS[1] = '' and ARGS[2] = 2):
		property['id'] = 2
		$property['code'] = 'parents_home'
		$property['name'] = 'Parent''s home in Pavlovsk'
		$property['display'] = 'your parent''s home in Pavlovsk'
		$property['town'] = 'pavlovsk'
		$property['location'] = 'pav_complex'
		$property['entrance'] = 'korrPar'
		$property['type'] = 'permanent_residence'
	elseif $ARGS[1] = 'village_cottage' or ($ARGS[1] = '' and ARGS[2] = 3) :
		property['id'] = 3
		$property['code'] = $ARGS[1]
		$property['name'] = 'My cottage in the cooperative village'
		$property['display'] = 'cottage in the cooperative village'
		$property['agency_display'] = 'A cottage located in the cooperative village'
		$property['town'] = 'city'
		$property['location'] = 'dachamy'
		$property['entrance'] = 'dachamy'
		$property['type'] = 'permanent_residence'
		$property['price'] = $prop_price['village_cottage']
		property['price'] = prop_price['village_cottage']
		!! Currently no cable or internet can be installed at this property, maybe later.
		property['internet_enabled'] = 0
	elseif $ARGS[1] = 'old_town_apartment' or ($ARGS[1] = '' and ARGS[2] = 4):
		property['id'] = 4
		$property['code'] = $ARGS[1]
		$property['name'] = 'Old town apartment'
		$property['display'] = 'old town apartment'
		$property['angecy-display'] = 'An apartment in the old town of Puskin.'
		$property['town'] = 'oldtown'
		$property['location'] = 'liames'
		$property['entrance'] = 'korr2x'
		$property['type'] = 'permanent_residence'
		$property['rent'] = $prop_rent['old_town_apartment']		
		property['price'] = prop_rent['old_town_apartment']
		property['rental'] = 1
		property['internet_enabled'] = 1
	elseif $ARGS[1] = 'matryona_mansion' or ($ARGS[1] = '' and ARGS[2] = 5):
		property['id'] = 5
		$property['code'] = $ARGS[1]
		$property['name'] = 'Matryona mansion'
		$property['display'] = 'the Matryona mansion'
		$property['agency_display'] = 'A plot of land in the St. Petersburg''s suburbs'
		$property['town'] = 'city'
		$property['location'] = 'city_mansion_entrance'
		$property['entrance'] = 'city_mansion_residence_1'
		$property['type'] = 'permanent_residence'
		$property['price'] = $prop_rent['matryona_mansion']
		property['price'] = prop_rent['matryona_mansion']
		property['internet_enabled'] = 1
		property['contstruction_needed'] = 1
	elseif $ARGS[1] = 'pavlovsk_hotel' or ($ARGS[1] = '' and ARGS[2] = 6):
		property['id'] = 6
		$property['code'] = $ARGS[1]
		$property['name'] = 'My hotel room in Pavlovsk'
		$property['display'] = 'your hotel room in Pavlovsk'
		$property['town'] = 'pavlovsk'
		$property['location'] = 'pav_market'
		$property['entrance'] = 'pav_hotel'
		$property['type'] = 'temporary_residence'
	elseif $ARGS[1] = 'maid_bedroom' or ($ARGS[1] = '' and ARGS[2] = 7):
		property['id'] = 7
		$property['code'] = $ARGS[1]
		$property['name'] = 'Nicholas'' Apartment'
		$property['display'] = 'your maid bedroom in Nicholas'' apartment'
		$property['town'] = 'city'
		$property['location'] = 'city_center'
		$property['entrance'] = 'nichBedroomServant'
		$property['type'] = 'permanent_residence'
	elseif $ARGS[1] = 'nikos_apartment' or ($ARGS[1] = '' and ARGS[2] = 8):
		property['id'] = 8
		$property['code'] = $ARGS[1]
		$property['name'] = 'Niko''s apartment'
		$property['display'] = 'Niko''s apartment'
		$property['town'] = 'pavlovsk'
		$property['location'] = 'pav_residential'
		$property['entrance'] = 'pav_residential'
		$property['type'] = 'temporary_residence'
	elseif $ARGS[1] = 'shulga_apartment' or ($ARGS[1] = '' and ARGS[2] = 9):
		property['id'] = 9
		$property['code'] = $ARGS[1]	
		$property['name'] = 'Uncle Sergey''s apartment'
		$property['display'] = 'Uncle Sergey''s apartment'
		$property['town'] = 'pavlovsk'
		$property['location'] = 'pav_complex'
		$property['entrance'] = 'shulgahall'
		$property['type'] = 'temporary_residence'
	elseif $ARGS[1] = 'lyceum_dorm' or ($ARGS[1] = '' and ARGS[2] = 10):
		property['id'] = 10
		$property['code'] = $ARGS[1]	
		$property['name'] = 'Lyceum school'
		$property['display'] = 'your room in the Lyceum dorm'
		$property['town'] = 'pavlovsk'
		$property['location'] = 'pav_commercial'
		$property['entrance'] = 'etoexhib'
		$property['type'] = 'permanent_residence'
	elseif $ARGS[1] = 'university_dorm' or ($ARGS[1] = '' and ARGS[2] = 11):
		property['id'] = 11
		$property['code'] = $ARGS[1]
		$property['name'] = 'University halls'
		$property['display'] = 'your room in the university dorm'
		$property['town'] = 'city'
		$property['location'] = 'city_island'
		$property['entrance'] = 'uni_dorm'
		$property['type'] = 'permanent_residence'
	elseif $ARGS[1] = 'hunters_lodge' or ($ARGS[1] = '' and ARGS[2] = 12):
		property['id'] = 12
		$property['code'] = $ARGS[1]
		$property['name'] = 'Hunter''s lodge in Gadukino'
		$property['display'] = 'the hunter''s lodge in Gadukino'
		$property['town'] = 'gadukino'
		$property['location'] = 'swamp_yard'
		$property['entrance'] = 'swamphouse'
		$property['type'] = 'temporary_residence'
	elseif $ARGS[1] = 'shared_apartment' or ($ARGS[1] = '' and ARGS[2] = 13):
		property['id'] = 13
		$property['code'] = $ARGS[1]	
		$property['name'] = 'Shared apartment in Pavlovsk'
		$property['display'] = 'the shared apartment in Pavlovsk'
		$property['town'] = 'pavlovsk'
		$property['location'] = 'pav_complex'
		$property['entrance'] = 'pav_shared_apt'
		$property['type'] = 'temporary_residence'
	elseif $ARGS[1] = 'grandparents_house' or ($ARGS[1] = '' and ARGS[2] = 14):
		property['id'] = 14
		$property['code'] = $ARGS[1]
		$property['name'] = 'Grandparent''s house in Gadukino'
		$property['display'] = 'your grandparent''s house in Gadukino'
		$property['town'] = 'gadukino'
		$property['location'] = 'gaddvor'
		$property['entrance'] = 'gaddvor'
		$property['type'] = 'temporary_residence'
	elseif $ARGS[1] = 'city_house' or ($ARGS[1] = '' and ARGS[2] = 15):
		property['id'] = 15
		$property['code'] = $ARGS[1]
		$property['name'] = 'City residential house'
		$property['display'] = 'house in the St. Petersburg''s residential area'
		$property['agency_display'] = 'A house in the St. Petersburg''s residential area'
		$property['town'] = 'city'
		$property['location'] = 'city_residential'
		$property['entrance'] = 'city_house_res_misc'
		$property['type'] = 'permanent_residence'
		$property['price'] = $prop_price['city_house']
		property['price'] = prop_price['city_house']
		property['internet_enabled'] = 1
	elseif $ARGS[1] = 'meynold_household' or ($ARGS[1] = '' and ARGS[2] = 16):
		property['id'] = 16
		$property['code'] = $ARGS[1]
		$property['name'] = 'Meynold Household'
		$property['display'] = 'the Meynold Household'
		$property['town'] = 'pavlovsk'
		$property['location'] = 'pav_residential'
		$property['entrance'] = 'mey_home'
		$property['type'] = 'temporary_residence'
	else
		!! Nonexisting property. CAn be useful if someone wants to check if something is a property or not. For example the
		!! internet transfer code does this right now.
		gs 'homes_properties', 'mod_property_query_hook'
		if $property = '': $property = 'none'  
	end
end

if $ARGS[0] = 'mod_property_query_hook':
	!! use this for the redirect
	mod_result = 1
end

!! Set the current home for Sveta based on either the property code or the id - for backward compatibility or some mods
!! $ARGS[1] - the property code, can be an empty string if using the id
!! ARGS[2] - the property id
if $ARGS[0] = 'set_home':
	gs 'homes_properties', 'get_property_attr', $ARGS[1], ARGS[2]
	COPYARR 'home','property'
	$home['current'] = $property['code']
	killvar 'property'
end

!! $ARGS[1]: the name of the property (the property code, e.g., 'city_apartment')
!! ARGS[2]: ownership type
!!			0: no acces -> 'no access'
!!			1: rented	-> 'rented'
!!			2: owned    -> 'owned'
!!			3: owned and rented out -> 'tenants'
!!			4: has acess -> 'has access' / homes that behave like rentals but no rent is tracked
!!				parents home, university dorm, lyceum dorm and the shared apartment, the Shulga and Meynold household, etc. 
!! ARGS[3]: construction status
!!			0: land only 
!!			1: foundation, frames built, utilities are connected
!!			2: fully built
!! ARGS[4]:renovation status
!!			0: not renovated
!!			1: fully renovated
if $ARGS[0] = 'set_ownership':
	gs 'homes_properties', 'get_property_attr', $ARGS[1]
	owned_property[$ARGS[1]] = ARGS[2]
	$owned_property[$ARGS[1]] = $ARGS[1]
	if $property['contstruction_needed'] = 1:
		owned_property[$ARGS[1]+'-construction-status'] = ARGS[3]
	else
		owned_property[$ARGS[1]+'-construction-status'] = 2
	end
	owned_property[$ARGS[1]+'-renovated'] = ARGS[4]
	if ARGS[2] = 1:
		owned_property[$ARGS[1]+'-days-left'] += func( 'homes_properties', 'rent_due')
	elseif ARGS[2] = 0:
		if $home['current'] = $ARGS[1]: killvar '$home'
		owned_property[$ARGS[1]+'-days-left'] = 0
	elseif ARGS[2] = 2 or ARGS[2] = 3:
		owned_property[$ARGS[1]+'-days-left'] = 0
	end
	killvar 'property'
end

!! Use it as a func() returns the list of property codes for the properties are homes and are owned (including wiht tenants)
if $ARGS[0] = 'get_owned_homes':
	i = 0
	j = 0
	count = ARRSIZE('owned_property')
	if count > 0: 
		:countloop
			gs 'homes_properties', 'get_property_attr', $owned_property[i]
			if $property['type'] = 'permanent_residence' and (owned_property[i] = 2 or owned_property[i] = 3): 
				$result[j] = $property['code']
				j += 1
			end
			i += 1
			killvar 'property'
		if i < count: jump 'countloop'
	end
	killvar 'count'
	killvar 'i'
	killvar 'j'
end

!! Use it as a func() returns the total count of rented properties that are homes
if $ARGS[0] = 'get_rented':
	i = 0
	count = ARRSIZE('owned_property')
	if count > 0: 
		:countloop
			gs 'homes_properties', 'get_property_attr', $owned_property[i]
			if $property['type'] = 'permanent_residence' or $property['type'] = 'temporary_residence' and owned_property[i] = 1: counter += 1
			i += 1
			killvar 'property'
		if i < count: jump 'countloop'
	end
	result = counter
	killvar 'counter'
	killvar 'count'
	killvar 'i'
end

!! Use it as a func() returns the total count of properties that are homes and accessible
if $ARGS[0] = 'get_accessible_count':
	i = 0
	count = ARRSIZE('owned_property')
	if count > 0: 
		:countloop
			gs 'homes_properties', 'get_property_attr', $owned_property[i]
			if $property['type'] = 'permanent_residence' or $property['type'] = 'temporary_residence' and owned_property[i] ! 0 and owned_property[i] ! 3: counter += 1
			i += 1
			killvar 'property'
		if i < count: jump 'countloop'
	end
	result = counter
	killvar 'counter'
	killvar 'count'
	killvar 'i'
end

!! Use it as a func() returns the total count of rented or woned properties that can be lived in a stuff can be bought for
!! TV, Computer, etc. that checks for this to allow purchase
if $ARGS[0] = 'get_count_of_furnishable_homes':
	i = 0
	count = ARRSIZE('owned_property')
	if count > 0: 
		:countloop
			gs 'homes_properties', 'get_property_attr', $owned_property[i]
			if $property['type'] = 'permanent_residence' or $property['type'] = 'temporary_residence' and owned_property[i] = 1 or owned_property[i] = 2: 
				if $property['code'] = 'matryona_mansion':
					if func('homes_properties', 'property_construction_status', 'matryona_mansion') = 2: result += 1
				else
					result += 1
				end
			end
			i += 1
			killvar 'property'
		if i < count: jump 'countloop'
	end
	killvar 'counter'
	killvar 'count'
	killvar 'i'
end


!! ARGS[1] = the code name of the property, e.g. 'city_apartment' 
!! ARGS[2] - the number of days to add.
!!           if the value is 0 then the code will add the number of days that is there in the month
if $ARGS[0] = 'add_rent_days':
	if ARGS[2] = 0:
		owned_property[$ARGS[1]+'-days-left'] += func( 'homes_properties', 'rent_due')
	else
		owned_property[$ARGS[1]+'-days-left'] += ARGS[2]
	end
end

!! ARGS[1] = the code name of the property, e.g. 'city_apartment' 
!! ARGS[2] - the number of days to set to.
if $ARGS[0] = 'set_rent_days':
	owned_property[$ARGS[1]+'-days-left'] = ARGS[2]
end

!! Call it as a func()
!! ARGS[1] = the code name of the property, e.g. 'city_apartment' 
if $ARGS[0] = 'get_rent_days':
	result = owned_property[$ARGS[1]+'-days-left']
end

!! Call it as a func()
!! ARGS[1] = the code name of the property, e.g. 'city_apartment' 
if $ARGS[0] = 'property_construction_status':
	result = owned_property[$ARGS[1]+'-construction-status']
end

!! ARGS[1] = the code name of the property, e.g. 'city_apartment' 
if $ARGS[0] = 'progress_construction_status':
	if owned_property[$ARGS[1]+'-construction-status'] < 2: owned_property[$ARGS[1]+'-construction-status'] += 1
end

!! Call it as a func()
!! ARGS[1] = the code name of the property, e.g. 'city_apartment' 
if $ARGS[0] = 'property_renovated':
	result = owned_property[$ARGS[1]+'-renovated']
end


!! Returns a simple string representation of the status
!! ARGS[1]: the code name of the property, e.g. 'city_apartment' 
if $ARGS[0] = 'property_status_to_text':
	if ARGS[1] = 0: 
		result = 'no access'
	elseif ARGS[1] = 1: 
		result = 'rented'
	elseif ARGS[1] = 2: 
		result = 'owned'
	elseif ARGS[1] = 3: 
		result = 'tenants'		
	elseif ARGS[1] = 4:
		result = 'has access'
	else
		result = 'invalid status'
	end
end

!! Returns a string that can be displayed in a longer text.
!! For example 'You have tenants living in your city apartment.'
!! ARGS[1]: The ownership type
if $ARGS[0] = 'property_status_to_display_text':
	if ARGS[1] = 0: 
		result = 'You have no access to '
	elseif ARGS[1] = 1: 
		result = 'You are renting '
	elseif ARGS[1] = 2: 
		result = 'You own '
	elseif ARGS[1] = 3: 
		result = 'You have tenants living in '		
	else
		result = 'invalid status'
	end
end

!! Returns the monthly rent the property has - both as a number and as a string (result and $result)
!! Call as a func()
!! ARGS[1] - the code (not display) name of the property, e.g. city_apartment
if $ARGS[0] = 'get_rent':
	result = prop_rent[$ARGS[1]]
end

!! Returns the price of the property - both as a number and as a string (result and $result)
!! Call as a func() 
!! ARGS[1] - the code (not display) name of the property, e.g. city_apartment
if $ARGS[0] = 'get_price':
	result = prop_price[$ARGS[0]]
end

!! Returns the rent due date for the given month. Call as a func()
if $ARGS[0] = 'rent_due':
	if month = 1 or month = 3 or month = 5 or month = 7 or month = 8 or month = 10 or month = 12:
		result = 31
	elseif month = 2:
		if ((year mod 4 = 0) and (year mod 100 ! 0)) or (year mod 400 = 0):
			result = 29
		else
			result = 28
		end
	else
		result = 30
	end
end

if $ARGS[0] = 'leave_home':
	killvar '$home_name'
end

--- homes_properties ---------------------------------

