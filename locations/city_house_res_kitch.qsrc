# city_house_res_kitch


if $ARGS[0] = 'kitch':
	$loc = 'city_house_res_kitch'
	$loc_arg = 'kitch'
	$menu_loc = 'city_house_res_kitch'
	$menu_arg = 'kitch'
	$locclass = 'kitr'

	menu_off = 0
	minut += 1
	gs 'kit_din'
	gs 'stat'

	*clr & cla
	'<center><b><font color="maroon">Kitchen</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/house/crh_kitchen.jpg"></center>'

	act 'Leave the room': gt 'city_house_res_misc', 'hallw'

	'A well stocked kitchen containing all the necessities to make whatever meal you please.'

	if cltarelka > 0:
		'You have <b><<cltarelka>></b> clean plates left.'
	else
		'<b><font color="red">You don''t have any clean plates left.</font></b>'
	end

	if dirttarelka > 0:'There are <b><<dirttarelka>></b> dirty dishes in the sink. <a href="exec:dynamic $dirtarm">Wash the dishes</a>.'

	if fairy > 0:
		'Under the sink is some dishwashing detergent, which is enough for <b><<fairy>></b> ' + iif(fairy = 1, 'time.', 'times.')
	else
		'<b><font color="red">You have nothing to wash dishes with. You should buy some detergent.</font></b>'
	end

	if eda > 0:
		if cltarelka = 0 or edahot > 0:$edagot = ''
		if cltarelka > 0 and edahot = 0:$edagot = '<a href="exec:dynamic $edagotd">Cook a meal</a>'

		'There''s enough food for <b><<eda>></b> ' + iif(eda = 1, 'serving', 'servings') + '. <<$edagot>>'
	elseif edaD = 0 and eda = 0:
		'<b><font color="red">The fridge is empty. You have nothing to eat.</font></b>'
	end

	if edahot > 0:'<a href="exec:dynamic $edahotd">There''s a warm meal on the table.</a>'

	if husband > 0 and husbandrink ! 10:
		if hour = 7 or hour = 17:'<a href="exec:gt ''husb'', ''start''">Your husband is eating at the table.</a>'
	end

	if husband > 0 and husbanday > 0 and huseatday ! daystart and eda >= 2:
		act 'Cook a meal for your husband and yourself (1:00)':
			cla
			*clr
			husband += 5
			eda -= 2
			minut += 60
			huseatday = daystart
			pcs_mood -= 25
			'<center><img <<$set_imgh>> src="images/shared/home/kitchen/cook.jpg"></center>'
			'You prepare a meal for your husband and yourself.'

			act 'Leave the meal':gt 'kuhr'
		end
	end

	dynamic $edasnack
	dynamic $sandwich
	dynamic $driwater
	dynamic $dritea
	dynamic $fill_bottle
	dynamic $edaD
	dynamic $pranik
	dynamic $fatdel
	dynamic $lekarstvo
	dynamic $vitamin
end

--- city_house_res_kitch ---------------------------------

