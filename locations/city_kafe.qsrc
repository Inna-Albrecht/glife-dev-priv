# city_kafe

if $ARGS[0] = 'start':
	$loc_arg = 'start'
	$loc = 'city_kafe'
	$menu_loc = 'city_kafe'
	$menu_arg = 'start'
	menu_off = 0
	kafe_food = (pcs_ate + pcs_drank)
	$location_type = 'public_indoors'
	CLOSE ALL
	if sound = 0:PLAY 'sound/cafe.mp3', 30
	gs 'stat'
	gs 'kit_din'
	'<center><b><font color="maroon">The Roadhouse</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/kafe.jpg"></center>'
	'The Roadhouse is decorated with bright neon signs, pictures of flashy American muscle cars and pop art. There''s even the body of an old pink Cadillac hanging from the ceiling, like something from an old American TV show. Although it looks cramped and small from the outside, The Roadhouse is surprisingly spacious inside, managing to fit a well-stocked bar and a moderately-sized dining area alongside several private booths tucked away in the corners.'
	'A large, flashy jukebox features popular hits from the 50s through to the 80s, mostly American and British but with a sprinkling of songs from other European countries. They serve American style meals.'
	
	if workKafe['job'] = 0:
		'You see the middle-aged owner of the diner sitting at the end of the bar, engrossed in paperwork.'
	elseif workKafe['job'] = -1:
		'You see Fabi sitting at one end of the bar, engrossed in paperwork.'
	elseif hour = 11:
		'There is a bustle of activity in the diner as Fabi notes in the log that you arrived for work on time, Ashot and Nika ready the kitchen for the day and Marisha bustles from table to table making sure everything is ready to receive patrons.'
	else
		'You see Fabi sitting at one end of the bar, engrossed in paperwork.'
	end

	act 'Leave': minut += 5 & gt 'city_residential'
	act 'Sit at a table': gt 'city_kafe', 'table'

	if workKafe['job'] = 0:
		act 'Talk to the owner about a job': gt 'city_kafe', 'fabi'
	elseif workKafe['job'] = -1:
		act 'Talk to Fabi about getting your old job back': gt 'city_kafe', 'old_job'
	elseif hour = 11:
		act 'Start your shift': gt 'city_kafe', 'work'
	else
		act 'Talk to Fabi': gt 'city_kafe', 'fabi2'
	end
end

if $ARGS[0] = 'table':
	$loc_arg = 'table'
	$loc = 'city_kafe'
	$menu_loc = 'city_kafe'
	$menu_arg = 'table'
	menu_off = 0
	$location_type = 'public_indoors'
	*clr & cla
	gs 'stat'
	'<center><b><font color="maroon">The Roadhouse</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/kafe.jpg"></center>'
	'The Roadhouse is decorated with bright neon signs, pictures of flashy American muscle cars and pop art. There''s even the body of an old pink Cadillac hanging from the ceiling, like something from an old American TV show. Although it looks cramped and small from the outside, The Roadhouse is surprisingly spacious inside, managing to fit a well-stocked bar and a moderately-sized dining area alongside several private booths tucked away in the corners.'
	'A large, flashy jukebox features popular hits from the 50s through to the 80s, mostly American and British but with a sprinkling of songs from other European countries. They serve American style meals.'

	act 'Get up from the table': gt 'city_kafe', 'start'
	act 'Order from the menu (0:05)': gs 'food_menu'
end

if $ARGS[0] = 'fabi2':
	$menu_loc = 'city_kafe'
	$menu_arg = 'fabi2'
	menu_off = 0
	minut += 5
	*clr & cla
	gs 'stat'
	'<center><b><font color="maroon">The Roadhouse</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/fabi/FAB-001.jpg"></center>'
	'Fabi is filling in some government forms as you sit next to him. "Hey <<$pcs_nickname>>, what''s up?"'
	
	act 'Leave': gt 'city_kafe', 'start'
	act 'Quit your job': gt 'city_kafe', 'quit'
end

if $ARGS[0] = 'quit':
	$menu_loc = 'city_kafe'
	$menu_arg = 'quit'
	menu_off = 0
	workKafe['job'] = -1
	minut += 5
	*clr & cla
	gs 'stat'
	'<center><b><font color="maroon">The Roadhouse</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/fabi/FAB-002.jpg"></center>'
	'"I''m going to need my work book back," you tell him. "I''m afraid I''m quitting."'
	'Fabi frowns, but nods. "Waitressing isn''t the sort of job young girls tend to stay in long-term, so I understand. Wait here."'
	'He disappears into the office and comes back a few minutes later with your work book. "You''re a good worker and we all like you here. If you change your mind, the position is likely to be open for a little while as I look for someone else," he says as he hands it to you.'

	act 'Leave the diner': minut += 5 & gt 'city_residential'
	act 'Sit at a table': gt 'city_kafe', 'table'
	act 'Talk to Fabi about getting your old job back': gt 'city_kafe', 'old_job'
end

if $ARGS[0] = 'old_job':
	$menu_loc = 'city_kafe'
	$menu_arg = 'old_job'
	menu_off = 0
	workKafe['job'] = 1
	*clr & cla
	gs 'stat'
	'<center><b><font color="maroon">The Roadhouse</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/kafe.jpg"></center>'
	'"Ah, <<$pcs_nickname>>! Good to see you again!" Fabi says with a smile. "What brings you back to The Roadhouse?"'
	'"I was wondering if you needed any help?" you reply. "I''d like to waitress for you again."'
	'His smile widens. "I loved having you here. We still need the help, so if you want to come back to work the same hours, then you''re welcome to do so."'

	act 'Leave the diner': minut += 5 & gt 'city_residential'
	act 'Sit at a table': gt 'city_kafe', 'table'
end

if $ARGS[0] = 'fabi':
	$menu_loc = 'city_kafe'
	$menu_arg = 'fabi'
	menu_off = 0
	minut += 5
	*clr & cla
	gs 'stat'
	'<center><b><font color="maroon">The Roadhouse</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/fabi/FAB-001.jpg"></center>'
	'As you approach the owner, he looks up from his papers and gives you a tired smile. "Hello. I''m Fabiyan Pankratov, owner of The Roadhouse, but everyone just calls me Fabi. I don''t think I''ve seen you around here before. What can I do for you?"'
	'"I''m <<$pcs_firstname>> <<$pcs_lastname>>," you tell him. "I was wondering if you have any waitress jobs available?"'
	'His smile broadens and he looks a little less tired. "Ah, so you want to work here? Well why wouldn''t you? Rockabilly music, the greaser subculture, Cadillacs and motorcycles and hamburgers! I love this period of American culture, it''s so vibrant and exciting! I''ve put every ruble I have into making this place the perfect recreation of an American diner from 1958. Who doesn''t love that?"'
	*nl
	'A glance around the place shows you that the diner is barely half full, and most of those dining are eating Russian food rather than American. Fabi is still raving about America in the 1950s, talking about Elvis Presley, someone called Carl Perkins and cheeseburgers and French fries. His enthusiasm is honest and infectious, but you came here for a reason, so you clear your throat. "Fabi? About the job?"'
	'He grins sheepishly. "Sorry, I get carried away sometimes. So, you want to work here. Let me tell you about the job. We''re open every day from 12:00 to 20:00. You''d be working five days a week, Monday through Friday. You''d be expected to arrive between 11:00 and 12:00, before we open, to help get the place ready – if you''re late you don''t work that day. You don''t work, you don''t get paid. Pay is 600 rubles per shift plus whatever you get in tips, and you get paid on the 25th of each month."'
	'"Oh, and local regulations say I have to hold your work book as long as you''re employed here," he adds. "So... Are you interested?"'

	act 'Yes':
		cla
		minut += 5
		workKafe['job'] = 1
		gs 'stat'
		'You hand over your work book and Fabi takes it with a smile. "Good! Now remember, you have to be here between 11:00 and noon, Monday through Friday."'
		act 'Leave': gt 'city_kafe', 'start'
	end
	
	act 'No': gt 'city_kafe', 'start'
end

!!--------------------------------------------Work day--------------------------------

if $ARGS[0] = 'work':
	$location_type = 'event'
	*clr & cla
	workKafe['at_work'] = 1
	gs 'stat'
	smenaKafe += 1
	
	if rand(0,5) = 0:
		gt 'city_kafe', 'beg1'
	elseif rand(0,4) = 0:
		gt 'city_kafe', 'beg2'
	elseif rand(0,3) = 0:
		gt 'city_kafe', 'beg3'
	elseif rand(0,2) = 0:
		gt 'city_kafe', 'beg4'
	elseif rand(0,1) = 0:
		gt 'city_kafe', 'beg5'
	else
		gt 'city_kafe', 'beg6'
	end
end

if $ARGS[0] = 'beg1':
	$menu_loc = 'city_kafe'
	$menu_arg = 'beg1'
	menu_off = 0
	workKafe['tips_roll'] += 30
	!!set time to 15:30
	if hour <= 15: minut += (15-hour) * 60 +30 - minut
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/BEG-001.jpg"></center>'
	'The lunch rush starts strong and stays strong, and you spend the first few hours running frantically just to keep up. It''s not bad though – all your customers are in good moods and nobody has any major problems, and being busy like this makes the time go fast. If every day were like this, then you''d have no complaints.'
	act 'Lunch time': gt 'city_kafe', 'lunch'
end

if $ARGS[0] = 'beg2':
	$menu_loc = 'city_kafe'
	$menu_arg = 'beg2'
	menu_off = 0
	workKafe['tips_roll'] -= 30
	!!set time to 15:30
	if hour <= 15: minut += (15-hour) * 60 +30 - minut
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/BEG-002.jpg"></center>'
	'Today starts off slow, but within a few hours the place is almost as full as it ever gets during lunch. It''s not bad at first, but there''s always that one table that messes up your day. This time it''s a group of young executive types who talk loudly amongst themselves and place very particular orders, each changing their minds several times as they tell you what they want. You double-check the orders before you leave the table and you''re sure you have it right, but when you bring them their food, each of them complains that something is wrong. It''s clear they''re just doing it to screw with you.'
	act 'Lunch time': gt 'city_kafe', 'lunch'
end

if $ARGS[0] = 'beg3':
	$menu_loc = 'city_kafe'
	$menu_arg = 'beg3'
	menu_off = 0
	workKafe['tips_roll'] += 30
	!!set time to 15:30
	if hour <= 15: minut += (15-hour) * 60 +30 - minut
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/BEG-003.jpg"></center>'
	'Some days are just fun, and today is one of them. The crowd for lunch isn''t huge but it is happy, with some families and a bunch of regular customers who are happy to see you and engage you in good-natured banter and teasing. Aside from the normal issues that arise, the first part of the day flows smoothly and easily, and you make some decent tips.'
	act 'Lunch time': gt 'city_kafe', 'lunch'
end

if $ARGS[0] = 'beg4':
	$menu_loc = 'city_kafe'
	$menu_arg = 'beg4'
	menu_off = 0
	workKafe['tips_roll'] -= 30
	!!set time to 15:30
	if hour <= 15: minut += (15-hour) * 60 +30 - minut
	gs 'stat'

	!!Fabi
	npc_rel['A41'] += 1
	!!Ashot
	npc_rel['A42'] += 1
	!!Marisha
	npc_rel['A93'] += 1
	!!Nika
	npc_rel['A43'] += 1
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/BEG-004.jpg"></center>'
	'It''s a slow lunch rush and you''re able to take some time and chat with friendly customers and your coworkers. A large group tourists from another part of Russia then arrive, and they''re fascinated by the décor and the theme. They take pictures of everything, including you. You cheerfully pose for them, and since it''s a quiet day, you spend time at their table telling them about the diner and what it''s like to work here. They vow to tell everyone back home to come to the Roadhouse whenever they visit the city.'
	act 'Lunch time': gt 'city_kafe', 'lunch'
end

if $ARGS[0] = 'beg5':
	$menu_loc = 'city_kafe'
	$menu_arg = 'beg5'
	menu_off = 0
	workKafe['tips_roll'] += 40
	!!set time to 15:30
	if hour <= 15: minut += (15-hour) * 60 +30 - minut
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/BEG-005.jpg"></center>'
	'You get slammed as soon as the doors open, a flood of people filling every table and not slowing for hours. All you can do to keep your head above water is run from table to table taking orders, delivering food, checking on customers and resolving issues. It''s stressful and challenging, but you don''t screw up anything major and your customers leave satisfied, which is about the best you can expect from a day like today.'
	act 'Lunch time': gt 'city_kafe', 'lunch'
end

if $ARGS[0] = 'beg6':
	$menu_loc = 'city_kafe'
	$menu_arg = 'beg6'
	menu_off = 0
	workKafe['tips_roll'] -= 40
	!!set time to 15:30
	if hour <= 15: minut += (15-hour) * 60 +30 - minut
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/BEG-006.jpg"></center>'
	'Today is one of those days where you wish you''d just stayed in bed. The customers are inexplicably grumpy and there are constant complaints, but it gets worse towards the end of the lunch rush when two groups of young men, all of them already very drunk in spite of the early hour, get into a shouting match that rapidly escalates. Fabi and Ashot hurry to push them out the doors and onto the street, where they continue to argue and shout at each other for some time. All of this frightens off customers, which means you have fewer people to wait on and you get fewer tips.'
	act 'Lunch time': gt 'city_kafe', 'lunch'
end

if $ARGS[0] = 'lunch':
	$location_type = 'event'
	*clr & cla
	'<center><b><font color="maroon">The Roadhouse</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/kafe.jpg"></center>'
	'Your break time comes, giving you a chance to sit down, eat some food, talk to your coworkers and maybe go to the bathroom.'
	if city_cafe['lunch'] ! daystart:
		act 'Eat lunch': gt 'city_kafe', 'lunch_alone'
	end
	if city_cafe['lunch_talk'] ! daystart:
		act 'Talk to Fabi': gt 'city_kafe', 'lunch_fabi'
		act 'Talk to Ashot': gt 'city_kafe', 'lunch_ashot'
		act 'Talk to Marisha': gt 'city_kafe', 'lunch_marisha'
		act 'Talk to Nika': gt 'city_kafe', 'lunch_nika'
	end
	!!act 'Go to the bathroom': gt 'city_kafe', 'lunch_bath'
	act 'Return to work': gt 'city_kafe', 'events'
end

if $ARGS[0] = 'lunch_alone':
	*clr & cla
	gs 'city_kafe', 'eat_lunch'
	'<center><img <<$set_imgh>> src="images/shared/food/food_'+rand(1,4)+'.jpg"></center>'
	'You take your time eating your meal and playing on your phone. It''s just a light lunch, but it''s quite tasty and<<$mtxt>>'
	act 'Finish': gt 'city_kafe', 'lunch'
end

if $ARGS[0] = 'lunch_fabi':
	*clr & cla
	city_cafe['lunch_talk'] = daystart
	minut += 10
	npc_rel['A41'] += 1
	!!gs 'city_kafe', 'eat_lunch'
	'<center><b><font color="maroon">The Roadhouse</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/fabi/FAB-001.jpg"></center>'
	'You spend much of your break chatting with Fabi. He''s a nice man, kind and generous, but he always has a lot on his mind and always looks tired. He talks to you about his favorite topics: American cuisine, American culture, American music, and how the people of St. Petersburg just don''t understand what he''s trying to achieve with the Roadhouse.'
	if city_cafe['lunch'] ! daystart:
		act 'Eat lunch': gt 'city_kafe', 'lunch'
	end
	!!act 'Go to the bathroom': gt 'city_kafe', 'lunch_bath'
	act 'Return to work': gt 'city_kafe', 'events'
end

if $ARGS[0] = 'lunch_ashot':
	*clr & cla
	city_cafe['lunch_talk'] = daystart
	minut += 10
	npc_rel['A42'] += 1
	!!gs 'city_kafe', 'eat_lunch'
	'<center><b><font color="maroon">The Roadhouse</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/Ashot/ASH-001.jpg"></center>'
	'Most of your break is spent in the kitchen chatting with Ashot, whose irrepressible good mood and constant smile seem as much a part of him as north and south are parts of the compass. He tells you stories about his family, many of which revolve around him doing something crazy and being a disappointment to his parents, something he doesn''t seem too bothered about. "They''ll be proud of me yet!" he vows cheerfully.'
	if city_cafe['lunch'] ! daystart:
		act 'Eat lunch': gt 'city_kafe', 'lunch'
	end
	!!act 'Go to the bathroom': gt 'city_kafe', 'lunch_bath'
	act 'Return to work': gt 'city_kafe', 'events'
end

if $ARGS[0] = 'lunch_marisha':
	*clr & cla
	city_cafe['lunch_talk'] = daystart
	minut += 10
	npc_rel['A93'] += 1
	gs 'city_kafe', 'eat_lunch'
	'<center><b><font color="maroon">The Roadhouse</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/Marisha/MAR-001.jpg"></center>'
	'Marisha has some quiet time when you''re on break and you two spend it chatting as much as her duties permit. You do most of the talking since she''s guarded and reserved as she always is, but she listens with interest and makes comments and asks questions that make you think about things in new ways. She''s a smart girl, even if she usually chooses not to show it, and you can''t help but wonder if waitressing in a diner is really the best use of her talents.'
	if city_cafe['lunch'] ! daystart:
		act 'Eat lunch': gt 'city_kafe', 'lunch'
	end
	!!act 'Go to the bathroom': gt 'city_kafe', 'lunch_bath'
	act 'Return to work': gt 'city_kafe', 'events'
end

if $ARGS[0] = 'lunch_nika':
	*clr & cla
	city_cafe['lunch_talk'] = daystart
	minut += 10
	npc_rel['A43'] += 1
	gs 'city_kafe', 'eat_lunch'
	'<center><b><font color="maroon">The Roadhouse</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/Nika/NIK-001.jpg"></center>'
	'You eat your lunch in the kitchen, spending the time talking to Nika as she washes dishes. She''s bright, bubbly and energetic as always, talking about partying and clubbing – she''s especially fond of the nightclub over in the City Center – and how much fun St. Petersburg can be if you don''t have a steady guy to tie you down. She definitely seems to be enjoying her life, and she tells you tales of prowling the streets until dawn and shutting down every fun bar in town.'
	if city_cafe['lunch'] ! daystart:
		act 'Eat lunch': gt 'city_kafe', 'lunch'
	end
	!!act 'Go to the bathroom': gt 'city_kafe', 'lunch_bath'
	act 'Return to work': gt 'city_kafe', 'events'
end

if $ARGS[0] = 'eat_lunch':
	city_cafe['lunch'] = daystart
	frost = 0
	minut += 15
	pcs_health += 10
	fat += 4
	pcs_energy += 40
	if pcs_hydra >= 100:
		pcs_hydra += 20
	else
		pcs_hydra += 40
	end
	cumspclnt = 2
	gs 'cum_cleanup'
	pcs_breath = 0
	gs 'food', 'aftermeal'
	gs 'stat'
end


if $ARGS[0] = 'events':
	!!set time to 18:00
	if hour < 18: minut += (18-hour)*60 - minut

	if city_cafe['events_firsttime'] = 0:
		gt 'city_kafe', 1
	elseif city_cafe['events_firsttime'] = 1:
		gt 'city_kafe', 2
	elseif city_cafe['events_firsttime'] = 2:
		gt 'city_kafe', 3
	elseif city_cafe['events_firsttime'] = 3:
		gt 'city_kafe', 4
!!	elseif rand(0,4) <= 1:
!!		gt random events yet to be added
	else
		gt 'city_kafe', 'shift_end'
	end
end

if $ARGS[0] = 1:
	*clr & cla
	npc_rel['A93'] += 1
	city_cafe['events_firsttime'] += 1
	minut += 10
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/Marisha/MAR-002.jpg"></center>'
	'During a lull, you find yourself chatting with the other waitress, Marisha. She''s a tall, pretty girl who always seems to be smiling, but who also always seems to be holding something back. You get to talking about the job and the subject of tips comes up. "Tips are what let you make a living from this job," she tells you. "You definitely want to do everything you can to maximize them."'
	'"How do I do that?" you ask.'
	'"Well, first of all wear a waitress uniform," she tells you. "There''s no dress code here, but you definitely make more if you wear appropriate clothing. Your skill as a waitress plays a big part too of course. And I hate to say it, but honestly the hotter you are, the better your tips. People, especially guys, want to give money to pretty women who show some skin."'
	'"I dunno... Isn''t that kind of tacky?" you ask.'
	*nl
	'She laughs. "We aren''t strippers, but I figure if I can bring home more at the end of the day by wearing a short skirt and showing some cleavage, then why not? I keep it professional – I don''t show nearly as much here as I would at a club or at the beach. I just leave the top button or two undone and make sure they can see some leg. Customers get to look at something they like, and I walk home with their money in my purse."'
	'"I guess that doesn''t sound too bad..." you admit.'
	'"And because of that, I can afford to pay my rent and buy the necessities of life," she says with a grin. "I couldn''t otherwise. Give it some thought."'
	act 'Return to work': gt 'city_kafe', 'shift_end'

elseif $ARGS[0] = 2:
	*clr & cla
	npc_rel['A41'] += 1
	city_cafe['events_firsttime'] += 1
	minut += 10
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/fabi/FAB-002.jpg"></center>'
	'Since there are almost no patrons at the moment, you wander over to the end of the bar where Fabi is sitting doing some paperwork. He welcomes you with a smile and asks how you like working here so far.'
	'"It''s only my second day," you reply. "But it''s fun so far. Everyone is nice and the customers seem to enjoy it."'
	'"Of course they enjoy it!" he laughs. "Why wouldn''t they? This is the best diner in St. Petersburg, and the only one with any style!" He goes on to tell you about how his family visited some friends in Chicago in the USA right after the USSR fell, and how those friends took them to a 1950s-themed restaurant. "It was the greatest day of my life. Ever since then, it''s been my dream to duplicate that experience here, with the music and the food and the aesthetic. Not that it''s been easy, and not that everyone seems to understand what I''m trying here."'
	*nl
	'"What are some of the problems?" you ask.'
	'"A lot of people around here don''t get it," he complains. "I try to serve burgers and fried chicken, and customers come in and complain that they don''t see herring blini or piroshki on the menu. I have Johnny Cash and Buddy Holly on the jukebox, but customers come in and complain that there''s no Little Big or Korni. I put up classy decorations like American advertisements and a Cadillac, and they come in and complain that there are no fucking stuffed bear heads on the wall."'
	'"Is there something you could do to...I don''t know, make it more what you want?"'
	'"I''ve thought about getting live music in here," he admits. "We could put a little stage right over there, just big enough for a trio or a quartet, but there''s no decent rockabilly bands in St. Petersburg and I can''t save enough money to do that with Rakov on my back."'
	*nl
	'"Rakov?" you ask. "Who''s Rakov?"'
	'Fabi suddenly becomes nervous, as though he just realized he said something he shouldn''t have. He gathers together the papers he was working on and stands up. "Um, never mind. That''s not something you need to worry about. I''ll just finish these up back in the office."'
	act 'Return to work': gt 'city_kafe', 'shift_end'

elseif $ARGS[0] = 3:
	*clr & cla
	npc_rel['A43'] += 1
	city_cafe['events_firsttime'] += 1
	minut += 10
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/Nika/NIK-002.jpg"></center>'
	'With things quiet for the moment, you end up talking with Nika, the dishwasher, as she takes a break. She''s a pretty woman in her mid-20s, and she''s reading a book at a table as you approach. "Hey, mind if I join you?"'
	'"Please do," she says with a smile as she sets her book down. "How do you like working here?"'
	'"So far, so good," you reply. "So many new faces. I''m sure there are regulars I''ll get to know, but it''s just been a whirlwind."'
	*nl
	'She laughs. "Oh, there are regulars alright. Some of them you should steer clear of, but they''re mostly alright."'
	'"Who should I avoid?" you ask.'
	'"One guy, Borushka, is always trying to get Marisha to do things with him," she says while shaking her head. "I think he''s wasting his time, I''m pretty sure she plays for the other team, if you know what I mean. On the other hand, there''s a guy who comes in here sometimes, Instinsky, who''s a gambler. He sometimes lays bets with bookies for us. He''s alright."'
	*nl
	'"Fabi mentioned someone called Rakov the other day. Is he a regular too?" you ask after a moment''s silence.'
	'Nika frowns. "Not exactly. Mr. Rakov is an Avtoritet in the Grekov gang. He''s a gangster, and very bad news. He comes in here sometimes with a couple of scary-looking guys. Fabi got into money trouble a few years back and borrowed from Rakov, thinking he could pay it back fast. But you know how that goes, you never really finish paying off guys like that. I don''t know how much Fabi still owes, but it''s a lot. Maybe even more than he originally borrowed."'
	'You don''t know what to say to that, so you change the subject to the St. Petersburg nightlife, which is something Nika seems intimately familiar with, and spend the next few minutes talking with her about party spots until some customers walk in.'
	act 'Return to work': gt 'city_kafe', 'shift_end'

elseif $ARGS[0] = 4:
	*clr & cla
	npc_rel['A42'] += 1
	minut += 10
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/Ashot/ASH-002.jpg"></center>'
	'You''re killing time in the kitchen during a slow period when you start chatting with Ashot, the fry cook. He''s a good-looking guy, funny and relaxed, and he''s enjoyable to talk to. You''re curious about him, because he looks like he''s from the Caucasus but sounds like a native of St. Petersburg, so you tactfully raise the topic.'
	'"Oh, my great-grandfather was a civil engineer," he tells you. "After the Great Patriotic War, Leningrad was trashed, so the state sent him up here to help rebuild, and we''ve been here ever since. I''ve never even been to Armenia. Nobody in my family goes there except to find a wife or husband, but then we come back. We''re Russian now."'
	'"Do you have any siblings?" you ask.'
	'He laughs. "Yes. Everyone in my family for generations have been professionals of some kind, and my brother and sister are no exception. My brother is a lawyer in Moscow, and my sister is in residency as a cardiac surgeon here in the city."'
	act 'Continue talking':
		cla
		'"And you''re a short-order cook in a diner..." you observe with a grin.'
		'"And my parents remind me how disappointed they are in me every time they see me," he agrees with a carefree laugh. "But all that studying and stuff isn''t for me. I''m going to make my fortune another way."'
		'"And what way is that?" you ask.'
		'"There''s plenty of money out there for a guy who''s willing to hustle for it," he shrugs. "I''ve got all kinds of things lined up, people who want things that I can find. Nothing illegal, just things you can''t look for on the internet."'
	
		act 'Continue talking':
			cla
			'You arch an eyebrow. "That sounds interesting. Like what?"'
			'"Oh, all kinds of stuff," he says with a mysterious grin. "I can cut you in if you''re willing to help me out with it."'
			'You see Nika roll her eyes and shake her head at her dishwashing station.'
			act 'Return to work': gt 'city_kafe', 'shift_end'
		end
	end
end

if $ARGS[0] = 'shift_end':
	!! set to 20:00
	if hour < 20: minut += (20-hour)*60 - minut

	if rand(0,5) = 0:
		gt 'city_kafe', 'end_1'
	elseif rand(0,4) = 0:
		gt 'city_kafe', 'end_2'
	elseif rand(0,3) = 0:
		gt 'city_kafe', 'end_3'
	elseif rand(0,2) = 0:
		gt 'city_kafe', 'end_4'
	elseif rand(0,1) = 0:
		gt 'city_kafe', 'end_5'
	else
		gt 'city_kafe', 'end_6'
	end
end

if $ARGS[0] = 'end_1':
	*clr & cla
	workKafe['tips_roll'] += 50
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/END-001.jpg"></center>'
	'You''re fortunate this evening to be able to wait on three regulars who are fully immersed in the vibe Fabi is trying to present. Each uses an English nickname – Lucky, Spike and Slim – and they pepper their conversation with 50s American slang. You follow most of it, though some is confusing: you have no idea what it means when they call you a “hepkitty”, for example, and their suggestions for your attire are confusing, but you laugh with them because they''re fun guys who eat here all the time and always tip well. They''re Fabi''s favorite customers, so he always spends time at their table chatting with them whenever they come in.'
	act 'Count your tips': gt 'city_kafe', 'tips'
end

if $ARGS[0] = 'end_2':
	*clr & cla
	workKafe['tips_roll'] -= 40
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/END-002.jpg"></center>'
	'It''s a bad evening. You''re saddled with a continuous stream of demanding customers who are constantly calling for your attention, and by the time your shift ends you''re so frazzled you barely know which end is up. You begin mixing up orders, forgetting drinks, dropping plates and generally having a lousy time of it. You''re ready to pull out your hair when Marisha grins at you. "Look at it this way: in a hundred years, who''s gonna care?"' 
	'You can''t help but feel like you heard that in a movie once, and it''s not particularly comforting.'
	act 'Count your tips': gt 'city_kafe', 'tips'
end

if $ARGS[0] = 'end_3':
	*clr & cla
	workKafe['tips_roll'] += 30
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/END-003.jpg"></center>'
	'It''s a busy night and the time flies. You''re constantly rushing back and forth between the kitchen and the dining area as fast as your feet can take you, and you find yourself in a good groove. You get all your orders right, you don''t forget anything and you make enjoyable, light conversation with most of your customers. Your tips should be decent.'
	act 'Count your tips': gt 'city_kafe', 'tips'
end

if $ARGS[0] = 'end_4':
	*clr & cla
	workKafe['tips_roll'] += 40
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/END-004.jpg"></center>'
	'Most of the time the customers at the Roadhouse are pretty cool, but sometimes, like tonight, they can be a real pain in the ass. An unusually high percentage of your tables tonight are guys with no women, and most of them seem to want to hit on you more than they want to eat. Some of it is playful, but a few of them get overly insistent and make it unpleasant just to walk past their table with their constant comments. One guy even gets a little too handsy and you ''accidentally'' dump his kvass into his lap; his suggestions for how you can clean it up are unwelcome. At least you made some decent tips.'
	act 'Count your tips': gt 'city_kafe', 'tips'
end

if $ARGS[0] = 'end_5':
	*clr & cla
	workKafe['tips_roll'] -= 30
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/END-005.jpg"></center>'
	'The evening starts pretty well, except for a couple of misbehaved kids whose parents are more interested in looking at their phones than monitoring them, but it all goes downhill with a single jerk: one customer seems determined to have a terrible time, which wouldn''t be so bad if he wasn''t also determined to do it loudly. No matter what you do, his complaints echo from one end of the room to the other. He shouts at you for being too slow and insists that you got his order wrong until his companion corrects him, much to his annoyance. He takes up way too much of your time and energy, and in the end stiffs you on the tip. Asshole.'
	act 'Count your tips': gt 'city_kafe', 'tips'
end

if $ARGS[0] = 'end_6':
	*clr & cla
	workKafe['tips_roll'] -= 50
	!!Fabi
	npc_rel['A41'] += 1
	!!Ashot
	npc_rel['A42'] += 1
	!!Marisha
	npc_rel['A93'] += 1
	!!Nika
	npc_rel['A43'] += 1
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/END-006.jpg"></center>'
	'Almost nobody comes in today and you and the rest of the staff spend most of your time standing around talking. You give your few customers excellent service and they tip you well, but there aren''t enough of them to make this a lucrative evening. However, you actually enjoy yourself more than you''d expect, because you and your coworkers tell each other funny stories and jokes and talk about music and movies to pass the time. By the time the last customer leaves, you feel like you know all of them a little bit better.'
	act 'Count your tips': gt 'city_kafe', 'tips'
end
	
if $ARGS[0] = 'tips':
	*clr & cla
	!! Bonus for wearing server outfit
	if PCloStyle2 = 2: workKafe['tips_roll'] += 50
	!! Tips calc: 4 x rands for a bell curve base. Add the daily modifier appearance -1 per point under 100 and +1 per point over and serving skill.
	workKafe['tips_total'] = (rand(1,125) + rand(1,125) + rand(1,125) + rand(1,125)) + workKafe['tips_roll'] + pcs_apprnc - 100 + pcs_servng
	!! minimum and maximum at 100 and 500 with small randomisation
	if workKafe['tips_total'] < 100: workKafe['tips_total'] = 110 - rand(0,20)
	if workKafe['tips_total'] > 500: workKafe['tips_total'] = 510 - rand(0,20)
	!! Give Sveta her tips
	money += workKafe['tips_total']
	!! Reset daily modifier
	workKafe['tips_roll'] = 0
	!! Skill and attribute gains
	if pcs_servng < 100: gs 'exp_gain', 'servng', 1
	if pcs_vital < 30: gs 'exp_gain', 'vital', 1 + rand(0,steroid_dose) & steroidcheck = 1
	if steroidcheck = 1 and steroid_dose > 0 and rand(1,5) <= steroid_dose:clit_size += rand(0,1) & killvar 'steroidcheck'
	gs 'stat'
	'<center><b><font color="maroon">The Roadhouse</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/cafe/kafe.jpg"></center>'
	'With your shift finally over, you sit at one of the tables and count out your tips for the day.'
	*nl
	'You made <<workKafe[''tips_total'']>> <b>₽</b> in tips.'
	*nl
	'Fabi switches the lights off in the kitchen and ushers you out with a smile so he can finish closing up.'
	if rand(0,3) = 0:
		act 'Leave': gt 'city_kafeend'
	else	
		act 'Leave': gt 'city_residential'
	end
end


--- city_kafe ---------------------------------

