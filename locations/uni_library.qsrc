# uni_library

!!This is the file containing the university library

if $ARGS[0] = 'start':
	$sexloc = 'uni_library'
    $location_type = 'public_indoors'
	cla & *clr
	minut += 5
	$menu_loc = 'uni_library'
	$menu_arg = 'start'
	menu_off = 0
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b><font color="maroon">The University Library</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/library/library.jpg"></center>'
	'One of the medium sized buildings is the University library, it is three stories tall and filled with books on each floor, sections with tables scattered around to study or relax, as well as computer stations on each floor to help the students study.'
	if (week < 6 or hour < 23) and hour > 7:
		act 'Study': gt 'uni_library', 'study'
		act 'Wander around': gt 'uni_library', 'wander'
	else
		'The library is closing'
	end
	act 'Leave':
		minut += 2
		gt 'uni_grounds', 'main'
	end
end

if $ARGS[0] = 'study':
	$sexloc = 'uni_library'
	cla & *clr
	minut += 5
	gs 'stat'
	'<center><b><font color="maroon">The University Library</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/library/study' + rand(1,7) + '.jpg"></center>'
	'You spend some time collecting some books on your subjects and grab one of the library laptops, then you find a quiet place to sit.' 
	if university['semester_week'] > 0:
		i = 0
		:study_loop
		if $class_list_institution[i] = 'uni_<<$university[''enrolled_in'']>>_semester_<<university[''enrolled_in_semester'']>>':
			if dyneval ('RESULT = class[''<<$class_list_institution[i]>>_<<$class_list_name[i]>>_optional_weekly_grade_gain'']') < dyneval ('RESULT = class[''<<$class_list_institution[i]>>_<<$class_list_name[i]>>_optional_weekly_max'']'):
				dynamic '	act ''Study for your <<$class_list_name[i]>> class (30 minuts)'': gt ''uni_library'', ''studying'', ''<<$class_list_institution[i]>>'', ''<<$class_list_name[i]>>'' '
			else
				*nl
				'You don''t need to study more this week for your <<$class_list_name[i]>> class'
			end
		end
		
		i += 1
		if ARRSIZE('class_list_institution') >= i: jump 'study_loop'
		killvar 'i'
	elseif university['exam_week'] > 0:
		i = 0
		:exam_loop
		if $class_list_institution[i] = 'uni_<<$university[''enrolled_in'']>>_semester_<<university[''enrolled_in_semester'']>>' and func('uni_programs', 'exam', 'is_over', $class_list_name[i]) = 0:
			dynamic '	act ''Study intensely for your <<$class_list_name[i]>> exam (30 minuts)'': gt ''uni_library'', ''studying_exam'', ''<<$class_list_institution[i]>>'', ''<<$class_list_name[i]>>'' '
		end
		
		i += 1
		if ARRSIZE('class_list_institution') >= i: jump 'exam_loop'
		killvar 'i'
	end	
	act 'Leave':
		minut += 5
		gt 'uni_grounds', 'main'
	end
	act 'Return to entrance':
		minut += 2
		gt 'uni_library', 'start'
	end
	act 'Wander around': gt 'uni_library', 'wander'
end

if $ARGS[0] = 'studying':
	cla & *clr
	'<center><b><font color="maroon">The University Library</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/library/study' + rand(1,7) + '.jpg"></center>'
	minut += 30
	gs 'grades', 'optional_activity_attribute', '<<$ARGS[1]>>', '<<$ARGS[2]>>', 'yes', min(pcs_intel+10,100)
	gs 'stat'
	'Once there, you open the books and start studying for your <<$ARGS[2]>> class. The library is mostly quiet, but you hear occasionally noises, some of them hard to make out and some of them fairly suspect. Still, for the most part it is a good environment to study in, there are a few other students you can see studying as well.'
	*nl
	if dyneval ('RESULT = class[''<<$ARGS[1]>>_<<$ARGS[2]>>_optional_weekly_grade_gain'']') < dyneval ('RESULT = class[''<<$ARGS[1]>>_<<$ARGS[2]>>_optional_weekly_max'']'):
		'You study for half an hour, and you can tell you will need to study more if you want to completly understand this weeks material'
	else
		'You study for half an hour, and you think you understand everything that is covered this week'
	end
	act 'Leave':
		minut += 5
		gt 'uni_grounds', 'main'
	end
	if (week < 6 or hour < 23) and hour > 7:
		act 'Return to entrance':
			minut += 2
			gt 'uni_library', 'start'
		end
		act 'Keep Studying': gt 'uni_library', 'study'
		act 'Wander around': gt 'uni_library', 'wander'
	else
		'The library is closing'
	end
end

if $ARGS[0] = 'studying_exam':
	cla & *clr
	'<center><b><font color="maroon">The University Library</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/library/study' + rand(1,7) + '.jpg"></center>'
	!!Nerds like studying and so their mood improves a little. Everybody else gets in a worse mood by studying.
	intel_exp += max(0,rand(-1,1))
	pcs_mood += (traits['nerd_status']*5 -10)
	minut += 30
	'Once there, you open the books and start studying for your <<$ARGS[2]>> exam. The library is mostly quiet, but you hear occasionally noises, some of them hard to make out and some of them fairly suspect. Still, for the most part it is a good environment to study in, there are a few other students you can see studying as well.'
	if pcs_sleep < 5:
		no_study = 1
		'You are so tired that you are constantly falling asleep. You get no studying done in this condition.'
	elseif pcs_sleep < 30:
		study_mod -= 20
		'You are very tired and have a hard time keeping you eyes open.'
	end
	if alko >= 6:
		no_study = 1
		'You are so drunk that your eyes can''t make sense of the letters written in the books. Holding your pen is also a problem.'
	elseif alko >= 4:
		study_mod -= 40
		'You are drunk and have trouble concentration on reading the books. When you look back on the notes you made, a lot of them are incomprehensible.'
	elseif alko >= 2:
		study_mod -= 20
		'You are feeling a little tipsy,still you think the studying is going great. But when you look at your notes afterwards, you notice that a lot of them make no sense.'
	end
	if mentats_dose >= 1: study_mod += 30
	If pcs_mood < 50:
		study_mod -= 20
		'You are in a bad mood and it clearly affects you studying.'
	end
	if StrongNarkota > 0 or jointhigh > 0:
		study_mod -= 40
		'You are stoned, and it is not making it easier to concentrate on studying.'
	end
	if amphHigh > 0: study_mod += 20
	if pcs_energy < 5:
		study_mod -= 20
		'You are extremely hungry and it is hard to think about anything else that food.'
	elseif pcs_energy < 10:
		study_mod -= 10
		'You are very hungry and it affects your ability to concentrate.'
	elseif pcs_energy < 20:
		study_mod -=10
		'You are hungry and your thoughts often drift to food, affecting your performance negatively.'
	end
	if pcs_horny > 90:
		study_mod -= 10
		'You are so aroused, that your thoughts often drift to sex, affecting you ability your study.'
	end
	if pain['total'] > 90:
		no_study = 1
		'You are in so much pain that you can''t study.'
	elseif pain['total'] > 75:
		study_mod -= 40
		'You are in so much pain that you have a very hard time to concentrate on studying.'
	elseif pain['total'] > 60:
		study_mod -= 20
		'Your pain is constantly bothering you and it makes it harder to study.'
	end
	*nl
 	study_mod = max(0,(rand(30 + study_mod,pcs_intel + study_mod) + 10)/33)
	if no_study = 1:
		'You try to study for half an hour, but get nothing done.'
	else
		if study_mod = 0:
			'You study for half an hour, but you don''t think you are improving.'
		elseif study_mod = 1:
			'You study for half an hour, and think you are improving a little.'
		elseif study_mod = 2:
			'You study for half an hour, and think you are making good progress in getting ready for the exam.'
		else
			'You study for half an hour, and think you are improving a lot.'
		end		
		gs 'grades', 'grade_award', '<<$ARGS[1]>>', '<<$ARGS[2]>>', 'yes', study_mod
	end
	
	gs 'stat'
	
	killvar 'study_mod'
	killvar 'nod_study'
	
	act 'Leave':
		minut += 5
		gt 'uni_grounds', 'main'
	end
	if (week < 6 or hour < 23) and hour > 7:
		act 'Return to entrance':
			minut += 2
			gt 'uni_library', 'start'
		end
		act 'Keep Studying': gt 'uni_library', 'study'
		act 'Wander around': gt 'uni_library', 'wander'
	else
		'The library is closing'
	end
end

if $ARGS[0] = 'wander':
	$sexloc = 'uni_library'
	cla & *clr
	minut += 15
	gs 'stat'
	'<center><b><font color="maroon">The University Library</font></b></center>'
	temp = rand(1,20)
	if temp = 1:
		'<center><img <<$set_imgh>> src="images/locations/city/island/university/library/events/accident_panty_flash.jpg"></center>'
		'As you wander around, you spot two girls in one of the rows of books. One of them starts climbing the shelf to get at a book out of reach, as she does her short skirt climbs up her thighs, showing off her panties to everyone looking. You can''t help but smile a bit at that as you continue on.'
		gs 'arousal', 'erotic_nudity', 3
		gs 'stat'
	elseif temp = 2:
		'<center><img <<$set_imgh>> src="images/locations/city/island/university/library/events/accident_pussy_flash.jpg"></center>'
		'As you wander around, you spot a girl in one of the rows of books. She starts climbing the shelf to get at a book out of reach, as she does her short skirt climbs up her thighs, showing off her pussy to everyone looking. Obviously she thought a short skirt and no panties was a good combo. You can''t help but smile a bit at that as you continue on.'
		gs 'arousal', 'erotic_nudity', 3
		gs 'stat'
	elseif temp = 3:
		'<center><img <<$set_imgh>> src="images/locations/city/island/university/library/events/asian_titflash.jpg"></center>'
		'As you wander around, you stop just inside of one of the rows of books before it opens up into another one of the small study areas. Before you are several guys, sitting at a table encouraging an Asian girl to pull up her shirt. She keeps telling them no, but you can tell she is getting aroused and excited by the attention they are giving her. Finally, blushing a bit, she pulls up her shirt and holds it up for a bit, showing off her firm little titties. The guys all grin and quietly clap, after a few moments she blushes even redder, pulls her shirt down and walks away. You wonder if she regrets doing it or not. After a few more moments you continue on your way.'
		gs 'arousal', 'erotic_nudity', 3
		gs 'stat'
	elseif temp = 4:
		'<center><img <<$set_imgh>> src="images/locations/city/island/university/library/events/black_titflash.jpg"></center>'
		'As you wander around, you stop just inside of one of the rows of books before it opens up into another one of the small study areas. Before you are several guys, sitting at a table encouraging a chubby black girl to pull up her shirt. She keeps telling them no, but you can tell she is getting aroused and excited by the attention they are giving her. Finally she exposes her large, saggy tits to them. The guys all grin and quietly clap, after a few moments she puts her tits away and goes back to studying. You wonder if she regrets doing it or not. After a few more moments you continue on your way.'
		gs 'arousal', 'erotic_nudity', 3
		gs 'stat'
	elseif temp = 5:
		'<center><img <<$set_imgh>> src="images/locations/city/island/university/library/events/girl_sleeping.jpg"></center>'
		'As you wander around, you spot a girl with her head down on one of the workstations, apparently having fallen asleep while studying. She snores softly, a few other students occasionally glance over and laugh softly at her. You shake your head and leave the girl to sleep in peace.'
	elseif temp = 6:
		'<center><img <<$set_imgh>> src="images/locations/city/island/university/library/events/laying_in_row.jpg"></center>'
		'As you wander around, you start down one of the rows, only to find a girl lying in the middle of the isle on her stomach, reading a book. She glances up briefly at you, then goes back to reading her book, ignoring you. Not able to easily get past her, and not wanting to be rude by trying to step over her, even if she is being rude in the first place, you decide to just skip down to the next isle of books and continue your wandering.'
	elseif temp = 7:
		'<center><img <<$set_imgh>> src="images/locations/city/island/university/library/events/naked_run.jpg"></center>'
		'As you wander around, you glimpse what appears to be a naked girl dash between two isles of books. You glance in the isle she went down and sure enough you see the backside of a naked girl running down the isle. You have no clue where her clothes are or why she''s doing it, but with how fast she''s running, you doubt she is enjoying it. She quickly disappears down another row of shelves.'
		gs 'arousal', 'erotic_nudity', 3
		gs 'stat'
	elseif temp = 8:
		'<center><img <<$set_imgh>> src="images/locations/city/island/university/library/events/naked_walk.jpg"></center>'
		'As you turn to head down an isle you come nearly face to face with a pretty blond girl standing there naked. She just smiles to you, saying hello. You respond in kind and then she walks past you like nothing is wrong. You shake your head and continue on your way. A few moments later you come across a guy holding a handful of girl clothes, who keeps glancing down each isle. You can only assume he is looking for the naked girl you've seen. So you point in the direction you saw her, he smiles and nods, then starts jogging in that direction.'
		gs 'arousal', 'erotic_nudity', 3
		gs 'stat'
	elseif temp = 9:
		'<center><img <<$set_imgh>> src="images/locations/city/island/university/library/events/pussy_flash.jpg"></center>'
		'As you wander around, you go through one of the small study areas. Before you are several guys, sitting at a table encouraging a girl standing just inside of one of the isles to flash them. She keeps telling them no, but you can tell she is getting aroused and excited by the attention they are giving her. Finally she lifts her skirt and flashes them her neatly trimmed pussy. One of the guys briefly woops loudly, and is promptly hit by his friends to be quiet. Several others look over at the boys, but the girl has already dropped her skirt back in place and with a huge grin walks off. You wonder if she regrets doing it or not, as you continue on your way.'
		gs 'arousal', 'erotic_nudity', 3
		gs 'stat'
	elseif temp = 10:
		'<center><img <<$set_imgh>> src="images/locations/city/island/university/library/events/tit_flash.jpg"></center>'
		'As you wander around, you go through one of the small study areas. Before you are several guys, sitting at a table encouraging a girl standing just inside of one of the isles to flash them. She keeps telling them no, but you can tell she is getting aroused and excited by the attention they are giving her. Finally she lifts her shirt and flashes them her firm titties. One of the guys briefly woops loudly, and is promptly hit by his friends to be quiet. Several others look over at the boys, but the girl has already dropped her shirt back in place and with a huge grin walks off. You wonder if she regrets doing it or not, as you continue on your way.'
		gs 'arousal', 'erotic_nudity', 3
		gs 'stat'
	elseif temp = 11:
		'<center><img <<$set_imgh>> src="images/locations/city/island/university/library/events/sex/bj.jpg"></center>'
		'As you walk around, you hear some moaning, and glancing down one of the rows of shelves, you see a boy sitting at one of the workstations. It takes a moment for you to notice the girl under the desk on her knees bobbing her head up and down in his lap. She is obviously giving him a blowjob and he is doing his best to be quiet but failing at it. You watch for a few more moments, feeling aroused at the scene before you decide to move on, before you are noticed.'
		gs 'arousal', 'erotic', 3
		gs 'stat'
	else
		'<center><img <<$set_imgh>> src="images/locations/city/island/university/library/library.jpg"></center>'
		'You wander around the library for a bit, looking at all the books. You pass several of your fellow students, some quietly studying, reading or just looking through the bookshelves. You don''t come across anything of interest.'
	end
	act 'Leave':
		minut += 5
		gt 'uni_grounds', 'main'
	end
	if (week < 6 or hour < 23) and hour > 7:
		act 'Return to entrance':
			minut += 2
			gt 'uni_library', 'start'
		end
		act 'Wander around':
			gt 'uni_library', 'wander'
		end 
	else
		'The library is closing'
	end
end

--- uni_library ---------------------------------

