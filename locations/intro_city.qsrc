# intro_city

gs 'themes', 'indoors'
killvar '$start_type'
$start_type = 'city'
$start_type[1] = 'nomagic'

if $ARGS[0] = 'start':

!!--------------------------inner thought stuff--------------------------------
    $OpenInnerThought = '<i><font color=''magenta''>'
    $CloseInnerThought = '</font></i>'

	age = 18
	month = 5
	$month = 'May'
	day = 4
	week = 3
	hour = 13

!!		Body
	pcs_hgt = 170
	salo = 60
	dick = 0
!!		Stats
	pcs_stren = 20
	strenbuf = 20
	pcs_agil = 15
	agilbuf = 15
	pcs_vital = 25
	vitalbuf = 25
	pcs_intel = 30
	pcs_sprt = 25
	pcs_react = 25
	pcs_chrsm = 15
	pcs_prcptn = 40
	money = 13000
	pcs_pubes = 30
	pcs_leghair = 12
	pcs_makeup = 5
	gm_purses[7] = 1
	$currentpursetype = 'gm'
	currentpursenumber = 7
	bag = 1
	home_owned[1] = 1

	pcs_inhib = 15
	pcs_heels = 20
	pcs_makupskl = 15
	pcs_artskls = 15
	pcs_compskl = 20
	pcs_comphckng = 10
	pcs_chess = 15
	pcs_danc = 30
	cltarelka = 1
	fairy = 10
	eda = 5
	poroshok = 5
	shampoo = 20
	tampon = 15
	pcs_energy = 100
	pcs_hydra = 100
	pcs_sleep = 100
	willpowermax = 100

	$start_type = 'nt'
	NTstart = 1
	
	gt 'intro_city', 'city_intro'
end

if $ARGS[0] = 'city_intro':
	$loc = 'intro_city'
	$loc_arg = '2'
	!! Intro to starting city
	!!variables for apartment living
	pcs_hairlng = 100
	*clr & cla
	!!pic of apartment or Gazelle?
	'The big day has finally arrived! Your family piled into your stepfather''s Gazelle pickup and you all traveled from your hometown, Pavlovsk, to the big city of St. Petersburg. Today is the day you move into your own apartment and start this new phase of your life.'
	'You think back to your school days and what kind of student you were...'

	act 'Continue': gt 'intro_city_select', 'start'
end

if $ARGS[0] = '2':
!!	Just hair and eye color here for now; the dialog will be update as more options are added
	'<center><b><font color = maroon>CHARACTER CUSTOMIZATION</font></b></center>'
	'<center><b>Allows you to alter hair and eyes, use to match fixed profile picture if in use.</b></center>'
	'<center><img <<$set_imgh>> src="images/system/1_openings/shared/character_creation_2.jpg"></center>'
	'As you look in the mirror, your face comes into focus. Some aspects of your appearance are already set, you can define the others now.'
	'You are fairly tall.'
	*nl
	'<center>Customize your:</center>'
	'<center><a href="exec:gs ''intro_customization'', ''hair''"><img src="images/system/1_openings/shared/icon_hair.png"></a> <a href="exec:gs ''intro_customization'', ''eyes''"><img src="images/system/1_openings/shared/icon_eye.png"></a></center>'
	
	act 'Done': gt 'intro_city', 'vlad_intro'
end

if $ARGS[0] = 'vlad_intro':
	gs 'intro_initialization'
	gs'stat'
	!! Intro to your stepfather
	*clr & cla
	'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big28.jpg"></center>'
	!!basic desc of Vlad, maybe a preset is used somewhere in game already? If not already in the intro (I think it is.)
	'Having spent almost an hour in the cramped truck, you jump out as soon as you arrive. Vlad gets out and stretches a bit, before heading to the back of the Gazelle to start unloading your stuff.'

	act 'Check Kolka': gt 'intro_city', 'kolka_intro'
end

if $ARGS[0] = 'kolka_intro':
	!! Intro to Kolka
	*clr & cla
	'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big34.jpg"></center>'
	!!basic desc of Kolka, maybe a preset is used somewhere in game already? If not already in the intro (I think it is.)
	'Your brother, caught up in the excitement of seeing the city, tries to slide past Vlad unnoticed but is called back to help unload. He sighs and shuffles his feet a bit before turning around to help his dad get your stuff.'
	'Giving him a smile to encourage him, you thank him for helping. Your effort pays off, his chest puffs out a bit as he starts to help his father.'

	act 'Head inside': gt 'intro_city', 'anya_intro'
end

if $ARGS[0] = 'anya_intro':
	!! Intro to Anya
	*clr & cla
	'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big33.jpg"></center>'
	!!basic desc of Anya, maybe a preset is used somewhere in game already? If not already in the intro (I think it is.)
	'Your sister comes up to you, links her arm with yours, and pulls you forward. "Come on, <<$pcs_nickname>>! I want to see this place."'
	'Already knowing the answer, you ask anyway "Why don''t you have your own apartment then?"'
	'"I''m comfortable where I am right now. When I''m ready, maybe I will."'
	'Rolling your eyes, you let her keep pulling on your arm.'

	act 'Enter the apartment': gt 'intro_city', 'apartment_intro'
end

if $ARGS[0] = 'apartment_intro':
	$temp_nickname = input('<center>What name do you address your mother by?<br>Mother (default), Mum, Mom, Mama, etc.</center>')
	if $temp_nickname ! '':$npc_nickname['A29'] = '<<$temp_nickname>>'
	killvar '$temp_nickname'

	!! Intro to apartment
	*clr & cla
	'<center><img <<$set_imgh>> src="images/locations/city/residential/apartment/home/korr.jpg"></center>'
	'Once you get inside you look around, already trying to decide where things should go. Anya sees this, and laughing, drags you through each room to check them out.'
	'By the time you''re done Kolka and Vladimir have started bringing boxes in. "Your <<$npc_nickname[''A29'']>> is staying outside for a bit to keep an eye on your stuff. I don''t think she likes the neighborhood much," he says with a shrug.'
	'Both you and Anya pick up the boxes they set down and take them to the rooms they were labeled for, while your brother and stepfather bring it all inside.'

	act 'Greet <<$npc_nickname[''A29'']>>': gt 'intro_city', 'mom_intro'
end

if $ARGS[0] = 'mom_intro':
	!! Intro to Mom - answer sets relationship level
	*clr & cla
	'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big29.jpg"></center>'
	'Your <<$npc_nickname[''A29'']>> comes inside finally, carrying food she made ahead of time to feed everybody.'
	'"<<$pcs_nickname>>, this is a pretty rough neighborhood. You need to be really careful of those boys outside. The sooner University starts the better, so you can get out of here. I don''t know why you couldn''t just wait until summer ended to move to the city."'
	act 'I will be fine (neutral response)':
		gs 'npc_relationship', 'set', 'A29', '50'
		*clr & cla
		'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big29.jpg"></center>'
		'"Everything will be fine, <<$npc_nickname[''A29'']>>. I will look into university prices and decide then."'
		'She gives you a hug before going to the kitchen to start setting up everybody with a plate of food.'

		act 'Have lunch': gt 'intro_city', 'lunch_break'
	end
	act 'Give it a rest (angry response)':
		gs 'npc_relationship', 'set', 'A29', '30'
		*clr & cla
		'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big29.jpg"></center>'
		'"Just stop <<$npc_nickname[''A29'']>>! I''m an adult now, and I will do whatever I want with who I want!"'
		'Her eyes flash in anger, but she doesn''t say anything. She goes to the kitchen to start setting up everybody with a plate of food.'

		act 'Have lunch': gt 'intro_city', 'lunch_break'
	end
	act 'Don''t worry (loving response)':
		gs 'npc_relationship', 'set', 'A29', '70'
		*clr & cla
		'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big29.jpg"></center>'
		'"I know, don''t worry so much <<$npc_nickname[''A29'']>>. I will be very careful, I promise. It''s only for a few months."'
		'She gives you a big hug before going to the kitchen to start setting up everybody with a plate of food.'

		act 'Have lunch': gt 'intro_city', 'lunch_break'
	end
end

if $ARGS[0] = 'lunch_break':
	*clr & cla
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/dinnerhome.jpg"></center>'
	if npc_rel['A29'] >= 60:
		'<<$npc_nickname[''A29'']>> hands you a plate with a big smile as you all sit down at your kitchen table.'
	elseif npc_rel['A29'] <= 30:
		'Lunch is a little tense at first, but for the most part everybody is used to you and your <<$npc_nickname[''A29'']>> fighting.'
	else
		'<<$npc_nickname[''A29'']>> hands plates of food as you all sit down at your kitchen table.'
	end
	'Before long, you are all laughing and joking with each other as you eat. You are still looking forward to being on your own, but you know times like this meal will become pretty rare once they head home.'
	'Once the meal is done and <<$npc_nickname[''A29'']>> cleans up the mess, everybody stands up to say goodbye.'

	act 'Say goodbye to your family': gt 'intro_city', 'family_goodbye'
end

if $ARGS[0] = 'family_goodbye':
	!! Say goodbye to your family
	*clr & cla
	'<center><img <<$set_imgh>> src="images/locations/city/residential/apartment/home/korr.jpg"></center>'
	'Anya gives a big hug, congratulates you on getting your own place, and then steps back to give the rest of your family room to do the same. Your brother gives you an awkward hug and whispers to text him what it''s like before stepping back too.'
	'Vladimir comes over and hugs you too, tells you how proud he is to be your stepfather and to call if you need anything.'
	if npc_rel['A29'] >= 60:
		'Your <<$npc_nickname[''A29'']>> finishes what she is doing and comes over, tears in her eyes. Wrapping you in a big hug, she reminds you again to stay away from the boys outside, before joining the rest of the family by the door.'
	elseif npc_rel['A29'] <= 30:
		'Your <<$npc_nickname[''A29'']>> finishes what she is doing and comes over and gives you an awkward hug, saying she is sorry for fighting with you on your big day, before joining the rest of the family by the door.'
	else
		'Your <<$npc_nickname[''A29'']>> finishes what she is doing and comes over and gives you a hug and reminds you to stay away from the boys outside, before joining the rest of the family by the door.'
	end
	*nl
	'They all wave and head out, leaving you alone for the first time. You spend some time unpacking your stuff, before you stop and stretch, deciding you can finish later.'

	act 'Continue': gt 'intro_city', 'welcome_intro'
end

if $ARGS[0] = 'welcome_intro':
	gs 'intro_initialization_city'
	!! Welcome to Girl Life speech
	MagikDostup = 1
	showstat 1
	showobjs 1
	gs 'obj_din', 'old'
	*clr & cla
	'<center><img <<$set_imgh>> src="images/system/1_openings/5_city/city_start.jpg"></center>'
	'Welcome to Girl Life. You are finally free of your parents and ready to face all the freedoms and challenges that entails. Your options are nearly unlimited and now it''s time to go out and discover them all.'
	'Don''t forget to enroll in university during August, if that''s a goal you want to pursue.'

	act 'Start the game': gt 'korr'
end


--- intro_city ---------------------------------

