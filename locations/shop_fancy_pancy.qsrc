# shop_fancy_pancy
!!2021/05/12

if $ARGS[0] = 'start':
	killvar 'clothingfilter'
	$loc_arg = $ARGS[0]
	$loc = 'shop_fancy_pancy'
    $location_type = 'public_indoors'
	$menu_loc = 'shop_fancy_pancy'
	$menu_arg = 'start'
	menu_off = 0
	*clr & cla
	mag = 1
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b><font color="maroon">Fancy Pancy</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/pushkin/fancypancy/shop.jpg"></center>'
	*nl
	'This small boutique shop looks more like a dressing room at the burlesque club nearby. Satin, leather, ruffles and glamour make it clear this is not a place to buy a practical outfit. It is the only place to buy outfits suitable for the burlesque club or perhaps spice up things in the bedroom.'
	*nl

	'The boutique sells designer winter coats, burlesque clothing, beautiful shoes and exquisite handbags.'

	act 'Leave the boutique': minut += 3 & gt 'liames'
	
	act 'View clothing': minut += 5 & gt 'shop_fancy_pancy', 'clothes'
end

if $ARGS[0] = 'clothes':
	$loc_arg = 'clothes'
	$loc = 'shop_fancy_pancy'
	$menu_loc = 'shop_fancy_pancy'
	$menu_arg = 'clothes'
	menu_off = 0
	*clr & cla
	gs'stat'
	'<center><b><font color="maroon">Viewing Fancy Pancy outfits</font></b></center>'
	*nl

	gs 'clothing_QV', 'shop_filter_header'

	act 'Return': minut += 1 & gt 'shop_fancy_pancy', 'start'

	!!To filter just this set
	clo_i = 0
	:loopfancy_burlesque_filter

		if Clothingstock > 11:
			i = 1
		else
			i = 2
		end

		:loopfancy_burlesque
			if fancy_burlesque[i] = 0:
				gs 'clothing_attributes', 'fancy_burlesque', i
				gs 'clothing_QV', 'shop_filter'

				!!sort by filter
				if (clothingfilter['quality'] = 0 and CloQuality = clo_i) or (clothingfilter['inhibition'] = 0 and CloInhibit = clo_i) or clothingfilter['number'] = 0:
					if clothingfilter['inc'] = 1 and clothingfilter['only'] = 1: *p '<a href="exec: i = <<i>> & gt ''shop_fancy_pancy'', ''fancy_burlesque''"><img src="images/pc/items/fancy/burlesque/<<i>>.jpg" 	height="250" /></a>'
				end
			end
			i += 2
		if i <= 40:jump 'loopfancy_burlesque'

	clo_i += 1
	if clothingfilter['quality'] = 0 and clo_i <= 7:
		jump 'loopfancy_burlesque_filter'
	elseif clothingfilter['inhibition'] = 0 and clo_i <= 40:
		jump 'loopfancy_burlesque_filter'
	end
end

if $ARGS[0] = 'fancy_burlesque':
	cla
	menu_off = 1
	if fancy_burlesque[i] ! 0:
		msg 'You already own these clothes.'
		gt 'shop_fancy_pancy', 'clothes'
	end

	gt 'clothing', 'view_clothing_item', 'fancy_burlesque',i, 20000, 'shop'

end

--- shop_fancy_pancy ---------------------------------

