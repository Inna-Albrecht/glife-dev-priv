# gdk
music_loop = 0
CLOSE ALL
$menu_loc = 'gdk'
$menu_arg = ''
menu_off = 0
$metka = ''
$loc = $curloc
$location_type = 'public_outdoors'
gs 'stat'
'<center><b><font color="maroon">Community Centre</font></b></center>'

act 'Leave': minut += 2 & gt 'pavResidential'

if hour >= 9 and hour < 20:
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/community/dk.jpg"></center>'
else
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/community/dk_night.jpg"></center>'
end

if hour >= 20 and hour <= 23 and week >= 5 and week < 7:
	'The entrance to the Community Centre is full of young people, the guys are just hanging around smoking and joking. The girls in small groups, all dressed up and gossiping. You see the bushes moving, Pavlovsks'' famous make out garden.'
	'You hear people in the alley beside the club. <a href="exec:minut += 5 & gt ''ugel''">Investigate</a>.'
	if gdkin_in = daystart:
		act 'Return to the dance':gt 'gdkin'
	elseif money >= 25:
		act 'Go to the dance (25 <b>₽</b>)':
			money -= 25
			minut += 5
			gt 'gdkin'
		end
	end
else
	'The Community Centre has sports facilities and offers classes in the evening.'
	'It houses a small library. Open from 8h until 18h.'
	'Upstairs, there are some hobby clubs available from 12h until 20h.'
	'The sports section is open from 14h until 20h during the week and 10h until 20h during the weekend.'
	'On Friday and Saturday evenings, from 20h to midnight, the main hall is used as a local club, with popular music and dancing. Entrance 25 <b>₽</b>.'
end


if (week < 6 and hour >= 14 and hour < 20) or (week >= 6 and hour >= 10 and hour < 20):act 'Go to the sports section':gt 'gdksport', 'start'

if hour >= 8 and hour < 18:act 'Go to the library':gt 'gdkbibl'

if hour >= 12 and hour < 20:act 'Go upstairs to the hobby clubs':gt'gdkkru'

if nerd_night_game = 1 and nerd_night_game_day = daystart and hour = 19:
	act 'Wait for game night to start':
		hour = 20
		minut = 0
		gt 'gdkbibl'
	end
end
if nerd_night_game = 1 and nerd_night_game_day = daystart and hour = 20: act 'Go up stairs to the hobby clubs and <b>Join nerds</b>': gt 'gschool_socialchg','nerd game night'

if hour >= 8:act 'Go to the toilets': gt 'gdktoilet', 'start'

if joint > 0 and jointhigh = 0: act 'Smoke a joint': gs 'drugs', 'joint' & gt $loc, $metka

gs'family'

--- gdk ---------------------------------

