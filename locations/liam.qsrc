# liam
!!2021/05/14

$loc_arg = ''
$loc = 'liam'
$location_type = 'public_outdoors'
$menu_loc = 'liam'
$region = 'puskin'
$menu_arg = ''
menu_off = 0
if sound = 0:PLAY 'sound/street.mp3',30
*clr & cla
gs'stat'
bordysh = 0
housrA = 0
palMir = 0
obdysh = 0
tanhouse1 = 0
obMir = 0
obLoker = 0
houseob = 0
borMir = 0
'<center><B>Pushkin Town Center</B></center>'
if month >= 11 or month <= 3:
	if hour >= 8 and hour <= 21:
		'<center><img <<$set_imgh>> src="images/locations/pushkin/townwinter.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/pushkin/townwinternight.jpg"></center>'
	end
else
	if hour >= 8 and hour <= 21:
		'<center><img <<$set_imgh>> src="images/locations/pushkin/liam.jpg" ></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/pushkin/townnight.jpg"></center>'
	end
end

'The old part of the city, usually called Old Town. A few centuries ago, this was the village of Pushkin.'
'Later, the rich bought the land off the poor and knocked down the wooden shacks and farm houses to build large estates. The city began to grow rapidly, and it became a desirable suburb.'
'Since then, even the old houses have been improved, extended, and modernized. The old buildings and beautiful park made it popular with tourists, and it has become part of Pavlovsk.'

'Not far from here is the <a href="exec:minut += 5 & gt ''liames''">Okhlopkov Square</a>.'
'Close by is a narrow street leading to a <a href="exec:gt ''dvor'',''start''">vacant house</a>.'
'You see a <a href="exec:gt ''ugol'',''start''">passage</a> between the houses leading to a courtyard.'
if car > 0 and cardrive = 14:
	'<a href="exec:gs ''carF'',''start''">Your <<$car>></a> is parked at the curb.'
end

if hour >= 13 and hour <= 15 and bodimodel = 1 and arts = 0 and tusa = 1:
	if rand(1,100) >= 90:
		cls
		minut += 5
		gs'stat'
		gs'dibodi'

		'<center><img <<$set_imgh>> src="images/characters/pushkin/marinka/marinka.jpg"></center>'
		'You notice a girl from the local hangout, Marinka, standing in a doorway nearby.'

		act 'Ask Marinka about the art studio':gt 'dibodi', 'sptusa'
	end
end

!!this location does not exist
!!if arts = 1:'<a href="exec:gt ''Marin'',''start''">Marinka''s Apartment</a>'

act 'Walk to Okhlopkov Square (0:05)':minut += 5 & gt 'liames'
act 'Walk to the train station (0:15)':minut += 15 & nroad = 15 & gt 'pav_train'
act 'Walk to Pavlovsk park (0:10)':minut += 10 & nroad = 10 & gt 'pav_park', 'start'
act 'Walk to the highway (0:05)':minut += 5 & nroad = 15 & gt 'road'

act 'Wait':gs 'obj_din', 'wait'

if pushkin_walk_day ! daystart:
	act 'Take a walk (1:00)':
		cla & *nl
		minut += 60
		pcs_mood += 10
		pcs_willpwr += 1
		pushkin_walk_day = daystart
		gs 'stat'
		'You breathe in the fresh air and slowly walk along the narrow streets of the Old Town, admiring the beautiful architecture.'
		act 'Continue': gt'liam'
	end
end

--- liam ---------------------------------

