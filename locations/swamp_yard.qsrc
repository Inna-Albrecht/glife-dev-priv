# swamp_yard

if $ARGS[0] = 'start':
	$loc = 'swamp_yard'
	$loc_arg = 'start'
	$menu_loc = 'swamp_yard'
	$menu_arg = 'start'
	menu_off = 0
	$location_type = 'secluded'
	hunterscabin = 1
	gs 'gadukino_event', 'sound'
	gs 'hunter_ambient', 'schedule'
	gs 'stat'
	if hour = 5:campfire = 0
	!the chance of encountering hunters
	!the chance of hitting GG, if she`s a slut
	huntslutsexrand = rand(1,8)
	!if lost make this trigger after 2 days in swamp house
	if lost_girl = 1 and daystart > forest_lostday+1 and hour >= 8 and hour < 20: gt 'gadforestlost','rescue','ranger','swamp_yard'
	dirty = 0
	temp_sy = rand(1,10)

	$dirty_swamp_yard = {
		cla
		*clr
		if dirty_swamp_yard < 10:minut += 60 & gs 'sweat', 'add', 10
		if dirty_swamp_yard >= 10 and dirty_swamp_yard < 20:minut += 120 & gs 'sweat', 'add', 20
		if dirty_swamp_yard >= 20:minut += 180 & gs 'sweat', 'add', 30
		if huntersAndreiQw < 15:huntersAndreiQw += 1
		if huntersIgorQw < 15:huntersIgorQw += 1
		if huntersSergeiQw < 15:huntersSergeiQw += 1
		gs 'stat'

		'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/dirty_swamp_yard.jpg"></center>'
		*nl
		'You notice that the yard has gotten pretty dirty. "I need to do something about this." you think to yourself. You grab a garbage bag and start picking up trash.'
		'After a while you''ve finished tidying up the yard and put the trash in the garbage bag. You''re quite pleased with yourself.'

		act 'Further':dirty_swamp_yard = 0 & gt 'swamp_yard', 'start'
	}

	'<center><B>The yard in front of the hut</B></center>'
	if month >= 5 and month <= 9:
		if hour >= 6 and hour < 22:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/swamp_yard.jpg"></center>'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/swamp_yard_night.jpg"></center>'
		end
	else
		if hour >= 8 and hour < 18:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/swamp_yard_winter.jpg"></center>'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/swamp_yard_winter_night.jpg"></center>'
		end
	end
	*nl
	'Behind the hut there is a <a href="exec: gt ''swampspring'' ">spring</a>, if you continue a little further down there is a small <a href="exec: gt ''backwater'' ">creek</a> with relatively clean water.'

	if hunters_available = 1 and hunters_outside = 1 and huntersknow = 0:
		*nl
		'You spot some unfamiliar <a href="exec: gt ''hunters'', ''start'' ">people</a> standing in front of the hut.'
	elseif hunters_available = 1 and hunters_outside = 1:
		*nl
		'You notice the <a href="exec: gt ''hunters'', ''start'' ">hunters</a> standing in the front yard.'
	elseif hunters_evening = 1 and hunters_outside = 1:
		*nl
		'You hear some gunshots and see <a href="exec: gt ''swamp_woods'', ''shooting_practice'' ">Andrei</a> not too far in the distance shooting some bottles.'
	elseif hunters_hunting = 1 and hunters_outside = 1:
		*nl
		'The hunters said they will be out hunting for a couple hours. You have the camp to yourself!'
	elseif (hunters_breakfast = 1 or hunters_lunch = 1 or hunters_drink = 1) and hunters_outside = 1:
		*nl
		'You see hunters are sitting by the <a href="exec: gt ''swamp_yard'', ''campfire''">campfire</a>'
	else
		if huntersKnow = 0:
			*nl
			'The camp is quiet. There''s nobody here right now, where could they be.'
		else
			*nl
			'The camp is quiet. The hunters aren''t here right now, where could they be.'
		end
	end
	
	act '<b>Go to the swamp</b>':
		!add chance to swamped by illume
		if hunters_outside = 1 and huntersknow = 0:
			if hour >= 6 and hour < 23:
				minut += 5
				if rand(1,10)>9:
					gt 'swamp', 'start'
				else
					gt 'swamp','stuck'
				end
			else
				*clr & cla
				gs 'stat'
				'<center><img <<$set_imgh>> src="images/pc/npc_headshots/big173.jpg"></center>'
				'As you start moving away from the hut Igor asks you if you''re going on a hike in the woods.'
				'"<<$pcs_nickname>>, it''s not the best time to hike in the woods, you better stay here.'
				act 'Stay':gt 'swamp_yard', 'start'
				act 'Disregard his advice':
					minut += 5
					if rand(1,10)>8:
						gt 'swamp', 'start'
					else
						gt 'swamp','stuck'
					end
				end
			end
		else
			minut += 5
			if (hour >= 6 and hour < 23) or rand(1,10)>7:
				gt 'swamp', 'start'
			else
				gt 'swamp','stuck'
			end
		end
		gt 'swamp','stuck'
	end
	gs 'bicycle', 'swamp_yard'
	act 'Go into the house':gt 'swamphouse', 'start'
	if dirty_swamp_yard > 10:act 'Go out in the yard':hunterslut -= 1 & dynamic $dirty_swamp_yard

	act 'Approach the firepit': gt 'swamp_yard', 'campfire'
	act 'Visit the spring': gt 'swampspring'
	act 'Visit the creek': gt 'backwater'
	act 'Explore the woods': minut += 10 & gt 'swamp_woods', 'start'


	if hunters_outside = 1 and hunters_available = 1 and huntslutsexrand = 1: gt 'hunter_interactions', 'naked_encounter'
end


if $ARGS[0] = 'campfire':
	!TODO: Add mechanic so that fire slowly goes through wood and extinguishes when there is no more left
	cla & *clr
	$loc = 'swamp_yard'
	$loc_arg = 'campfire'
	menu_off = 1
	gs 'hunter_ambient', 'schedule'
	gs 'stat'

	act 'Move away': gt 'swamp_yard', 'start'

	if campfire = 1:
		if hour >= 8 and hour < 20:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/campfire_day.jpg"></center>'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/campfire_night.jpg"></center>'
		end
		*nl
		'A nice, warm campfire you can sit and relax by. You can use it to cook something or heat up some water to bathe.'
		'You can also put it out with the bucket if there is enough water in it'
	else
		if hour >= 8 and hour < 20:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/campfire_out_day.jpg"></center>'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/campfire_out_night.jpg"></center>'
		end
		*nl
		'You can use this firepit to light a fire, if you have some firewood available.'
	end

	if campfire = 1:
		if hunters_breakfast = 1 or hunters_lunch = 1:
			*clr
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/hanters1.jpg"></center>'
			*nl
			'Hunters are also sitting around the campfire, talking and joking about all kinds of stuff before heading out hunting'

			act 'Chat (0:30)':
				gt 'hunter_interactions', 'chat_with_hunters'
			end

			!TODO: Flesh out the learning content here
			act 'Ask for some survival tips (0:10)':
				hunter_lessons += 1
				*clr & cla

				if hunter_collective_opinion > 25:
					hunter_collective_opinion -= 1
					pcs_bushcraft += 1
					gs 'stat'

					minut += 10

					*nl
					'Hunters talk to you about some of their experiences when they first started out'
				else
					if hunter_lessons > 10 AND hunter_sexual_comfort > 25:
						!TODO: Prototype implementation for the creekside event, to be revamped
						'Sergei tells you to go by the creek for the next lesson about how to scope the area for a hunt.'
						hunter_lessons -= 8
						act 'Further': gt 'hunter_favors', 'creekside_gangbang_request'
					else
						minut += 20
						'The hunters do not even bother to answer you. You simply sit in awkward silence for twenty minutes before getting to to other business'
					end
				end

				act 'Further': gt 'swamp_yard', 'campfire'
			end
		end

		if hunters_drink = 1:
			*clr
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/fireside_drink.jpg"></center>'
			*nl
			'Hunters are drinking and chatting loudly. You can join them for a chat, or maybe a drink or two if you would like'
			act 'Chat (0:30)':
				gt 'hunter_interactions', 'chat_with_hunters'
			end

			act 'Ask them for a drink':
				gt 'hunter_interactions', 'join_drinking'
			end
		end

		if bucket >= 3:
			act 'Put out the fire':
				cla & *clr
				'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/campfire_put_out.jpg"></center>'
				*nl
				'You pour some water to extinguish the campfire'

				campfire = 0
				minut += 5
				bucket -= 3

				act 'Further': gt 'swamp_yard', 'campfire'
			end
		end

		if bucket > 0 and boletus > 0 and mushroom_soup = 0:
			act 'Cook some mushroom soup (0:30)': gs 'hunter_favors', 'mushroom_soup'
		end

		if bucket > 0 and raw_meat > 0 and meat_stew = 0:
			act 'Cook some meat stew (1:00)': gs 'hunter_favors', 'meat_stew'
		end

		if bucket >= 5 and hotwater = 0:
			act 'Heat up water (0:30)':
				cla
				*clr
				minut += 30
				bucket -= 5
				hotwater = 1

				water_heated += 1
				gs 'stat'

				'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/hotwater.jpg"></center>'
				*nl

				if water_heated > 5 and (inhibition < 85 or hunter_sexual_comfort > 25):
					'"It is too tedious to keep heatiing up water", you think to yourself.'
				else
					'With the now heated water, you can bathe yourself in the tub.'
				end

				act 'Continue': gt 'swamp_yard', 'campfire'
			end
		end

		act 'Eat a meal':
			cla
			act 'Return': gt 'swamp_yard', 'campfire'
			act 'Eat jerky and trail mix':gt 'food', 'jerky'
			if meat_stew > 0:
				act 'Eat meat stew':
					gs 'food', 'meat_stew'
					meat_stew -= 1
					*nl
					if meat_stew > 0:
						'There''s <<meat_stew>> '+iif(meat_stew > 1, 'servings','serving')+' of stew left in the pot'
					else
						'You have finished the last of the stew, the pot is empty.'
					end
				end
			end
			if mushroom_soup > 0:
				act 'Eat mushroom soup':
					gs 'food', 'mush_soup'
					mushroom_soup -= 1
					if mushroom_soup > 0:
						'There''s <<mushroom_soup>> '+iif(mushroom_soup > 1, 'servings','serving')+' of soup left in the pot'
					else
						'You have finished the last of the soup, the pot is empty.'
					end
					gs 'stat'
				end
			end
			if bilberry > 0:
				act 'Eat berries':gt 'food','bilberry'
			end
		end

		if alko > 8:
			gt 'hunter_interactions', 'drunken_blackout'
		end
	else
		!TODO: Tie fire lighting to bushcraft, make hunters do it on their schedule or with Sveta asking
		if firewood >= 10 and campfire = 0:
			act 'Light fire':
				cla & *clr

				if $clothingworntype ! 'nude':
					'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/campfire_light.jpg"></center>'
				else
					'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/campfire_light_nude.jpg"></center>'
				end

				'You use some chopped wood, dry branches and twigs to light a campfire in the firepit'

				firewood -= 10
				campfire = 1
				minut += 15

				act 'Further': gt 'swamp_yard', 'campfire'
			end
		elseif firewood < 10 and campfire = 0:
			*nl
			'There is some wood and twigs by the pit but not enough to light a new fire. You will probably need to collect some branches, chop some or ask a hunter to do so.'
		end
	end
end

if $ARGS[0] = 'daytime_flavor_events':
	gs 'hunter_ambient', 'schedule'
	if rand(1,50) = 1:
		gs 'swamp_yard', 'skirt_breeze'
	elseif rand(1,20) = 1:
		gs 'hunter_favors', 'housekeeping_request'
	elseif rand(1,25) = 1 and hunters_breakfast = 1 or hunters_lunch = 1 or hunters_drink = 1:
		gs 'hunter_ambient' , 'yard_convo'
	elseif rand(1, hunter_sexual_comfort / 4 + 2) = 1:
		gs 'hunter_interactions', 'naked_encounter'
	elseif rand(1,15) = 1:
		gs 'hunter_ambient', 'bring_food'                        
	end
end

--- swamp_yard ---------------------------------

