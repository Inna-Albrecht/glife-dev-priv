# korrPar

music_loop = 0
CLOSE ALL
if sound = 0:PLAY 'sound/korr.mp3',30

$metkaM = $ARGS[0]
$locM = $CURLOC
$metka = $ARGS[0]
$loc = $CURLOC
$location_type = 'private'
clr
popolaini = 0
saunaYouRoom = 0
boycherdaksex = 0
minut += 1
bonusZ = 0
curr_home = 1
gs 'stat'
if night_mode = 1:
	fcolor = rgb(255, 255, 255)
	bcolor = rgb(0, 0, 0)
	lcolor = rgb(106, 90, 205)
else
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)
end
if hour ! 18 and blockBedrPar = 1:blockBedrPar = 0

if blockBedrPar = 0:
	if sisboyday + 1 = daystart and hour = 18 and sisboytrioQW = 1:
		act 'Go to your room':gt 'sisterQW', 'sisboytrioQW_bedroom', 'start'
	elseif sisboyday + 1 = daystart and hour = 18 and (npc_QW['A33'] = 3 or npc_QW['A33'] = 5 or npc_QW['A33'] = 7 or npc_QW['A33'] = 9):
		act 'Go to your room':gt 'sisterQW', 'sisboyQW_bedroom'
	else
		act 'Go to your room':gt 'bedrPar'
	end
end

act 'Go to your parents'' room':gt 'bedrPar2'
act 'Go to the living room':gt 'sitrPar'
act 'Go to the bathroom':minut += 1 & gt 'vanrPar'
act 'Go to the kitchen':gt 'kuhrPar'

act '<font color = maroon>Leave the apartment and go to the stairwell</font>':
	if $clothingworntype ! 'nude':
		gt'pod_ezd','etaj_2'
	else
		msg'<b><font color = red>You need to get dressed.</font></b>'
		gt 'korrPar'
	end
end

act '<font color = maroon><b>Leave the apartment and go outside</b></font>':
	if $clothingworntype ! 'nude':
		if hour < 4 and motherWorry = 0 and age < 18:
			'<font color=red>Are you sure? Your mother will worry if you go out at this time.</font>'
			cla
			act '<font color=red>Yes, go outside</font>': minut += 5 & gt'pavResidential'
			act 'No, stay inside':gt 'korrPar'
		else
			minut += 5
			gt'pavResidential'
		end
	else
		msg'<b><font color = red>You need to get dressed.</font></b>'
		gt 'korrPar'
	end
end

'<center><h1><font color="maroon">Corridor</font></h1></center>'
'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/korrpar.jpg"></center>'
*nl
'The corridor of your parent''s apartment. There are several doors which lead to the other rooms of the apartment, as well as the front door which leads outside to the stairwell.'
'A <a href="exec:gt ''mirror'', ''start''">mirror</a> hangs on the wall near the front door.'

gs 'family'

if indorf = 1:'With your parents being away to visit your grandparents, the apartment seems quiet and empty.'

!to add a counter if you avoid Dimka
if nodimkaK >= rand(7,10) and DimkaWarnedToday = 0:
	nodimkaK = 0
	if hour >= 14 and hour < 24 and week <=5 and DimkaWarnedToday = 0: gt 'dimaEv', 'Dimka_warning'
	if hour >= 9 and hour < 24 and week >=6 and DimkaWarnedToday = 0: gt 'dimaEv', 'Dimka_warning'
end

$korrParMomHeader = '<center><h1><font color="maroon">Mom</font></h1></center><center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/mom/mother.jpg"></center><br />'

if hour >= 16 and hour <= 20 and indorf = 0:
	if artemvbrosvid = 1 and artemhakday ! daystart and npc_QW['A29'] < 2 and mothervideotalk = 0 or BrotherNo = 1 and BrotherNoDay ! daystart:
		cls
		BJvideoPublic = 1
		BrotherNo = 0
		BrotherNoDay = daystart
		pcs_mood = 5
		npc_rel['A29'] -= 40
		gs 'stat'
		*pl $korrParMomHeader
		'Your mother stands in the hallway waiting for you, clearly angry. As soon as she sees you, she grabs your ear and pinches it hard, making you wince in pain. "I just saw this video of you sucking some guy''s dick! What the hell were you thinking!? Your stepfather and I worked so hard to raise you properly, and then you do this. Do you realize that maybe this video was sent to others too? What will the people in town think of us now? I oughta teach you a lesson. What do you have to say for yourself!? Speak up!"'

		act 'Beg her to do nothing':
			cla
			mothervideotalk = 2
			minut += 5
			sub += 5
			gs 'stat'
			*nl
			'You burst into tears: "Mommy, please forgive me! It will will never happen again! I made a stupid mistake, I''m sorry... I didn''t mean to... please... I don''t... sorry..." You fall to your knees sobbing uncontrollably, hoping she will have mercy on you. Your mother looks down at you, still angry but not quite sure what to do. She lets out an exasperated gasp, shakes her head and walks away.'

			act 'Get back on your feet':gt $curloc
		end

		act 'Be silent':
			cls
			mothervideotalk = 3
			minut += 5
			pcs_dom += 10
			spank += 1
			gs 'pain', 5, 'asscheeks', 'spank'
			gs 'stat'
			'<center><h1><font color="maroon">Mom</font></h1></center>'
			'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/mom/spank.jpg"></center>'
			*nl
			'You stand in the hallway staring at the floor, too embarrassed to say anything. What is there to say, anyway?'
			'Your mother grabs you by the arm and pulls you into the living room: "Take off your pants and bend over!" You shake your head, not liking where this is going at all. Getting even angrier by your refusal, she shouts: "Vladimir, come and hold her down!"'
			'A few seconds later your stepfather enters the living room and immediately pulls you down and keeps you in place, while your mother pulls away your clothing. She then grabs a belt and starts to flog your bare ass with it, not holding back at all.'
			'You take your punishment as good as you can, but towards the end you''re in tears and your cries are heard throughout the apartment. When it''s finally over, you raise your head just in time to catch the startled faces of your brother and sister in the doorway.'

			act 'Stand up and straighten your clothes':gt $curloc
		end

		if momslut >= 2:
			act 'Threaten her':
				cla
				mothervideotalk = 1
				npc_rel['A29'] -= 10
				minut += 5
				gs 'stat'
				*nl
				'You slap away her hand with a defiant look in your eyes. "Oh, really? You''re no angel yourself either, Mom... does Dad know what YOU have been up to lately? Why don''t we go ask him, while we''re on this subject?"'
				'Your mother looks at you furiously, but is at a loss for words. After a few seconds of awkward silence she storms off, slamming the door shut behind her.'

				act 'Continue':gt $curloc
			end
		end

		exit
	end

	if artemvbrosvid=1 and artemhakday ! daystart and npc_QW['A29'] >= 2 and mothervideotalk = 0:
		cls
		BJvideoPublic = 1
		pcs_mood = 5
		gs 'stat'
		*pl $korrParMomHeader
		'When you enter the hallway, you find your mother waiting there for you. "What''s all this I hear about a video of you sucking some guy''s dick? How could you be so careless, <<$pcs_nickname>>? You do realize this makes our whole family look bad..."'

		act 'Apologize':
			cla
			mothervideotalk = 1
			minut += 5
			npc_rel['A29'] -= 40
			gs 'stat'
			*nl
			'"I don''t know what to say, mom... I''m really sorry."'
			'Your mother is clearly very upset with you. After a short discussion, she walks away.'

			act 'Continue':gt $curloc
		end

		exit
	end

	if schoolprogul > 2 and SchoolAtestat = 0:
		cls
		gs 'stat'
		*pl $korrParMomHeader

		if npc_QW['A113'] = 10000:
			npc_QW['A113'] = 10001
			schoolprogul = 0
			GspravkaT = 2
			motherKnowSpravka = 1
			'"Oh, my darling, where have you been? We have been so worried."'
			'You go into the kitchen with your mom and spend some time explaining what happened to you. Many of the details are difficult to talk about, and you cry and hug your mom as you tell your story.'
			'Your mom tells you she will sort out everything with the school and makes you a bowl of chicken soup.'
			if pcs_energy >= 30:
				'You are too full to eat it.'
			elseif pcs_energy >= 20 and pcs_energy < 30:
				if pcs_hydra < 20:
					pcs_hydra += 20
					cumspclnt = 2
					gs 'cum_cleanup'
					pcs_breath = 0
				end
				fat += 3
				pcs_energy += 10
				'You have some of the chicken soup, but can''t finish it.'
			elseif pcs_energy < 20:
				if pcs_hydra < 20:
					pcs_hydra += 20
					cumspclnt = 2
					gs 'cum_cleanup'
					pcs_breath = 0
				end
				pcs_energy += 20
				'You gladly eat it all.'
			end

			act 'Leave':gt 'kuhrPar'

			exit
		elseif schoolPredupr = 2:
			schoolPredupr = 3
			SchoolBlock = 1
			home_owned[2] = 0
			schoolprogul = 0
			if $clothingworntype = 'school' and clothingwornnumber = 6:gs 'clothing', 'strip'
			'As you enter the hallway, you find your mother there waiting for you. You have never seen her this furious. "<<$pcs_nickname>>? I just got off the phone with the school, they told me you didn''t show up yet again and expelled you. I''ve had it with you! I''ve warned you so many times! Go ahead, live your irresponsible life the way you want. You''re no longer welcome here." She already packed up all your things from your room before you got home, and roughly shoves the bag into your hands. After that, she pushes you out the door and slams it shut in your face.'

			act 'Leave':gt 'pavResidential'

			exit
		elseif schoolPredupr = 1:
			schoolPredupr = 2
			schoolprogul = 0
			pcs_grades -= 40
			'As you enter the hallway, you find your mother there waiting for you. She seems very angry with you. "School called, they said you didn''t show up for class again. They wanted to expel you! I had to go over there and beg the principal to give you one last chance. I''ve never felt so humiliated! This is the last straw for me, <<$pcs_nickname>>. You will not embarrass me any further. If you miss one more day of school, they will kick you out. And if you get kicked out of school, I will personally kick you out of the house. If you cannot be responsible and behave like a normal person, then get out, no one is forcing you to stay." Having said her piece, your mother turns around and leaves.'
		elseif schoolPredupr = 0:
			schoolPredupr = 1
			npc_rel['A29'] = 0
			schoolprogul = 0
			pcs_grades -= 20
			'As you enter the hallway, you find your mother there waiting for you. "<<$pcs_nickname>>? The school called, they said you weren''t at school today. You know they''re very unforgiving when it comes to absenteeism, they said they would expel you if it happens again. What are you doing with your life? Get it together!" Your mother angrily leaves the room, leaving you to your thoughts.'
		end

		act 'Continue':gt $curloc

		exit
	end
end

if hour >= 15 and hour < 20:
	if Gcall = 0:
		numnpcboy = 0
		:markboyfrendcall
		numnpcboy += 1

		if otnBoyFrend[numnpcboy] >= 20 and boyinday[numnpcboy]!daystart:
			Gcall = 1
			'You hear the <a href="exec:gt ''gtelefon''">phone</a> ringing in the hallway.'
		elseif numboyFrend > numnpcboy:
			jump 'markboyfrendcall'
		end
	end
else
	if Gcall > 0:Gcall = 0
end

if motherWorry = 1 and indorf = 0:
	gt'mother'
elseif motherWorry = 1 and indorf > 0:
	motherWorry = 0
end

if notathomesleep > 7 and hour >= 16 and hour < 21 and mother_go = 0:
	notathomesleep = 0
	cls
	*pl $korrParMomHeader
	if npc_rel['A29'] < 40:
		'You run into your mother in the hallway. "Well well, look who decided to come home for once.'
		act '<B>Continue</B>':gt 'korrPar'
	end
	if npc_rel['A29'] >= 40:
		'You run into your mother in the hallway. She looks happy to see you. "What a surprise! Welcome home, honey! I missed you.'
		act '<B>Continue</B>':gt 'korrPar'
	end
end

!------------------------------------------!
!gg whore if she had given herself to boys in the stairwell, then customers hammering at the door: !
gs'pod_whore'
pod_client = RAND(0,100)
if pod_whore_countQW <= 15:
	if pod_client <= 5 and hour >= 10 and hour <= 23 and pav_slut >= 150 and podwhoreQW >=3:
		play 'sound/door1.mp3',100
		'Someone is at the door: <a href="exec: gt ''pod_whore'', ''client_talk''">Answer</a>'	
	end
elseif pod_whore_countQW > 15:
	if pod_client <= 20 and hour >= 10 and hour <= 23 and pav_slut >= 150 and podwhoreQW >=3:
		play 'sound/door1.mp3',100
		'Someone is at the door: <a href="exec: gt ''pod_whore'', ''client_talk''">Answer</a>'
	end
end	
!'<<$pod_client>>'
!==========================================!

if grad_miss ! 0 and grad_miss <= daystart:
	SchoolAtestat = 1
	intel_exp += 10
	grad_miss = 0
	gs 'stat'
	'Your diploma from school has arrived in the post.'
end

if rand (0,2) ! 0:gs 'din_bad' & dynamic $mobile_check

--- korrPar ---------------------------------

