# shop_photography
!! Photography shop in the City Center Mall
!! Variables used:
!! photographyEv = Track shop progression through buying a camera
!!	0 = First visit
!!	1 = Return visitor, but have not bought camera
!!	2 = Bought camera, unlocks portfolio sharing
!! photography_job = Track progression through job events
!! photojob_daystart = Track when job last accepted - only allowed one job per week
!! portfolio_locations[n] for storing which images Sveta has of locations (value of 1 for have)
!! portfolio_people[n] for storing which images Sveta has of people

! The owner of the shop is Branko Volenskii
if $ARGS[0] = 'start':
	$loc = 'shop_photography'
	$loc_arg = 'start'
	$location_type = 'public_indoors'
	cls
	menu_off = 0
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b><font color="maroon">Camera & Photography Store</font></b></center>'
	'<center><img src="images/locations/city/citycenter/mall/photoshop/shop.jpg"></center>'
	'The shop is dedicated to cameras and is celebrating this fact by displaying a myriad of them along every wall. A singular counter sits just off the adjacent wall with the shopkeeper standing behind it, clearly eager for some trade to enter his shop.'

	if photography_start = 1 and week >= 6:
		act 'Ask about work':		
			*clr & cla
			minut += 10
			gs 'stat'
			'<center><img src="images/locations/city/citycenter/mall/photoshop/owner.jpg"></center>'
			if photography_start = 1:
				dynamic $photojob[evt_PhotoID]
				'You walk up to Branko. "Excuse me Branko, but you said you might have some work for me. Would you happen to have anything?"'
				'He smiles at you. "Yes yes, I remember. You''re <<$pcs_nickname>>,  Let me check." He opens a laptop sitting on the counter next to him and taps on the keys before he looks up at you.'
				
				!! Init Job Array
				gs 'shop_photography', 'job_init'
				gs 'emp_functions', 'disp_evt', 1
				
			end
		end
	end
	if photographyEv = 2 and photography_start = 0:
		act 'Show portfolio': gt 'shop_photography' , 'showportfolio'
	end
	act 'Talk to the shop owner':
		*clr & cla
		minut += 10
		gs 'stat'
		if photographyEv = 0:
			!!first time in shop:
			photographyEv = 1
			'<center><img src="images/locations/city/citycenter/mall/photoshop/owner.jpg"></center>'
			'"Hello there! What''s your name?" the friendly man behind the counter asks with a smile.'
			'"<<$pcs_firstname>>." you reply.'
			'"Nice to meet you, <<$pcs_firstname>>. You look like the sort of person who would be interested in a quality camera. In fact, judging by the sort of person I see before me, I have just the camera right here that would be perfect for you, only 20,000₽!"'
			*nl
			'"Well, I''ve been thinking about getting into photography. It might be worth it." you tell him and he launches into a long conversation about the joys you can have with a camera. He talks about what the camera in question can do, along with all it''s features. It does sound like the perfect camera for someone that is serious about getting into photography, but is that you?'
			gs 'shop_photography', 'camera_options', 'first'
		elseif photographyEv = 1:
			!!returning to shop without buying camera:
			'<center><img src="images/locations/city/citycenter/mall/photoshop/owner.jpg"></center>'
			'The owner smiles when he sees you. "Aha! I remember you! <<$pcs_firstname>>! How fantastic it is that you should return to my shop! Have you changed your mind about buying a camera?"'
			gs 'shop_photography', 'camera_options', 'repeat'
		else
			'The owner smiles when he sees you. "Aha! I remember you! <<$pcs_firstname>>! How fantastic it is that you should return to my shop! How are you getting on with your recent purchase? Brilliantly I hope! I know what I see and I see brilliance before me so it can only be that you would get on brilliantly!"'
			act 'Walk away': gt 'shop_photography' , 'start'
		end
	end
	act 'Leave':
		*clr & cla
		minut += 3
		gt 'torgcentr'
	end
end

if $ARGS[0] = 'camera_options':
	if money >= 20000:
		act 'Purchase DSLR Camera for 20000₽ (with Cash)' : gt 'shop_photography' , 'buycamera' , 'cash'
	elseif karta >= 20000:
		act 'Purchase DSLR Camera for 20000₽ (with Card)' : gt 'shop_photography' , 'buycamera' , 'card'
	else 
		act 'Can''t afford camera':
			*clr & cla
			minut += rand(1,2)
			gs 'stat'
			'<center><img src="images/locations/city/citycenter/mall/photoshop/owner.jpg"></center>'
			if $ARGS[1] = 'first':
				'You consider it for a moment. You would love to buy it, but you know you don''t have enough money for it right now. "Maybe some other time? I can''t really afford it, but thank you for taking the time to explain things to me though."'
				'He smiles. "It was my pleasure young lady. I hope to see you again soon."'
			else
				'You consider it for a moment. You would love to buy it, but you know you don''t have enough money for it right now. "I would love to, but I still can''t afford it. I''m saving up to buy it though."'
				'He smiles. "Best of luck. Feel free to look around if you like." You spend a little time looking at all the cameras and accessories you can''t really afford.'
			end
			act 'Leave':
				cla
				minut += 3
				gt 'torgcentr'
			end
		end
	end
	act 'Don''t buy camera':
		*clr & cla
		minut += rand(1,2)
		gs 'stat'
		'<center><img src="images/locations/city/citycenter/mall/photoshop/owner.jpg"></center>'
		if $ARGS[1] = 'first':
			'You consider it for a moment, but decide against it. "Maybe some other time, but thank you for taking the time to explain things to me."'
			'He smiles. "It was my pleasure young lady. If you change your mind, you know where to find me."'
		else
			'You still don''t know if you want to get into photography. "Maybe some other time, I just wanted to look around."'
			'He smiles. "Of course of course, look around all you like young lady, but if you change your mind you know where to find me."'
		end
		
		act 'Leave':
			cla
			minut += 3
			gt 'torgcentr'
		end
	end
end

if $ARGS[0] = 'buycamera':
	*clr & cla
	minut += rand(1,2)
	if $ARGS[1] = 'cash':
		money -= 20000
	elseif $ARGS[1] = 'card':
		karta -= 20000
	end
	photographyEv = 2
	gs 'stat'
	'<center><img src="images/locations/city/citycenter/mall/photoshop/owner.jpg"></center>'
	'"You have made a momentous decision with this purchase! Here is your very own DSLR camera to do anything from professional commissioned photography to personal snaps of small events. And now that I have launched your photographic crusade to enlightenment, what are you indeed planning to use this opportunity for in this world of possibility?"'
	*nl
	'"I''m thinking of just building a portfolio of pictures for now; something to test how I can build up my ability to frame a scene. I might start by going around the area and looking for pictures to take to build a catalog of photographs."'
	*nl
	'"Fantastic!  Brilliant! Tell you what; once you''ve completed your portfolio, bring it in as I would love to see it! Now go! I release you into this new world of possibility!"'
	act 'Walk away': gt 'shop_photography' , 'start'
end

if $ARGS[0] = 'showportfolio':
	*clr & cla
	minut += 15
	gs 'stat'
	'<center><img src="images/locations/city/citycenter/mall/photoshop/owner.jpg"></center>'
	if pcs_photoskl <= 35:
		'"Ah, your portfolio, by all means I would love to peruse through it!"'
		'"Mmmm.... not bad. You have some natural talent, but you still need to learn how to use lighting and angles better. Yes yes, you have some raw talent but you need more experience. Come back when you can, I would love to see you grow into the amazing photographer I know you can be."'
		'"Yes yes, when you get more experience I might have a fantastic business opportunity for you! You see, in addition to the shop you see before you, I also have a side business in commissioned photography and from what I''ve seen, you might just be the person I''m looking for; you''ll be paid of course! But first practice, practice practice!"'
		'You frown a little. At least he likes your work. "Okay, I will. I''ll keep practicing and when I feel I''ve improved, I''ll return."'
	elseif pcs_photoskl <= 50:
		photography_start = 1
		'"Ah, your portfolio, by all means I would love to peruse through it!"'
		'"Mmmm.... these photographs are good. You have a real eye! With a little more practice, you will become a very skilled photographer."'
		'"You can call me Branko. I have a fantastic business opportunity for you! You see, in addition to the shop you see before you, I also have a side business in commissioned photography and from what I''ve seen, you''re the perfect person to help me with some additional work. There will be no pressure, just visit my beautiful shop at the weekend and I''ll let you know if there''s any photography jobs you can do for me; you''ll be paid of course!"'
		'"Wow, that sounds interesting. I''ll visit when I can."'
	elseif pcs_photoskl <= 75:
		photography_start = 1
		'"Ah, your portfolio, by all means I would love to peruse through it!"'
		'"Mmmm.... these photographs are terrific. Sensational, absolutely sensational."'
		'"You can call me Branko. I have a fantastic business opportunity for you! You see, in addition to the shop you see before you, I also have a side business in commissioned photography and from what I''ve seen, you''re the perfect person to help me with some additional work. There will be no pressure, just visit my beautiful shop and I''ll let you know if there''s any photography jobs you can do for me; cash in hand of course!"'
		'"Wow, that sounds interesting. I''ll visit when I can."'
	else
		photography_start = 1
		'"Ah, your portfolio, by all means I would love to peruse through it!"'
		'"Mmmm.... My god... These are simply breathtaking my dear, just breathtaking! I don''t think I''ve ever meet anyone with as much potential as you in my life! With a little more seasoning, you could become one of the greatest photographers of our time!"'
		'"You can call me Branko. I have a fantastic business opportunity for you! You see, in addition to the shop you see before you, I also have a side business in commissioned photography and from what I''ve seen, you''re the perfect person to help me with some additional work. There will be no pressure, just visit my beautiful shop and I''ll let you know if there''s any photography jobs you can do for me; you''ll be paid of course!"'
		'"Wow, that sounds interesting. I''ll visit when I can!"'
	end
	act 'Walk away': gt 'shop_photography' , 'start'
end

if $ARGS[0] = 'work':
	*clr & cla
	minut += 10
	gs 'stat'
	'<center><img src="images/locations/city/citycenter/mall/photoshop/owner.jpg"></center>'
	'You can''t help but smile and feel excited. Yet another paying job as a photographer! "Yes I would love to, just tell me what you have."'
	'He smiles. "Excellent! I need photos of <<$transient_desc>>" He prints out a page and hands it to you. "Okay, here''s where it is and a list of the photos I need. When you finish, come back here to give me the photos and get paid."'
	'You smile at him. "Thank you, I''ll go there now." With that, you leave the store.'
	act 'Go to work': gt 'shop_photography' , 'photojob'
end

if $ARGS[0] = 'photojob':
	*clr & cla
	gs 'exp_gain', 'photoskl', rand(1,50)
	gs 'stat'
	photography_job += 1
	photojob_daystart = daystart
	! Prevent taking a new job on the sunday following today
	if week = 6: photojob_daystart += 1
	minut += rand(1,2)
	gs 'stat'
	$loc =$transient_mc_locat
	$loc_arg = $photojob_loc_arg
	gt 'shop_photography', $transient_trigger
end

if $ARGS[0] = 'photojob_pay':
	if pcs_photoskl > 125:
		gs 'emp_functions', 'wage_evt', 4
	elseif pcs_photoskl > 100:
		gs 'emp_functions', 'wage_evt', 3
	elseif pcs_photoskl > 75:
		gs 'emp_functions', 'wage_evt', 2
	elseif pcs_photoskl > 50:
		gs 'emp_functions', 'wage_evt', 1
	else
		gs 'emp_functions', 'wage_evt', 5, 100
	end
end

!! Photography jobs functions

if $ARGS[0] = 'job_init':
	!! Set up arrays if not initialised
	if $evt_job[1] = '' :  gs 'emp_functions', 'job_evt'
	array_len = ARRSIZE('$photojob')
	array_len = array_len -1	
	evt_PhotoID = rand(1, array_len)
	dynamic $photojob[evt_PhotoID]
	!! set Day of Week for job to appear on	
	transient_evt_dow = 6
	!! evt_silent - hide event from player  in journal (or other locations) - 1 to enable.
    !! Now set up the event date
    gs 'shop_photography' , 'photojob_pay'
    gs 'emp_functions', 'gen_evt'
end


! Array of photography jobs - additional events should be easy to wire up
$photojob[1] = {
	!!Ensure the event is visible in journal:
	transient_evt_silent = 1
	!! Set the job type as per list
	transient_evt_type = 1
	transient_evt_sub_type = 1
	$photojob_shortname = 'Tank'
	$transient_desc = 'Tank and Bazooka Experience Day in Levingrad Oblast'
	!! Field for trigger location
	$transient_loc = 'city_industrial'
	$transient_arg = ''
	!! Journal loc description
	$transient_mc_locat = 'St Petersburg Industrial Area'
	!! Event content location
	$transient_evt_exec=  'shop_photography'
	$transient_trigger = 'phototank'
}

$photojob[2] = {
	transient_evt_silent = 1
	transient_evt_type = 1
	transient_evt_sub_type = 2
	$photojob_shortname = 'Model'
	$transient_desc = 'Newspaper Commission at Petrovskaya Akvatoria'
	$transient_loc = 'shop_photography'
	$transient_arg = 'start'
	$transient_mc_locat = 'Meet Branko at the Photography Shop'
	$transient_trigger = 'photomodel'
}

$photojob[3] = {
	transient_evt_silent = 1
	transient_evt_type = 1
	transient_evt_sub_type = 3
	$photojob_shortname = 'Wedding'
	$transient_desc = 'Wedding at Yelagin Palace'
	$transient_loc = 'city_residential'
	$transient_arg = ''
	$transient_mc_locat = 'St Petersburg Residential Area'
	$transient_trigger = 'photowedding'
}

$photojob[4] = {
	transient_evt_silent = 1
	transient_evt_type = 1
	transient_evt_sub_type = 4
	$photojob_shortname = 'Motorsport'
	$transient_desc = 'Russian Touring Car Championship Event at St. Petersburg Autodrome'
	$transient_loc = 'city_industrial'
	$transient_arg = ''
	$transient_mc_locat = 'St Petersburg Industrial Area'
	$transient_trigger = 'photomotorsport'
}

$photojob[5] = {
	transient_evt_silent = 1
	transient_evt_type = 1
	transient_evt_sub_type = 5
	$photojob_shortname = 'Court'
	$transient_desc = 'Press Event at City Courthouse'
	$transient_loc = 'shop_photography'
	$transient_arg = 'start'
	$transient_mc_locat = 'Meet Branko at the Photography Shop'
	$transient_trigger = 'photocourt'
}

if $ARGS[0] = 'phototank':
	*clr & cla
	minut += 20
	gs 'stat'
	'<center><img src="images/characters/shared/headshots_generic/37.jpg"></center>'
	'You head over to the industrial region where you quickly meet up with your contact at the Experience Center.'
	'"Hi there; you must be <<$pcs_firstname>>. The group is already on their way to the site, so I''ll take you up there in my car. I''m sure you know everything you need to do, but feel free to ask if you have any questions."'
	'You get in the car and drive for nearly half an hour until you reach the Experience Center. Once there, the representative parks the car and you get out and head inside.'
	act 'Go to the Experience Center':
		*clr & cla
		minut += rand(120,150)
		gs 'exp_gain', 'photoskl', rand(6,10)
		gs 'stat'
		gs 'shop_photography', 'photojob_pay'
		'<center><img src="images/pc/activities/photography/job/tank.jpg"></center>'
		'You arrive at the Experience Center and spend the next two hours taking as many pictures of the experience as you can. The group is clearly enjoying the event; They get to ride in military vehicles across rough terrain and fire weapons at the shooting range.'
		'Once the day is over, you remove your SD card and hand it to the representative, who shows the photos to the group. Once they''ve sorted out whether any of the customers want additional copies, your contact takes you back to the city''s industrial region. When you check your account, you see that you have been paid <<photopay>>.'
		gs 'shop_photography', 'photojob_cleanup'
		act 'Return': gt $loc, $loc_arg
	end
end

if $ARGS[0] = 'photomodel':
	*clr & cla
	minut += rand(60,90)
	gs 'exp_gain', 'photoskl', rand(3,5)
	gs 'stat'
	gs 'shop_photography', 'photojob_pay'
	'<center><img src="images/pc/activities/photography/job/model<<rand(1,3)>>.jpg"></center>'
	'You go to the Petrovskaya Akvatoria Model Museum to take pictures of the various exhibits on behalf of an international newspaper.'
	'After a brief introduction to a member of staff to confirm your activities, you walk around the exhibit, taking a large number of photographs so that the article has a wealth to choose from.'
	'Once you''re finished, you return to the shop and hand the SD card to Branko, who gives you your fee of <<photopay>>₽.'
	gs 'shop_photography', 'photojob_cleanup'
	act 'Return' : gt $loc, $loc_arg
end

if $ARGS[0] = 'photowedding':
	*clr & cla
	minut += rand(10,18)
	gs 'stat'
	'<center><img src="images/characters/shared/headshots_generic/18.jpg"></center>'
	'You head over to the residential district where you meet one of the family members of the betrothed.'
	'"Hello, you must be the photographer! If you follow me, I''ll take you to the happy couple to take the pictures of them."'
	act 'Follow':
		*clr & cla
		minut += rand(90,120)
		gs 'exp_gain', 'photoskl', rand(6,10)
		gs 'stat'
		gs 'shop_photography', 'photojob_pay'
		'<center><img src="images/pc/activities/photography/job/wedding.jpg"></center>'
		'You meet the bride and groom and help direct them into a number of framed situations for you to take some beautiful photos of them together.'
		'Once you have enough, you spend the rest of the day in the background taking a number of photos of the wedding. Once it''s over, you meet back up with the relative who takes the SD card from you.'
		'Leaving them to close up the venue, you walk out back into the residential district. When you check your account, you see that you have been paid <<photopay>>.'
		gs 'shop_photography', 'photojob_cleanup'
		act 'Return' : gt $loc, $loc_arg
	end
end

if $ARGS[0] = 'photomotorsport':
	*clr & cla
	minut += rand(15,20)
	gs 'stat'
	'<center><img src="images/characters/shared/headshots_generic/11.jpg"></center>'
	'You head over to the industrial region and meet a representative of the Autodrome. You will be expected to take pictures of the event and deliver them to your contact.'
	'"Why hello there, you must be the person Branko sent. I''m sure you already know everything you have to do at the track, so I''ll give you this press pass and take you to the Autodrome."'
	'You look at the pass they gave you before getting in their car to be taken to the track.'
	act 'Go to the Autodrome':
		*clr & cla
		minut += rand(180,240)
		gs 'exp_gain', 'photoskl', rand(9,15)
		gs 'stat'
		gs 'shop_photography', 'photojob_pay'
		'<center><img src="images/pc/activities/photography/job/motorsport.jpg"></center>'
		'You''re dropped off at the employee entrance and directed to the pit area as the teams get their vehicles prepared.'
		'You start off by getting shots of the teams in the build-up to the race, showcasing the tension and focus as the countdown approaches zero.'
		'When the race itself starts you roam the track, taking pictures of any event as it happens, getting high intensity shots of fast paced action, as well as the smooth professionalism of the leading drivers. It is a fantastic experience.'
		*nl
		'With your job done, you find your contact again and hand them a full SD card for which they are gratified.'
		'Your contact then takes you back to the city''s industrial region. When you check your account, you see that you have been paid <<photopay>>.'
		gs 'shop_photography', 'photojob_cleanup'
		act 'Return' : gt $loc, $loc_arg
	end
end

if $ARGS[0] = 'photocourt':
	*clr & cla
	minut += 60
	gs 'exp_gain', 'photoskl', rand(3,5)
	gs 'stat'
	gs 'shop_photography', 'photojob_pay'
	'<center><img src="images/pc/activities/photography/job/court.jpg"></center>'
	'"You need to go to the courthouse to take publicity photographs of the completed court session as they leave the building, then deliver them back to me to give you your commission."'
	'You head into the City Center and straight to the courthouse, which has already seen a number of people turn up.'
	'Once you''ve ingratiated yourself into the crowd, you position yourself to take some good shots of the plaintiff accompanied by their lawyers and representatives.'
	'With the verdict read, you quickly make your way outside to the front of the courthouse to take photos of him leaving.'
	'Once you''re finished, you return to the shop and hand the SD card to Branko, who gives you your fee of <<photopay>>₽.'
	gs 'shop_photography', 'photojob_cleanup'
	act 'Return' : gt $loc, $loc_arg
end

--- shop_photography ---------------------------------

