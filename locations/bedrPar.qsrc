# bedrPar

CLOSE ALL
*clr & cla
fromoutside = iif($loc = 'bedrPar',0,1)
$loc = 'bedrPar'
$loc_arg = ''
$location_type = 'private'
$locclass = 'bedr'
$menu_loc = 'bedrPar'
$menu_arg = ''
menu_off = 0
minut += 1
!!day check so mom doesn''t think you have been out all night if you get up very early 
motherworry[1] = daystart
gs 'stat'
gs 'themes', 'indoors'
gs 'family_schedule'

!! This is runs only once, just to make sure that Sveta can sing from the start, does not have to wait a whole day for `cikl` to run.
if ml_performance['max_perform_minutes'] = 0: ml_performance['max_perform_minutes'] = pcs_instrmusic + pcs_vokal

if $ARGS[0] = '' or $ARGS[0] = 'start':
	if locat['Mother'] = 26 and fromoutside:
		'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/mom/prost1.jpg"></center>'
		if prezik > 0 and rand(1,3) = 1:
			gt 'mother', 'condom_find'
		elseif dildo > 0 and motherQW['seen_dildos'] ! 1 and rand(1,3) = 1:
			gt 'mother', 'dildo_find'
		elseif analplug = 1 and analplugin = 0 and motherQW['seen_plug'] ! 1 and rand(1,3) = 1:
			gt 'mother', 'plug_find'
		else
			'You walk into your bedroom and find your <<$npc_nickname[''A29'']>> sweeping and dusting the room, folding clothes and putting them away and making the beds.'
			act 'Access wardrobe':
				$loc = 'korrPar'
				$loc_arg = ''
				gt 'wardrobe', 'start'
			end
			act 'Leave': gt 'korrPar'
		end
	elseif ParrotQW['Level'] = 2:
		*clr & cla
		menu_off = 1
		'As soon as you enter the room you rush over to <<$ParrotQW[''Name'']>>''s cage and pour the contents of the vial into his water bowl.'
		'You hope this will work and keep him from spilling you and your sister''s bedroom secrets.'
		ParrotQW['Level'] = 3
		ParrotQW['Day'] = 3
		
		act 'Continue':gt $loc, $loc_arg
	elseif ParrotQW['SisterReleaseParrotDay'] + 3 < daystart and ParrotQW['Level'] = 1:
		*clr & cla
		menu_off = 1
		'When you enter the room, you notice <<$ParrotQW[''Name'']>>''s cage is open and he is gone.'
		'Anya must have released him since you weren''t able to find a solution in time.'
		ParrotQW['Owned2'] = 0
		ParrotQW['Level'] = 0
		ParrotQW['Day'] = 2
		ParrotQW['Failed'] = 0
		gs 'stat'
		
		act 'Continue':gt $loc, $loc_arg
	elseif pcs_magik >= 6 and spellbefshild = 1 and tobiQW = 0 and locat['Anya_inroom'] = 0:
		gt 'tobiQW', 'start'
		
	elseif locat['Anya'] = 11: 
		gt 'sisterQW', 'sisboyQW_expel'
		
	elseif locat['Anya_inroom'] = 1 and ParrotQW['Owned2'] = 1 and ParrotQW['SisKeepParrot'] = 0:
		*clr & cla
		menu_off = 1
		'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/anya/sister2.jpg"></center>'
		'"What is that?" Anya asks while pointing at <<$ParrotQW[''Name2'']>> in his cage.'
		'"I went to the mall and bought this bird. Isn''t he the cutest?"'
		if npc_rel['A33'] > 50:
			'"Not really, but I can see this is something you really want. As long as he doesn''t make too much noise, I don''t really care."'
			ParrotQW['SisKeepParrot'] = 1
		end
		if npc_rel['A33']  <= 50:
			'"No he isn''t cute, and I suggest you get it out of here. I''m not sharing my room with a bird!"'
			'You are somewhat upset, but you understand. You should have asked her before buying it.'
			'You go to the window, open it and release the bird.'
			ParrotQW['Owned2'] = 0
		end
		gs 'stat'

		act 'Leave': gt 'korrPar'
	else
		'<center><h1><font color="maroon">Your bedroom</font></h1></center>'
		! WD: Correct Image path ~ "images/qwest/alter/<<$loc>>.jpg"
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/bedrpar.jpg"></center>'
		*nl
		'This is the small bedroom you share with your sister, Anya. On one side of the room is a <a href="exec:gt ''wardrobe'', ''start''">wardrobe</a> where you can choose outfits and organise your clothing.'
		*nl
		'Your <a href="exec:gt ''bed'', ''start''">bed</a> stands opposite the wardrobe, and your <a href="exec:gt ''stol'' ,''stolPar''">desk</a> is next to it'+iif(strapon = 1, '. You keep your strapon inside it.', '')+'. Your sister''s bed is in the other corner.'
		*nl
		if $ml_guitar['location'] = $loc: 'Your guitar rests on its stand next to your bed.'

		if ParrotQW['Owned2'] = 1:
			'<a href="exec:gt ''popu'',''start2''"><<$ParrotQW[''Name2'']>></a> is sitting in his cage'
			if daystart - ParrotQW['BuyDate'] <= 10: 
				'<<$ParrotQW[''Name2'']>> starts squawking at you when you enter the room.'
			elseif daystart - ParrotQW['BuyDate'] <= 20: 
				'When <<$ParrotQW[''Name2'']>> sees you, he starts ''speaking''. It sounds almost like he is saying "<b>Hello <<$pcs_nickname>>!</b>"'
			elseif daystart - ParrotQW['BuyDate'] <= 30: 
				'When <<$ParrotQW[''Name2'']>> sees you, he starts ''speaking''. He is clearly saying "<b>Hello <<$pcs_nickname>>!</b>"'
			elseif daystart - ParrotQW['BuyDate'] <= 35: 
				'When <<$ParrotQW[''Name2'']>> sees you, he starts ''speaking''. He is saying "<b><<$pcs_nickname>> very pretty!</b>"'
			elseif daystart - ParrotQW['BuyDate'] <= 40: 
				'When <<$ParrotQW[''Name2'']>> sees you, he starts ''speaking''. He is saying "<b>Great body, <<$pcs_nickname>>!</b>"'
			elseif daystart - ParrotQW['BuyDate'] <= 50: 
				'When <<$ParrotQW[''Name2'']>> sees you, he starts ''speaking''. He is saying "<b>You''re the best, Anya!</b>"'
			elseif daystart - ParrotQW['BuyDate'] <= 55: 
				'When <<$ParrotQW[''Name2'']>> sees you, he starts ''speaking''. He is saying "<b>Yeah, that''s it Anya! Swallow it all!</b>"'
			else
				if ParrotQW['Level'] < 3: 
					'When <<$ParrotQW[''Name2'']>> sees you, he starts ''speaking''. He is saying "<b>Your pussy tastes great, Anya!</b>"'
				else
					parfixrand = rand(0,9)
					if parfixrand = 0:'When <<$ParrotQW[''Name2'']>> sees you, he starts ''speaking''. He is saying "<b>What did you say to me?</b>"'
					if parfixrand = 1:'When <<$ParrotQW[''Name2'']>> sees you, he starts ''speaking''. He is saying "<b>3.1415926535897932384...</b>"'
					if parfixrand = 2:'When <<$ParrotQW[''Name2'']>> sees you, he starts ''speaking''. He is saying "<b>Come closer, my friend.</b>"'
					if parfixrand = 3:'When <<$ParrotQW[''Name2'']>> sees you, he starts ''speaking''. He is saying "<b>Here kitty, kitty!</b>"'
					if parfixrand = 4:'When <<$ParrotQW[''Name2'']>> sees you, he starts ''speaking''. He is saying "<b>I just met you, and this is crazy!</b>"'
					if parfixrand = 5:'When <<$ParrotQW[''Name2'']>> sees you, he starts ''speaking''. He is saying "<b>Bow before me!</b>"'
					if parfixrand = 6:'When <<$ParrotQW[''Name2'']>> sees you, he starts ''speaking''. He is saying "<b>Heâ€™s just a friend, honest!</b>"'
					if parfixrand = 7:'When <<$ParrotQW[''Name2'']>> sees you, he starts ''speaking''. He is saying "<b>What did you do to my socks?</b>"'
					if parfixrand = 8:'When <<$ParrotQW[''Name2'']>> sees you, he starts ''speaking''. He is saying "<b>That''s what she said!</b>"'
					if parfixrand = 9:'When <<$ParrotQW[''Name2'']>> sees you, he starts ''speaking''. He is saying "<b>Help! They''ve turned me into a parrot!</b>"'
					killvar 'parfixrand'
				end
			end
			*nl
		end
		
		'<table><tr><td valign="top">Your <a href="exec:gt ''budilnik'', ''start''">alarm clock</a> is set to <b>'+iif(timer < 10,'0<<timer>>','<<timer>>')+':'+iif(timerM < 10,'0<<timerM>>','<<timerM>>')+'</b> during the week and <b>'+iif(timerEnd < 10,'0<<timerEnd>>','<<timerEnd>>')+':'+iif(timerEndM < 10,'0<<timerEndM>>','<<timerEndM>>')+'</b> during the weekend. It is currently turned '+iif(budilnikOn = 0, '<a href="exec: budilnikOn = 1 & gt $loc">OFF</a>.', '<a href="exec: budilnikOn = 0 & gt $loc">ON</a>.')

		if tabletki > 0 and prezik + prezikcount + preziksabo > 0:
			'Your <a href="exec:gt ''stol'', ''bc''">birth control</a> is hidden in one of your desk drawers, as are your <a href="exec:gt ''stol'', ''bc''">condoms</a>.'
		elseif tabletki > 0:
			'Your <a href="exec:gt ''stol'', ''bc''">birth control</a> is hidden in one of your desk drawers.'
		elseif prezik + prezikcount + preziksabo > 0:
			'Your <a href="exec:gt ''stol'', ''bc''">condoms</a> are hidden in one of your desk drawers.'
		end
		
		if mid($start_type,1,2) = 'sg' and SchoolAtestat = 0 and week = 6 and hour > 4 and hour < 9 and detention_set = 1 and SchoolAtestat = 0 and detention_warning ! daystart:
			detention_warning = daystart
			msg 'You have detention this morning and must be at school before 09:00.'
		end

		if komp = 1: 'Your <a href="exec: gt ''Komp'',''start''">computer</a> is on your desk.'

		if npc_QW['A33'] > 10:npc_QW['A33'] = 10

		if locat['Anya_Gad'] = 0:
			if sisboyparty_day + 1 = daystart and sisboyparty = -1 and hour = 22:
				*clr & cla
				menu_off = 1
				minut += 5
				gs 'stat'
				'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/rekshome/party/sisboyqwparty_6_<<rand(1,7)>>.jpg"></center>'
				'Anya bursts into the room, drunk and disheveled. You notice that her hair and clothes are covered in cum, but she doesn''t seem to care as she falls onto her bed and instantly falls asleep.'
				sisboyparty = 1
				act 'Move away': gt $loc
			else
				sisterdialog = 1

				if locat['Anya'] = 1:
					'<a href="exec:gt ''sistersleep'', ''sister_sleep''">Anya</a> is asleep in her bed.'
				elseif locat['Anya'] = 4:
					sisterdialog = 3
					'<a href="exec:gt ''sister_chat'', ''sister_chat''">Anya</a> is getting ready to go out.'
				elseif locat['Anya'] = 15:
					if anyaQW['bathroom_dildos'] > 0:gt'sister_chat','bathroom_dildo_talk1'
					'<a href="exec:gt ''sister_chat'', ''sister_chat''">Anya</a> is sitting on her bed, reading a book.'
				elseif locat['Anya'] = 26:
					if sisboypartyQW = 1: sisboypartyQW = 2
					if sisboyparty = -1: sisboyparty = 1
					'A drunken Anya is sound asleep in her bed.'
				end
			end
		end

		act 'Exit the room':
			if (locat['Anya'] = 4 or locat['Anya'] = 15) and daystart - ParrotQW['BuyDate'] > 55 and ParrotQW['Owned2'] = 1 and ParrotQW['SisterReleaseParrotDay'] = 0 : gt 'sister_chat', 'pargone'
			gt 'korrPar'
		end
		
		act 'Relax on your bed': gt 'bed', 'start'

		act 'Open wardrobe': gt 'wardrobe', 'start'
		
		gs 'prostitution_functions', 'prostitute_outfit_at_home'
		
		if pornMagazine > 0:'You have hidden your porn magazines under the mattress of your bed.'
		!!Masturbation, allowed after sister threesome (when sis_watch is used for a message) or sister not here.
		sis_watch = 0

		if locat['Anya_inroom'] = 0 or sisboytrio > 0:
			if pornMagazine > 0:gs 'events', 'read_porn'
			if rexCarCount >= 9 and hour < 3 and rexSmsDay ! daystart and smsForRex < 13:
				act 'Send Rex a text message': gt 'rex_events','sms'
			end
			if pcs_horny >= 50:
				act 'Masturbate':gt 'selfplay', 'start'
			end

			if vcard > 0: act 'View your vintage porn cards':card_deck = 0 & gt 'card'
			if card > 0: act 'View your new porn cards':card_deck = 1 & gt 'card'
		end

		gs 'events', 'read'

		if tovarL > 0:'You currently have <<tovarL>> trinkets to sell at the station. The maximum you can carry is 30.'

		if locat['Anya_inroom'] = 0: dynamic $palelady

		gs 'exercise', 'start'

		if sewing_kit = 1:
			'Your <a href="exec:gt ''sewing'',''start''">sewing kit</a> is stored neatly under your bed.'
			*nl
			if tkan <= 0:
				'You do not have any fabric.'
			else
				if pcs_sewng < 40:
					'You have <<tkan>> pieces of sewing fabric left.'
				elseif pcs_sewng < 80:
					'You have enough fabric for <<tkan>> lessons.'
				else
					if newgobelen = 0:
						act'Start a new tapestry':
							*clr & cla
							menu_off = 1
							tkan -= 1
							newgobelen = 1
							minut += 15
							gs'stat'
							'You spend 15 minutes at the embroidery frame, preparing the fabric and outlining the pattern for the tapestry.'

							act'Set the tapestry aside':gt $curloc
						end
					end
				end
			end	
			if newgobelen>=1:
				'Your tapestry is <<newgobelen/10>> percent finished.'
				act'Work on the tapestry':
					*clr & cla
					menu_off = 1
					minut+=60
					gs 'exp_gain', 'sewng', rand(pcs_intel/10, pcs_intel/5)
					gobramax = (pcs_sewng - 70) * 6
					gobramin = (pcs_sewng - 70) * 2
					newgobelen += rand(gobramin,gobramax)
					gs'stat'
					if newgobelen < 1000:'You spend an hour working on your tapestry, which is now <<newgobelen/10>> percent finished.'
						if newgobelen >= 1000:
						newgobelen = 0
						gobelen += 1
						'Your work on the tapestry is done.'
					end
					act'Set the tapestry aside':gt $curloc
				end
			end
			if gobelen > 0: 'You have <<gobelen>> completed tapestries.'
		end

		!! Computer, music and internet use.
		gs 'internet_mobile', 'get_access'
		if locat['Anya_inroom'] = 1:
			gs 'music_actions', 'not_alone'

			if sisboytrio = 0: access['noporn'] = 1
			access['nocamshow'] = 1
			$access['noporn-message'] = 'You shouldn''t watch porn with your sister in the room, she would notice for sure!'
			$access['nocamshow-message'] ='You can''t do any webcam activities with your sister in the room!'
		else
			gs 'music_actions', 'clear_restrictions'
		end

		if locat['Anya_inroom'] = 0 and breastpump = 1 and bp_unbox <= 0:
			'<a hlpf="exec: gt ''lact_bp'', ''bp_unbox_event''">Unbox</a> your breast pump'
		elseif kid >= 1 and breastpump = 1 and bp_unbox <= 0:
			'<a href="exec: gt ''lact_bp'', ''bp_unbox_event''">Unbox</a> your breast pump'
		elseif locat['Anya_inroom'] = 0 and breastpump = 1 and bp_unbox = 1 and pcs_inhib > 40:
			'<a href="exec: gt ''lact_bp'', ''bp_unbox_event''">Try out</a> your breast pump'
		elseif kid >= 1 and breastpump = 1 and bp_unbox = 1 and pcs_inhib > 30:
			'<a href="exec: gt ''lact_bp'', ''bp_unbox_event''">Try out</a> your breast pump'
		end
		gs 'lact_bp', 'massage'

		'</td><td width = "100" valign="top"></td><td>'
		gs 'subkid'
		'</td></tr></table>'

		!! NOTE: This has to go here, otherwise gs 'subkid' removes the icon. 
		gs 'music_actions', 'music_icon'
	end
end

if $ARGS[0] = 'intro':
	grandmaQW['last_month_paid'] = month
	menu_off = 1
	hour = 6
	gs 'stat'
	music_loop = 0
	'<center><h1><font color="maroon">Your Bedroom</font></h1></center>'
	'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/mom/prost4.jpg"></center>'
	*nl
	'All of a sudden, you hear a familiar voice yell out. "<<$pcs_nickname>>, time to get up! You can''t spend the whole day laying around!"'
	wait 1000
	$temp_nickname = input('<center>What name do you address your mother by?<br>Mother (default), Mum, Mom, Mama, etc.</center>')
	if $temp_nickname ! '':$npc_nickname['A29'] = '<<$temp_nickname>>'
	killvar '$temp_nickname'
	act 'Continue':
		cla & *clr
		minut += 2
		gs 'stat'
		'<center><h1><font color="maroon">Your Bedroom</font></h1></center>'
		'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/mom/prost5.jpg"></center>'
		'You tiredly open your eyes and see your <<$npc_nickname[''A29'']>> standing over you. "Fifteen more minutes..." you mumble.'
		'Your <<$npc_nickname[''A29'']>> lets out a loud sigh. "No, up now!" she says as she yanks the covers off.'
		'"<<$npc_nickname[''A29'']>>!" you exclaim as you feel a slightly cold breeze hit your body.'
		'"No excuses <<$pcs_nickname>>. The summer holidays will be over before you know it and you mustn''t get used to sleeping in, otherwise you won''t be able to get up early when school starts again. From now on, set your alarm to wake up at a reasonable time." your <<$npc_nickname[''A29'']>> says as she stares at you expectantly.'
		act 'Get up':
			cla & *clr
			minut += 2
			gs 'stat'
			'<center><h1><font color="maroon">Your Bedroom</font></h1></center>'
			'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/mom/prost4.jpg"></center>'
			'As soon as you get off your now coverless bed, your <<$npc_nickname[''A29'']>> is quick to resume her nagging, repeating what you already heard from her a few moments ago. "Summer is no excuse to laze around all day. You can''t sleep in anymore, not even for another fifteen minutes." She''s not even looking at you while she talks, but luckily for you that means she misses you rolling your eyes.'
			'"What are you going to do when school starts, or when you get a job? You better get into the habit of setting your alarm so you can wake up bright and early."'
			act 'Continue':
				cla & *clr
				minut += 2
				gs 'stat'
				'<center><h1><font color="maroon">Your Bedroom</font></h1></center>'
				'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/mom/prost5.jpg"></center>'
				'You let out an exasperated sigh. "Yeah yeah I know, you told me that already. So how early are we talking?"'
				'Your <<$npc_nickname[''A29'']>> shoots you a sharp look and you know to fix your attitude or else. "Well, that depends on what you plan to do in the morning. There''s breakfast, showering and brushing your teeth, doing your hair and then makeup and shaving if you really need to. If I remember right... Anya used to wake up at 06:00 for school."'
				act 'Continue':
					cla & *clr
					minut += 2
					gs 'stat'
					'<center><h1><font color="maroon">Your Bedroom</font></h1></center>'
					'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/mom/prost4.jpg"></center>'
					'"You want me to wake up that early?!" you huff. "Can''t I wake up at like... 07:00?"'
					'Your <<$npc_nickname[''A29'']>> shrugs. "You could, but then you might have to cut corners or even end up running late to school, and I won''t be having any of that. Your teachers won''t either." she warns you before making her way towards the kitchen.'
					if stat['think_virgin'] = 1: '"Oh, don''t forget; you need to head down to the clinic and see the gynecologist for a check up. Not just once, but every month." she adds quickly.'
					act 'But why do I--':
						cla & *clr
						minut += 2
						gs 'stat'
						'<center><h1><font color="maroon">Your Bedroom</font></h1></center>'
						'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/mom/prost5.jpg"></center>'
						'Before you can finish, she cuts you off. "I''m not going to have you running around town like so many of those other girls I see! Opening their legs for any boy that comes around..." She shakes her head in disgust. "I want a referral every month and it better tell me you''re still a virgin or else, understand?" your <<$npc_nickname[''A29'']>> says before she storms out of the room without giving you a chance to respond.'
						act 'I understand...':gt 'budilnik', 'start'
					end
				end
			end
		end
	end
end

--- bedrPar ---------------------------------

