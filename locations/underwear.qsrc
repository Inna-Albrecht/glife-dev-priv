# underwear

!!gs 'underwear', 'dispose'
!!gs 'underwear', 'remove'
!!gs 'underwear', 'wear'
!! As remove and wear but with a backup in place to ensure the correct underwear state is restored
!!gs 'underwear', 'backup'
!!gs 'underwear', 'restore'

if $ARGS[0] = 'dispose':
	if underwear['type'] = 2:
		gs 'underwear_bodysuits', 'dispose'
	else
		gs 'panties', 'dispose'
		gs 'bras', 'dispose'
	end
end

if $ARGS[0] = 'backup':
	if underwear['type'] = 2:
		$lastwornbodysuittype['backup'] = $bodysuitworntype
		lastwornbodysuitnumber['backup'] = bodysuitwornnumber
		gs 'underwear_bodysuits', 'remove'
	else
		$lastwornpantytype['backup'] = $pantyworntype
		lastwornpantynumber['backup'] = pantywornnumber
		$lastwornbratype['backup'] = $braworntype
		lastwornbranumber['backup'] = brawornnumber
		gs 'panties', 'remove'
		gs 'bras', 'remove'
	end
end

if $ARGS[0] = 'remove':
	!!check for swimwear to prevent duplicate stripping stopping Sveta putting underwear on when she puts her clothes on
	if $clothingworntype ! 'danilovich_swimsuit' and $clothingworntype ! 'scandalicious_swimsuit' and $clothingworntype ! 'scandalicious_bikinis' and $clothingworntype ! 'allure_swimsuit' and $clothingworntype ! 'allure_bikinis' and $clothingworntype ! 'nerdvana_swimsuit' and $clothingworntype ! 'nerdvana_bikinis' and ($pantyworntype ! 'none' or $braworntype ! 'none'):
		if underwear['type'] = 2:
			gs 'underwear_bodysuits', 'remove'
		else
			gs 'panties', 'remove'
			gs 'bras', 'remove'
		end
	end
end

if $ARGS[0] = 'restore':
	if underwear['lastworntype'] = 2:
		gs 'underwear_bodysuits', 'wear', $lastwornbodysuittype['backup'], lastwornbodysuitnumber['backup']
	else
		gs 'panties', 'wear', $lastwornpantytype['backup'], lastwornpantynumber['backup']
		gs 'bras', 'wear', $lastwornbratype['backup'], lastwornbranumber['backup']
	end
end

if $ARGS[0] = 'wear':
	if underwear['lastworntype'] = 2:
		gs 'underwear_bodysuits', 'wear'
	else
		gs 'panties', 'wear'
		gs 'bras', 'wear'
	end
end

if $ARGS[0] = 'dresser':
	'In your dresser, you can sort through all your underwear. Much of it is just regular underwear, but you can also own some distinct items.'
	'The distinct items are sorted into bras and panties and by the shop in which they were purchased. They must be picked here, and automatic options will default to regular underwear.'
	*nl
	'The wear panties and bra options here are for the regular underwear, the large buttons for choosing unique items.'
	*nl
	'Your dresser contains <<func(''panties'',''sum'',''dresser'')>> pairs of regular panties and <<func(''bras'',''sum'',''dresser'')>> regular bras.'
	*nl
	*nl
	*nl
	*nl

	*p '<center><a href="exec:gt ''bras'', ''view_bra_list'', ''clean'', 6"><img src="images/system/icons/bra.png"></a>                               <a href="exec:gt ''panties'', ''view_panty_list'', ''clean'', 6"><img src="images/system/icons/panties.png"></a></center>'

	dynamic $wearpan
	dynamic $removepan

	act 'Return': gt $loc, $loc_arg
end

--- underwear ---------------------------------

