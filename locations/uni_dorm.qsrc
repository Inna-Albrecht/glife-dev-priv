# uni_dorm
!!2021/08/13

if $ARGS[0] = 'start':
	*clr & cla
	minut += 5
	$menu_loc = 'uni_dorm'
	$menu_arg = 'start'
	menu_off = 0
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b><font color="maroon">Dormitory</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm.jpg"></center>'
	'You walk up to the dorm building and go inside. It is pretty clean and in good shape. There is a older woman, with a very unpleasant and judgmental look on her face in a room just off the main hallway. It has a partially glass wall and a window, splitting it from the main room. She looks up at you as you enter, her look tells you she is already judging you, as a face frowns into a look of disapproval.'
	if home_owned[11] = 1 or university['student'] = 1:
		act 'Show her your student ID':
			*clr & cla
			minut += 1
			'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm.jpg"></center>'
			'As you walk by you take out your student ID and show it to her. She looks for just a moment then goes back to doing what ever it was, she was doing before you walked in. You head for the elevators.'
			act 'Take elevator':gt 'uni_dorm', 'elevator'
		end
	else
		menu_off = 1
		'Since you''re not attending the University, you know without a ID you are not allowed in the dorms. Unless you are on the guest list of a student you know.'
		act 'Leave': minut += 5 & gt 'city_island'
		if DjibrilQW['invite'] = 1 and hour >= 8 and hour < 22:
			act 'I''m here to visit Djibril':
				*clr & cla
				'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm.jpg"></center>'
				'You walk over to the counter pulling our your ID as you say. "I''m here to visit Djibril."' 
				'The woman gives you a look of complete disgust and remarks "What, your own kind not good enough for you? Go ahead and take care of those black cocks. He is on the eighth floor room 810." Then she turns away from you and goes back to what he was doing, not wanting to reply and possibly get in a argument that would cause her to call security, you say nothing and go get in the elevator to the eighth floor.'
				act 'Take elevator':gt 'uni_dorm', 'elevator'
			end
		end
		if gosh > 0 and hour >= 8 and hour < 22:
			act 'I''m here to visit Gosha':
				*clr & cla
				'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm.jpg"></center>'
				'You walk over to the counter pulling our your ID as you say. "I''m here to visit Gosha."' 
				'The woman shakes her head in disapproval, as she remarks "Gosha''s new whore? What you sluts see in that scrawny bastard, I''ll never understand. He''s been through half of the dorm already. He is on the sixth floor room 602." Then she turns away from you and goes back to what she was doing, not wanting to reply and possibly get in a argument that would cause her to call security, you say nothing and go get in the elevator to the sixth floor.'
				reccoldorm = 1
				goshiflag = 1
				act 'Take elevator':gt 'uni_dorm', 'elevator'
			end
		end
		if meet_kendra = 1 and hour >= 8 and hour < 22:
			act 'I''m here to visit Kendra':
				*clr & cla
				'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm.jpg"></center>'
				if npc_rel['A84'] < 40:
					'You walk over to the counter pulling our your ID as you say. "I''m here to visit Kendra."' 
					'The woman gives you a look of complete disgust and remarks "Seems she has gotten tired of you, you''re no longer on the guest list. Now get out of here before I call security." Surprised Kendra removed you from her guest list, you turn and leave the dorm before the old woman calls security.'
					act 'Leave': minut += 5 & gt 'city_island'
				else
					'You walk over to the counter pulling our your ID as you say. "I''m here to visit Kendra."' 
					'The woman gives you a look of complete disgust and remarks "What, a man not good enough for you? Go ahead and go serve that black whore. She is on the eighth floor room 801." Then she turns away from you and goes back to what she was doing, not wanting to reply and possibly get in a argument that would cause her to call security, you say nothing and go get in the elevator to the eighth floor.'
					act 'Take elevator':gt 'uni_dorm', 'elevator'
				end
			end
		end
	end
	act 'Leave':gt 'city_island'
end

if $ARGS[0] = 'elevator':
	*clr & cla
	minut += 5
	$menu_loc = 'uni_dorm'
	$menu_arg = 'elevator'
	menu_off = 0
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b><font color="maroon">Dormitory</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/elevator.jpg"></center>'
	'You enter the dorm elevator, it seems to be in decent shape and fairly well maintained. There is a line of buttons to all the different floors in the dorm building.'
	if func('uniutil', 'student', 'enrolled'):
		'Your dorm room is at the tenth floor'
	end
	act 'Go to sixth floor': gt 'uni_dorm', 'sixth_floor'
	act 'Go to eighth floor': gt 'uni_dorm', 'eighth_floor'
	act 'Go to tenth floor': gt 'uni_dorm', 'tenth_floor'
	act 'Go to the first floor and leave the dorm':gt 'city_island'
end

if $ARGS[0] = 'sixth_floor':
	$loc = 'uni_dorm'
	$loc_arg = 'sixth_floor'
	$menu_loc = 'uni_dorm'
	$menu_arg = 'sixth_floor'
	menu_off = 0
	$location_type = 'public_indoors'
	killvar '$locclass'
	*clr & cla
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm_hall.jpg"></center>'
	'Before you is the usual dorm hallway with dilapidated walls of the sixth floor. You can hear music and loud talking from drunk students escaping from some rooms.'

	if gosh > 0:
		act 'Gosha''s room':
			*clr & cla 
			if (week <= 4 and hour >= 16 and hour < 23 and rand(1, 2) = 2) or (week = 6 and hour >= 9 and hour < 23 and rand(1, 2) = 2) or (week = 7 and hour >= 9 and hour < 23 and rand(1, 3) >= 2):
				'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big83.jpg"></center>'
				'Gosha opens the door. "Hey <<$pcs_nickname>>, why don''t you come in." He then stands aside so you can walk in, closing the door after you.'
				act 'Enter':gt 'gosha', 'gosha_room'
			else
				!!not home text
				minut += 5
				gs 'stat'
				'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm_hall.jpg"></center>'
				'You knock on the door, but no one answers. He must be asleep or not home.'
				act 'Leave':gt 'uni_dorm', 'elevator'
			end
		end
	end

	act 'Leave':gt 'uni_dorm', 'elevator'
end

if $ARGS[0] = 'eighth_floor':
	$loc = 'uni_dorm'
	$loc_arg = 'eighth_floor'
	$menu_loc = 'uni_dorm'
	$menu_arg = 'eighth_floor'
	menu_off = 0
	$location_type = 'public_indoors'
	killvar '$locclass'
	*clr & cla
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm_hall.jpg"></center>'
	'Before you is the usual dorm hallway with dilapidated walls of the eighth floor. You can hear music and loud talking from drunk students escaping from some rooms.'
	if DjibrilQW['invite'] = 1:
		act 'Djibril'' room':
			*clr & cla
			if (week <= 4 and hour >= 16 and hour < 23 and rand(1, 2) = 2) or (week = 6 and hour >= 9 and hour < 23 and rand(1, 2) = 2) or (week = 7 and hour >= 9 and hour < 23 and rand(1, 3) >= 2):
				'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big82.jpg"></center>'
				'You knock on the door. After a couple of seconds, Djibril opens the door. He smiles as he notices it''s you. "I was hoping you would stop by <<$pcs_nickname>>, come on in." He says as he invites you into his room.'
				act 'Enter': gt 'djibril', 'djibril_room'
			else
				'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm_hall.jpg"></center>'
				minut += 5
				gs 'stat'
				'You knock on the door, but no one answers. He must be asleep or not home.'
				act 'Leave':gt 'uni_dorm', 'elevator'
			end
		end
	end

	if meet_kendra = 1:
		act 'Kendra''s room':
			*clr & cla
			if (week <= 4 and hour >= 16 and hour < 23 and rand(1, 2) = 2) or (week = 6 and hour >= 9 and hour < 23 and rand(1, 2) = 2) or (week = 7 and hour >= 9 and hour < 23 and rand(1, 3) >= 2):
				if kendra_busy ! daystart:
					if rand(1,5) = 1:
						'<center><img <<$set_imgh>> src="images/characters/city/university/girl/kendra/door.jpg"></center>'
						'As you get close to the door you can hear her voice. It is fairly loud and sounds harsh in tone, but you can''t quite make out what she is saying. You hear a muffled reply that sounds male. It sounds like she is busy, you could knock or just leave her to her company.'
						act 'Knock':
							kendra_busy = daystart
							*clr & cla
							'<center><img <<$set_imgh>> src="images/characters/city/university/girl/kendra/strapon_door.jpg"></center>'
							if npc_rel['A84'] < 40:
								'You knock on the door. After a couple of seconds, Kendra opens the door. She is standing in the doorway completely naked other than the strapon harness buckled in place around her hips, with a large black dildo attached to it that she is playing with. When she see''s you she frowns. "I told you last time, I don''t have time for games and I am tired of yours. Now get lost, I''m busy." She closes the door in your face.'
								act 'Leave':gt 'uni_dorm', 'elevator'
							else
								'You knock on the door. After a couple of seconds, Kendra opens the door. She is standing in the doorway completely naked other than the strapon harness buckled in place around her hips, with a large black dildo attached to it that she is playing with. "Well if it isn''t my new favorite white slave bitch. I am kinda busy at the moment, I am punishing one of my other slaves."'
								*nl
								'She starts to close the door before you can even say anything, then she stops and smiles at you, like she just had a good idea. "Or you could come in and watch." She stands aside to let you come in, holding the door open. As you glance in the room, you see a naked college boy kneeling next to the couch. When he notices you he blushes bright red and moves to cover himself up, but stops with one look from Kendra and instead just lowers his gaze to the floor. You have no doubt what she is going to do to him, considering the strapon she is wearing. Is that something you want to watch? It could be interesting and you might learn something from her as well. On the other hand do you want to humiliate him more than he already is?'
								gs 'arousal', 'erotic_nudity', 3
								gs 'stat'
								act 'Enter': gt 'kendra', 'kendra_male_slave'
								act 'Decline':
									*clr & cla
									gs 'stat'
									'<center><img <<$set_imgh>> src="images/characters/city/university/girl/kendra/strapon_door.jpg"></center>'
									'You shake your head no. "No thanks you look busy and I don''t want to disturb you."'
									'She laughs loudly and roughly grabs your chin in her hand. "Aww so shy, adorable... but I will break you of that in time. Maybe next time then." Then she closes the door, as she does. You see her stroking her strapon as she heads for the young man. Once the door is closed you turn and leave yourself.'
									gs 'arousal', 'end'
									gs 'stat'
									act 'Leave':gt 'uni_dorm', 'elevator'
								end
							end
						end
						act 'Leave':gt 'uni_dorm', 'elevator'
					else
						'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big84.jpg"></center>'
						if npc_rel['A84'] < 40:
							'You knock on the door. After a couple of seconds, Kendra opens the door. "I told you last time, I don''t have time for games and I am tired of yours. Now get lost." She closes the door in your face.'
							act 'Leave':gt 'uni_dorm', 'elevator'
						else
							'You knock on the door. After a couple of seconds, Kendra opens the door. "Well if it isn''t my new favorite white slave bitch. Well come in." She stands aside to let you come in, as you enter she closes the door.'
							act 'Enter': gt 'uni_dorm', 'kendra_room_start'
						end
					end
				else
					'<center><img <<$set_imgh>> src="images/characters/city/university/girl/kendra/door.jpg"></center>'
					'As you get close to the door you can hear her voice. It is fairly loud and sounds harsh in tone, but you can''t quite make out what she is saying. You do hear a muffled reply that sounds male. She seems busy right now, you shouldn''t bother her.'
					act 'Leave':gt 'uni_dorm', 'elevator'
				end
			else
				minut += 5
				gs 'stat'
				'<center><img <<$set_imgh>> src="images/characters/city/university/girl/kendra/door.jpg"></center>'
				'You knock on the door, but no one answers. She must be asleep or not home.'
				act 'Leave':gt 'uni_dorm', 'elevator'
			end
		end
	end

	act 'Leave':gt 'uni_dorm', 'elevator'
end

if $ARGS[0] = 'tenth_floor':
	$loc = 'uni_dorm'
	$loc_arg = 'tenth_floor'
	$menu_loc = 'uni_dorm'
	$menu_arg = 'tenth_floor'
	$locM = 'uni_dorm'
	$locM_arg = 'tenth_floor'
	menu_off = 0
	$location_type = 'public_indoors'
	killvar '$locclass'
	*clr & cla
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm_hall.jpg"></center>'
	if func('uniutil', 'student', 'enrolled'):
		'Before you is the dorm hallway with it''s dilapidated walls, that leads to your dorm room. You can hear music and loud talking from drunk students escaping from some rooms.'
	else
		'Before you is the usual dorm hallway with dilapidated walls. You can hear music and loud talking from drunk students escaping from some rooms.'
	end
	gs 'willpower', 'exhib', 'self'
	if will_cost <= pcs_willpwr:
		act 'Enter the men''s restroom (<<will_cost>> Willpower)':
			gs 'willpower', 'exhib', 'self'
			gs 'willpower', 'pay', 'self'
			gs 'stat'
			gt 'uni_dorm', 'mens_restroom'
		end
	else
		act 'Enter the men''s restroom (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
	end
	act 'Enter the women''s restroom': gt 'uni_dorm', 'womens_restroom'
	act 'Leave':gt 'uni_dorm', 'elevator'
	act 'Go to the shared shower':
		minut += 5
		obdysh = 1
		gt 'vann', 'start'
	end

	if home_owned[11] = 1:
		if courtletter_date <= daystart and courtletter_date ! 0:
			gt 'sentence', 'letter'
		end
		act 'Your room':gt 'uni_dorm', 'dorm_room'
		act 'Go to the shared kitchen':
			minut += 5
			gt 'uni_dorm', 'dorm_kitchen'
		end
	end
	if (home_owned[11] = 1 or university['student'] = 1) and week = 5 and hour > 18 and hour < 23:
		'You can hear music blasting in the corridor. It seems the Friday party is in full swing.'
		act 'The party':gt 'vecher', 'start'
	end
end

if $ARGS[0] = 'mens_restroom':
	$location_type = 'public_indoors'
	*clr & cla
	$menu_loc = 'uni_dorm'
	$menu_arg = 'mens_restroom'
	$locM = 'uni_dorm'
	$locM_arg = 'mens_restroom'
	menu_off = 0
	gs 'stat'
	'<center><b><font color="maroon">Dormitory - Men''s Restroom</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/restrooms/bathroom_mens.jpg"></center>'
	'You peek around the corner. Inside this room, stalls line the right side, with urinals on the far wall and rows of sinks and <a href="exec:gt ''mirror'', ''start''">mirrors</a> to the left. The room has a smell of disinfectant and stale piss.'
	act 'Leave': gt 'uni_dorm', 'tenth_floor'
	
	dynamic $tampon
	dynamic $quickwash
	dynamic $basin
end

if $ARGS[0] = 'womens_restroom':
	$location_type = 'public_indoors'
	*clr & cla
	$menu_loc = 'uni_dorm'
	$menu_arg = 'womens_restroom'
	$locM = 'uni_dorm'
	$locM_arg = 'womens_restroom'
	menu_off = 0
	gs 'stat'
	'<center><b><font color="maroon">Dormitory - Women''s Restroom</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/restrooms/bathroom_womens.jpg"></center>'
	'You peek around the corner. Inside this room, stalls line the right side and rows of sinks, with <a href="exec:gt ''mirror'', ''start''">mirrors</a> to the left. The room has a smell of disinfectant.'
	act 'Leave': gt 'uni_dorm', 'tenth_floor'
	
	dynamic $tampon
	dynamic $quickwash
	dynamic $basin
end


if $ARGS[0] = 'dorm_room':
	$sexloc = 'uni_dorm'
	CLOSE ALL
	$loc_arg = 'dorm_room'
	$loc = 'uni_dorm'
	$locM_arg = 'dorm_room'
	$locM = 'uni_dorm'
    $location_type = 'private'
	$menu_loc = 'uni_dorm'
	$menu_arg = 'dorm_room'
	menu_off = 0
	cla
	*clr
	odkomp = 1
	gs 'stat'
	'<center><b>Your room</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm_room/room.jpg"></center>'
	'This is your room which you share with another girl named Vika Kirilova.'
	'At the right of the entrance, you find an old <a href="exec:gt ''wardrobe'', ''start''">wardrobe</a> (where you can choose outfits and organize your clothing).'
	'There''s a <a href="exec:gt ''mirror'',''start''">mirror</a> hanging on the wall.'
	'Your <a href="exec:gt ''bed'',''start''">bed</a> is positioned in one of the corners of the room.'
	if komp = 1:'There''s a <a href="exec:gt ''Komp'',''start''">laptop</a> lying on your bed.'
	*nl
	gs 'exercise', 'start'
	if hour > 14 and hour < func('bordel', 'open_hour'):
		vika_sleep = 0
		if npc_rel['A220'] >= 60 and birthday = day and birthmonth = month and vikaslut = 1 and b_vika_present_Day ! daystart:
			'<a href="exec:gt''Vika''">Vika</a> seems excited when she notices you, trying to hide a box behind her back.'
		else
			'<a href="exec:gt''Vika''">Vika</a> is lying on her bed.'
		end
	elseif hour > func('bordel', 'close_hour') and hour < 8:
		vika_sleep = 1
		if vikaslut = 0:
			'<a href="exec:gt''Vika''">Vika</a> is sleeping in her bed after being out the whole night.'
		elseif vikaslut = 1:
			'<a href="exec:gt''Vika''">Vika</a> is sleeping in her bed after working hard in the brothel all night.'
		end
	end

	act 'Walk out in the corridor':
		if $clothingworntype ! 'nude':
			odkomp = 0
			minut += 5
			gt 'uni_dorm', 'tenth_floor'
		else
			'You need to get dressed.'
		end
	end

	if university['semester_week'] > 0:
		i = 0
		:study_loop
		if $class_list_institution[i] = 'uni_<<$university[''enrolled_in'']>>_semester_<<university[''enrolled_in_semester'']>>':
			if dyneval ('RESULT = class[''<<$class_list_institution[i]>>_<<$class_list_name[i]>>_optional_weekly_grade_gain'']') < dyneval ('RESULT = class[''<<$class_list_institution[i]>>_<<$class_list_name[i]>>_optional_weekly_max'']'):
				dynamic '	act ''Study for your <<$class_list_name[i]>> class (30 minuts)'': gt ''uni_dorm'', ''studying'', ''<<$class_list_institution[i]>>'', ''<<$class_list_name[i]>>'' '
			else
				*nl
				'You don''t need to study more this week for you <<$class_list_name[i]>> class'
			end
		end
		
		i += 1
		if ARRSIZE('class_list_institution') >= i: jump 'study_loop'
		killvar 'i'
	end

	act 'Read the course literature (2:00)':
		cla
		menu_off = 1
		minut += 120
		intel_exp += 1 + (mentats_dose - rand(0,mentats_dose))
		gs 'stat'
		'You spend two hours reading the course literature. You feel like you''ve picked up some stuff from the book that will come in handy in time for the examination.'

		act 'Finish':gt 'uni_dorm', 'dorm_room'
	end

	gs 'unicoursework', 'start', $loc, $loc_arg

	dynamic $wearpan
	dynamic $removepan

	if curr_home ! 11:
		act 'Set the this dorm room as your main home':gs 'set_home', 11 & gt 'uni_dorm', 'dorm_room'
	end
end

if $args[0] = 'studying':
	cla & *clr
	'<center><img <<$set_imgh>> src="images/locations/shared/apartment/homework.jpg"></center>'
	minut += 30
	if komp = 1:
		gs 'grades', 'optional_activity_attribute', '<<$ARGS[1]>>', '<<$ARGS[2]>>', 'yes', pcs_intel
	else
		gs 'grades', 'optional_activity_attribute', '<<$ARGS[1]>>', '<<$ARGS[2]>>', 'no', pcs_intel
	end
	gs 'stat'
	if dyneval ('RESULT = class[''<<$ARGS[1]>>_<<$ARGS[2]>>_optional_weekly_grade_gain'']') < dyneval ('RESULT = class[''<<$ARGS[1]>>_<<$ARGS[2]>>_optional_weekly_max'']'):
		'You study for half an hour, and you can tell you will need to study more if you want to completly understand this weeks material'
	else
		'You study for half an hour, and you think you understand everything that is covered this week'
	end
	act 'Get up from you desk': gt 'uni_dorm', 'dorm_room'
end

if $args[0] = 'dorm_kitchen':
	$loc_arg = 'dorm_kitchen'
	$loc = 'uni_dorm'
    $location_type = 'private'
	$locclass = 'kitr'
	$menu_loc = 'uni_dorm'
	$menu_arg = 'dorm_kitchen'
	menu_off = 0
	gs 'stat'
	'<center><b>in the dorm kitchen</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/obkitchen.jpg"></center>'
	'This is this floors large shared kitchen, it is fairly tidy. There is a lot of cabnits along two walls, two ovens and two refrigerators as well. There is several tables with chairs around them scattered around the room.'
	act 'Return to the corridor':gt 'uni_dorm', 'tenth_floor'

	if eda > 0:
		'Your shelf in the refrigerator holds <b><<eda>></b> servings of food.'
		if edahot = 0:
			act 'Cook food (0:25)':
				cla
				*clr
				menu_off = 1
				minut += 25
				edahot += 1
				eda -= 1
				pcs_mood -= 5
				gs 'stat'
				'<center><img <<$set_imgh>> src="images/shared/home/kitchen/cook.jpg"></center>'
				'You prepare a meal.'
				gs 'uni_dorm', 'food'
			end
		end
	end

	if edaD = 0 and eda = 0:'<b><font color = red>Your shelf in the refrigerator is bare, there is nothing left for you to eat.</font></b>'


	if edahot > 0:
		'A cooked meal is on the table.'
		gs 'uni_dorm', 'food'
	end

	dynamic $edasnack
	dynamic $sandwich
	dynamic $driwater
	dynamic $dritea
	dynamic $fill_bottle
	dynamic $edaD
	dynamic $pranik
	dynamic $fatdel
	dynamic $lekarstvo
	dynamic $vitamin
end

if $args[0] = 'food':
	act 'Eat cooked food (0:20)':
		edahot = 0
		gt 'food', 'm_meal'
	end
end

--- uni_dorm ---------------------------------

