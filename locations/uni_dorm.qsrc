# uni_dorm
!!2021/08/13

if $ARGS[0] = 'start':
	*clr & cla
	minut += 5
	$menu_loc = 'uni_dorm'
	$menu_arg = 'start'
	menu_off = 0
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b><font color="maroon">Dormitory</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm.jpg"></center>'
	'You walk up to the dorm building and go inside. It is pretty clean and in good shape. An older woman, with a very unpleasant and judgmental look on her face sits in a room just off the main hallway. It has a partially glass wall and a window, splitting it from the main room. She looks up at you as you enter, her look telling you she is already judging you as her face frowns into a look of disapproval.'
	if home_owned[11] = 1 or university['student'] = 1:
		act 'Show her your student ID':
			*clr & cla
			minut += 1
			'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm.jpg"></center>'
			'As you walk by, you take out your student ID and show it to her. She looks for just a second and nods before going back to what she was doing. You head for the elevators.'
			act 'Take the elevator':gt 'uni_dorm', 'elevator'
		end
	else
		menu_off = 1
		'Since you''re not attending the university and don''t have an ID, you won''t be allowed in the dorms unless you''re on the guest list of a student.'
		act 'Leave': minut += 5 & gt 'city_island'
		if DjibrilQW['invite'] = 1 and hour >= 8 and hour < 22:
			act 'I''m here to visit Djibril':
				*clr & cla
				'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm.jpg"></center>'
				'You walk over to the counter and pull out your ID. "I''m here to visit Djibril."' 
				'The woman gives you a look of complete disgust. "What, your own kind not good enough for you? Go ahead and take care of those black cocks. He''s on the eighth floor, room 810." She turns away from you and goes back to what she was doing. Not wanting to get into an argument with her, you say nothing and quickly head for the elevator.'
				act 'Take the elevator':gt 'uni_dorm', 'elevator'
			end
		end
		if gosh > 0 and hour >= 8 and hour < 22:
			act 'I''m here to visit Gosha':
				*clr & cla
				'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm.jpg"></center>'
				'You walk over to the counter and pull out your ID. "I''m here to visit Gosha."' 
				'The woman shakes her head in disapproval. "You''re Gosha''s new whore? I''ll never understand what you sluts see in that scrawny bastard. He''s been through half of the dorm already! He''s on the sixth floor, room 602." She turns away from you and goes back to what she was doing. Not wanting to get into an argument with her, you say nothing and quickly head for the elevator.'
				reccoldorm = 1
				goshiflag = 1
				act 'Take the elevator':gt 'uni_dorm', 'elevator'
			end
		end
		if meet_kendra = 1 and hour >= 8 and hour < 22:
			act 'I''m here to visit Kendra':
				*clr & cla
				'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm.jpg"></center>'
				if npc_rel['A84'] < 40:
					'You walk over to the counter and pull out your ID. "I''m here to visit Kendra."' 
					'The woman gives you a look of complete disgust. "It seems like she''s gotten tired of you since you''re no longer on the guest list. Now get out of here before I call security." Surprised, you turn and quickly leave before the woman calls security.'
					act 'Leave': minut += 5 & gt 'city_island'
				else
					'You walk over to the counter and pull out your ID. "I''m here to visit Kendra."' 
					'The woman gives you a look of complete disgust. "What, a man not good enough for you? Go ahead and serve that black whore. She''s on the eighth floor, room 801." She turns away from you and goes back to what she was doing. Not wanting to get into an argument with her, you say nothing and quickly head for the elevator.'
					act 'Take the elevator':gt 'uni_dorm', 'elevator'
				end
			end
		end
	end
	act 'Leave':gt 'city_island'
end

if $ARGS[0] = 'elevator':
	*clr & cla
	minut += 5
	$menu_loc = 'uni_dorm'
	$menu_arg = 'elevator'
	menu_off = 0
	gs 'stat'
	gs 'themes', 'indoors'
	'<center><b><font color="maroon">Dormitory</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/elevator.jpg"></center>'
	'You enter the dorm elevator. It seems to be in decent shape and fairly well maintained. The panel of floor numbers awaits your input.'
	if func('uniutil', 'student', 'enrolled'):
		'Your dorm room is on the tenth floor.'
	end
	act 'Go to the sixth floor': gt 'uni_dorm', 'sixth_floor'
	act 'Go to the eighth floor': gt 'uni_dorm', 'eighth_floor'
	act 'Go to the tenth floor': gt 'uni_dorm', 'tenth_floor'
	act 'Go to the first floor and leave the dorm':gt 'city_island'
end

if $ARGS[0] = 'sixth_floor':
	$loc = 'uni_dorm'
	$loc_arg = 'sixth_floor'
	$menu_loc = 'uni_dorm'
	$menu_arg = 'sixth_floor'
	menu_off = 0
	$location_type = 'public_indoors'
	killvar '$locclass'
	*clr & cla
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm_hall.jpg"></center>'
	'Before you is the usual dorm hallway with the dilapidated walls of the sixth floor. You can hear music and loud talking from drunk students escaping from some rooms.'

	if gosh > 0:
		act 'Gosha''s room':
			*clr & cla 
			if (week <= 4 and hour >= 16 and hour < 23 and rand(1, 2) = 2) or (week = 6 and hour >= 9 and hour < 23 and rand(1, 2) = 2) or (week = 7 and hour >= 9 and hour < 23 and rand(1, 3) >= 2):
				'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big83.jpg"></center>'
				'Gosha opens the door. "Hey <<$pcs_nickname>>, why don''t you come in?" He stands aside so you can walk in before closing the door behind you.'
				act 'Enter':gt 'gosha', 'gosha_room'
			else
				minut += 5
				gs 'stat'
				'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm_hall.jpg"></center>'
				'You knock on the door, but no one answers. He must be asleep or not home.'
				act 'Leave':gt 'uni_dorm', 'elevator'
			end
		end
	end

	act 'Leave':gt 'uni_dorm', 'elevator'
end

if $ARGS[0] = 'eighth_floor':
	$loc = 'uni_dorm'
	$loc_arg = 'eighth_floor'
	$menu_loc = 'uni_dorm'
	$menu_arg = 'eighth_floor'
	menu_off = 0
	$location_type = 'public_indoors'
	killvar '$locclass'
	*clr & cla
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm_hall.jpg"></center>'
	'Before you is the usual dorm hallway with the dilapidated walls of the eighth floor. You can hear music and loud talking from drunk students escaping from some rooms.'
	if DjibrilQW['invite'] = 1:
		act 'Djibril'' room':
			*clr & cla
			if (week <= 4 and hour >= 16 and hour < 23 and rand(1, 2) = 2) or (week = 6 and hour >= 9 and hour < 23 and rand(1, 2) = 2) or (week = 7 and hour >= 9 and hour < 23 and rand(1, 3) >= 2):
				'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big82.jpg"></center>'
				'You knock on the door. After a few seconds, Djibril opens the door. He smiles as he notices it''s you. "I was hoping you would stop by <<$pcs_nickname>>. Come on in."'
				act 'Enter': gt 'djibril', 'djibril_room'
			else
				'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm_hall.jpg"></center>'
				minut += 5
				gs 'stat'
				'You knock on the door, but no one answers. He must be asleep or not home.'
				act 'Leave':gt 'uni_dorm', 'elevator'
			end
		end
	end

	if meet_kendra = 1:
		act 'Kendra''s room':
			*clr & cla
			if (week <= 4 and hour >= 16 and hour < 23 and rand(1, 2) = 2) or (week = 6 and hour >= 9 and hour < 23 and rand(1, 2) = 2) or (week = 7 and hour >= 9 and hour < 23 and rand(1, 3) >= 2):
				if kendra_busy ! daystart:
					if rand(1,5) = 1:
						'<center><img <<$set_imgh>> src="images/characters/city/university/girl/kendra/door.jpg"></center>'
						'You can hear Kendra''s voice as you get close to the door. It is fairly loud and sounds harsh in tone, but you can''t quite make out what she is saying. You hear a muffled reply that sounds male. It sounds like she''s already busy. You could knock or just leave her to her company.'
						act 'Knock':
							kendra_busy = daystart
							*clr & cla
							'<center><img <<$set_imgh>> src="images/characters/city/university/girl/kendra/strapon_door.jpg"></center>'
							if npc_rel['A84'] < 40:
								'You knock on the door. After a few seconds, Kendra opens the door. She is completely naked and wearing a strapon harness around her hips. A large black dildo is attached to it and she plays with it as she frowns at you. "I told you last time, I don''t have time for games and I''m tired of yours! Now get lost, I''m busy!" She then slams the door in your face.'
								act 'Leave':gt 'uni_dorm', 'elevator'
							else
								if kendra_male_slave = 0:
									'You knock on the door. After a few seconds, Kendra opens the door. She is completely naked and wearing a strapon harness around her hips. A large black dildo is attached to it and she plays with it as she looks at you. "Well if it isn''t my new favorite white slave bitch. I''m kind of busy punishing one of my other slaves right now."'
									*nl
									'She starts to close the door before you can reply, then stops and smiles at you. "You could come in and watch." She holds the door open and stands aside to let you enter. You glance in the room and see a naked college boy kneeling next to the couch. When he notices you, he blushes bright red and moves to cover himself up, but stops with one look from Kendra and instead just lowers his gaze to the floor. You have no doubt what she''s going to do to him, but is it something you want to watch? It could be interesting and you might learn something from her as well. On the other hand, do you want to humiliate him more than he already is?'
								else
									'You knock on the door. After a few seconds, Kendra opens the door. She is completely naked and wearing a strapon harness around her hips. A large black dildo is attached to it and she plays with it as she looks at you. "Well if it isn''t my new favorite white slave bitch. I''m punishing one of the boys again. Want to come in and watch again?"'
									*nl
									'She holds the door open and stands aside to let you enter. You glance in the room and see the same college boy kneeling naked next to the couch, just like last time. When he notices you, he blushes bright red and moves to cover himself up, but stops with one look from Kendra and instead just lowers his gaze to the floor. You know what she''s going to do to him after watching last time. Is it something you want to watch again? Is this something you might want to learn to do to others yourself? There are a few boys you know that it might be fun to do it with.'
								end
								gs 'arousal', 'erotic_nudity', 3
								gs 'stat'
								act 'Enter': gt 'kendra', 'kendra_male_slave'
								act 'Decline':
									*clr & cla
									gs 'stat'
									'<center><img <<$set_imgh>> src="images/characters/city/university/girl/kendra/strapon_door.jpg"></center>'
									if kendra_male_slave = 0:
										'You shake your head. "No thanks. You look busy and I don''t want to disturb you..."'
										'She laughs loudly and roughly grabs your chin in her hand. "Aww so shy, adorable... but I will break you in time. Maybe next time then." She closes the door and you watch her stroking her strapon as she heads for the young man. Once the door is closed, you turn and leave.'
									else
										'You shake your head. "No thanks. Last time was entertaining, but I didn''t come to see that again."'
										'She laughs loudly and roughly grabs your chin in her hand. "Your loss. I might have let you take a turn with him, or maybe not." She closes the door and you watch her stroking her strapon as she heads for the young man. Once the door is closed, you turn and leave.'
									end
									gs 'arousal', 'end'
									gs 'stat'
									act 'Leave':gt 'uni_dorm', 'elevator'
								end
							end
						end
						act 'Leave':gt 'uni_dorm', 'elevator'
					else
						'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big84.jpg"></center>'
						if npc_rel['A84'] < 40:
							'You knock on the door. After a few seconds, Kendra opens the door. "I told you last time, I don''t have time for games and I''m tired of yours. Now get lost." She slams the door in your face.'
							act 'Leave':gt 'uni_dorm', 'elevator'
						else
							'You knock on the door. After a few seconds, Kendra opens the door. "Well if it isn''t my new favorite white slave bitch. Come in." She stands aside to let you enter. She then closes the door behind you.'
							act 'Enter': gt 'kendra', 'kendra_room_start'
						end
					end
				else
					'<center><img <<$set_imgh>> src="images/characters/city/university/girl/kendra/door.jpg"></center>'
					'You can hear Kendra''s voice as you get close to the door. It is fairly loud and sounds harsh in tone, but you can''t quite make out what she is saying. You do hear a muffled reply that sounds male. It sounds like she''s busy right now, so you shouldn''t bother her.'
					act 'Leave':gt 'uni_dorm', 'elevator'
				end
			else
				minut += 5
				gs 'stat'
				'<center><img <<$set_imgh>> src="images/characters/city/university/girl/kendra/door.jpg"></center>'
				'You knock on the door, but no one answers. She must be asleep or not home.'
				act 'Leave':gt 'uni_dorm', 'elevator'
			end
		end
	end

	act 'Leave':gt 'uni_dorm', 'elevator'
end

if $ARGS[0] = 'tenth_floor':
	$loc = 'uni_dorm'
	$loc_arg = 'tenth_floor'
	$menu_loc = 'uni_dorm'
	$menu_arg = 'tenth_floor'
	$locM = 'uni_dorm'
	$locM_arg = 'tenth_floor'
	menu_off = 0
	$location_type = 'public_indoors'
	killvar '$locclass'
	*clr & cla
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm_hall.jpg"></center>'
	if func('uniutil', 'student', 'enrolled'):
		'Before you is the dorm hallway with it''s dilapidated walls leading to your dorm room. You can hear music and loud talking from drunk students escaping from some rooms.'
	else
		'Before you is the usual dorm hallway with dilapidated walls. You can hear music and loud talking from drunk students escaping from some rooms.'
	end
	gs 'willpower', 'exhib', 'self'
	if will_cost <= pcs_willpwr:
		act 'Enter the men''s restroom (<<will_cost>> Willpower)':
			gs 'willpower', 'exhib', 'self'
			gs 'willpower', 'pay', 'self'
			minut =+ 1
			gs 'stat'
			gt 'uni_dorm', 'mens_restroom'
		end
	else
		act 'Enter the men''s restroom (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
	end
	act 'Enter the women''s restroom': minut += 1 & gt 'uni_dorm', 'womens_restroom'
	act 'Leave':gt 'uni_dorm', 'elevator'
	act 'Go to the shared shower':
		minut += 1
		obdysh = 1
		gt 'vann', 'start'
	end

	if home_owned[11] = 1:
		if courtletter_date <= daystart and courtletter_date ! 0:
			gt 'sentence', 'letter'
		end
		act 'Your room': minut += 1 & gt 'uni_dorm', 'dorm_room'
		act 'Go to the shared kitchen': minut += 1 & gt 'uni_dorm', 'dorm_kitchen'
	end
	if (home_owned[11] = 1 or university['student'] = 1) and week = 5 and hour > 18 and hour < 23:
		'You can hear music blasting in the corridor. It sounds like the Friday night party is in full swing.'
		act 'The party': minut += 1 & gt 'vecher', 'start'
	end
end

if $ARGS[0] = 'mens_restroom':
	$location_type = 'public_indoors'
	*clr & cla
	$menu_loc = 'uni_dorm'
	$menu_arg = 'mens_restroom'
	$locM = 'uni_dorm'
	$locM_arg = 'mens_restroom'
	menu_off = 0
	gs 'stat'
	'<center><b><font color="maroon">Dormitory - Men''s Restroom</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/restrooms/bathroom_mens.jpg"></center>'
	'You peek around the corner. Inside this room, stalls line the right side, with urinals on the far wall and rows of sinks and <a href="exec:gt ''mirror'', ''start''">mirrors</a> to the left. The room has a smell of disinfectant and stale piss.'
	act 'Leave': minut += 1 & gt 'uni_dorm', 'tenth_floor'
	
	dynamic $tampon
	dynamic $quickwash
	dynamic $basin
end

if $ARGS[0] = 'womens_restroom':
	$location_type = 'public_indoors'
	*clr & cla
	$menu_loc = 'uni_dorm'
	$menu_arg = 'womens_restroom'
	$locM = 'uni_dorm'
	$locM_arg = 'womens_restroom'
	menu_off = 0
	gs 'stat'
	'<center><b><font color="maroon">Dormitory - Women''s Restroom</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/restrooms/bathroom_womens.jpg"></center>'
	'You peek around the corner. Inside this room, stalls line the right side and rows of sinks, with <a href="exec:gt ''mirror'', ''start''">mirrors</a> to the left. The room has a smell of disinfectant.'
	act 'Leave': minut += 1 & gt 'uni_dorm', 'tenth_floor'
	
	dynamic $tampon
	dynamic $quickwash
	dynamic $basin
end


if $ARGS[0] = 'dorm_room':
	$sexloc = 'uni_dorm'
	CLOSE ALL
	$loc_arg = 'dorm_room'
	$loc = 'uni_dorm'
	$locM_arg = 'dorm_room'
	$locM = 'uni_dorm'
    $location_type = 'private'
	$menu_loc = 'uni_dorm'
	$menu_arg = 'dorm_room'
	menu_off = 0
	cla
	*clr
	odkomp = 1
	gs 'stat'
	'<center><b>Your room</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm_room/room.jpg"></center>'
	'This is the dorm room you share with Vika.'
	'Your <a href="exec:gt ''bed'',''start''">bed</a> is positioned in one of the corners of the room. Your <a href="exec:gt ''stol'' ,''stolPar''">desk</a> sits at the foot of it.'
	if pornMagazine > 0:'You''ve hidden your porn magazines under the mattress of your bed.'
	if komp = 1:'Your <a href="exec:gt ''Komp'',''start''">laptop</a> sits on your desk.'
	'To the left of the entrance is an old <a href="exec:gt ''wardrobe'', ''start''">wardrobe</a>.'
	'There''s a <a href="exec:gt ''mirror'',''start''">mirror</a> hanging on the wall.'

	if tabletki > 0 and prezik + prezikcount + preziksabo > 0:
		'Your <a href="exec:gt ''stol'', ''bc''">birth control</a> is hidden in one of your desk drawers, as are your <a href="exec:gt ''stol'', ''bc''">condoms</a>.'
	elseif tabletki > 0:
		'Your <a href="exec:gt ''stol'', ''bc''">birth control</a> is hidden in one of your desk drawers.'
	elseif prezik + prezikcount + preziksabo > 0:
		'Your <a href="exec:gt ''stol'', ''bc''">condoms</a> are hidden in one of your desk drawers.'
	end

	*nl
	'<table><tr><td valign="top">Your <a href="exec:gt ''budilnik'', ''start''">alarm clock</a> is set to <b>'+iif(timer < 10,'0<<timer>>','<<timer>>')+':'+iif(timerM < 10,'0<<timerM>>','<<timerM>>')+'</b> during the week and <b>'+iif(timerEnd < 10,'0<<timerEnd>>','<<timerEnd>>')+':'+iif(timerEndM < 10,'0<<timerEndM>>','<<timerEndM>>')+'</b> during the weekend. It is currently turned '+iif(budilnikOn = 0, '<a href="exec: budilnikOn = 1 & gt $loc">OFF</a>.', '<a href="exec: budilnikOn = 0 & gt $loc">ON</a>.')
	*nl
	gs 'exercise', 'start'

	if hour > 14 and hour < func('bordel', 'open_hour'):
		vika_sleep = 0
		if npc_rel['A220'] >= 60 and birthday = day and birthmonth = month and vikaslut = 1 and b_vika_present_Day ! daystart:
			'<a href="exec:gt''Vika''">Vika</a> seems excited when she notices you. She is trying to hide a box behind her back.'
		else
			'<a href="exec:gt''Vika''">Vika</a> is lying on her bed.'
		end
	elseif hour > func('bordel', 'close_hour') and hour < 8:
		vika_sleep = 1
		if vikaslut = 0:
			'<a href="exec:gt''Vika''">Vika</a> is sleeping in her bed after being out the whole night.'
		elseif vikaslut = 1:
			'<a href="exec:gt''Vika''">Vika</a> is sleeping in her bed after working hard in the brothel all night.'
		end
	end

	act 'Walk out to the corridor':
		if $clothingworntype ! 'nude':
			odkomp = 0
			minut += 1
			gt 'uni_dorm', 'tenth_floor'
		else
			'You need to get dressed first!'
		end
	end
	
	act 'Relax on your bed': gt 'bed', 'start'

	if university['semester_week'] > 0:
		i = 0
		:study_loop
		if $class_list_institution[i] = 'uni_<<$university[''enrolled_in'']>>_semester_<<university[''enrolled_in_semester'']>>':
			if dyneval ('RESULT = class[''<<$class_list_institution[i]>>_<<$class_list_name[i]>>_optional_weekly_grade_gain'']') < dyneval ('RESULT = class[''<<$class_list_institution[i]>>_<<$class_list_name[i]>>_optional_weekly_max'']'):
				dynamic '	act ''Study for your <<$class_list_name[i]>> class (0:30)'': gt ''uni_dorm'', ''studying'', ''<<$class_list_institution[i]>>'', ''<<$class_list_name[i]>>'' '
			else
				*nl
				'You don''t need to study any more this week for your <<$class_list_name[i]>> class.'
			end
		end
		
		i += 1
		if ARRSIZE('class_list_institution') >= i: jump 'study_loop'
		killvar 'i'
	end

	act 'Read the course literature (2:00)':
		cla
		menu_off = 1
		minut += 120
		intel_exp += 1 + (mentats_dose - rand(0,mentats_dose))
		gs 'stat'
		'You spend two hours reading the course literature. You feel like you''ve picked up some stuff from the book that will come in handy for the exam.'

		act 'Finish':gt 'uni_dorm', 'dorm_room'
	end

	gs 'unicoursework', 'start', $loc, $loc_arg

	dynamic $wearpan
	dynamic $removepan

	if curr_home ! 11:
		act 'Set this dorm room as your main home':gs 'set_home', 11 & gt 'uni_dorm', 'dorm_room'
	end
end

if $args[0] = 'studying':
	cla & *clr
	'<center><img <<$set_imgh>> src="images/locations/shared/apartment/homework.jpg"></center>'
	minut += 30
	if komp = 1:
		gs 'grades', 'optional_activity_attribute', '<<$ARGS[1]>>', '<<$ARGS[2]>>', 'yes', pcs_intel
	else
		gs 'grades', 'optional_activity_attribute', '<<$ARGS[1]>>', '<<$ARGS[2]>>', 'no', pcs_intel
	end
	gs 'stat'
	if dyneval ('RESULT = class[''<<$ARGS[1]>>_<<$ARGS[2]>>_optional_weekly_grade_gain'']') < dyneval ('RESULT = class[''<<$ARGS[1]>>_<<$ARGS[2]>>_optional_weekly_max'']'):
		'You study for half an hour and can tell that you will need to study more if you want to understand this week''s material.'
	else
		'You study for half an hour and believe that you now understand everything that is being covered this week.'
	end
	act 'Get up from your desk': gt 'uni_dorm', 'dorm_room'
end

if $args[0] = 'dorm_kitchen':
	$loc_arg = 'dorm_kitchen'
	$loc = 'uni_dorm'
    $location_type = 'private'
	$locclass = 'kitr'
	$menu_loc = 'uni_dorm'
	$menu_arg = 'dorm_kitchen'
	menu_off = 0
	gs 'kit_din'
	gs 'stat'
	'<center><b>Dorm Kitchen</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/dorm/dorm_kitchen.jpg"></center>'
	'The large shared kitchen for this floor. It is fairly tidy, with a lot of cabinets along two of the walls. Two ovens and two refrigerators, as well as several tables with chairs around them, are scattered around the room.'
	act 'Return to the corridor': minut += 1 & gt 'uni_dorm', 'tenth_floor'

	if eda > 0:
		if edahot = 0:
			$edagot = '<a href="exec:dynamic $edagotd">Cook a meal</a>'
		else
			$edagot = ''
		end
		'Your shelf in the refrigerator holds enough food for <b><<eda>></b> ' + iif(eda = 1, 'serving', 'servings') + '. <<$edagot>>'
	else
		'<b><font color="red">Your shelf in the refrigerator is bare. There is nothing left for you to eat.</font></b>'
	end

	if edahot > 0:'<a href="exec:dynamic $edahotd">There''s a warm meal on the table.</a>'

	dynamic $edasnack
	dynamic $sandwich
	dynamic $driwater
	dynamic $dritea
	dynamic $fill_bottle
	dynamic $edaD
	dynamic $pranik
	dynamic $fatdel
	dynamic $lekarstvo
	dynamic $vitamin
end

--- uni_dorm ---------------------------------

