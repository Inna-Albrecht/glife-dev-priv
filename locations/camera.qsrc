# camera

! $camera_locations is an array of dynamics that describe various camera event locations that a player can photograph.
! The named index in $camera_locations should match the $loc name where a player may want to take a picture
! Other variables:
!	$camera_description = the text rendered in photo actions
!	$camera_event = a named event in the camera location that has special handling for the location
!	$camera_return_loc = the location the player should be returned to after the event
!	$camera_return_loc_arg = argument for the location
$camera_locations['city_library'] = {
	$camera_description = 'City Library'
	$camera_event = 'city_library'
	$camera_return_loc = 'city_center'
	$camera_return_loc_arg = ''
}

$camera_locations['city_island'] = {
	$camera_description = 'University'
	$camera_event = 'city_uni'
	$camera_return_loc = 'city_island'
	$camera_return_loc_arg = ''
}

$camera_locations['gadukino'] = {
	$camera_description = 'Gadukino Village'
	$camera_event = 'gadukino'
	$camera_return_loc = 'gadukino'
	$camera_return_loc_arg = ''
}

$camera_locations['gadforest'] = {
	$camera_description = 'Gadukino Forest'
	$camera_event = 'gadforest'
	$camera_return_loc = 'gadforest'
	$camera_return_loc_arg = 'forest_edge'
}

$camera_locations['pav_complex'] = {
	$camera_description = 'Pavlovsk Five Estates'
	$camera_event = 'pav_complex'
	$camera_return_loc = 'pav_complex'
	$camera_return_loc_arg = 'start'
}

$camera_locations['pav_market'] = {
	$camera_description = 'Pavlovsk Train Station'
	$camera_event = 'pav_train_hall'
	$camera_return_loc = 'pav_market'
	$camera_return_loc_arg = ''
}

$camera_locations['gschool_grounds'] = {
	$camera_description = 'Pavlovsk School'
	$camera_event = 'pav_school'
	$camera_return_loc = 'gschool_grounds'
	$camera_return_loc_arg = 'main'
}

$camera_locations['pav_park'] = {
	$camera_description = 'Pavlovsk Park'
	$camera_event = 'pav_park'
	$camera_return_loc = 'pav_park'
	$camera_return_loc_arg = 'start'
}

$camera_locations['pav_lake'] = {
	$camera_description = 'Pavlovsk Lake'
	$camera_event = 'pav_lake'
	$camera_return_loc = 'pav_lake'
	$camera_return_loc_arg = ''
}

$camera_locations['pav_church'] = {
	$camera_description = 'Pavlovsk Church'
	$camera_event = 'pav_church'
	$camera_return_loc = 'pav_church'
	$camera_return_loc_arg = 'start'
}

! People that should show up when the player is looking at her camera considering who to photograph. Also used by events that attempt to
! inject camera opportunities into existing locations
$camera_people['mother'] = {
	$camera_description = $npc_nickname['A29']
	$camera_event = 'mother'
	$camera_return_loc = ''
	$camera_return_loc_arg = ''
	camera_requirement = (npc_rel['A29'] >= 50 and month > 2 and month < 9)
}

$camera_people['anya'] = {
	$camera_description = 'Anya'
	$camera_event = 'anya'
	$camera_return_loc = ''
	$camera_return_loc_arg = ''
	camera_requirement = (npc_rel['A33'] >= 50)
}

$camera_people['anushka'] = {
	$camera_description = 'Anushka'
	$camera_event = ''
	$camera_return_loc = ''
	$camera_return_loc_arg = ''
	camera_requirement = (npc_rel['A144'] >= 50)
}

$camera_people['lazar'] = {
	$camera_description = 'Lazar'
	$camera_event = 'lazar'
	$camera_return_loc = ''
	$camera_return_loc_arg = ''
	camera_requirement = (npc_rel['A149'] >= 50 and month > 2 and month < 9)
}

$camera_people['eugene'] = {
	$camera_description = 'Eugene'
	$camera_event = 'eugene'
	$camera_return_loc = ''
	$camera_return_loc_arg = ''
	camera_requirement = (LCEugenefirst >= 1)
}


if $ARGS[0] = 'start':
	*clr & cla
	'<center><img src="images/pc/items/accessories/camera.jpg"></center>'
	'You can''t help but smile as you check your camera. You love capturing life as it happens, but can be hard to decide exactly who or what you want to take photographs of.'
	'You spend a few minutes thinking about places and people that might make for interesting subjects. Perhaps...'
	*nl
	! Get the number of locations in the array
	location_count = ARRSIZE('$camera_locations')
	iterator = 0
	! For every location
	if location_count > 0:
		:location_loop
		! Assign the variables
		dynamic $camera_locations[iterator]
		! Show the player unphotographed locations
		if camera_found[$camera_event] ! 1: '<<$camera_description>>'
		iterator += 1
		if iterator < location_count: jump 'location_loop'
	end
	*nl
	people_count = ARRSIZE('$camera_people')
	iterator = 0
	! For every person in the array
	if people_count > 0:
		:people_loop
		! Assign the variables
		dynamic $camera_people[iterator]
		! If the subject has not been photographed and requirements are met (-1), show the name
		if camera_found[$camera_event] ! 1 and camera_requirement = -1: '<<$camera_description>>'
		iterator += 1
		if iterator < people_count: jump 'people_loop'
	end

	act 'Put your camera away': gt $loc, $loc_arg
end

if $ARGS[0] = 'check_location':
	! If the player has a camera and this location exists in the array
	if photographyEv >= 2 and $camera_locations[$loc] ! 0:
		! Assign the variables
		dynamic $camera_locations[$loc]
		! If they have not already photographed the location
		if camera_found[$camera_event] ! 1:
			! Offer the action
			act 'Photograph the <<$camera_description>>':
				camera_found[$camera_event] = 1
				$loc = $camera_return_loc
				$loc_arg = $camera_return_loc_arg
				gt 'camera', $camera_event
			end
		! If they photographed the lake but did not trigger the sonia conditions, offer her
		elseif $camera_event = 'pav_lake' and camera_found['sonia'] ! 1 and sunWeather = 1 and soniaPS > 0:
			gs 'camera', 'pav_lake_sonia', 'describe'
		end
	end
end



if $ARGS[0] = 'check_person':
    ! If the player has a camera and this person exists in the array
    if photographyEv >= 2 and $camera_people[$ARGS[1]] ! 0:
        ! Assign the variables
        dynamic $camera_people[$ARGS[1]]
        ! If they have not already photographed the person
        if camera_found[$ARGS[1]] ! 1 and camera_requirement = -1:
            ! Offer the action
            dynamic ' act ''Ask to take a photo'': camera_found["<<$ARGS[1]>>"] = 1 & gt ''camera'', $camera_event'
        end
    end
end


if $ARGS[0] = 'eugene':
	*clr & cla
	npc_rel['A89'] += 1
	'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big89.jpg"></center>'
	'"Hey, can I ask you something?"'
	'She smiles and nods. "Sure. What is it sweetie?"'
	'"Can I take your photo?"'
	'"Sure thing, sweetie." She stops to let you take a photo, misunderstanding what you meant.'
	'You shake your head and pull out your camera. "No, I meant with this."'
	'She looks a little surprised and sheepish. "Oh! Are you working on becoming a photographer?"'
	*nl
	'You nod. "Yes, I just need more practice. Which is where you come in."'
	'She laughs softly. "Okay, why not? I havn''t taken a break yet today. We can go to my place and you can take my picture there. Sound good?"'
	'You nod. "Sure, that works for me." She tells her sister that she''s taking a break and leads you out of the cafe to her apartment building. She leads you upstairs and into her apartment. Once inside, you take out your camera.'
	act 'Start taking her picture': gt 'eugeneev1', 'photography'
end

if $ARGS[0] = 'mother':
	cla & *clr
	'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/mom/mother.jpg"></center>'
	'You take our your camera. "Hey <<$npc_nickname[''A29'']>>, do you mind if I take your photograph for my portfolio?"'
	'She frowns at you. "Where did you get that camera?"'
	'You shrug a little. "I saved up and bought it. Please? I need the practice and I think you would make a great subject."'
	'She shakes her head. "Seems like a waste of money to me."'
	'You sigh. "I love doing it, plus I can make a living from it. Please?"'
	*nl
	'She sighs. "Fine, but I still think it''s a waste of time."'
	'You nod. "I know. Let''s go outside."'
	'"I don''t have time for that, just take my picture." she sternly replies'
	'"Please?" you plead.'
	'She shakes her head. "Fine. The things I do for you kids..."'
	act 'Take her photo':
		cla & *clr
		minut += 15
		photoskl_exp += rand(3,5)
		portfolio_people[1] = 1
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/pc/activities/photography/foto/people/1.jpg"></center>'
		'You go outside and have her stand in a few poses around the courtyard before finally settling on her standing in front of some flowers. "I got it!"'
		'She walks over to you. "Finally. Now I can get back to the housework."'
		'You show her the photos you took and the one you finally decided on. "Here look, this one here is the one I will use for my portfolio."'
		'She smiles. "They do look nice... I still say it''s a waste of time though."'
		'You roll your eyes. "I know <<$npc_nickname[''A29'']>>." you reply as she walks back inside.'
		act 'Put your camera away': gt 'pav_complex', 'start'
	end
end

if $ARGS[0] = 'anya':
	cla & *clr
	'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/anya/home/sisboyqw_05.jpg"></center>'
	'You climb off the bed and grab your camera off your desk. "Hey, do you mind if I take your photograph for my portfolio?"'
	'She smiles. "What is it with you and that camera?"'
	'You shrug a little. "I just love doing it. If I get good enough I could do it for a living. So please? I need the practice and I think you would make a great subject."'
	'She giggles. "Fine, but something classy."'
	'You nod. "Deal."'
	act 'Take her photo':
		cla & *clr
		minut += 15
		photoskl_exp += rand(3,5)
		portfolio_people[2] = 1
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/pc/activities/photography/foto/people/2.jpg"></center>'
		'You direct her to lie on the bed in several poses, then stand in various spots around the room before you finally find the perfect image. Once you''ve taken it, you sit down on the bed. "I got it."'
		'She sits down next to you. "Let me see."'
		'You show her the photos you took and the one you finally decided on. "You sure my ass doesn''t look fat in this? I think it looks fat." she says with a pout.'
		'You shake your head and reassure her. "Your ass looks perfect. That''s why I took this photo. Trust me."'
		'"Fine!" she pouts before sits back down on her bed. You put your camera away and join her.'
		act 'Finish': gt 'sister_chat', 'sister_chat'
	end
end

if $ARGS[0] = 'lazar':
	cla & *clr
	'<center><img <<$set_imgh>> src="images/characters/shared/headshots_main/big149.jpg"></center>'
	'You smile at him as you pull out your camera. "Hey, do you mind if I take your photograph for my portfolio?"'
	'He smiles. "Sure, but only if you promise to show me your portfolio when you''re done."'
	'You nod your head. "Deal." He jogs over to Kolka and his classmates playing.'
	act 'Take his photo':
		cla & *clr
		minut += 15
		photoskl_exp += rand(3,5)
		portfolio_people[3] = 1
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/pc/activities/photography/foto/people/3.jpg"></center>'
		'You watch as he starts showing off his skills, pretending to be teaching them, but you''re fairly sure he''s just showing off for you. You start taking several photos of him and they''re looking pretty good. He then suddenly pulls off his shirt and you keep taking photographs as you try to remain focused. You finally get the shot you want and put your camera away before giving Lazar a wave. He returns the wave as you leave.'
		act 'Walk off the pitch': gt 'gschool_grounds', 'sports'
	end
end

if $ARGS[0] = 'city_library':
	*clr & cla
	minut += 15
	photoskl_exp += rand(3,5)
	portfolio_locations[1] = 1
	gs 'stat'
	'<center><img src="images/pc/activities/photography/sveta_camera.jpg"></center>'
	'You make your way over to the city library. It is a grand building and would make for a wonderful photograph. You move around several times until you find the perfect spot and take several photos before pausing and checking them. You''re happy with the results, knowing that you have something to add to your portfolio.'
	act 'Put your camera away': gt $loc, $loc_arg
end

if $ARGS[0] = 'city_uni':
	*clr & cla
	minut += 25
	photoskl_exp += rand(5,7)
	portfolio_locations[2] = 1
	gs 'stat'
	'<center><img src="images/pc/activities/photography/sveta_camera.jpg"></center>'
	'You make your way over to the university. It is a grand building and would make for a wonderful photograph. You move around several times until you find the perfect spot and take several photos before pausing and checking them. You can''t help but notice all the students moving around and feel that they would make for a great photo as well. You stop and take several more photos of the students before stopping. You''re happy with the results, knowing that you have something to add to your portfolio.'
	act 'Put your camera away': gt $loc, $loc_arg
end

if $ARGS[0] = 'gadukino':
	*clr & cla
	minut += 25
	photoskl_exp += rand(5,7)
	portfolio_locations[4] = 1
	gs 'stat'
	'<center><img src="images/pc/activities/photography/sveta_camera.jpg"></center>'
	'Standing in the village, you realize what a wonderful photograph this place would make. You move around several times until you find the perfect spot and take several photos before pausing and checking them. You can''t help but notice the houses and feel that they would make for a great photo as well. You stop and take several photos of different houses before finally settling on your grandparents home. You''re happy with the results, knowing that you have something to add to your portfolio.'
	act 'Put your camera away': gt $loc, $loc_arg
end

if $ARGS[0] = 'gadforest':
	*clr & cla
	minut += 15
	photoskl_exp += rand(3,5)
	portfolio_locations[6] = 1
	gs 'stat'
	'<center><img src="images/pc/activities/photography/sveta_camera.jpg"></center>'
	'Standing in the forest, you are amazed by how breathtakingly beautiful it is out here. You walk around taking photos of the trees until you''re happy and check all the photos you''ve taken. You feel confident that you''ve found something to add to your portfolio and put your camera away.'
	act 'Put your camera away': gt $loc, $loc_arg
end

if $ARGS[0] = 'pav_church':
	*clr & cla
	minut += 25
	photoskl_exp += rand(5,7)
	portfolio_locations[12] = 1
	gs 'stat'
	'<center><img src="images/pc/activities/photography/sveta_camera.jpg"></center>'
	'Standing in front of the local church, you realize what a wonderful photo this place would make. You move around several times until you find the perfect spot and take several photos before pausing and checking them. You then spot the cemetery and walk over to take some photos of it as well. You''re happy with the results, knowing that you have something to add to your portfolio.'
	act 'Put your camera away': gt $loc, $loc_arg
end

if $ARGS[0] = 'pav_train_hall':
	*clr & cla
	minut += 15
	photoskl_exp += rand(3,5)
	portfolio_locations[8] = 1
	gs 'stat'
	'<center><img src="images/pc/activities/photography/sveta_camera.jpg"></center>'
	'Standing in front of the local train station, you realize what a wonderful photo this place would make. You move around several times until you find the perfect spot and take several photos before pausing and checking them. You''re happy with the results, knowing that you have something to add to your portfolio.'
	act 'Put your camera away': gt $loc, $loc_arg
end

if $ARGS[0] = 'pav_complex':
	*clr & cla
	minut += 15
	photoskl_exp += rand(3,5)
	portfolio_locations[7] = 1
	gs 'stat'
	'<center><img src="images/pc/activities/photography/sveta_camera.jpg"></center>'
	'Standing in the courtyard, you realize what a wonderful photo this place would make. You move around several times until you find the perfect spot and take several photos before pausing and checking them. You''re happy with the results, knowing that you have something to add to your portfolio.'
	act 'Put your camera away': gt $loc, $loc_arg
end

if $ARGS[0] = 'pav_school':
	*clr & cla
	minut += 15
	photoskl_exp += rand(3,5)
	portfolio_locations[9] = 1
	gs 'stat'
	'<center><img src="images/pc/activities/photography/sveta_camera.jpg"></center>'
	'Standing in front of the school, you realize what a wonderful photo this place would make. You move around several times until you find the perfect spot and take several photos before pausing and checking them. You''re happy with the results, knowing that you have something to add to your portfolio.'
	act 'Put your camera away': gt $loc, $loc_arg
end

if $ARGS[0] = 'pav_park':
	*clr & cla
	minut += 15
	photoskl_exp += rand(3,5)
	portfolio_locations[10] = 1
	gs 'stat'
	'<center><img src="images/pc/activities/photography/sveta_camera.jpg"></center>'
	'Walking through the park, you realize what a wonderful photo this place would make. You move around several times until you find the perfect spot and take several photos before pausing and checking them. You''re happy with the results, knowing that you have something to add to your portfolio.'
	act 'Put your camera away': gt $loc, $loc_arg
end

if $ARGS[0] = 'pav_lake':
	*clr & cla
	minut += 15
	photoskl_exp += rand(3,5)
	portfolio_locations[11] = 1
	gs 'stat'
	'<center><img src="images/pc/activities/photography/sveta_camera.jpg"></center>'
	'As you look out over the lake, you realize what a wonderful photo this place would make. You walk around the shore several times until you find the perfect spot and take several photos before pausing and checking them. You''re happy with the results, knowing that you have something to add to your portfolio.'
	act 'Put your camera away': gt $loc, $loc_arg
	if soniaPS > 0 and sunWeather = 1 and temp > 15 and hour > 6 or hour < 22:
		'Just as you''re about to put your camera away, you notice a girl sunbathing topless. It takes you a minute to realize that it''s Sonia and she appears to be alone. You wonder if she would make a good subject, a take on how innocence can be lost in a cruel world.'
		gs 'camera', 'pav_lake_sonia'
	else
		act 'Put your camera away': gt $loc, $loc_arg
	end
end

if $ARGS[0] = 'pav_lake_sonia':
	if $ARGS[1] = 'describe': 'You notice a girl sunbathing topless. It takes you a minute to realize that it''s Sonia and she appears to be alone. You wonder if she would make a good subject, a take on how innocence can be lost in a cruel world.'
	act 'Take her photograph':
		*clr & cla
		camera_found['sonia'] = 1
		photoskl_exp += rand(1,2)
		gs 'stat'
		'<center><img src="images/characters/pavlovsk/school/girl/sonia/lake/photography/photo1.jpg"></center>'
		'You get a little closer and start taking her photograph, moving around and taking a number of shots from different angles before she suddenly sits up and looks in your direction.'
		gs 'arousal', 'voyeur', 5
		gs 'stat'
		act 'Caught':
			*clr & cla
			photoskl_exp += rand(1,2)
			gs 'stat'
			'<center><img src="images/characters/pavlovsk/school/girl/sonia/lake/photography/photo2.jpg"></center>'
			'She covers herself up and looks at you in surprise. "What the fuck <<$pcs_nickname>>?! What are you doing?"'
			'You smile and take another photo of her. "Sorry, I was just taking your photo. You''re just so beautiful and I couldn''t help myself."'
			'She smiles at the compliment. "Really?"'
			'You nod as you take another photo of her. "Yes, now why don''t you get up and stop covering up?"'
			'She smiles even more. "Okay, but just a few." She gets up on her knees and removes her hands, giving you a nice shot of her topless body.'
			gs 'arousal', 'voyeur', 2
			gs 'stat'
			act 'Take more photos':
				*clr & cla
				minut += 5
				photoskl_exp += rand(1,2)
				gs 'stat'
				'<center><img src="images/characters/pavlovsk/school/girl/sonia/lake/photography/photo3.jpg"></center>'
				'She is a little awkward at first, not knowing what to do, but as you continue taking more photos, she starts to relax and become more natural in her poses. She even starts to get into it herself and enjoying it.'
				act 'Take more photos':
					*clr & cla
					photoskl_exp += rand(1,2)
					gs 'stat'
					'<center><img src="images/characters/pavlovsk/school/girl/sonia/lake/photography/photo4.jpg"></center>'
					'As you continue photographing her, she starts teasing you by pulling on her bottoms while her hands run along her body or play with her breasts.'
					'You can''t help but smile and see how far she wants to go. "Come on, lose the bottoms. You know you want to..."'
					gs 'arousal', 'voyeur', 5
					gs 'stat'
					act 'No bottoms':
						*clr & cla
						photoskl_exp += rand(1,2)
						gs 'stat'
						'<center><img src="images/characters/pavlovsk/school/girl/sonia/lake/photography/photo5.jpg"></center>'
						'She grins and unties her bottoms from one hip, which lets them partially fall off her, exposing her pussy. You take more photos as she pulls the other tie loose and her bottoms fall away.'
						gs 'arousal', 'voyeur', 5
						gs 'stat'
						act 'Naked':
							*clr & cla
							photoskl_exp += rand(1,2)
							portfolio_people[4] = 1
							gs 'stat'
							'<center><img src="images/characters/pavlovsk/school/girl/sonia/lake/photography/photo6.jpg"></center>'
							'You continue taking photos of her now naked body and start getting a lot closer to her. The longer it goes on, the more turned on she seems to get, eventually playing with her pussy. Finally, having taken several dozen photos, you feel that you''ve taken enough and stop. "Thanks Sonia, I got what I needed."'
							'She frowns before bobbing her head slightly and smiling. "It was my pleasure, I really liked it. M-Maybe we can do it again sometime?" You can hear her shy sweet former self coming out.'
							'You feel bad for her, knowing that almost nobody talks to her anymore. "Yeah sure, I would like that."'
							'She breaks into a huge smile and looks happier than you''ve seen her in a long time. "Okay, just let me know or maybe stop by my house sometime?"'
							'You nod. "I will, but for now I''ve got all I need. I''ll let you get back to sunbathing. Thanks again." She smiles and gets dressed as you put your camera away.'
							gs 'arousal', 'voyeur', 5
							gs 'arousal', 'end'
							gs 'stat'
							act 'Leave': gt $loc, $loc_arg
						end
					end
				end
			end
		end
	end
end


--- camera ---------------------------------

