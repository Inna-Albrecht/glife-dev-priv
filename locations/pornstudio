# pornstudio
$location_type = 'public_indoors'

if $ARGS[0] = 'start':
	CLOSE ALL
	$metka = $ARGS[0]
	$loc = $CURLOC
	gs 'pornhist', 'short'
	cls
	minut += 5
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/studia_0.jpg"></center>'

	act 'Leave':gt 'Nord', 'redlight'

	if hour < 10 or hour > 22:
		'The Porn studio is closed. Opening hours are between 10 and 22'
		exit
	end

	'Manager''s office open.'

	act 'Go to the manager':gt 'pornstudio', 'manager'

	act 'Visit the managers PA':gt 'pornstudio', 'pa'

	if bomzQW = 1 and week = 4 and workDisk = 0:
		act 'Arrive for work':gt 'pornstudio', 'delivery'
	end

	if workDolg > 0 and money >= workDolg:
		act 'Repay your debt':
			cls
			minut += 5
			workDisk = 0
			money -= workDolg
			workDolg = 0
			gs 'stat'
			'You give the money that you owe to the studio.'

			act 'Leave':gt 'pornstudio', 'start'
		end
	elseif workDolg = 0:
		if workDisk = 1 and week = 5:
			act 'Take the disk':
				cls
				minut += 5
				workDisk = 2
				gs 'stat'
				'You are given a thumb drive, and asked if you remember all the instructions. You nod your head and leave with the drive.'
				'Moving away from the studio near you hear someone else came out of a nearby building and follow for you.'
				'<font color="magenta"> "Avoid deserted places, maybe this will blow over"</font> you reassure yourself, and carry on.'

				act 'Leave':gt 'Nord', 'redlight'
			end
		elseif workDisk = 3 and week = 5:
			act 'Give the envelope':
				cls
				minut += 5
				workDisk = 4
				gs 'stat'

				if konvert = 0:
					workDolg = 200000
					workDolgDay = 7
					'Digging through your things you begin to panic. No envelope. You search everything again, but you don''t find the envelope.'
					'SHIT!! But how could you let this happen! What have you gotten into now?!'
					'You stand there unable to speak. Looks like you''re fucked.'
					'The Manager grabs his phone and makes a call. You can''t hear exactly what he is saying because your heart is beating so loud, he hangs up.'
					'"You are very lucky, the boss is in a good mood. So has given you one week to cover his losses."'
					'"You have until next Friday evening to bring me 200,000 <b>₽</b>. Do this and we''re done, you will never work for us anymore.'
					'<font color="#0D42C0">Fail, and bad things will happen to you.</font>'
				else
					konvert = 0
					money += 5000
					'You give the envelope to the manager and he gives you 5,000 <b>₽</b>.'
				end

				act 'Leave':gt 'Nord', 'redlight'
			end
		end
	end

	if pfilmNO = 1 and pfilmNoVenera = 1 and Venera = 0:
		act 'Show your medical certificate':
			cls
			minut += 5
			pfilmNoVenera = 0
			pfilmNO = 0
			gs 'stat'
			'You hand over your medical certificate showing that you are STD free. They promise to consider your reinstatement as a porn actress.'

			act 'Leave':gt 'pornstudio', 'start'
		end
	end
end

if $ARGS[0] = 'pa':
	cla
	*clr
	minut += 5
	gs 'stat'
	'There is a young guy tapping on the computer keyboard in a small office.'

	if film = 0:
	'"Who are you? This office is for staff only."'
	else
		'<font color="#0D42C0"> "Hi <<$pfname>>, what can I do for you?"</font>'
		act 'Change your porn name':
			cla
			*clr
			'"I would like to change my professional name. Is it possible?"'
			'"Of course, just give me a minute to register it in the database. So, what would you ike to be called?"'
			act '"I want to be called..."':$pfname = input("Enter the name you want to be known as.") & gt 'pornstudio', 'pa'
			act '"Ehh, leave it."':gt 'pornstudio', 'pa' 
		end
		if pfilmSTOP = 0:
			act 'What''s the rating of my videos?':
				cla
				minut += 5
				gs 'stat'

				'"You''ve made <<film>> films.'

				if porn_fame < 10:
					'You are unknown, nobody knows your name. You are one of hundreds. You will get to play tertiary characters in big movies. Your own movies sold really cheap.'
				elseif porn_fame < 20:
					'You are still unknown, but your name is mentioned in the credits. You are unlikely to get recognized. Your own movies are still sold cheap.'
				elseif porn_fame < 35:
					'You are mentioned here and there, but you will still mostly get secondary roles in most of your porn movies. Your name is slowly emerging and you have your own fans.'
				elseif porn_fame < 50:
					'You are a minor, but known porn actress. You occasionally get primary roles, and could get back cover appearances. Your movies are getting more popular, which means more fans and money.'
				elseif porn_fame < 70:
					'You are a known porn actress. You play mostly primary roles, and you appear on the front cover. Your movies are sold at a decent price, and in big number. You may appear in a variety of magazines, with reports about you.'
				elseif porn_fame < 95:
					'You are a famous porn actress. You always play the main role, with your name everything can be sold. You are a brand name, it is on T-shirts and sex toys.'
				else
					'You are a true Porn Star, your name and face is everywhere!'
				end

				if porn_fame >= 20:
					ftypecount[0] = pornvag & $ftypecount[0] = 'vaginal'
					ftypecount[1] = pornanal & $ftypecount[1] = 'anal'
					ftypecount[2] = pornbeast & $ftypecount[2] = 'beastiality'
					ftypecount[3] = porngang & $ftypecount[3] = 'group sex'
					ftypecount[4] = porncream & $ftypecount[4] = 'creampie'
					ftypecount[5] = pornhole & $ftypecount[5] = 'gloryhole'
					ftypecount[6] = pornbdsm & $ftypecount[6] = 'bdsm'
					ftypecount[7] = pornlesbo & $ftypecount[7] = 'lesbian'
					ftypecount[8] = pornpreg & $ftypecount[8] = 'pregnant'
					ftypecount[9] = pornexhib & $ftypecount[9] = 'public'
					ftypecount[10] = pornmast & $ftypecount[10] = 'masturbation'
					ftypecount[11] = pornbukk & $ftypecount[11] = 'cumshot'
					ftypecount[12] = pornbj & $ftypecount[12] = 'blowjob'
					ftypecount[13] = pornhj & $ftypecount[13] = 'handjob'
					ftypecount[14] = pornswall & $ftypecount[14] = 'cum swallowing'
					ftypecount[15] = porngasm & $ftypecount[15] = 'orgasm'
					k = 1
					:repeating
						c = 15
						:ftypecount
							current = ftypecount[c]
							$current = $ftypecount[c]
							c -= 1
							if current > ftypecount[c]:
								ftypecount[c+1] = ftypecount[c]
								$ftypecount[c+1] = $ftypecount[c]
								ftypecount[c] = current
								$ftypecount[c] = $current
							end
						if c >= k:jump 'ftypecount'
						k += 1
					if k < 15:jump 'repeating'
					if ftypecount[1] = ftypecount[0]:
						if ftypecount[0] = ftypecount[2]:
							'You are known from your <<$ftypecount[0]>>, <<$ftypecount[1]>> and <<$ftypecount[2]>> scenes.'
						else
							'You are known from your <<$ftypecount[0]>> and <<$ftypecount[1]>> scenes.'
						end
					else
						'You are known from your <<$ftypecount[0]>> scenes.'
					end
				end
				
				if film < 10 and porn_fame < 10:

					'The guy whispers "By the way, if you wanted, I could completely remove them from our systems - for a good price of course."'

					act 'Tell me more':
						cla
						minut += 5
						films_stop_payment = film * 40000
						gs 'stat'
						'"Seeing as you''re not well known, it''s unlikely anyone would notice. However, to remove them you''d have to cover the studios potential future profits, 40,000 <b>₽</b> per film. That''s a total of <<films_stop_payment>> <b>₽</b>.'
						'Also, that would piss off your co-stars and our director. It would end your porn career. You should know, there is no guarantee that there are no other copies of your movies out there anyway, so think carefully."'

						if money >= films_stop_payment:
							act 'Remove them':
								minut += 5
								money -= films_stop_payment
								pfilmSTOP = 1
								killvar 'pfilmday'
								killvar 'pornplan'
								killvar 'porntaken'
								killvar 'pornstack'
								killvar 'prodcosttrue'
								killvar 'prodcost'
								film = 0
								gs 'stat'
								'We never met. Have a nice day, and don''t come back'

								act 'Leave':gt 'pornstudio', 'start'
							end
						end

						act 'Not interested':gt 'pornstudio', 'start'
					end

					act 'Leave':gt 'pornstudio', 'start'
				end
				
				act 'Leave':gt 'pornstudio', 'start'
			end
		end

		act 'Leave':gt 'pornstudio', 'start'

	end

	act 'Leave':gt 'pornstudio', 'start'
end

if $ARGS[0] = 'delivery':
	cls
	minut += 15
	gs 'stat'
	'We made inquiries about you, and everything seems in order. Essentially, we give you a thumb drive, you deliver it to the sauna, they give you an envelope and you bring it to us. Immediately.'
	'Upon delivery of envolope you get 5000 <b>₽</b>. Deliveries are available every Friday, after the studio has opened. If you agree, then visit any Friday, and choose a safe mode of transport.'

	act 'No, I don''t think it''s for me (leave)':bomzQW = -1 & gt 'Nord', 'redlight'

	act 'Agree to work':
		cls
		minut += 15
		gs 'stat'
		'Just this time, we will take you. In the future you''ll have to make your own way, because Fridays are getting risky for us, but we need to intruduce you to our colleagues.'

		act 'Let''s go':
			cls
			workDisk = 1
			minut += 15
			gs 'stat'
			'You slip into a jeep by the entrance, and are driven to the sauna.'

			if saunasex = 1:
				'You recall going to this sauna. You had some pretty energetic sex here.'
			elseif sauna > 0:
				'Oh, it''s the sauna near your home.'
			else
				'This sauna is located near your home, it is strange that you did not notice it before.'
			end

			'You go to the sauna, and you''re introduced to the local managers. it is explained to you who you need to deliver to, and reminded that it is necessary to return the envelope the same day you receive it.'
			'"Don''t forget to use secure transport. We''ll see you on Fridays and don''t fuck up."'

			act 'Leave':bomzQW = 2 & gt 'street'
		end
	end
end

if $ARGS[0] = 'shower':
	cla
	*clr
	$location_type = 'bathroom'
	$locM = 'pornstudio'
	$metkaM = 'bathroom'
	minut += 5
	pcs_horny += 1
	pcs_makeup = 1
	dynamic $showerdin
	'<center><video autoplay loop src="images/shared/home/bathroom/dush.mp4"></video></center>'
	'You enter the shower cubicle. Turned on the shower and gently wash all traces of semen from your body.'

	act 'Look in the mirror': gt 'mirror', 'start'
	act 'Go and see Peter':gt 'pornstudio', 'postvideo'
end

if $ARGS[0] = 'bathroom':
	cla
	*clr
	$location_type = 'bathroom'
	$locM = 'pornstudio'
	$metkaM = 'bathroom'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/photo/shower.jpg"></center>'
	'You are in the shower room after your movie shoot, you should go and see Peter to get your pay unless you need to use the mirror again.'

	act 'Look in the mirror': gt 'mirror', 'start'
	act 'Go and see Peter':gt 'pornstudio', 'postvideo'
end

if $ARGS[0] = 'manager':
	cls
	minut += 5
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/studia_0.jpg"></center>'
	'You walk into the office of the producer in the porn production company, and you see that the producer is here.'

	if firstkasting = 0:gt 'pornstudio', 'first'
	
	if pregtime >= 90 and pregtalkpornstudio = 0:gt 'pornstudio', 'pregreact'

	if pfilmNO = 1:
	! Banned from shooting porn
		'You''re blacklisted, no decent porn studio will hire you, goodbye.'
	elseif pfilmSTOP = 1:
	! No longer shooting porn
		'I''m sorry I thought you had retired...'
	elseif ((pfilmday[0] > 0 and hour > 12) or pornmiss = 1) and prodcosttrue <= 20000 and missedshoot = 0:
	! You missed your shooting time, but you have a chance to work it off
		missedshoot = 1
		pornmiss = 0
		'Peter seems really agitated, as he storms to you, ready to yell your head off.'
		'"Didn''t I tell you not to be late? Where the fuck where you? I''ve wasted a lot of money! I had to pay actors, assistants, the director and the doctor just to sit on their asses! The whole shoot was a bust, just because of you, slut!"'
		'He managed to cool off a little with the airing some of his frustration, and after a deep breath he continues more calmly.'
		if pfilmday[1] = 0:
			missday = rand(0,3)
			porntaken['1,<<missday>>'] = 0
			act 'Jump in tomorrow':
				prodcost = 0
				pfilmNOPAY = 1
				porntaken['1,<<missday>>'] = 2
				pfilmday[1] = pornplan['1,<<missday>>']
				pornstack += 1
				freefilm = 1
				gt 'pornstudio', 'start'
			end
			'"I''ll give you a chance to make it right. An another actress has gotten sick and cancelled her shoot. If you jump in instead of her, your debt is canceled. Or you can work it off, but that will take longer. Of course you can also pay it back, if you have the money. And no, I do not remember what the fuck are we going to shoot, in this whole shitstorm I didn''t even remembered to eat!"'
		else
			'You know what, you can work it off, but it will take you a while. Of course you can also pay it back, if you have the money."'
		end
		
		act 'Accept your debt':prodcosttrue += prodcost & gt 'pornstudio', 'start'
	
		act 'Fuck this shit, I quit!':
			pfilmNO = 1
			killvar 'prodcosttrue'
			killvar 'pfilmday'
			killvar 'pornplan'
			killvar 'porntaken'
			gt 'pornstudio', 'start'
		end
		exit
	elseif prodcosttrue > 20000 and prodcost > 0:
	! You really fucked up, you are banned from porn for life
		pfilmNO = 1
		killvar 'prodcosttrue'
		killvar 'pfilmday'
		killvar 'pornplan'
		killvar 'porntaken'
		'You did not come to the shoot. You fucked me, and you already owed me a lot of money! I paid a lot of money for assistants, actors, cameramen, plus studio rental, and you took it and threw it in my face.'
		'No, I do not want to work with you and moreover, you''re blacklisted.'
	elseif pfilmday[0] > 0 and hour >= 10 and hour <= 12:
	! You have porn shoot today, and you are in time
		pornnow = 1
		if hour = 10:'"Come on honey, inspection."'
		if hour = 11:'"You''re late, my patience is not eternal", Peter says strictly. "Quickly now, to the inspection!"'
		if hour = 12:'"What the hell? Everyone''s waiting for you. Hurry up, get inspected! Now!"'

		act 'Go for medical check-up':gt 'pornstudio', 'medical', pfilmday[0]
		exit
	elseif film = 0 and pornstack = 0:
	! Your first ever movie offer
		if firstoffer = 0:
			firstoffer = 1
			'So, you''re the new girl. As luck would have it we are looking for a brand new girl for our shoot. It''s called ''Girls Jerk Guys #36''.'
			'Guess what? You have to jerk off a guy, in this one he will finish on you tits. It pays 2,000 <b>₽</b>.'
		else
			'Hello <<$pfname>>, since you are brand new, all I can offer is an introductory movie called ''Girls Jerk Guys #36''.'
			'Yes, you have to jerk off a guy, in this one he will finish on you tits. It pays 2,000 <b>₽</b>.'
		end

		act 'Agree':
			cla
			pornstack += 1
			pfilmday[1] = 1
			'Cool, we shoot tomorrow. Get here between 10 and 11, and please do not be late.'

			act 'Leave':gt 'pornstudio', 'start'
		end

		act 'I don''t think I''m ready':gt 'pornstudio', 'start'
		exit
	elseif film > 0:
		gs 'pornstudio', 'options'
	else
		'Peter looks up from his paperwork, and greets you. "Welcome <<$pfname>>! Can I help you with something?"'
	end
	
	if porndebt > 0:
		act 'Ask about your debt':'"Of course, <<$pcs_nickname>>! You current debt to the company is <<porndebt>> <b>₽</b>."'
	end

	act 'Leave':gt 'pornstudio', 'start'
end

if $ARGS[0] = 'options':
	'"Come on <<$pfname>>. Let''s see what I can offer for you."'
	if film < 5:
		fmulti = 100
	elseif film < 10:
		fmulti = 120
	elseif film < 15:
		fmulti = 140
	elseif film < 25:
		fmulti = 160
	elseif film < 50:
		fmulti = 180
	elseif film >= 50:
		fmulti = 200
	end
	
	act 'Filter by date':
		$selection = {
			*clr
			'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/studia_0.jpg"></center>'
			'<font color="grey">The selected days will not be displayed</font><br>'
			m1 = month
			d1 = day + 1
			if monthsend[m1] < d1: m1 += 1 & d1 = 1
			if m1 > 12: m1 = 1
			w2 = week + 1
			if w2 > 7: w2 = 1
			p1 = 1
			w1 = 1
			:monthloop
				*p '<Table border=0 cellpadding=1><TR><TH colspan=7><b><i><font size=6 color="brown"><<$monthName[m1]>></font></i></b></TH></TR>'
				*p '<TR align="center"><TH>   Monday    </TH><TH>   Tuesday   </TH><TH>Wednesday</TH><TH>  Thursday   </TH><TH>     Friday     </TH><TH>  Saturday   </TH><TH>    Sunday    </TH></TR>'
				:weekloop
					selected[p1] = p1
					if w1 = 1:*p '<TR>'
					!making sure that the date and the day of the week are in the correct position
					if w2 ! 1:
						if w2 = 7:
							w1 = 7 & *p '<TD></TD><TD></TD><TD></TD><TD></TD><TD></TD><TD></TD>'
						elseif w2 = 6:
							w1 = 6 & *p '<TD></TD><TD></TD><TD></TD><TD></TD><TD></TD>'
						elseif w2 = 5:
							w1 = 5 & *p '<TD></TD><TD></TD><TD></TD><TD></TD>'
						elseif w2 = 4:
							w1 = 4 & *p '<TD></TD><TD></TD><TD></TD>'
						elseif w2 = 3:
							w1 = 3 & *p '<TD></TD><TD></TD>'
						else
							w1 = 2 & *p '<TD></TD>'
						end
						w2 = 1
					end
					!selectable day display comes
					if inversefilter = 1:
						if datefilter['<<selected[p1]>>'] = 1:
							datefilter['<<selected[p1]>>'] = 0
						else
							datefilter['<<selected[p1]>>'] = 1
						end
					end
					
					if m1 = 1 and d1 >= 1 and d1 <= 8:nh1 = 1
					if m1 = 2 and d1 = 23:nh1 = 1
					if m1 = 3 and d1 = 8:nh1 = 1
					if m1 = 5 and (d1 = 1 or d1 = 2):nh1 = 1
					if m1 = 5 and d1 = 9:nh1 = 1
					if m1 = 6 and (d1 = 12 or d1 = 13):nh1 = 1
					if m1 = 11 and d1 = 4:nh1 = 1
					
					if nh1 = 1:
						$option = '<font color="red"><del><b><<d1>><b></del></font>'
					elseif pfilmday[p1] > 0:
						$option = '<font color="green"><<d1>></font>'
					elseif datefilter['<<selected[p1]>>'] = 1 or (weekdayfilter = 1 and w1 < 6) or (weekendfilter = 1 and w1 >= 6):
						$option = '<font color="grey"><del><<d1>></del></font>'
					else
						$option = '<a href="exec:datefilter[''<<selected[p1]>>''] = 1 & dynamic $selection"><font color="black"><<d1>></font></a>'
					end
					killvar 'nh1'
					*p '<TD align="center"><font size=5><<$option>></font></TD>'
					!preparing for next day
					d1 += 1
					p1 += 1
					w1 += 1
			!we have to check the end of the month too
			if monthsend[m1] < d1:
				!Oh-oh, this is the next month already!
				*p '</TR></TABLE><br>'
				if w1 = 8: w1 = 1
				w2 = w1
				w1 = 1
				m1 += 1
				if m1 > 12: m1 = 1
				d1 = 1
				!does it necessary to display the next month?
				if p1 < 50:jump 'monthloop'
			else
				!the next day is still the same month
				if p1 < 50:
					!the next day have to be displayed
					if w1 = 8: *p '</TR>' & w1 = 1
					jump 'weekloop'
				else
					!the next day does not have to be displayed
					*p '</TR></TABLE><br>'
				end
			end
			inversefilter = 0
			''
			if weekdayfilter = 0: 
				$weekdayfilter = '<a href="exec:weekdayfilter = 1 & dynamic $selection">Remove weekdays</a>'
			else
				$weekdayfilter = '<a href="exec:weekdayfilter = 0 & dynamic $selection">Restore weekdays</a>'
			end
			if weekendfilter = 0: 
				$weekendfilter = '<a href="exec:weekendfilter = 1 & dynamic $selection">Remove weekends</a>'
			else
				$weekendfilter = '<a href="exec:weekendfilter = 0 & dynamic $selection">Restore weekends</a>'
			end
			*p '<<$weekdayfilter>>  <<$weekendfilter>>  <a href="exec:inversefilter = 1 & dynamic $selection">Inverse selection</a><br>'
			*p '<a href="exec:killvar ''datefilter'' & killvar ''selected'' & weekdayfilter = 0 & weekendfilter = 0 & dynamic $selection">Clear date filters</a>'
		}
		dynamic $selection 
	end
	
!{	act 'Filter by tag':
		$displaytag = {
			*clr
			'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/studia_0.jpg"></center>'
			'<font color="grey">The selected tags will not be displayed</font><br><br>Currenty filtered tags: <font color="green"><<$tagfilter>></font>'
			if flag_tag[1] = 0:'<a href="exec:$tagfilter += ''hetero '' & flag_tag[1] = 1 & dynamic $displaytag">heterosexual</a>'
			if flag_tag[2] = 0:'<a href="exec:$tagfilter += ''lesbian '' & flag_tag[2] = 1 & dynamic $displaytag">lesbian</a>'
			if flag_tag[3] = 0:'<a href="exec:$tagfilter += ''threesome '' & flag_tag[3] = 1 & dynamic $displaytag">threesome</a>'
			if flag_tag[4] = 0:'<a href="exec:$tagfilter += ''gangbang '' & flag_tag[4] = 1 & dynamic $displaytag">gangbang</a>'
			if flag_tag[5] = 0:'<a href="exec:$tagfilter += ''facial '' & flag_tag[5] = 1 & dynamic $displaytag">facial</a>'
			if flag_tag[6] = 0:'<a href="exec:$tagfilter += ''vaginal '' & flag_tag[6] = 1 & dynamic $displaytag">vaginal</a>'
			if flag_tag[7] = 0:'<a href="exec:$tagfilter += ''anal '' & flag_tag[7] = 1 & dynamic $displaytag">anal</a>'
			if flag_tag[8] = 0:'<a href="exec:$tagfilter += ''blowjob '' & flag_tag[8] = 1 & dynamic $displaytag">blowjob</a>'
			if flag_tag[9] = 0:'<a href="exec:$tagfilter += ''handjob '' & flag_tag[9] = 1 & dynamic $displaytag">handjob</a>'
			if flag_tag[10] = 0:'<a href="exec:$tagfilter += ''cumshot '' & flag_tag[10] = 1 & dynamic $displaytag">cumshot</a>'
			if flag_tag[11] = 0:'<a href="exec:$tagfilter += ''swallowing '' & flag_tag[11] = 1 & dynamic $displaytag">swallowing</a>'
			if flag_tag[12] = 0:'<a href="exec:$tagfilter += ''DP '' & flag_tag[12] = 1 & dynamic $displaytag">DP</a>'
			if flag_tag[13] = 0:'<a href="exec:$tagfilter += ''creampie '' & flag_tag[13] = 1 & dynamic $displaytag">creampie</a>'
			''
			'<a href="exec:killvar ''$tagfilter'' & killvar ''flag_tag'' & dynamic $displaytag">Clear date filters</a>'
		}
		dynamic $displaytag
	end
}
	act 'Filter by participants':
		$displaynum = {
			*clr
			'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/studia_0.jpg"></center>'
			'<font color="grey">The movies above the selected number of partners will not be displayed</color><br>'
			if ppnum = 1: 
				$pp1 = '<b><font color="red" size=8>1</font></b>'
			else
				$pp1 = '<a href="exec:ppnum = 1 & dynamic $displaynum"><font size=8 color="black">1</font></a>'
			end
			if ppnum = 2: 
				$pp2 = '<b><font color="red" size=8>2</font></b>'
			else
				$pp2 = '<a href="exec:ppnum = 2 & dynamic $displaynum"><font size=8 color="black">2</font></a>'
			end
			if ppnum = 3: 
				$pp3 = '<b><font color="red" size=8>3</font></b>'
			else
				$pp3 = '<a href="exec:ppnum = 3 & dynamic $displaynum"><font size=8 color="black">3</font></a>'
			end
			if ppnum = 4: 
				$pp4 = '<b><font color="red" size=8>4</font></b>'
			else
				$pp4 = '<a href="exec:ppnum = 4 & dynamic $displaynum"><font size=8 color="black">4</font></a>'
			end	
			if ppnum = 5: 
				$pp5 = '<b><font color="red" size=8>5</font></b>'
			else
				$pp5 = '<a href="exec:ppnum = 5 & dynamic $displaynum"><font size=8 color="black">5</font></a>'
			end
			'<Table border=0 cellspacing=10><TR><TH><<$pp1>></TH><TH><<$pp2>></TH><TH><<$pp3>></TH><TH><<$pp4>></TH><TH><<$pp5>></TH></TR>'
			''
			'<a href="exec:killvar ''ppnum'' & dynamic $displaynum">Clear participant limitations</a>'
		}
		dynamic $displaynum
	end	
	
	act 'Porn production schedule':
		$contracting = {
			*clr
			'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/studia_0.jpg"></center>'
			''
			d1 = day
			w1 = week
			m1 = month
			counter = 1
			$pornofday = ''
			:pornopt
				killvar 'nh1'
				pcount[counter] = counter
				w1 += 1
				if w1 > 7:w1 = 1
				d1 += 1
				if monthsend[m1] < d1: m1 += 1 & d1 = 1
				if m1 > 12: m1 = 1
				
				if m1 = 1 and d1 >= 1 and d1 <= 8:nh1 = 1
				if m1 = 2 and d1 = 23:nh1 = 1
				if m1 = 3 and d1 = 8:nh1 = 1
				if m1 = 5 and (d1 = 1 or d1 = 2):nh1 = 1
				if m1 = 5 and d1 = 9:nh1 = 1
				if m1 = 6 and (d1 = 12 or d1 = 13):nh1 = 1
				if m1 = 11 and d1 = 4:nh1 = 1
				
				if (weekdayfilter = 1 and w1 >= 6) or (weekendfilter = 1 and w1 < 6) or (weekdayfilter = 0 and weekendfilter = 0) or nh1 = 1:
					if datefilter['<<selected[counter]>>'] = 0:
						if nh1 = 1:
							$pornofday = '<i><b>National holiday, no shootings!</b></i><br>'
						else
							porns = 0
							:pornplanloop
								dynamic $pornfilmdesc, pornplan['<<counter>>,<<porns>>']
								if (pfactor <= ppnum) or (ppnum = 0):
									if porntaken['<<counter>>,<<porns>>'] = 2:
										$pornofday += '<font color="green"><b><<$porndesc[pornplan[''<<counter>>,<<porns>>'']]>></b> - <i>Already contracted</i></font><br>'
									elseif pfilmday[counter] > 0 and porntaken['<<counter>>,<<porns>>'] = 0:
										$pornofday += '<font color="grey"><<$porndesc[pornplan[''<<counter>>,<<porns>>'']]>></font><br>'
									elseif porntaken['<<counter>>,<<porns>>'] = 1:
										$pornofday += '<font color="red"><<$porndesc[pornplan[''<<counter>>,<<porns>>'']]>> - <i>Taken by someone else</i></font><br>'
									elseif pornstack < 10:
										$pornofday += '<a href="exec:pornstack += 1 & porntaken[''<<counter>>,<<porns>>''] = 2 & pfilmday[<<pcount[counter]>>] = pornplan[''<<counter>>,<<porns>>''] & gs ''stat'' & dynamic $contracting"><<$porndesc[pornplan[''<<counter>>,<<porns>>'']]>></a><br>'
									else
										$pornofday += '<font color="grey"><<$porndesc[pornplan[''<<counter>>,<<porns>>'']]>></font><br>'
									end
								end
							if porns < 3: porns += 1 & jump 'pornplanloop'
						end
						if $pornofday ! '':
							'<<$monthName[m1]>> <<d1>>, <<$week[w1]>>:'
							'<<$pornofday>>'
							$pornofday = ''
						end
					end
				end
			if counter < 49:
				counter += 1
				jump 'pornopt'
			end
		}
		dynamic $contracting
	end	
	
	if pornstack > 1 or (freefilm = 0 and pornstack > 0):
		act 'Cancel out a contract':
			$canceling = {
				*clr
				'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/studia_0.jpg"></center>'
				d1 = day
				w1 = week
				m1 = month
				i = 1
				:nopornopt
					pcount[i] = i
					w1 += 1
					if w1 > 7:w1 = 1
					d1 += 1
					if monthsend[m1] < d1: m1 += 1 & d1 = 1
					if (pfilmday[i] = 1 and freefilm = 0) or pfilmday[i] > 1:'<<$monthName[m1]>> <<d1>>, <<$week[w1]>>:'
					porns = 0
					:nopornplanloop
						if porntaken['<<i>>,<<porns>>'] = 2:'<a href="exec:pornstack -= 1 & porntaken[''<<i>>,<<porns>>''] = 0 & pfilmday[<<pcount[i]>>] = 0 & gs ''stat'' & dynamic $canceling"><<$porndesc[pornplan[''<<i>>,<<porns>>'']]>></a>'
					if porns < 3: porns += 1 & jump 'nopornplanloop'					
				if i < 49:
					i += 1
					jump 'nopornopt'
				end
			}
			dynamic $canceling
		end
	end
	
	act 'Leave':
		killvar 'ppnum'
		killvar 'displaynum'
		killvar 'displaytext'
		killvar 'displayimage'
		killvar '$pp1'
		killvar '$pp2'
		killvar '$pp3'
		killvar '$pp4'
		killvar '$pp5'
		killvar 'displaytag'
		killvar 'flag_tag'
		killvar 'selection'
		killvar 'datefilter'
		killvar 'selected'
		killvar 'option'
		killvar 'tagfilter'
		killvar 'contracting'
		killvar 'pcount'
		killvar 'weekdayfilter'
		killvar 'weekendfilter'
		gt 'pornstudio', 'start'
	end
end

if $ARGS[0] = 'first':
	cls
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/studia_0.jpg"></center>'
	'You sit down on the sofa and greet the boss. "I see it''s your first time with us. Do you want to be a porn star?'

	if pcs_exhibition < 40:
		'You do not feel confident enough to have start a porn career, you should get more used to showing off your body first.'
		act 'Leave':gt 'pornstudio', 'start'
	else
		act 'Yes':
			cla
			minut += 5
			gs 'stat'
			'You confirm your desire to act in a porn movie. The boss nods, "All right. First we''ll need to do a casting."'
			'"Take off your clothes and let me see your body."'

			act 'Make excuses and leave':gt 'pornstudio', 'start'
			'You confirm your desire to act in a porn movie. The boss nods, "Let''s see your papers, are you 18 or older?"'
			if age >= 18:
				'"Yes I am." You answer, handing him your passport.'
				'"All right. First we''ll need to do a casting. Take off your clothes and let me see your body."'
			elseif fakepassport = 1:
				'"Yes I am." You answer, handing him your fake passport.'
				'You see him frown as he checks your passport with suspicion.'
				'"Well, I guess everything is in order. First we''ll need to do a casting. Take off your clothes and let me see your body."'
			else
				'"Nearly..." You plead.'
				'"Right. Then here''s my card, be sure to come back when you are old enough.'
				fakepassport = 2
				exit
			end

			act 'Undress':
				cla
				minut += 5
				gs 'stat'
				'You undress in front of the boss, he gets up from his chair and walks towards you. "Lie down on the couch and spread your legs."'

				act 'Grab your clothes and run out':gt 'pornstudio', 'start'

				act 'Lie down on the sofa and spread your legs':
					cla
					minut += 5
					gs 'stat'
					'You lie down on the sofa and spread your legs. The boss takes a few pictures on his phone, explaining that it is for the portfolio. After that, the boss asks you to kneel.'

					act 'Kneel':
						cla
						minut += 5
						gs 'stat'
						'You get up from the sofa and kneel in front of the boss. He takes a few more pictures. "Now open your mouth and stick out your tongue as if I was about to finish on your face."'

						act 'Open your mouth and stick out your tongue':
							cla
							minut += 5
							gs 'stat'
							'You have open your mouth and stuck out your tongue. The boss says, "Not bad, now suck my thumb."'

							act 'Suck his thumb':
								cla
								minut += 5
								gs 'stat'
								'You start to suck his thumb trying to look as sexy as possible. Meanwhile he touchs your breast with his other hand while you are busy with his finger.'
								'He pulls his finger out of your mouth and curtly tells you, "get dressed". He sits down at his desk. You quickly got dressed and sat down.'
								'"You can call me Peter. You are going to need a pornstar name.'

								act 'Choose your pornstar name':
									cla
									firstkasting = 1
									$pfname = input ("What will be your pornstar name?")

									if $pfname <= 0:$pfname = '<<$pcs_nickname>> BimboStar'

									'"<<$pfname>>, wonderful name. Now listen, before shooting starts you have to pass the medical examination. If we find any sign of a sexually transmitted disease, you are out, no exceptions.'
									'Now you are registered. We are quite big in the industry, shooting about four movies every day so you can choose what movies are you willing to be in. Remember to take care of your health, but if you got sick or somehing, just cancel the shoot. You can do that up to the day of the shooting.'
									'Come see me when you want a contract."'

									act '.....':gt 'pornstudio', 'start'
								end
							end
						end
					end
				end
			end
		end
	end
end

if $ARGS[0] = 'medical':
	cla
	*clr
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/exam.jpg"></center>'
	minut += 5
	gs 'stat'
	'You go into the office where the doctor was waiting in a white robe. He quickly pulls on rubber gloves and tells you to undress.'
	'You removed the clothes and sit on the gynecological chair. The doctor comes up to you and starts the check up. After inspection, he takes a blood sample.'
	'"It will be about half an hour for the tests to be completed. It will tell us if you have caught something."'

	act 'Wait':
		cla
		minut += 30
		gs 'stat'
		'...'
		'...'
		if venera > 0:
			pfilmNO = 1
			pfilmNoVenera = 1
			'Your analysis showed an STD. The doctor tells you and calls Peter. Peter looks at the test results.'
			'"Sorry, but as long as you have an STD you cannot make films."'
			act 'Leave':gt 'pornstudio', 'start'
		else
			'"Very good, you are clean. You can go to the dressing room."'

			act 'Go to the dressing room':
				pf = ARGS[1]
				dynamic $pornfilmdesc, pf
				gs 'pornfilm', 'pornactor'
				cla
				*clr
				'<center><img width=700 src="images/locations/city/citycenter/studio/makeup.jpg"></center>'
				minut += 30
				cumspclnt = 20
				gs 'cum_cleanup'
				pcs_sweat = 0
				pcs_hairbsh = 1
				pcs_makeup = 4
				pcs_lipbalm += 8
				pcs_breath = 1
				gs 'stat'
!!				pornvnesh uses the same calc as pcs_apprnc except the clothing bonus is hard coded (to 900 and then reduced) and the glasses penalty is removed
				pornvnesh = pcs_apprncbase + (pcs_apprncbase * 3 / 5) + mopkoef + (pcs_hairbsh * 10) + lipbalmKoef + (pcs_breath * 5) - sweatKoef - dyevmod - hairkoef - legkoef
				'In the dressing room a skinny whiter-haired woman is waiting for you.'
				'You take a thorough shower, carefully washing yourself, and getting an enema too, then you put on some makeup and get your hair done.'

				act 'Go to the set':gt 'pornfilm', '<<pf>>'
			end
		end
	end
end

if $ARGS[0] = 'postvideo':
	cls
	minut += 30
	pf = pfilmday[0]
	pfilmday[0] = 0
	pornstack -= 1
	pornnow = 0
	if film < 5:
		fmulti = 100
	elseif film < 10:
		fmulti = 120
	elseif film < 15:
		fmulti = 140
	elseif film < 25:
		fmulti = 160
	elseif film < 50:
		fmulti = 180
	elseif film >= 50:
		fmulti = 200
	end

	film += 1
	if film = 1:
		$porntitle[film] = 'Girls Jerk Guys #36 - '
		$pornfilmstory[film] = '<<$pfname>>''s introduction to the world of porn, where she jerk off <<$pakter[1]>> on her tits.'
	else
		if $pstory = '':
			$pornfilmstory[film] = input('You can enter the story of the porn movie. This action can be done only once, but you can choose to do it later! (Just hit Enter/Esc when blank)<br>Tags of the current movie: <font color=#541717 size=2><<$pfilmtags>></font>')
		else
			$pornfilmstory[film] = $pstory
		end
		$porntitle[film] = input("Enter the name of the porn movie. <i>This action can be done only once, but you can choose to do it later! (Just hit Enter/Esc when blank)</i>")
	end
	
	if pfilmNOPAY = 0:
		pfcash = pcash * fmulti
		money += pfcash
		'Peter counts <<pfcash>> <b>₽</b> out for your film and wishes good luck.'
	else
		pfilmNOPAY = 0
		pfcash = 0
		'You have fulfilled your obligation and the made the film for free.'
	end
	pornfilmYear[film] = year
	pornfilmMonth[film] = month
	pornfilmDay[film] = day
	porndaystart[film] = daystart
	pornfilmType[film] = pf
	pornfilmCash[film] = pfcash
	pornfilmActAge[film] = age
	pornfilmAppAge[film] = vidage
	pornfilmpregchem[film] = pregchem
	pornfilmacting[film] = acting+orgasmbonus+pcs_intel/10
	dynamic $pornfilmdesc, pf
	killvar 'orgasmbonus'
	if pornfilmacting[film] >= 90:
		porn_fame += 3
	elseif pornfilmacting[film] >= 75:
		porn_fame += 2
	elseif pornfilmacting[film] >= 60:
		porn_fame += 1
	end
	pornfilmvnesh[film] = pornvnesh
	killvar 'pornvnesh'
	if pornfilmvnesh[film] >= 200:
		porn_fame += 3
	elseif pornfilmvnesh[film] >= 150:
		porn_fame += 2
	elseif pornfilmvnesh[film] >= 90:
		porn_fame += 1
	end
	
	acting += actbonus

	if pfType ! 0:
		unprotfilm = -1
		if succubusflag = 1:
!!			May add a check to clear unprotfilm based on sucpcinfo (the PC knows Succubae are immune to STD''s)
			if pfcash > 0:
				$tmp = '"I almost can''t believe <i>they</i> paid <i>me</i> <<pfcash>><b>₽</b> for a wonderful meal!"'
			else
				$tmp = '"And they think I''m doing this for free, when really they''re paying in other coin!"'
			end
			'You let slip a satisfied smirk and think to yourself, <<$tmp>>'
		elseif knowpreg = 0 and thinkpreg = 0 and pillcon2 < 1000:
			pornDanger[film] = 1
!!			Left "ppreg" pending its eventual use
			if ppreg = 0:
				if pfcash > 0:
					$tmp = ' Was <<pfcash>> <b>₽</b> worth the risk of getting pregnant by porn actor you''ll probably never see again?'
				else
					$tmp = ' You didn''t even get paid to risk pregnancy by a porn actor you''ll never see again.'
				end
				'With a growing sense of horror you realize that you aren''t on the shot and have no idea when you last took a birth control pill.<<$tmp>>'
			else
				!{If she has birth control and deliberately isn''t using it, obviously she wants to get pregnant or just doesn''t care.}
				if pfcash > 0:
					$tmp = ' Either way, <<pfcash>> <b>₽</b> will definitely help make a better life for you and your baby if you end up pregnant.'
				else
					$tmp = ' The fact that you weren''t even paid to take this risk excites you more than you''d like to admit.'
				end
				'From the moment you stepped onto the set you kept thinking about how you were completely unprotected during this shoot. You aren''t sure if you''re afraid of the consequences- or excited by them.<<$tmp>>'
				pcs_horny += 10
			end
		end
		killvar '$tmp'
	end
	
	if pfactor = 50:
		$pornfilmActor[film] = 'Fifty fans of'
	else
		dynamic $pornactor
		$pornfilmActor[film] = ''
		pact = pfactor
		:pornactortext
			if pact > 1:
				$pornfilmActor[film] += $pakter[pact]+', '
			else
				$pornfilmActor[film] += $pakter[pact]
			end
			pact -= 1
		if pact > 0:jump 'pornactortext'
	end

	if film < 10:
		filmkoef = 10
	elseif film < 25:
		filmkoef = 5
	else
		filmkoef = 0
	end
	
	pfcash = 0
	gs 'stat'

	act 'Leave':gt 'pornstudio', 'start'
end

if $ARGS[0] = '1':
	$metka = $ARGS[0]
	$loc = $CURLOC

	minut = minut + 5
	clr
	gs'stat'

	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/pierre_1.jpg"></center>'
	'You''ve come to the studio, eyes looking for someone-anything, Who can come up with a question.'
	'To you I approached a young man, introduced by Peter, you are greeted and met.'

	act 'To state the purpose of your visit':
		cla
		*clr
		minut = minut+60

		'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/pierre_2.jpg"></center>'
		'Peter smiled and said, that the new actresses are always welcome, just terrible, of course, business do not take. On the first couple have held auditions, If you like public - they can not be held.'
		'I''ll tell you, here and how, but under one condition - you have to undress.'

		act 'Undress':
			cla
			*clr
			'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/casting_3.jpg"></center>'
			'First held casting actresses - as I have said, if you are terrible - You can not hope, comes only when you have a beautiful appearance.'

			act '...':
				cla
				*clr
				'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/model_0.jpg"></center>'
				'Then the girl chosen for the lead role.'
				'Here you have to be the most beautiful.'

				act '...':
					cla
					*clr
					'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/film_0.jpg"></center>'
					'Well, then she shot - interesting and exciting processes'
					'And the output we obtain...'

					act '...':
						cla
						*clr
						'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/dvd-disx.jpg"></center>'
						'So on what I make money.'

						act 'All clear, and how and where it is all sold? Can, I want to see for yourself...':
							cla
							*clr
							'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/pierre_2.jpg"></center>'
							'The sauna is in a residential area we have one man.. And in a sex shop, How could you understand, there garters. Now that''s thinking of selling the university. If we would have with the people who helped - b good profits increased, entitlements to dividends assistant, of course.'

							act 'And what-problems with the law?':
								cla
								*clr
								'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/ment_0.jpg"></center>'
								'Specifically for you - unavailable. We''ve got their contracts with the cops, maybe in the future you will be able to help us to keep them in force, In the meantime - I told you everything, weekend tasted.'

								act 'Well, sort of understood everything':
									cla
									*clr
									pornstudio = 3

									'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/pierre_2.jpg"></center>'
									'Good for, girl, walk and not take away from me more time'

									gt'pornstudio','start'
								end
							end
						end
					end
				end
			end
		end
	end
end

if $ARGS[0] = '2':
	$metka = $ARGS[0]
	$loc = $CURLOC

	clr
	! WD: Fix Uninitialised ~ 'carrf' to 'carrar'
	carrar = RAND(40, 100)
	gs 'stat'

	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/studio/casting_<<rand(1,6)>>.jpg"></center>'

	'You spent eight hours possing in a variety of sexy underwears, for the casting director.'

	minut = minut+120
	gs 'stat'
	minut = minut+120
	gs 'stat'
	minut = minut+120
	gs 'stat'
	minut = minut+120
	gs 'stat'

	if pcs_apprnc < carrar:
		'You don''t have what it takes, sorry'
	elseif pcs_apprnc >= carrar:
		actr = 1

		'You get the Role'
	end

	act 'Finish':
		gt 'pornstudio','start'
	end
end

if $ARGS[0] = '3':
	$metka = $ARGS[0]
	$loc = $CURLOC

	clr
	actr = 0
	gs 'stat'
	gt 'bdsm'
end
if $ARGS[0] = 'pregreact':
	'"Wow, since you are so visibly pregnant. I''m surprised you still want to do the shoot. The good news is, we''ve been looking into appealing to people with different fetishes, so this is the perfect opportunity to start marketing to pregnancy fetishists. But, this that is also going to change the shoots you can do. You see, this is more of a niche thing and not something we can really use for wide appeal. You can still work, but only on specific pregnancy shoots, so you might not get the same amount of work you got before. But don''t worry, if we can find a way to make money off of you, we definitely will," the manager ends with a smile.'
	act 'Continue':pregtalkpornstudio = 1 & gt 'pornstudio', 'manager'
end

--- pornstudio ---------------------------------

