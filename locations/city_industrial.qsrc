# city_industrial
!!2021/04/23
if $ARGS[0] = '':
	$loc_arg = ''
	$loc = 'city_industrial'
	$region = 'city_industrial'
	$location_type = 'public_outdoors'
	$menu_loc = 'city_industrial'
	$menu_arg = ''
	menu_off = 0
	CLOSE ALL
	if sound = 0:
		if month >=11 and month <= 12 or month >=1 and month <=3:
			if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street7.mp3',30 & PLAY 'sound/zima.mp3',30
			if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
		else
			if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street7.mp3',30 & PLAY 'sound/street_leto.mp3',30
			if hour >= 0 and hour <= 7: PLAY 'sound/street_night_leto.mp3' & PLAY 'sound/street_night.mp3',30
		end
	end

	gs 'stat'

	!call random events from [street_events], and display them before show location.
	if streetevent_hour + 2 < hour: gt 'street_event','city_industrial', 'main'

	'<center><h2>St. Petersburg</h2></center>'
	'<center><b>Industrial Region</b></center>'
	if month >= 11 or month <= 3:
		if daystage = 2 or daystage = 3:
			'<center><img <<$set_imgh>> src="images/locations/city/industrial/industw.jpg"></center>'
		else
			'<center><img <<$set_imgh>> src="images/locations/city/industrial/industwn.jpg"></center>'
		end
	else
		if daystage = 2 or daystage = 3:
			'<center><img <<$set_imgh>> src="images/locations/city/industrial/nord.jpg"></center>'
		else
			'<center><img <<$set_imgh>> src="images/locations/city/industrial/nord2.jpg"></center>'
		end
	end

	gs'street_event','city_industrial', 'extras'

	act 'Walk to the main road to Pavlovsk (0:15)': minut += 15 & nroad = 0 & gt'road'
	act 'Visit the Red Light District (0:05)': minut += 5 & gt 'city_redlight', 'start'

	act 'Wait':gs 'obj_din', 'wait'

	gs 'taxi'

	if car > 0:
		if wrek = 0 and ((cardrive = 7 and func('autoservisF', 'is_open') = 0) or (cardrive = 9 and func('autotraidF', 'is_open') = 0)):
			'It looks like the ' + iif(cardrive = 7, 'repair shop', 'car dealership') + ' moved your car outside before they closed.'
			cardrive = 3
		end
		if cardrive = 3 or cardrive = 6 or (cardrive = 7 and func('autoservisF', 'is_open')) or (cardrive = 9 and func('autotraidF', 'is_open')) or cardrive = 27:
			'In the parking stands your <a href="exec:' + iif(cardrive = 3, '', 'minut += 5 & ') + 'gs ''carF'', ''start''"><<$car>></a>.'
		end
	end

	'The town''s <a href="exec:minut += 60 & gt ''city_park'', ''start''">central park</a> is a one hour walk from here.'

	if hour > 7 and hour < 23:
		'You can see the <a href="exec:$loc = ''city_industrial'' & minut += 1 & gt ''kiosk'', ''start''">kiosk</a> selling cigarettes and magazines.'
	else
		'The kiosk is closed between 23:00 and 07:00.'
	end

	'At the center of the industrial region is a <a href="exec:minut += 15 & gt ''city_industrial_train'', ''outside''">train station</a>.'

	if week = 3 or hour < 16:
		'The "Rabotnik" bar is here, but it seems to be closed. A sign by the door tells you that it''s open every day (except Wednesday) between 16:00 and midnight...'
	else
		'The <a href="exec: minut += 5 & gt ''qwBarPolet''">Bar "Rabotnik"</a> seems to be open.'
	end
	![slavaQW - Related to Glory; references to bar "Flight" have to be fixed - requirement to access the bar has been removed for now, since content not related - Variable not used by anything else at the moment]

	litltown = 0
	'There is a <a href="exec: minut += 5 & gt ''zaprF'', ''start''">gas station</a> near the street'

	if hour >= 5 and hour <= 23:
		'The <a href="exec: minut += 5 & gt ''metro'', ''start''">metro station</a> is within a 5 minute walk from here.'
	else
		'The metro station is within a 5 minute walk from here. The metro system is closed between midnight and 05:00.'
	end
	if func('autotraidF', 'is_open'):
		'A <a href="exec: minut += 5 & gt ''autotraidF'', ''start''">car dealership</a> is nearby.'
	else
		'A car dealership is nearby' + iif(week = 1, ', but it''s closed on Mondays.', '. It''s currently closed and will open between 09:00 and 18:00.') + iif(car > 0 and cardrive = 9, ' Your <<$car>> is currently locked inside.', '')
	end
	if func('autoservisF', 'is_open'):
		'Next to the car dealership is a <a href="exec: minut += 5 & gt ''autoservisF'', ''start''">repair shop</a>.'
	else
		'Next to the car dealership is a repair shop.' + iif(week = 7, 'It''s closed on Sundays.', 'It will be open between 09:00 and 20:00.') + iif(car > 0 and cardrive = 7, ' Your <<$car>> is currently locked inside.', '')
	end
	if hour >= 9 and hour < 17 and week < 6:
		'The <a href="exec: minut += 5 & gt ''dk'', ''start''">community centre</a> is currently open.'
	else
		'The community centre is currently closed. It''s only open during the week between 09:00 and 17:00.'
	end

	if hour >= 9 and hour <= 17 and (StoryLine = 1 and SchoolAtestat = 0) or bumtolik > 3: 
		'<a href="exec: gt ''buklinik'',''start''">Mercy clinic</a> is open.'
	elseif (hour < 9 or hour > 17) and (StoryLine = 1 and SchoolAtestat = 0) or bumtolik > 3:
		'Mercy clinic is closed.'
	end
	if hour >= 8 and hour <= 20:
		'The parking lot, which is engulfed in bright lights, belongs to a <a href="exec: minut += 2 & gt ''shop'', ''start''">supermarket</a>.'
	else
		'The empty parking lot, which is engulfed in bright lights, belongs to a supermarket that is open between 08:00 and 20:00'
	end
	if hour >= 12 and hour <= 19:
		'Above the door to a basement room is a bright neon sign for the <a href="exec: minut += 5 & gt ''pirsingsalon'', ''start''">Tattoo & Piercing parlor</a>.'
	else
		'Above the door to a basement room is a bright neon sign for the Tattoo & Piercing parlor. The opening hours are 12:00 to 20:00'
	end
	if tobiQW >= 1:'The old mansion, home to <a href="exec:gt ''kakuzu'', ''start''">Kakuzu</a>'

	'There is a huge building, with bright lights, which houses a <a href="exec: minut += 10 & gt ''casino'', ''outside''">Casino</a>.'

	if hour >= 14 or hour < 4:
		'Located far away from the buildings is a <a href="exec: minut += 2 & gt ''lakecafe'', ''main''">barbecue</a>.'
	else
		'Located far away from the buildings is a barbecue. It''ll be open between 14:00 and 04:00'
	end
	if jouryResult = 10:'You can go to one of the apartment blocks and visit <a href="exec:gt ''youry'',''start''">Yuri''s</a> flat'
	if BurgerQW['IlyQWTatianaHelp'] > 0:'In one of these building is <a href="exec:gt ''ilyroom''">Manager Ilyushkins</a> apartment'

	'A long and high fence, which is build around the <a href="exec:gt ''Terminal''">storage terminal</a>'
end


--- city_industrial ---------------------------------

