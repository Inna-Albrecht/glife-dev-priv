# photography_work

!! 05-2023 - First Draft
!! Conversion from dynamic arrays to sub-functions to reduce save footprint

!! job offer init:

if $ARGS[0] = 'photojob_pay':
	if pcs_photoskl > 125:
		gs 'emp_functions', 'wage_evt', 4
	elseif pcs_photoskl > 100:
		gs 'emp_functions', 'wage_evt', 3
	elseif pcs_photoskl > 75:
		gs 'emp_functions', 'wage_evt', 2
	elseif pcs_photoskl > 50:
		gs 'emp_functions', 'wage_evt', 1
	else
		gs 'emp_functions', 'wage_evt', 5, 100
	end
end

!! Photography jobs functions


if $ARGS[0] = 'job_init':
	!! Set up arrays if not initialised
	if $evt_job[1] = '' :  gs 'emp_functions', 'job_evt'
    !! ensure it matches total jobs on offer
	 evt_PhotoID = rand(1, 5)
	gs 'photography_work', 'job_' + evt_PhotoID
	!! set Day of Week for job to appear on	
	transient_evt_dow = 6
    !! Now set up the event date
    gs 'photography_work' , 'photojob_pay'
    !! Set job duration of 2-4 hours
    transient_evt_duration = rand(2,4)
    gs 'emp_functions', 'gen_evt'
end

!! Event player:

if $ARGS[0] = "evt_stage" or $ARGS[0] = '':
    !! Track how many gigs completed
    photography['experience'] += 1
    karta += evt_wages[act_idx]
	gs 'stat'
   !! runs all events:
    gt 'photography_work', 'stage_' + evt_event_sub[act_idx]

end

!! These are the event stages to play the events, should be equal to the amount of jobs

if $ARGS[0] = 'stage_1':
    *clr & cla
	minut += 20
	gs 'stat'
	'<center><img src="images/characters/shared/headshots_generic/37.jpg"></center>'
	'You head over to the industrial region where you quickly meet up with your contact at the Experience Center.'
	'"Hi there; you must be <<$pcs_firstname>>. The group is already on their way to the site, so I''ll take you up there in my car. I''m sure you know everything you need to do, but feel free to ask if you have any questions."'
	'You get in the car and drive for nearly half an hour until you reach the Experience Center. Once there, the representative parks the car and you get out and head inside.'
	act 'Go to the Experience Center':
		*clr & cla
		minut += rand(120,150)
		gs 'exp_gain', 'photoskl', rand(6,10)
		gs 'stat'
		gs 'shop_photography', 'photojob_pay'
		'<center><img src="images/pc/activities/photography/job/tank.jpg"></center>'
		'You arrive at the Experience Center and spend the next two hours taking as many pictures of the experience as you can. The group is clearly enjoying the event; They get to ride in military vehicles across rough terrain and fire weapons at the shooting range.'
		'Once the day is over, you remove your SD card and hand it to the representative, who shows the photos to the group. Once they''ve sorted out whether any of the customers want additional copies, your contact takes you back to the city''s industrial region. When you check your account, you see that you have been paid <<evt_wages[act_idx]>>.'
		!! gs 'shop_photography', 'photojob_cleanup'
		act 'Return': gt $loc, $loc_arg
	end
end

if $ARGS[0] = 'stage_2':
    *clr & cla
	minut += rand(60,90)
	gs 'exp_gain', 'photoskl', rand(3,5)
	gs 'stat'
	gs 'shop_photography', 'photojob_pay'
	'<center><img src="images/pc/activities/photography/job/model<<rand(1,3)>>.jpg"></center>'
	'You go to the Petrovskaya Akvatoria Model Museum to take pictures of the various exhibits on behalf of an international newspaper.'
	'After a brief introduction to a member of staff to confirm your activities, you walk around the exhibit, taking a large number of photographs so that the article has a wealth to choose from.'
	'Once you''re finished, you return to the shop and hand the SD card to Branko, who gives you your fee of <<evt_wages[act_idx]>>₽.'
	!! gs 'shop_photography', 'photojob_cleanup'
	act 'Return' : gt $loc, $loc_arg
end

if $ARGS[0] = 'stage_3':
    *clr & cla
	minut += rand(10,18)
	gs 'stat'
	'<center><img src="images/characters/shared/headshots_generic/18.jpg"></center>'
	'You head over to the residential district where you meet one of the family members of the betrothed.'
	'"Hello, you must be the photographer! If you follow me, I''ll take you to the happy couple to take the pictures of them."'
	act 'Follow':
		*clr & cla
		minut += rand(90,120)
		gs 'exp_gain', 'photoskl', rand(6,10)
		gs 'stat'
		gs 'shop_photography', 'photojob_pay'
		'<center><img src="images/pc/activities/photography/job/wedding.jpg"></center>'
		'You meet the bride and groom and help direct them into a number of framed situations for you to take some beautiful photos of them together.'
		'Once you have enough, you spend the rest of the day in the background taking a number of photos of the wedding. Once it''s over, you meet back up with the relative who takes the SD card from you.'
		'Leaving them to close up the venue, you walk out back into the residential district. When you check your account, you see that you have been paid <<evt_wages[act_idx]>>.'
		!! gs 'shop_photography', 'photojob_cleanup'
		act 'Return' : gt $loc, $loc_arg
	end
end

if $ARGS[0] = 'stage_4':
    *clr & cla
	minut += rand(15,20)
	gs 'stat'
	'<center><img src="images/characters/shared/headshots_generic/11.jpg"></center>'
	'You head over to the industrial region and meet a representative of the Autodrome. You will be expected to take pictures of the event and deliver them to your contact.'
	'"Why hello there, you must be the person Branko sent. I''m sure you already know everything you have to do at the track, so I''ll give you this press pass and take you to the Autodrome."'
	'You look at the pass they gave you before getting in their car to be taken to the track.'
	act 'Go to the Autodrome':
		*clr & cla
		minut += rand(180,240)
		gs 'exp_gain', 'photoskl', rand(9,15)
		gs 'stat'
		gs 'shop_photography', 'photojob_pay'
		'<center><img src="images/pc/activities/photography/job/motorsport.jpg"></center>'
		'You''re dropped off at the employee entrance and directed to the pit area as the teams get their vehicles prepared.'
		'You start off by getting shots of the teams in the build-up to the race, showcasing the tension and focus as the countdown approaches zero.'
		'When the race itself starts you roam the track, taking pictures of any event as it happens, getting high intensity shots of fast paced action, as well as the smooth professionalism of the leading drivers. It is a fantastic experience.'
		*nl
		'With your job done, you find your contact again and hand them a full SD card for which they are gratified.'
		'Your contact then takes you back to the city''s industrial region. When you check your account, you see that you have been paid <<evt_wages[act_idx]>>.'
		!! gs 'shop_photography', 'photojob_cleanup'
		act 'Return' : gt $loc, $loc_arg
	end
end

if $ARGS[0] = 'stage_5':
    *clr & cla
	minut += 60
	gs 'exp_gain', 'photoskl', rand(3,5)
	gs 'stat'
	gs 'shop_photography', 'photojob_pay'
	'<center><img src="images/pc/activities/photography/job/court.jpg"></center>'
	'"You need to go to the courthouse to take publicity photographs of the completed court session as they leave the building, then deliver them back to me to give you your commission."'
	'You head into the City Center and straight to the courthouse, which has already seen a number of people turn up.'
	'Once you''ve ingratiated yourself into the crowd, you position yourself to take some good shots of the plaintiff accompanied by their lawyers and representatives.'
	'With the verdict read, you quickly make your way outside to the front of the courthouse to take photos of him leaving.'
	'Once you''re finished, you return to the shop and hand the SD card to Branko, who gives you your fee of <<evt_wages[act_idx]>>₽.'
	!! gs 'shop_photography', 'photojob_cleanup'
	act 'Return' : gt $loc, $loc_arg
end

! Array of photography jobs - additional events should be easy to wire up
if $ARGS[0] = 'job_1':
	!!Ensure the event is visible in journal:
	transient_evt_silent = 1
	!! Set the job type as per list
	transient_evt = 1
	transient_evt_sub = 1
	$photojob_shortname = 'Tank'
	$transient_desc = 'Tank and Bazooka Experience Day in Levingrad Oblast'
	!! Field for trigger location
	$transient_loc = 'city_industrial'
	$transient_arg = ''
	!! Journal loc description
	$transient_journal = 'Travel to St Petersburg Industrial Area'

end

if $ARGS[0] = 'job_2':
	transient_evt_silent = 1
	transient_evt = 1
	transient_evt_sub = 2
	$photojob_shortname = 'Model'
	$transient_desc = 'Newspaper Commission at Petrovskaya Akvatoria'
	$transient_loc = 'shop_photography'
	$transient_arg = 'evt_stage'
	$transient_journal = 'Meet Branko at the Photography Shop'
end

if $ARGS[0] = 'job_3':
	transient_evt_silent = 1
	transient_evt = 1
	transient_evt_sub = 3
	$photojob_shortname = 'Wedding'
	$transient_desc = 'Wedding at Yelagin Palace'
	$transient_loc = 'city_residential'
	$transient_arg = ''
	$transient_journal = 'Travel to St Petersburg Residential Area'
end

if $ARGS[0] = 'job_4':
	transient_evt_silent = 1
	transient_evt = 1
	transient_evt_sub = 4
	$photojob_shortname = 'Motorsport'
	$transient_desc = 'Russian Touring Car Championship Event at St. Petersburg Autodrome'
	$transient_loc = 'city_industrial'
	$transient_arg = ''
	$transient_journal = 'Travel to St Petersburg Industrial Area'

end

if $ARGS[0] = 'job_5':
	transient_evt_silent = 1
	transient_evt_ = 1
	transient_evt_sub = 5
	$photojob_shortname = 'Court'
	$transient_desc = 'Press Event at City Courthouse'
	$transient_loc = 'shop_photography'
	$transient_arg = 'start'
	$transient_journal = 'Meet Branko at the Photography Shop'
end

--- photography_work ---------------------------------