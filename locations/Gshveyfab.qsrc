# Gshveyfab
$metka = $ARGS[0]
$loc = $CURLOC
$location_type = 'public_outdoors'

if $ARGS[0] = 'start':
	cla
	*clr
	gs'stat'
	
	'<center><B>TEXTILE FACTORY, "PARIS COMMUNE"</B></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/factory/gshveyfab.jpg"></center>'
	'The local textile factory; one of the few corporate enterprises in Pavlovsk. It makes clothing for G&M Store, which has a branch next door. Open from 8:00-20:00 Monday - Friday.'
	if hour >= 8 and hour <= 20 and week >= 1 and week <= 5:
		'The <a href="exec:minut += 5 & GT ''Gshveyfab'', ''enter''">entrance</a> sits behind a security checkpoint.'
	elseif hour < 8 or hour > 20 or week > 5:
		'The factory is currently closed.' 
		'Hours M-F:'
		'First shift - 8:00 to 16:00.'
		'Second shift - 16:00 to 20:00.'
	end
	if week < 6 and hour >= 8 and hour <= 20:
		*pl 'The <a href="exec:torg = 0 & GT ''budgetclothes'',''start''">G&M Store</a> is currently open.'
	else
		*pl 'The G&M Store is currently closed. Hours are 8:00-20:00 Monday - Friday.'
	end
	
	if hour >= 8 and hour <= 15:
		*p 'A small room near the entrance is being used as a tailor shop.'
		act 'Go to the tailor':
			cla
			*clr
			minut += 5
			gs'stat'
			'An elderly man is inspecting finished garments fresh from the factory floor, he pulls his glasses down a little and looking over the top of them walks up to the desk.'
			
			'"Hello there, come to get some clothes adjusted? It''ll cost you 500 <b>₽</b> per item."'

			if money >= 500:
				act 'Take in the clothes you are wearing (500 <b>₽</b>)':
					cla
					*clr
					minut += 15
					if CloStyle = 5 or $clothingworntype = 'exercise':
						'What are you wasting my time for? This type of clothing does not need to be resized.'
					else
						money -= 500
						dynamic '<<$clothingworntype>>B[<<clothingwornnumber>>] = pcs_hips'
						'You undress and give the clothes you need adjusting to the tailor, it is fifteen minutes until your clothes are ready. you redress.'
					end

					act 'Leave': minut += 5 & gt 'Gshveyfab', 'start'
				end

				act 'Take in different clothes':gt 'clothing', 'view_clothing_list', 'resize'
				act 'Take in all the clothes':gt 'portnoi2'
			end
			act 'Leave': minut += 5 & gt 'Gshveyfab', 'start'
	
		end
	end

	act 'Go to the residential area': minut += 15 & gt 'pavResidential'  
	act 'Go to the high street': minut += 15 & gt 'pavCommercial'
end

if $ARGS[0] = 'enter':
CLOSE ALL
	cla
	*clr
	gs'stat'
	'<center><B>TEXTILE FACTORY, "PARIS COMMUNE"</B></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/factory/gshveyfab.jpg" ></center>'
	'The local textile factory; one of the few corporate enterprises in Pavlovsk.'
	'There is a work schedule hanging on the wall:'
	'First shift - 8:00 to 16:00.'
	'Second shift - 16:00 to 20:00.'
	''
	if workFabrika = 0 and age >= 16 and age < 18:'You could apply for a <a href="exec:minut += 15 & GT ''Gshveyfab'', ''job_start''">part-time job</a> here.'
	if work = 0 and workFabrika = 0 and age >= 18:'You could apply for a <a href="exec:minut += 15 & GT ''Gshveyfab'', ''job_start''">job</a> here.'
	
	if workFabrika = 1 and week >= 1 and week <= 5 and hour >= 8 and hour < 9 and age >= 18:
		'You can start your shift now.'
		act 'Start shift': gt'Gshveyfab','job'
	end
	if workFabrika = 1 and week >= 1 and week <= 5 and hour >= 16 and hour < 17 and age >= 16 and age < 18:
		'You can start your shift now.'
		act 'Start shift': gt'Gshveyfab','job'
	end
	if workFabrika = 1:
		'You can quit your job, but early processing of your payment will cost you the wage of one shift.'
		act 'Quit job': 
			if age >= 16 and age < 18:
				smenaFabrika -= 1
			elseif age >= 18:
				smenaFabrika -= 2
			end
			if smenaFabrika < 0:
				smenaFabrika = 0
			end
			pay = smenaFabrika * 420
			karta += pay
			smenaFabrika = 0
			workFabrika = 0 
			work = 0
			minut += 5
			gt 'Gshveyfab','enter'
		end
	end
	act 'Leave': minut += 5 & gt 'Gshveyfab', 'start'
end

if $ARGS[0] = 'job_start':
	cla
	*clr
	gs'stat'
	'<center><B>FOREMAN''S OFFICE</B></center>'
	'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/stepan/nachalnik.jpg" ></center>'
	''
	'You go to the factory foreman, Stepan Sergeyevich, and ask for a job.'
	if age >= 16 and age < 18 and workFabrika = 0:
		'"Sure, have a seat," he says. "Now, the work is difficult and meticulous, but doesn''t require any special knowledge. You should catch on pretty quick. Since you are not an adult yet, you can only work half-time, and the pay is 105 <b>₽</b> per hour. Once you become an adult, we can transfer you to full-time. Naturally, as this is a state-recognized company, we do require all the appropriate documentation. The main shift is on weekdays from 8:00 to 16:00, but you will be working the second shift, from 16:00 to 20:00. And if you miss more than three days of work in a single month, you will be fired. You will be paid at the end of the month and getting fired before then will forfeit any pay not yet received. So, is that okay?"'
			act 'Take the job':
				cla
				minut += 10
				work = 1
				workFabrika = 1
				smenaFabrika = 0
				FabrikaProgul = 0

				'You give him your documents, which he takes and then reminds you to come in on every weekday, at 16:00.'

				act 'Leave':gt'Gshveyfab', 'enter'
			end	
	elseif age >= 18 and workFabrika = 0:
		'"Sure, have a seat," he says. "Now, the work is difficult and meticulous, but doesn''t require any special knowledge. You should catch on pretty quick. Naturally, as this is a state-recognized company, we do require all the appropriate documentation. The main shift is on weekdays from 8:00 to 16:00, and the pay is 105 <b>₽</b> per hour. And if you miss more than three days of work in a single month, you will be fired. You will be paid at the end of the month, and getting fired before then will forfeit any pay not yet received. So, is that okay?"'
			act 'Take the job':
				cla
				minut += 10
				work = 1
				workFabrika = 1
				smenaFabrika = 0
				FabrikaProgul = 0

				'You give him your documents, which he takes and then reminds you to come in on every weekday, at 8:00.'

				act 'Leave':gt'Gshveyfab', 'enter'
			end  
	end
	act 'Leave':gt'Gshveyfab', 'enter'
end

if $ARGS[0] = 'job':
	cla
	*clr
	FabrikaProgul -= 1
	if FabrikaProgul < 0: FabrikaProgul = 0
	if age >= 16 and age < 18:
		minut += 240
		smenaFabrika += 1		
	end 
	if age >= 18: 
		minut += 480
		smenaFabrika += 2
	end 
	gs'stat'
	'<center><B>TEXTILE FACTORY, "PARIS COMMUNE"</B></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/factory/gshveyfab.jpg" ></center>'
	''
	'You work through your shift at the factory.'

	act 'Leave': minut += 5 & gt 'Gshveyfab', 'start'
end

--- Gshveyfab ---------------------------------

