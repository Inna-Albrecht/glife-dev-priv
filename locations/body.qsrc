# body

!! I have gone through and added a new category of string variables, standardised to be used for descriptive writing. You will notice some duplication here -- this is for purposes of backwards compatibility, as if I completely rename and discard some of these strings,
!! extant saved games will irreparably break, and this would be Bad. One day down the line, most of the duplicated variables here can be culled, when there is a new release that warrants new saves being used, but until then the duplication needs to stay. Use of
!! strings for descriptions that do not begin with $pcdesc_ should be avoided, as it would be best to have everything standardised when moving forward into the present and future.
!! Available strings follow this format: $pcdesc_type, $pcdesc_typewordy, and $pcdesc_typerandom. The last will give a new string each time, to allow for repeatedly referencing something accurately without it becoming repetitive.  Not EVERYTHING has a wordy 
!! or even random variation, but these should work regardless -- they just may use a non-wordy, or non-random version, if it is something that does not really warrant such. 
!! There is also a special string, $pcdesc_typeinsertive, that follows the format " , descriptive text" as in, " , invitingly-glossed". Please check the master string document to see if this exists for the string you need. - xerya, July 2017

$this = 'body'

if $ARGS[0] = '':
	gs 'body_shape', 'RegularUpdate'
	gs 'body_desc'

	!! Skin1 is only used in the foto location, and can be replaced at a later date. From here on out, do not use it.
	if pcs_skin > 100:
		pcs_skin = 100
	elseif pcs_skin < 0:
		pcs_skin = 0
	end

	mopkoef = pcs_makupskl / 5
	if pcs_makeup = 0: mopkoef = -5
	if pcs_makeup = 1: mopkoef = 0
	if pcs_makeup = 5: mopkoef = 30
	if pcs_makeup = 6 and bimbolevel >= 2: extra_supnatvnesh = 2*bimbolevel - 2

	if dounspell = 1:
		bodytipe = pcs_hips - pcs_waist
		if bodytipe < 20:
			bodykoef = 0
		elseif bodytipe >= 20 and bodytipe < 25:
			bodykoef = 2
		elseif bodytipe >= 25 and bodytipe < 30:
			bodykoef = 4
		elseif bodytipe >= 30 and bodytipe < 35:
			bodykoef = 8
		elseif bodytipe >= 35:
			bodykoef = 4
		end
	end

	!!This is to force a base update if teeth changed since that is immediate
	if zublast ! pcs_teeth:
		gs 'body_shape', 'basebeautycalc', 1
		zublast = pcs_teeth
	end

	!! Calculate hotcat etc
	clothingBonus = FUNC('body', 'CalcClothesBonus', PXCloThinness, PXCloTopCut, PXCloBottomShortness, PCloQuality, $clothingworntype, pcs_bmi)
	accessoriesBonus = FUNC('body', 'CalcAccessoriesBonus', bonusZ, PShoQuality, $pantyworntype, $braworntype)
	groomingBonus = FUNC('body', 'CalcGroomingBonus', pcs_lipbalm, mopkoef, pcs_hairbsh, pcs_breath, deodorant_on, pcs_bmi)
	groomingPenalty = FUNC('body', 'CalcGroomingPenalty', pcs_hairlng, pcs_sweat, glassvnesh, dyevmod, legkoef)
	Hotcat = FUNC('body', 'calcHotcat', pcs_apprncbase, (supnatvnesh + extra_supnatvnesh), clothingBonus, accessoriesBonus, groomingBonus, groomingPenalty)
	pcs_apprnc = temppcs_apprnc & killvar 'temppcs_apprnc' & killvar 'temppcs_maxapprnc'


	if StrongNarkota <= 0:
		if SNarkTimes >= 3 and SLomka = 0:SLomka = 1
		if Gerpes >= 3 or Sifilis >= 21 or Triper > 2:pcs_apprnc = -10
	end

	if amphCount > 3 and amphHigh <= 0:
		addictChance = rand(1,10)
		if addictChance > 8:
			amphWithdrawl = 2
		end
	end

	if StrongNarkota > 0:
		if SLomka > 0:SLomka = 0
		pcs_mood = 100
	end

	if pcs_apprnc = -10:
		$pcs_apprnc = 'Your looks are even worse than terrible. It''s god-awful. No one wants to see or talk to you and people avoid you at any cost.'
	elseif pcs_apprnc < 20:
		$pcs_apprnc = 'Your looks are terrible. People try to avoid looking and talking to you.'
	elseif pcs_apprnc < 60:
		$pcs_apprnc = 'You''re a wallflower. Hardly anyone notices you.'
	elseif pcs_apprnc < 120:
		$pcs_apprnc = 'You have good looks. Men and women check you out when they think you aren''t looking.'
	elseif pcs_apprnc < 160:
		$pcs_apprnc = 'You''re unquestionably gorgeous. Men constantly get caught staring at you by their girlfriends.'
	elseif pcs_apprnc < 200:
		$pcs_apprnc = 'You have a simply stunning appearance. Other girls are a bit jealous and guys constantly check you out.'
	else
		$pcs_apprnc = 'Your appearance is divine! No one is able to take his or her eyes off of you.'
	end

	!!Since "musle" is used all over the place
	musle = strenbuf


	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	!! kill temporary variables
	killvar 'tempclothingbonus'
	killvar 'tempAccessoriesBonus'
	killvar 'tempGroomingBonus'
	killvar 'tempGroomingPenalty'
end

if $ARGS[0] = 'SetBodyKoef':
	!!Setting the pcs_apprnc bonus based on fat and strength

	if pcs_bmi < 16:
		!! severely underweight
		tempbodykoef = 30
    elseif pcs_bmi < 19:
		!! underweight
		tempbodykoef = 65
    elseif pcs_bmi < 25:
		!! healthy weight
		tempbodykoef = 85
    elseif pcs_bmi < 30:
		!! overweight
		tempbodykoef = 75
    elseif pcs_bmi < 35:
		!! moderately obese
		tempbodykoef = 45
    elseif pcs_bmi < 40:
		!! severely obese
		tempbodykoef = 25
    elseif pcs_bmi < 45:
		!! very severely obese
		tempbodykoef = 5
    else
		!! morbidly obese
		tempbodykoef = 0
    end

	if strenbuf >= 80:
		tempbodykoef += 8
	elseif strenbuf >= 60:
		tempbodykoef += 10
	elseif strenbuf >= 40:
		tempbodykoef += 8
	elseif strenbuf >= 20:
		tempbodykoef += 5
	else
		tempbodykoef += 2
	end

	!!This modifies bodykoef for high or low salo values
	if salocatnow = 0 or salocatnow >= 7:
		tempbodykoef -= 8
	elseif salocatnow = 1 or salocatnow = 6:
		tempbodykoef -= 4
	end

	if vofat > 0: tempbodykoef -= vofat

	result = tempbodykoef

	killvar 'tempbodykoef'
end

if $args[0] = 'CalcBaseBeauty':
	if pcs_lashes <= 0: pcs_lashes = 0
	pcs_HeadBonus = FUNC('body_shape', 'calcHeadBonus')
	tempAttributeBonus = FUNC('body_shape', 'calcAttributeBonus')
	pcs_apprncbase = FUNC('body_shape', 'CalcBaseBeauty2', ARGS[1])

	if ARGS[1] = 0:
	!!	This is the rate of change limiter
		if pcs_apprncbase > vneshtemp:
			if pcs_apprncbase - 5 > vneshtemp:
				pcs_apprncbase -= 5
			elseif pcs_apprncbase - 3 > vneshtemp:
				pcs_apprncbase -= 3
			else
				pcs_apprncbase -= 1
			end
		elseif pcs_apprncbase < vneshtemp:
			if pcs_apprncbase + 5 < vneshtemp:
				pcs_apprncbase += 5
			elseif pcs_apprncbase + 3 < vneshtemp:
				pcs_apprncbase += 3
			else
				pcs_apprncbase += 1
			end
		end
		killvar 'vneshtemp'
	end

	killvar 'pcs_HeadBonus'
	killvar 'tempAttributeBonus'
end 

if $ARGS[0] = 'CalcHeadBonus':

	!!	Setting Eyelashes bonus
	if pcs_lashes <= 0:
		resnicbonus = 0
	elseif pcs_lashes = 1:
		resnicbonus = 2
	elseif pcs_lashes = 2:
		resnicbonus = 5
	elseif pcs_lashes = 3:
		resnicbonus = 7
	elseif pcs_lashes = 4:
		resnicbonus = 8
	elseif pcs_lashes = 5:
		resnicbonus = 10
	else
		resnicbonus = 0		
	end
	resnicbonus = FUNC('body_shape', 'bmiadjust', resnicbonus, pcs_bmi)

	!!	Setting Eye Size bonus
	if pcs_eyesize = 1:
		glarazbonus = 1
	elseif pcs_eyesize = 2:
		glarazbonus = 3
	elseif pcs_eyesize = 3:
		glarazbonus = 2
	else
		glarazbonus = 0
	end
	glarazbonus = FUNC('body_shape', 'bmiadjust', glarazbonus, pcs_bmi)

	!!	Setting the Lip size bonus
	if pcs_lip = 0:
		lipbonus = -2
	elseif pcs_lip = 1:
		lipbonus = 0
	elseif pcs_lip = 2:
		lipbonus = 3
	elseif pcs_lip = 3:
		lipbonus = 5
	else
		lipbonus = 3
	end
	lipbonus = FUNC('body_shape', 'bmiadjust', lipbonus, pcs_bmi)
	
	teethcalc = (5 * pcs_teeth)
	if teethcalc < 0: teethcalc = FUNC('body_shape', 'bmiadjust', teethcalc, pcs_bmi)

	result = resnicbonus + glarazbonus + lipbonus - teethcalc

end

if $ARGS[0] = 'CalcAttributeBonus':
	tempAttributeBonus = (pcs_agil / 10) + (pcs_vital / 10)

	result = FUNC('body_shape', 'bmiadjust', tempAttributeBonus, pcs_bmi)
	killvar 'tempAttributeBonus'
end

	!!This is the calculation for the base appearance if calling for testing always set ARGS[1] to 1
if $args[0] = 'CalcBaseBeauty2':
	!!ARGS[1] reset pcs_apprncbase
	!! result of pcs_HeadBonus
	!! result of calcAttributeBonus


	!!	Setting vidagebonus
	vidagebonus = 100
	if vidage < 20: vidagebonus = 60 + (2 * vidage)
	if vidage > 30: vidagebonus = 160 - (2 * vidage)

	!!	Preforming the calculation
	skincalc = (pcs_skin * 2 / 5)
	vneshtemp = vidagebonus * (skincalc + bodykoef + tempAttributeBonus + pcs_HeadBonus) / 100

	!!	pcs_skin = skin condition, corrected to a 0 to 40 range; bodykoef set in main body_shape; pcs_teeth is missing teeth or cosmetically improved teeth (-1)

	!!	This is the first limiter
		!!if vneshtemp > 100: vneshtemp = 100

	!!	This adds the supernatural pcs_apprnc bonus; currently Bimbo and Succubus
	vneshtemp += supnatvnesh

	!!	This is for use when a reset is called for by setting args[1] = 1
	if args[1] = 1: pcs_apprncbase = vneshtemp & killvar 'vneshtemp'

	result = pcs_apprncbase

	!!	This is to clean up variables only used here
		!!do not kill vneshtemp here it gets killed in basebeautycalc
	killvar 'skincalc'
	killvar 'vidagebonus'
	killvar 'tempHeadBonus'
	killvar 'tempAttributeBonus'

	!!	This is to unload old variables
	killvar 'CloNaturalBeauty'
	killvar 'CloBaseBeauty'
	killvar 'skin'
	killvar 'bimbobeauty'
end

if $ARGS[0] = 'CalcClothesBonus':
	!! Note there is a limit of 9 args
	tempPXCloThinness = ARGS[1] & !! PXCloThinness
	tempPXCloTopCut = ARGS[2] & !! PXCloTopCut
	tempPXCloBottomShortness = ARGS[3] & !! PXCloBottomShortness
	tempPCloQuality = ARGS[4] & !! PCloQuality
	$tempclothingworntype = $ARGS[5] & !!$clothingworntype
	temppcs_bmi = ARGS[6] & !! pcs_bmi

	if temppcs_bmi < 16:
		!!severely underweight
		if $tempclothingworntype = 'nude':
			tempRevealing = 0
			tempPCloQuality = 1
		else
			tempRevealing = (400 - tempPXCloThinness) + (500 - tempPXCloTopCut) + (400 - tempPXCloBottomShortness)
			tempRevealing = tempRevealing / 2
		end
	elseif temppcs_bmi >= 16 and temppcs_bmi < 19:
		!!underweight
		if $tempclothingworntype = 'nude':
			tempRevealing = 0
			tempPCloQuality = 1
		else
			tempRevealing = (400 - tempPXCloThinness) + (500 - tempPXCloTopCut) + (400 - tempPXCloBottomShortness)
			tempRevealing = tempRevealing / 2
		end
	elseif temppcs_bmi >= 19 and temppcs_bmi < 25:
		!!normal, healthy weight
		if $tempclothingworntype = 'nude':
			tempRevealing = 405
			tempPCloQuality = 3
		else
			tempRevealing = tempPXCloThinness + tempPXCloTopCut + tempPXCloBottomShortness
			tempRevealing = tempRevealing / 2
		end
	elseif temppcs_bmi >= 25 and temppcs_bmi < 30:
		!!overweight
		if $tempclothingworntype = 'nude':
			tempRevealing = 405
			tempPCloQuality = 3
		else
			tempRevealing = tempPXCloThinness + tempPXCloTopCut + tempPXCloBottomShortness
			tempRevealing = tempRevealing / 2
		end
	elseif temppcs_bmi >= 30 and temppcs_bmi < 35:
		!!moderately obese
		if $tempclothingworntype = 'nude':
			tempRevealing = 0
			tempPCloQuality = 1
		else
			tempRevealing = (400 - tempPXCloThinness) + (500 - tempPXCloTopCut) + (400 - tempPXCloBottomShortness)
			tempRevealing = tempRevealing * 3 / 4
		end
	elseif temppcs_bmi >= 35 and temppcs_bmi < 40:
		!!severely obese
		if $tempclothingworntype = 'nude':
			tempRevealing = 0
			tempPCloQuality = 1
		else
			tempRevealing = (400 - tempPXCloThinness) + (500 - tempPXCloTopCut) + (400 - tempPXCloBottomShortness)
			tempRevealing = tempRevealing * 3 / 4
		end
	elseif temppcs_bmi >= 40 and temppcs_bmi < 45:
		!!very severely obese
		if $tempclothingworntype = 'nude':
			tempRevealing = 0
			tempPCloQuality = 1
		else
			tempRevealing = (400 - tempPXCloThinness) + (500 - tempPXCloTopCut) + (400 - tempPXCloBottomShortness)
			tempRevealing = tempRevealing * 3 / 4
		end
	elseif temppcs_bmi >= 45:
		!!morbidly obese
		if $tempclothingworntype = 'nude':
			tempRevealing = 0
			tempPCloQuality = 1
		else
			tempRevealing = (400 - tempPXCloThinness) + (500 - tempPXCloTopCut) + (400 - tempPXCloBottomShortness)
			tempRevealing = tempRevealing * 3 / 4
		end
	end

	tempRevealing = tempRevealing / 76

	tempCloAdjustedBeauty = tempRevealing * tempPCloQuality

	!!if $tempclothingworntype = 'danilovich_outfits': tempCloAdjustedBeauty = tempCloAdjustedBeauty * 2 / 3

	result = tempCloAdjustedBeauty

	killvar 'tempPXCloThinness'
	killvar 'tempPXCloTopCut'
	killvar 'tempPXCloBottomShortness'
	killvar 'tempPCloQuality'
	killvar '$tempclothingworntype'
	killvar 'tempRevealing'
	killvar 'tempCloAdjustedBeauty'
	killvar 'temppcs_bmi'
end

if $ARGS[0] = 'CalcAccessoriesBonus':
	tempcoatbonus = ARGS[1] & !! bonusZ
	tempPShoQuality = ARGS[2] & !! PShoQuality
	$temppantyworntype = $ARGS[3] & !! $pantyworntype
	$tempbraworntype = $ARGS[4] & !! $braworntype
	
	!!bonuses for certain underwear
	if $temppantyworntype = 'boutique':
		temppantybounus = 4
	elseif $temppantyworntype = 'fashionista':
		temppantybounus = 2
	end

	if $tempbraworntype = 'boutique':
		tempbrabounus = 4
	elseif $tempbraworntype = 'fashionista':
		tempbrabounus = 2
	end

	tempAccessoriesBonus = tempcoatbonus + tempPShoQuality + temppantybounus + tempbrabounus

	result = tempAccessoriesBonus
	killvar 'tempcoatbonus'
	killvar 'tempPShoQuality'
	killvar 'temppantybounus'
	killvar 'tempbrabounus'
	killvar '$temppantyworntype'
	killvar '$tempbraworntype'
	killvar 'tempAccessoriesBonus'
end

if $ARGS[0] = 'calcGroomingBonus':
	!! Note there is a limit of 9 args
	temppcs_lipbalm = ARGS[1] & !! pcs_lipbalm
	tempmakeupkoef = ARGS[2] & !! mopkoef 
	temppcs_hairbsh = ARGS[3] & !! pcs_hairbsh
	temppcs_breath = ARGS[4] & !! pcs_breath
	tempdeodorant_on = ARGS[5] & !! deodorant_on
	temppcs_bmi = ARGS[6] & !! pcs_bmi

	if temppcs_lipbalm > 0:
		lipbalmKoef = 5
	else
		lipbalmKoef = 0
	end

	tempHairBonus = temppcs_hairbsh * 10

	tempBreathBonus = temppcs_breath * 5

	tempGroomingBonus = tempmakeupkoef + tempHairBonus + lipbalmKoef + tempBreathBonus

	!!Small bonus for wearing deodorant, if pcs_sweat is low enough
	if tempdeodorant_on = 1 and temppcs_sweat < 20: tempGroomingBonus += 5

	tempGroomingBonus = FUNC('body_shape', 'bmiadjust', tempGroomingBonus, temppcs_bmi)

	result = tempGroomingBonus

	killvar 'temppcs_lipbalm'
	killvar 'tempmakeupkoef'
	killvar 'temppcs_hairbsh'
	killvar 'temppcs_breath'
	killvar 'tempdeodorant_on'
	killvar 'tempHairBonus'
	killvar 'tempBreathBonus'
	killvar 'tempGroomingBonus'

!! commented out as not used in the original calc
!!Removed as primary appearance factors; will be eventually used in an NPC preference system instead; tanKoef = suntan
!!	if temppcs_tan > 0:
!!		tanKoef = 1
!!	else
!!		tanKoef = 0
!!	end

end

if $ARGS[0] = 'CalcGroomingPenalty':
	temppcs_hairlng = ARGS[1] & !! pcs_hairlng
	temppcs_sweat = ARGS[2] & !! pcs_sweat
	tempglassvnesh = ARGS[3] & !! glassvnesh
	tempdyevmod = ARGS[4] & !! dyevmod
	templegkoef = ARGS[5] & !! legkoef

	!! buzzcut penalty to pcs_apprnc
	if temppcs_hairlng < 10:
		hairkoef = 10
	else
		hairkoef = 0
	end

	if temppcs_sweat < 22:
		sweatKoef = 0
	elseif temppcs_sweat < 38:
		sweatKoef = (temppcs_sweat - 10) / 4
	elseif temppcs_sweat < 54:
		sweatKoef = (temppcs_sweat - 10) / 2
	else
		sweatKoef = 3 * (temppcs_sweat - 10) / 4
	end

	tempGroomingPenalty = sweatKoef + tempglassvnesh + tempdyevmod + hairkoef + templegkoef

	result = tempGroomingPenalty

	killvar 'temppcs_hairlng'
	killvar 'temppcs_sweat'
	killvar 'tempglassvnesh'
	killvar 'tempdyevmod'
	killvar 'templegkoef'
	killvar 'tempGroomingPenalty'
end


if $ARGS[0] = 'CalcHotcat':
	!!Appearance factors; hairkoef = very short hair / mopkoef = makeup bonus / legkoef = shaved legs / CloAdjustedBeauty = clothes / pcs_hairbsh = Hair brushed / lipbalmKoef = lip balm / pcs_breath = freash breath / sweatKoef = pcs_sweat / glassvnesh = glasses / dyevmod = hair dye fading
	!!Moved to base calculation in "body_shape"; bimbobeauty = Bimbo bonus; pcs_lip = lip size; skin = skin condition; pcs_lashes = eyelashes; pcs_eyesize = eyes; bodykoef = hips:waist ratio; pcs_teeth = missing teeth; vidagebonus = youth
	temppcs_apprncbase = ARGS[1] & !! pcs_apprncbase
	tempsupnatvnesh = ARGS[2] & !! supnatvnesh
	tempCloAdjustedBeauty = ARGS[3] & !! result of calcClothesBonus
	tempAccessoriesBonus = ARGS[4] & !! result of calcAccessoriesBonus
	tempGroomingBonus = ARGS[5] & !! result of calcGroomingBonus
	tempGroomingPenalty = ARGS[6] & !! result of calcGroomingPenalty

	temppcs_apprnc = temppcs_apprncbase + tempCloAdjustedBeauty + tempAccessoriesBonus + tempGroomingBonus - tempGroomingPenalty
	!!adjustment to make max achievable
	!!temppcs_maxapprnc = temppcs_apprnc*200/170
	temppcs_maxapprnc = temppcs_apprnc

	if temppcs_maxapprnc >= 200: 
		temppcs_apprnc = 200
	else
		temppcs_apprnc = temppcs_maxapprnc
	end 

	temppcs_apprnc = temppcs_apprnc + tempsupnatvnesh

	if temppcs_apprnc < 0: temppcs_apprnc = 0

	!!This sets the "hotness catagory" varibale for use in checks
	if temppcs_apprnc <= 200:
		temphotcat = temppcs_apprnc / 20
	else
		temphotcat = 10
	end

	result = tempHotcat
	!! do not kill temppcs_apprnc this is used as a second returned variable set pcs_apprnc = temppcs_apprnc and kill temppcs_apprnc after calling it
	killvar 'tempsupnatvnesh'
	killvar 'temppcs_apprncbase'
	killvar 'tempCloAdjustedBeauty'
	killvar 'tempGroomingBonus'
	killvar 'tempGroomingBonus'
	killvar 'tempGroomingPenalty'
end 

if $ARGS[0] = 'AdjustFromBMI':
	tempValue = ARGS[1] & !!value to be adjusted
	tempadjpcs_bmi = ARGS[2] & !!pcs_bmi

	if tempadjpcs_bmi < 16:
		!! severely underweight
		tempValue = tempValue * 50 / 100
    elseif tempadjpcs_bmi < 19:
		!! underweight
		tempValue = tempValue * 95 / 100
    elseif tempadjpcs_bmi < 25:
		!! healthy weight
		!! normal bonus
    elseif tempadjpcs_bmi < 30:
		!! overweight
		tempValue = tempValue * 95 / 100
    elseif tempadjpcs_bmi < 35:
		!! moderately obese
		tempValue = tempValue * 80 / 100
    elseif tempadjpcs_bmi < 40:
		!! severely obese
		tempValue = tempValue * 55 / 100
    elseif tempadjpcs_bmi < 45:
		!! very severely obese
		tempValue = tempValue * 50 / 100
    else
		!!morbidly obese
		tempValue = tempValue * 40 / 100
    end

	result = tempValue
	killvar 'tempValue'
	killvar 'tempadjpcs_bmi'
end
--- body ---------------------------------
