# body

!! I have gone through and added a new category of string variables, standardised to be used for descriptive writing. You will notice some duplication here -- this is for purposes of backwards compatibility, as if I completely rename and discard some of these strings,
!! extant saved games will irreparably break, and this would be Bad. One day down the line, most of the duplicated variables here can be culled, when there is a new release that warrants new saves being used, but until then the duplication needs to stay. Use of
!! strings for descriptions that do not begin with $pcdesc_ should be avoided, as it would be best to have everything standardised when moving forward into the present and future.
!! Available strings follow this format: $pcdesc_type, $pcdesc_typewordy, and $pcdesc_typerandom. The last will give a new string each time, to allow for repeatedly referencing something accurately without it becoming repetitive.  Not EVERYTHING has a wordy 
!! or even random variation, but these should work regardless -- they just may use a non-wordy, or non-random version, if it is something that does not really warrant such. 
!! There is also a special string, $pcdesc_typeinsertive, that follows the format " , descriptive text" as in, " , invitingly-glossed". Please check the master string document to see if this exists for the string you need. - xerya, July 2017

$this = 'body'

if $ARGS[0] = '':
	if pcs_makeup = 6 and bimbolevel >= 2: extra_supnatvnesh = 2*bimbolevel - 2

	!! Calculate hotcat etc
	clothingBonus = func($this, 'CalcClothingBonus')
	accessoriesBonus = func($this, 'CalcAccessoriesBonus', bonusZ, PShoQuality)
	groomingBonus = func($this, 'CalcGroomingBonus')
	groomingPenalty = func($this, 'CalcGroomingPenalty')
	pcs_apprnc = func($this, 'CalcAppearance', (supnatvnesh + extra_supnatvnesh))
	Hotcat = func($this, 'ConvertToHotcat')


	if StrongNarkota <= 0:
		if SNarkTimes >= 3 and SLomka = 0:SLomka = 1
		if Gerpes >= 3 or Sifilis >= 21 or Triper > 2:pcs_apprnc = -10
	end

	if amphCount > 3 and amphHigh <= 0:
		addictChance = rand(1,10)
		if addictChance > 8:
			amphWithdrawl = 2
		end
	end

	if StrongNarkota > 0:
		if SLomka > 0:SLomka = 0
		pcs_mood = 100
	end

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	!! kill temporary variables
	!{killvar 'clothingBonus'
	killvar 'accessoriesBonus'
	killvar 'groomingBonus'
	killvar 'groomingPenalty'}
end

if $args[0] = 'UpdateBaseAppearnce':
	!{
		Base Appearance is updated once a day at midnight and called from cikl
		Base Appearance is calculated from:
			vidage, skin, body shape (fat and strength), face (lip size, eyes size, and eyelashes)
			attributes (endurance and agility)
	}
	faceBonus = func($this, 'CalcFaceBonus')
	attributeBonus = func($this, 'CalcAttributeBonus')

	!!	Setting vidageBonus
	if vidage < 20: 
		vidageBonus = 60 + (2 * vidage)
	elseif vidage > 30: 
		vidageBonus = 160 - (2 * vidage)
	else
		vinageBonus = 100
	end

	skinBonus = (pcs_skin * 2 / 5)
	bodyShapeBonus = func($this, 'SetBodyShapeBonus')

	! Calculte vnesh
	pcs_apprncbase = (vidageBonus * (skinBonus + bodyShapeBonus + faceBonus + attributeBonus) / 100) + supnatvnesh

	killvar 'vidageBonus'
end 

if $ARGS[0] = 'CalcFaceBonus':
	!!	Setting Eyelashes bonus
	if pcs_lashes <= 0:
		eyelashesBonus = 0
	elseif pcs_lashes = 1:
		eyelashesBonus = 2
	elseif pcs_lashes = 2:
		eyelashesBonus = 5
	elseif pcs_lashes = 3:
		eyelashesBonus = 7
	elseif pcs_lashes = 4:
		eyelashesBonus = 8
	elseif pcs_lashes = 5:
		eyelashesBonus = 10
	else
		eyelashesBonus = 0		
	end
	eyelashesBonus = FUNC($this, 'AdjustFromBMI', eyelashesBonus)

	!!	Setting Eye Size bonus
	if pcs_eyesize = 1:
		eyeSizeBonus = 1
	elseif pcs_eyesize = 2:
		eyeSizeBonus = 3
	elseif pcs_eyesize = 3:
		eyeSizeBonus = 2
	else
		eyeSizeBonus = 0
	end
	eyeSizeBonus = FUNC($this, 'AdjustFromBMI', eyeSizeBonus)

	!!	Setting the Lip size bonus
	if pcs_lip = 0:
		lipBonus = -2
	elseif pcs_lip = 1:
		lipBonus = 0
	elseif pcs_lip = 2:
		lipBonus = 3
	elseif pcs_lip = 3:
		lipBonus = 5
	else
		lipBonus = 3
	end
	lipBonus = FUNC($this, 'AdjustFromBMI', lipBonus)

	result = eyelashesBonus + eyeSizeBonus + lipBonus

end

if $ARGS[0] = 'CalcAttributeBonus':
	tempAttributeBonus = (pcs_agil / 10) + (pcs_vital / 10)

	result = func($this, 'AdjustFromBMI', tempAttributeBonus)
	killvar 'tempAttributeBonus'
end

if $ARGS[0] = 'SetBodyShapeBonus':
	! Magic uses a different calculation
	if dounspell = 1:
		bodytipe = pcs_hips - pcs_waist
		if bodytipe < 20:
			result = 0
		elseif bodytipe >= 20 and bodytipe < 25:
			result = 2
		elseif (bodytipe >= 25 and bodytipe < 30) or bodytipe >=35:
			result = 4
		elseif bodytipe >= 30 and bodytipe < 35:
			result = 8
		end

	!!Setting the pcs_apprnc bonus based on fat and strength
	else
		if pcs_bmi < 16:
			!! severely underweight
			tempBodyShapeBonus = 30
		elseif pcs_bmi < 19:
			!! underweight
			tempBodyShapeBonus = 65
		elseif pcs_bmi < 25:
			!! healthy weight
			tempBodyShapeBonus = 85
		elseif pcs_bmi < 30:
			!! overweight
			tempBodyShapeBonus = 75
		elseif pcs_bmi < 35:
			!! moderately obese
			tempBodyShapeBonus = 45
		elseif pcs_bmi < 40:
			!! severely obese
			tempBodyShapeBonus = 25
		elseif pcs_bmi < 45:
			!! very severely obese
			tempBodyShapeBonus = 5
		else
			!! morbidly obese
			tempBodyShapeBonus = 0
		end

		if (strenbuf >= 40 and strenbuf < 60) or strenbuf >= 80:
			tempBodyShapeBonus += 8
		elseif strenbuf >= 60:
			tempBodyShapeBonus += 10
		elseif strenbuf >= 20:
			tempBodyShapeBonus += 5
		end

		!!This modifies bodykoef for high or low salo values
		if salocatnow = 0 or salocatnow >= 7:
			tempBodyShapeBonus -= 8
		elseif salocatnow = 1 or salocatnow = 6:
			tempBodyShapeBonus -= 4
		end

		if vofat > 0: tempBodyShapeBonus -= vofat

		result = tempBodyShapeBonus

		killvar 'tempBodyShapeBonus'
	end
end

if $ARGS[0] = 'CalcClothingBonus':

	if $clothingworntype = 'nude':
		!heathy weight and overweight
		if pcs_bmi >= 19 and pcs_bmi < 30:
			tempRevealing = 405
			tempPCloQuality = 3
		else
			tempRevealing = 0
			tempPCloQuality = 1
		end
	else
		!heathy weight and overweight
		if pcs_bmi >= 19 and pcs_bmi < 30:
			tempRevealing = PXCloThinness + PXCloTopCut + PXCloBottomShortness
		else
			tempRevealing = (400 - PXCloThinness) + (500 - PXCloTopCut) + (400 - PXCloBottomShortness)
		end
		tempRevealing = tempRevealing / 2
	end
	tempRevealing = tempRevealing / 76

	result = tempRevealing * PCloQuality

	killvar 'tempRevealing'
end

if $ARGS[0] = 'CalcAccessoriesBonus':
	coatQualityBonus = ARGS[1] & !! bonusZ
	shoesQualityBonus = ARGS[2] & !! PShoQuality
	
	!!bonuses for certain underwear
	if $pantyworntype = 'boutique':
		pantyBonus = 4
	elseif $pantyworntype = 'fashionista':
		pantyBonus = 2
	end

	if $braworntype = 'boutique':
		braBonus = 4
	elseif $braworntype = 'fashionista':
		braBonus = 2
	end

	result = coatQualityBonus + shoesQualityBonus + pantyBonus + braBonus

	killvar 'coatQualityBonus'
	killvar 'shoesQualityBonus'
	killvar 'pantyBonus'
	killvar 'braBonus'
end

if $ARGS[0] = 'CalcGroomingBonus':
	if pcs_lipbalm > 0:
		lipBalmBonus = 5
	else
		lipBalmBonus = 0
	end

	makeupBonus = pcs_makupskl / 5
	if pcs_makeup = 0: makeupBonus = -5
	if pcs_makeup = 1: makeupBonus = 0
	if pcs_makeup = 5: makeupBonus = 30

	hairBonus = pcs_hairbsh * 10

	breathBonus = pcs_breath * 5

	tempGroomingBonus = makeupBonus + hairBonus + lipBalmBonus + breathBonus

	!!Small bonus for wearing deodorant, if pcs_sweat is low enough
	if deodorant_on = 1 and pcs_sweat < 20: tempGroomingBonus += 5

	!result = tempGroomingBonus
	result = func($this, 'AdjustFromBMI', tempGroomingBonus)

	!{killvar 'lipBalmBonus'
	killvar 'makeupBonus'
	killvar 'hairBonus'
	killvar 'breathBonus'
	killvar 'tempGroomingBonus'}
end

if $ARGS[0] = 'CalcGroomingPenalty':
	!! buzzcut penalty to pcs_apprnc
	if pcs_hairlng < 10:
		buzzCutPenalty = 10
	else
		buzzCutPenalty = 0
	end

	if pcs_sweat < 22:
		sweatPenalty = 0
	elseif temppcs_sweat < 38:
		sweatPenalty = (pcs_sweat - 10) / 4
	elseif temppcs_sweat < 54:
		sweatPenalty = (pcs_sweat - 10) / 2
	else
		sweatPenalty = 3 * (pcs_sweat - 10) / 4
	end

	!Glasses Penalty
	if glass >= 2 or glass = 0:
		glassesPenalty = 0
	elseif glass = 1:
		glassesPenalty = 10
	end

	!hair color fade penalty
	if pcs_haircol ! nathcol:
		if dyefade > 0 and dyefade < 7: hairDyePenalty = 5
		if dyefade = 0: hairDyePenalty = 15
	end

	! Leg hair penalty
	if pcs_leghair <= 0:
		legPenalty = 0
	elseif pcs_leghair <= 3:
		legPenalty = 3
	elseif pcs_leghair <= 6:
		legPenalty = 6
	else
		legPenalty = 9 
	end

	result = sweatPenalty + glassesPenalty + hairDyePenalty + buzzCutPenalty + legPenalty


	killvar 'sweatPenalty'
	killvar 'glassesPenalty'
	killvar 'hairDyePenalty'
	killvar 'buzzCutPenalty'
	killvar 'legPenalty'
end


if $ARGS[0] = 'CalcAppearance':
	!!Appearance factors; hairkoef = very short hair / mopkoef = makeup bonus / legkoef = shaved legs / CloAdjustedBeauty = clothes / pcs_hairbsh = Hair brushed / lipbalmKoef = lip balm / pcs_breath = freash breath / sweatKoef = pcs_sweat / glassvnesh = glasses / dyevmod = hair dye fading
	!!Moved to base calculation in "body_shape"; bimbobeauty = Bimbo bonus; pcs_lip = lip size; skin = skin condition; pcs_lashes = eyelashes; pcs_eyesize = eyes; bodykoef = hips:waist ratio; pcs_teeth = missing teeth; vidagebonus = youth
	tempsupnatvnesh = ARGS[1] & !! supnatvnesh

	temp_apprnc = pcs_apprncbase + clothingBonus + accessoriesBonus + groomingBonus - groomingPenalty

	if temp_apprnc >= 200: temp_apprnc = 200 

	temp_apprnc = temp_apprnc + tempsupnatvnesh

	if temp_apprnc < 0: temp_apprnc = 0

	result = temp_apprnc
	
	killvar 'tempsupnatvnesh'
	killvar 'temp_apprnc'
end

if $ARGS[0] = 'ConvertToHotcat':
	if pcs_apprnc <= 200:
		result = pcs_apprnc / 20
	else
		result = 10
	end
end

if $ARGS[0] = 'AdjustFromBMI':
	tempValue = ARGS[1] & !!value to be adjusted

	if pcs_bmi < 16:
		!! severely underweight
		tempValue = tempValue * 50 / 100
    elseif pcs_bmi < 19:
		!! underweight
		tempValue = tempValue * 95 / 100
    elseif pcs_bmi < 25:
		!! healthy weight
		!! normal bonus
    elseif pcs_bmi < 30:
		!! overweight
		tempValue = tempValue * 95 / 100
    elseif pcs_bmi < 35:
		!! moderately obese
		tempValue = tempValue * 80 / 100
    elseif pcs_bmi < 40:
		!! severely obese
		tempValue = tempValue * 55 / 100
    elseif pcs_bmi < 45:
		!! very severely obese
		tempValue = tempValue * 50 / 100
    else
		!!morbidly obese
		tempValue = tempValue * 40 / 100
    end

	result = tempValue
	killvar 'tempValue'
	killvar 'tempadjpcs_bmi'
end
--- body ---------------------------------
