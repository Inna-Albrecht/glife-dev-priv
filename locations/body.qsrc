# body

!! I have gone through and added a new category of string variables, standardised to be used for descriptive writing. You will notice some duplication here -- this is for purposes of backwards compatibility, as if I completely rename and discard some of these strings,
!! extant saved games will irreparably break, and this would be Bad. One day down the line, most of the duplicated variables here can be culled, when there is a new release that warrants new saves being used, but until then the duplication needs to stay. Use of
!! strings for descriptions that do not begin with $pcdesc_ should be avoided, as it would be best to have everything standardised when moving forward into the present and future.
!! Available strings follow this format: $pcdesc_type, $pcdesc_typewordy, and $pcdesc_typerandom. The last will give a new string each time, to allow for repeatedly referencing something accurately without it becoming repetitive.  Not EVERYTHING has a wordy 
!! or even random variation, but these should work regardless -- they just may use a non-wordy, or non-random version, if it is something that does not really warrant such. 
!! There is also a special string, $pcdesc_typeinsertive, that follows the format " , descriptive text" as in, " , invitingly-glossed". Please check the master string document to see if this exists for the string you need. - xerya, July 2017

if $ARGS[0] = '':
	gs 'body_desc'
	!!!!!!!!!!!!!!!!
	!!!!!SIZE AND WEIGHT!!!
	!!!!!!!!!!!!!!!!
	if dounspell = 0:
		pcs_hips = (pcs_hgt * hratio) / 100 + vhips
		pcs_waist = (pcs_hips * wratio) / 100 + vofat
		pcs_band = (pcs_waist * bratio) / 100 + vofat
		pcs_bust = (pcs_waist * bratio) / 100 + nbsize + magicf2b + silicone
		pcs_butt = (pcs_hips / 10) + silicone_butt + butt_cheat
		pcs_cupsize = pcs_bust - pcs_band

		!! If the PC has vastly less breast tissue than she does silicone, use a slightly more derogatory term. There is actually not a great way to calculate this; silicone counts almost the same on all body types, even though an increase of 200cc, for example, would be vastly
		!! more noticeable on a thinner body than a heavier one. I have experimented with moving where the silicone is factored in to the body shape calculations, but ultimately there is just no better way to do it than how it is now, given the things that increase the amount
		!! of silicone in the PC body. The best kludge I can make is working off of stored fat (pcs_weight does actually not do the job at all, as a fat PC and thin PC are almost the same, oddly, in my tests) and making an approximation from there. The amount of the silicone
		!! variable does not really equate to any particular real-life cc calculation for implant size, so we cannot go off of that. This will have to do for now.
		pcs_bustdisparity = (silicone/10) - salocatnow

		if pcs_bustdisparity >=1 and pcs_bustdisparity < 2:
			$pcdesc_breastsqualifier = ''
			$pcdesc_breastsqualifierinsertive = ''
			$pcdesc_breastsword = 'bolt-ons'
			pcdesc_breastimplantcheck = 1
		elseif pcs_bustdisparity >= 2 and pcs_bustdisparity < 3:
			$pcdesc_breastsqualifier = ' semi-spherical'
			$pcdesc_breastsqualifierinsertive = ', semi-spherical'
			$pcdesc_breastsword = 'bolt-ons'
			pcdesc_breastimplantcheck = 1
		elseif pcs_bustdisparity >= 3 and pcs_bustdisparity < 4:
			$pcdesc_breastsqualifier = ' firm and round'
			$pcdesc_breastsqualifierinsertive = ', firm and round'
			$pcdesc_breastsword = 'bolt-ons'
			pcdesc_breastimplantcheck = 1
		!! Already we are about as far as a normal, non-fetishistic human being would get -- the maximum size of implant, the XXL, results in 40 silicone, and an extremely thin and well-kept PC will have a minimum salocatnow of 1.
		!! That means that anything below this line means it was a very intentional move toward sexual objectification, as it could only come from fillable or string implants (and on a thin frame, no less), so we can take some liberties.
		elseif pcs_bustdisparity >= 4 and pcs_bustdisparity < 5:
			$pcdesc_breastsqualifier = ' clearly man-made'
			$pcdesc_breastsqualifierinsertive = ', clearly man-made'
			$pcdesc_breastsword = 'bolt-ons'
			pcdesc_breastimplantcheck = 1
		elseif pcs_bustdisparity >= 5 and pcs_bustdisparity < 6:
			$pcdesc_breastsqualifier = ' plastic fuckdoll'
			$pcdesc_breastsqualifierinsertive = ', plastic fuckdoll'
			$pcdesc_breastsword = 'bolt-ons'
			pcdesc_breastimplantcheck = 1
		elseif pcs_bustdisparity >= 6 and pcs_bustdisparity < 7:
			$pcdesc_breastsqualifier = ' walking fucktoy'
			$pcdesc_breastsqualifierinsertive = ', walking fucktoy'
			$pcdesc_breastsword = 'bolt-ons'
			pcdesc_breastimplantcheck = 1
		elseif pcs_bustdisparity >= 7 and pcs_bustdisparity < 8:
			$pcdesc_breastsqualifier = ' laughably overdone fuckdoll'
			$pcdesc_breastsqualifierinsertive = ', laughably overdone fuckdoll'
			$pcdesc_breastsword = 'bolt-ons'
			pcdesc_breastimplantcheck = 1
		elseif pcs_bustdisparity >= 8:
			$pcdesc_breastsqualifier = ' <<$pcdesc_haircolourwordy>> overdone fuckdoll'
			$pcdesc_breastsqualifierinsertive = ', <<$pcdesc_haircolourwordy>> overdone fuckdoll'
			$pcdesc_breastsword = 'bolt-ons'
			pcdesc_breastimplantcheck = 1
		!! This triggers if they have fake breasts, but they are not obviously, blatantly fake breasts. In fact, this should be what triggers most of the time with a PC who has implants, given average cases.
		elseif pcs_bustdisparity < 1 and silicone > 0:
			if pcs_cupsize <= 30:
				$pcdesc_breastsword = 'breasts'
			else
				$pcdesc_breastsword = 'tits'
			end
			$pcdesc_breastsqualifier = ''
			$pcdesc_breastsqualifierinsertive = ''
			pcdesc_breastimplantcheck = 1
		!! For all-natural PCs.
		else
			if pcs_cupsize <= 30:$pcdesc_breastsword = 'breasts'
			if pcs_cupsize > 30:$pcdesc_breastsword = 'tits'
			$pcdesc_breastsqualifier = ''
			$pcdesc_breastsqualifierinsertive = ''
			pcdesc_breastimplantcheck = 0
		end
		
		!! These will not be used below, but can be referenced by descriptive writers in case they want to be able to quickly reference whether or not the PC has implants, as in "He reaches up and cups your<<$pcdesc_breastsfakeinsertive>> breasts."
		if pcdesc_breastimplantcheck = 1:
			$pcdesc_breastsfakeinsertive = ' fake'
		else
			$pcdesc_breastsfakeinsertive = ''
		end

		if pcs_cupsize <= 5:
			tits = 0
			$titsize = 'AA cup'
			rand_breasts = rand(0,1)
			if rand_breasts = 0:
				$pcdesc_breastsrandom = 'tiny'
			elseif rand_breasts = 1:
				$pcdesc_breastsrandom = 'barely-there'
			end
			!! Honestly, I am not even sure it is possible to have silicone and be this small. But just in case...
			if pcdesc_breastimplantcheck = 1:
				$pcdesc_breastswordy = 'scarcely-augmented<<$pcdesc_breastsqualifierinsertive>> <<$pcdesc_breastsword>>'
			else
				$pcdesc_breastswordy = 'tiny, mosquito-bite <<$pcdesc_breastsword>>'
			end
			$pcdesc_breasts = 'tiny'
		elseif pcs_cupsize <= 10:
			tits = 1
			$titsize = 'A cup'
			rand_breasts = rand(0,1)
			if rand_breasts = 0:
				$pcdesc_breastsrandom = 'petite'
			elseif rand_breasts = 1:
				$pcdesc_breastsrandom = 'smaller than a handful'
			end
			if pcdesc_breastimplantcheck = 1:
				$pcdesc_breastswordy = 'barely modified<<$pcdesc_breastsqualifierinsertive>> <<$pcdesc_breastsword>>'
			else
				$pcdesc_breastswordy = 'petite, puffy little <<$pcdesc_breastsword>>'
			end
			$pcdesc_breasts = 'petite'
		elseif pcs_cupsize <= 15:
			tits = 2
			$titsize = 'B cup'
			rand_breasts = rand(0,1)
			if rand_breasts = 0:
				$pcdesc_breastsrandom = 'small'
			elseif rand_breasts = 1:
				$pcdesc_breastsrandom = 'sporty'
			end
			if pcdesc_breastimplantcheck = 1:
				$pcdesc_breastswordy = 'small, subtly augmented<<$pcdesc_breastsqualifierinsertive>> <<$pcdesc_breastsword>>'
			else
				$pcdesc_breastswordy = 'small, handful-sized <<$pcdesc_breastsword>>'
			end
			$pcdesc_breasts = 'small'
		elseif pcs_cupsize <= 20:
			tits = 3
			$titsize = 'C cup'
			rand_breasts = rand(0,1)
			if rand_breasts = 0:
				$pcdesc_breastsrandom = 'ample'
			elseif rand_breasts = 1:
				$pcdesc_breastsrandom = 'average-sized'
			end
			!! At this point PCs should now be able to get the bolted-on look, so we need to be a bit more careful to avoid contradictions in descriptive phrasing. It is OK to have some repetition here for a cup size or two.
			if pcdesc_breastimplantcheck = 1:
				$pcdesc_breastswordy = 'ample, silicone-padded<<$pcdesc_breastsqualifierinsertive>> <<$pcdesc_breastsword>>'
			else
				$pcdesc_breastswordy = 'ample, handful-sized <<$pcdesc_breastsword>>'
			end
			$pcdesc_breasts = 'ample'
		elseif pcs_cupsize <= 25:
			tits = 4
			$titsize = 'D cup'
			rand_breasts = rand(0,1)
			if rand_breasts = 0:
				$pcdesc_breastsrandom = 'full'
			elseif rand_breasts = 1:
				$pcdesc_breastsrandom = 'hefty'
			end
			if pcdesc_breastimplantcheck = 1:
				$pcdesc_breastswordy = 'full, silicone-padded<<$pcdesc_breastsqualifierinsertive>> <<$pcdesc_breastsword>>'
			else
				$pcdesc_breastswordy = 'jiggly, womanly full <<$pcdesc_breastsword>>'
			end
			$pcdesc_breasts = 'full'
		elseif pcs_cupsize <= 30:
			tits = 5
			$titsize = 'E cup'
			rand_breasts = rand(0,2)
			if rand_breasts = 0:
				$pcdesc_breastsrandom = 'generous'
			elseif rand_breasts = 1:
				$pcdesc_breastsrandom = 'heavy'
			elseif rand_breasts = 2:
				$pcdesc_breastsrandom = 'fairly large'
			end
			if pcdesc_breastimplantcheck = 1:
				$pcdesc_breastswordy = 'heavy, fake<<$pcdesc_breastsqualifierinsertive>> <<$pcdesc_breastsword>>'
			else
				$pcdesc_breastswordy = 'soft, generously-sized <<$pcdesc_breastsword>>'
			end
			$pcdesc_breasts = 'generous'
		elseif pcs_cupsize <= 35:
			!! From F cup and onward, breasts are now automatically referred to as tits, since that is how most others will see them. It is a subtle sexualisation, but it should be one that matches with the pre-generated descriptions from here on out.
			tits = 6
			$titsize = 'F cup'
			rand_breasts = rand(0,1)
			if rand_breasts = 0:
				$pcdesc_breastsrandom = 'big'
			elseif rand_breasts = 1:
				$pcdesc_breastsrandom = 'large'
			end
			if pcdesc_breastimplantcheck = 1:
				$pcdesc_breastswordy = 'big, implant-stuffed<<$pcdesc_breastsqualifierinsertive>> <<$pcdesc_breastsword>>'
			else
				$pcdesc_breastswordy = 'big, jiggly and sizeable <<$pcdesc_breastsword>>'
			end
			$pcdesc_breasts = 'big'
		elseif pcs_cupsize <= 40:
			tits = 7
			$titsize = 'G cup'
			rand_breasts = rand(0,1)
			if rand_breasts = 0:
				$pcdesc_breastsrandom = 'considerably large'
			elseif rand_breasts = 1:
				$pcdesc_breastsrandom = 'quite large'
			end
			if pcdesc_breastimplantcheck = 1:
				$pcdesc_breastswordy = 'considerably large, implant-stuffed<<$pcdesc_breastsqualifierinsertive>> <<$pcdesc_breastsword>>'
			else
				$pcdesc_breastswordy = 'quite large and jiggly <<$pcdesc_breastsword>>'
			end
			$pcdesc_breasts = 'considerably large'
		elseif pcs_cupsize <= 45:
			tits = 8
			$titsize = 'H cup'
			rand_breasts = rand(0,1)
			if rand_breasts = 0:
				$pcdesc_breastsrandom = 'extremely large'
			elseif rand_breasts = 1:
				$pcdesc_breastsrandom = 'incredibly stacked'
			end
			if pcdesc_breastimplantcheck = 1:
				$pcdesc_breastswordy = 'extremely large, silicone-stuffed<<$pcdesc_breastsqualifierinsertive>> <<$pcdesc_breastsword>>'
			else
				$pcdesc_breastswordy = 'extremely large, over-stacked <<$pcdesc_breastsword>>'
			end
			$pcdesc_breasts = 'extremely large'
		elseif pcs_cupsize <= 50:
			tits = 9
			$titsize = 'I cup'
			rand_breasts = rand(0,2)
			if rand_breasts = 0:
				$pcdesc_breastsrandom = 'huge'
			elseif rand_breasts = 1:
				$pcdesc_breastsrandom = 'impractically big'
			elseif rand_breasts = 2:
				$pcdesc_breastsrandom = 'figure-dominating'
			end
			if pcdesc_breastimplantcheck = 1:
				$pcdesc_breastswordy = 'huge, silicone-swollen<<$pcdesc_breastsqualifierinsertive>> <<$pcdesc_breastsword>>'
			else
				$pcdesc_breastswordy = 'huge, wholly impractical <<$pcdesc_breastsword>>'
			end
			$pcdesc_breasts = 'huge'
		elseif pcs_cupsize <= 55:
			tits = 10
			$titsize = 'J cup'
			rand_breasts = rand(0,1)
			if rand_breasts = 0:
				$pcdesc_breastsrandom = 'massive'
			elseif rand_breasts = 1:
				$pcdesc_breastsrandom = 'enormous'
			end
			if pcdesc_breastimplantcheck = 1:
				$pcdesc_breastswordy = 'massively fake<<$pcdesc_breastsqualifierinsertive>> <<$pcdesc_breastsword>>'
			else
				$pcdesc_breastswordy = 'massive, attention-grabbing <<$pcdesc_breastsword>>'
			end
			$pcdesc_breasts = 'massive'
		else
			tits = 11
			$titsize = 'K cup'
			rand_breasts = rand(0,2)
			if rand_breasts = 0:
				$pcdesc_breastsrandom = 'whorishly oversized'
			elseif rand_breasts = 1:
				$pcdesc_breastsrandom = 'fucktoy-appropriate'
			elseif rand_breasts = 2:
				$pcdesc_breastsrandom = 'ridiculously big'
			end
			if pcdesc_breastimplantcheck = 1:
				$pcdesc_breastswordy = 'whorishly oversized<<$pcdesc_breastsqualifierinsertive>> <<$pcdesc_breastsword>>'
			else
				$pcdesc_breastswordy = 'ridiculously big and figure-dominating <<$pcdesc_breastsword>>'
			end
			$pcdesc_breasts = 'whorishly oversized'
		end
		if pcs_butt <= 4:
			buttpic = 1
			$pcs_butt_adj1 = 'flat ass'
			$pcs_butt_adj2 = 'flat butt'
			$pcs_butt = 'Your <a href="exec:view''images/pc/body/ass/1.jpg''">ass</a> is square and as flat as a plank. It can be considered very masculine looking and isn''t very impressive.'
		elseif pcs_butt <= 8: 
			buttpic = 2
			$pcs_butt_adj1 = 'ass'
			$pcs_butt_adj2 = 'butt'
			$pcs_butt = 'You have an average, feminine looking <a href="exec:view''images/pc/body/ass/2.jpg''">ass</a>. While it''s not flat, it''s nothing special either.'
		elseif pcs_butt <= 12: 
			buttpic = 3
			$pcs_butt_adj1 = 'tight ass'
			$pcs_butt_adj2 = 'nice ass'
			$pcs_butt = 'Your tight <a href="exec:view''images/pc/body/ass/3.jpg''">ass</a> is a little bigger, rounder and firmer than average. You often draw the stares of guys around you.'
		elseif pcs_butt <= 16: 
			buttpic = 4
			$pcs_butt_adj1 = 'fat ass'
			$pcs_butt_adj2 = 'bubble butt'
			$pcs_butt = 'You have a large and natural bubble <a href="exec:view''images/pc/body/ass/4.jpg''">butt</a> that jiggles and sways as you walk. Guys hungrily fixate on you with their tongues hanging out and other girls are often envious of your booty.'
        end
		if silicone_butt > 0:
			buttpic = 4
			$pcs_butt = 'Through the use of implants, you have given yourself a jiggly bubble <a href="exec:view''images/pc/body/ass/4.jpg''">butt</a> that attracts the attention of the people around you. It might be fake, but you''ve got some junk in the trunk.'
		end

		!! 'Thanks to the workouts you''ve been doing, you have gained a large and natural bubble butt that''s rounded and very firm. Guys hungrily fixate on you with their tongues hanging out and other girls are often envious of your booty.'

		

		!!pcs_weight: Svetas weight in kg 
		!!Optimal Weight (170cm): 60kg, 60 salo
		!!This is an approximation, so that a sveta with a different height has the same BMI for the same salo values.
		!!The default height of 170 cm is used as a starting point. The bmi deviates by 0.06 for a deviation of 10cm and 0.4 for a deviation of 20cm. 
		
		pcs_weight = 30 + salo / 2 + (pcs_hgt - 170) * 7 / 10
		
		!!Do not remove (julzor)
		!!This is the old more complex function, but it doesn''t work correctly.
		!!This variable is actually only used ONCE (except for the BMI calculation), when stripping, to see if you fall off the pole from being too heavy. I am reluctant to touch it, since the math is not explained. Maybe later.
		!!Weight = (height in cm x bottom of average weight in kilos for a 170cm female / 170cm + (hip modifier + 2 x overrun fat)/4 + cup size
		
		!!pcs_weight = (pcs_hgt * 62 / 170) + (vhips + (vofat * 2)) / 4 + tits
			
		!!BMI Calculation

		bmi_calc = FUNC('body_shape', 'bmicalc', salo, pcs_hgt)

		!!BMI Descriptions
		if bmi_calc < 16:
			$bmi_desc = 'You are severely underweight.'
		elseif bmi_calc >= 16 and bmi_calc < 19:
			$bmi_desc = 'You are underweight'
		elseif bmi_calc >= 19 and bmi_calc < 25:
			$bmi_desc = 'You have a normal, healthy weight.'
		elseif bmi_calc >= 25 and bmi_calc < 30:
			$bmi_desc = 'You are overweight.'
		elseif bmi_calc >= 30 and bmi_calc < 35:
			$bmi_desc = 'You are moderately obese.'
		elseif bmi_calc >= 35 and bmi_calc < 40:
			$bmi_desc = 'You are severely obese.'
		elseif bmi_calc >= 40 and bmi_calc < 45:
			$bmi_desc = 'You are very severely obese.'
		elseif bmi_calc >= 45:
			$bmi_desc = 'You are morbidly obese.'
		end
	end

	!!! Maruda Hair Mod

	!! Appearance factors and calculations

	!! Skin1 is only used in the foto location, and can be replaced at a later date. From here on out, do not use it.
	if pcs_skin > 100:
		pcs_skin = 100
	elseif pcs_skin < 0:
		pcs_skin = 0
	end

	mopkoef = pcs_makupskl / 5
	if pcs_makeup = 0: mopkoef = -5
	if pcs_makeup = 5: mopkoef = 30
	if pcs_makeup = 6 and bimbolevel >= 2: extra_supnatvnesh = 2*bimbolevel - 2

	if dounspell = 1:
		bodytipe = pcs_hips - pcs_waist
		if bodytipe < 20:
			bodykoef = 0
		elseif bodytipe >= 20 and bodytipe < 25:
			bodykoef = 2
		elseif bodytipe >= 25 and bodytipe < 30:
			bodykoef = 4
		elseif bodytipe >= 30 and bodytipe < 35:
			bodykoef = 8
		elseif bodytipe >= 35:
			bodykoef = 4
		end
	end

	!!This is to force a base update if teeth changed since that is immediate
	if zublast ! pcs_teeth:
		gs 'body_shape', 'basebeautycalc', 1
		zublast = pcs_teeth
	end

	!! Calculate hotcat etc
	tempclothingbonus = FUNC('body', 'calcClothesBonus', PXCloThinness, PXCloTopCut, PXCloBottomShortness, PCloQuality, $clothingworntype, bmi_calc)
	tempAccessoriesBonus = FUNC('body', 'calcAccessoriesBonus', bonusZ, PShoQuality, $pantyworntype, $braworntype)
	tempGroomingBonus = FUNC('body', 'calcGroomingBonus', pcs_lipbalm, mopkoef, pcs_hairbsh, pcs_breath, deodorant_on, bmi_calc)
	tempGroomingPenalty = FUNC('body', 'calcGroomingPenalty', pcs_hairlng, pcs_sweat, glassvnesh, dyevmod, legkoef)
	Hotcat = FUNC('body', 'calcHotcat', pcs_apprncbase, (supnatvnesh + extra_supnatvnesh), tempclothingbonus, tempAccessoriesBonus, tempGroomingBonus, tempGroomingPenalty)
	pcs_apprnc = temppcs_apprnc & killvar 'temppcs_apprnc' & killvar 'temppcs_maxapprnc'


	if StrongNarkota <= 0:
		if SNarkTimes >= 3 and SLomka = 0:SLomka = 1
		if Gerpes >= 3 or Sifilis >= 21 or Triper > 2:pcs_apprnc = -10
	end

	if amphCount > 3 and amphHigh <= 0:
		addictChance = rand(1,10)
		if addictChance > 8:
			amphWithdrawl = 2
		end
	end

	if StrongNarkota > 0:
		if SLomka > 0:SLomka = 0
		pcs_mood = 100
	end

	if pcs_apprnc = -10:
		$pcs_apprnc = 'Your looks are even worse than terrible. It''s god-awful. No one wants to see or talk to you and people avoid you at any cost.'
	elseif pcs_apprnc < 20:
		$pcs_apprnc = 'Your looks are terrible. People try to avoid looking and talking to you.'
	elseif pcs_apprnc < 60:
		$pcs_apprnc = 'You''re a wallflower. Hardly anyone notices you.'
	elseif pcs_apprnc < 120:
		$pcs_apprnc = 'You have good looks. Men and women check you out when they think you aren''t looking.'
	elseif pcs_apprnc < 160:
		$pcs_apprnc = 'You''re unquestionably gorgeous. Men constantly get caught staring at you by their girlfriends.'
	elseif pcs_apprnc < 200:
		$pcs_apprnc = 'You have a simply stunning appearance. Other girls are a bit jealous and guys constantly check you out.'
	else
		$pcs_apprnc = 'Your appearance is divine! No one is able to take his or her eyes off of you.'
	end

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	!!!!!!!!BODY
	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	if dounspell = 1:
		salocatnow = (pcs_hips / 10) - 5
		if bdsetlock = 0:
			if (knowpreg = 1 or (preg = 1 and thinkpreg = 1) or (preg = 1 and PregChem > 3600)):
				bodset = 3
			elseif strenbuf >= 70:
				bodset = 2
			elseif strenbuf <= 40:
				bodset = 0
			else
				bodset = 1
			end
		else
			bodset = fixbodset
		end
	elseif (knowpreg = 1 or (preg = 1 and thinkpreg = 1) or (preg = 1 and PregChem > 3600)) and bodet ! 3 and bdsetlock = 0:
		bodset = 3
	elseif preg = 0 and bodset = 3 and bdsetlock = 0:
		if strenbuf >= 70:
			bodset = 2
		elseif strenbuf <= 40:
			bodset = 0
		else
			bodset = 1
		end
	end 

	if (knowpreg = 1 or (preg = 1 and thinkpreg = 1) or (preg = 1 and PregChem > 3600)) and bodset = 3:
		if PregChem > 6216:
			$body = $bodimgsets[((bodset * 10) + 8)]
		elseif PregChem < 2688:
			$body = $bodimgsets[(bodset * 10)]
		else 
			$body = $bodimgsets[((bodset * 10)+((PregChem - 2184)/504))]
		end
	elseif salocatnow <= 7:
		$body = $bodimgsets[((bodset * 10) + salocatnow)]
	else
		$body = $bodimgsets[((bodset * 10) + 7)]
	end

	!!Since "musle" is used all over the place
	musle = strenbuf


	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	!! kill temporary variables
	killvar 'rand_breasts'
	killvar 'rand_hairmess'
	killvar 'rand_hairword'
	killvar 'rand_bangs'
	killvar 'rand_hair'
	killvar 'rand_hairstyle'
	killvar 'rand_hairobject'
	killvar 'rand_hairlength'
	killvar 'rand_tongue'
	killvar 'rand_eyecol'
	killvar 'rand_lashes'
	killvar 'rand_vision'
	killvar 'rand_eyesize'
	killvar 'rand_makeup'
	killvar 'rand_legs'
	killvar 'rand_clit'
	killvar 'rand_pubes'
	killvar 'rand_nipples'
	killvar 'tempclothingbonus'
	killvar 'tempAccessoriesBonus'
	killvar 'tempGroomingBonus'
	killvar 'tempGroomingPenalty'
end

if $ARGS[0] = 'calcClothesBonus':
	!! Note there is a limit of 9 args
	tempPXCloThinness = ARGS[1] & !! PXCloThinness
	tempPXCloTopCut = ARGS[2] & !! PXCloTopCut
	tempPXCloBottomShortness = ARGS[3] & !! PXCloBottomShortness
	tempPCloQuality = ARGS[4] & !! PCloQuality
	$tempclothingworntype = $ARGS[5] & !!$clothingworntype
	tempbmi_calc = ARGS[6] & !! bmi_calc

	if tempbmi_calc < 16:
		!!severely underweight
		if $tempclothingworntype = 'nude':
			tempRevealing = 0
			tempPCloQuality = 1
		else
			tempRevealing = (400 - tempPXCloThinness) + (500 - tempPXCloTopCut) + (400 - tempPXCloBottomShortness)
			tempRevealing = tempRevealing / 2
		end
	elseif tempbmi_calc >= 16 and tempbmi_calc < 19:
		!!underweight
		if $tempclothingworntype = 'nude':
			tempRevealing = 0
			tempPCloQuality = 1
		else
			tempRevealing = (400 - tempPXCloThinness) + (500 - tempPXCloTopCut) + (400 - tempPXCloBottomShortness)
			tempRevealing = tempRevealing / 2
		end
	elseif tempbmi_calc >= 19 and tempbmi_calc < 25:
		!!normal, healthy weight
		if $tempclothingworntype = 'nude':
			tempRevealing = 405
			tempPCloQuality = 3
		else
			tempRevealing = tempPXCloThinness + tempPXCloTopCut + tempPXCloBottomShortness
			tempRevealing = tempRevealing / 2
		end
	elseif tempbmi_calc >= 25 and tempbmi_calc < 30:
		!!overweight
		if $tempclothingworntype = 'nude':
			tempRevealing = 405
			tempPCloQuality = 3
		else
			tempRevealing = tempPXCloThinness + tempPXCloTopCut + tempPXCloBottomShortness
			tempRevealing = tempRevealing / 2
		end
	elseif tempbmi_calc >= 30 and tempbmi_calc < 35:
		!!moderately obese
		if $tempclothingworntype = 'nude':
			tempRevealing = 0
			tempPCloQuality = 1
		else
			tempRevealing = (400 - tempPXCloThinness) + (500 - tempPXCloTopCut) + (400 - tempPXCloBottomShortness)
			tempRevealing = tempRevealing * 3 / 4
		end
	elseif tempbmi_calc >= 35 and tempbmi_calc < 40:
		!!severely obese
		if $tempclothingworntype = 'nude':
			tempRevealing = 0
			tempPCloQuality = 1
		else
			tempRevealing = (400 - tempPXCloThinness) + (500 - tempPXCloTopCut) + (400 - tempPXCloBottomShortness)
			tempRevealing = tempRevealing * 3 / 4
		end
	elseif tempbmi_calc >= 40 and tempbmi_calc < 45:
		!!very severely obese
		if $tempclothingworntype = 'nude':
			tempRevealing = 0
			tempPCloQuality = 1
		else
			tempRevealing = (400 - tempPXCloThinness) + (500 - tempPXCloTopCut) + (400 - tempPXCloBottomShortness)
			tempRevealing = tempRevealing * 3 / 4
		end
	elseif tempbmi_calc >= 45:
		!!morbidly obese
		if $tempclothingworntype = 'nude':
			tempRevealing = 0
			tempPCloQuality = 1
		else
			tempRevealing = (400 - tempPXCloThinness) + (500 - tempPXCloTopCut) + (400 - tempPXCloBottomShortness)
			tempRevealing = tempRevealing * 3 / 4
		end
	end

	tempRevealing = tempRevealing / 76

	tempCloAdjustedBeauty = tempRevealing * tempPCloQuality

	!!if $tempclothingworntype = 'danilovich_outfits': tempCloAdjustedBeauty = tempCloAdjustedBeauty * 2 / 3

	result = tempCloAdjustedBeauty

	killvar 'tempPXCloThinness'
	killvar 'tempPXCloTopCut'
	killvar 'tempPXCloBottomShortness'
	killvar 'tempPCloQuality'
	killvar '$tempclothingworntype'
	killvar 'tempRevealing'
	killvar 'tempCloAdjustedBeauty'
	killvar 'tempbmi_calc'
end

if $ARGS[0] = 'calcAccessoriesBonus':
	tempcoatbonus = ARGS[1] & !! bonusZ
	tempPShoQuality = ARGS[2] & !! PShoQuality
	$temppantyworntype = $ARGS[3] & !! $pantyworntype
	$tempbraworntype = $ARGS[4] & !! $braworntype
	
	!!bonuses for certain underwear
	if $temppantyworntype = 'boutique':
		temppantybounus = 4
	elseif $temppantyworntype = 'fashionista':
		temppantybounus = 2
	end

	if $tempbraworntype = 'boutique':
		tempbrabounus = 4
	elseif $tempbraworntype = 'fashionista':
		tempbrabounus = 2
	end

	tempAccessoriesBonus = tempcoatbonus + tempPShoQuality + temppantybounus + tempbrabounus

	result = tempAccessoriesBonus
	killvar 'tempcoatbonus'
	killvar 'tempPShoQuality'
	killvar 'temppantybounus'
	killvar 'tempbrabounus'
	killvar '$temppantyworntype'
	killvar '$tempbraworntype'
	killvar 'tempAccessoriesBonus'
end

if $ARGS[0] = 'calcGroomingBonus':
	!! Note there is a limit of 9 args
	temppcs_lipbalm = ARGS[1] & !! pcs_lipbalm
	tempmakeupkoef = ARGS[2] & !! mopkoef 
	temppcs_hairbsh = ARGS[3] & !! pcs_hairbsh
	temppcs_breath = ARGS[4] & !! pcs_breath
	tempdeodorant_on = ARGS[5] & !! deodorant_on
	tempbmi_calc = ARGS[6] & !! bmi_calc

	if temppcs_lipbalm > 0:
		lipbalmKoef = 5
	else
		lipbalmKoef = 0
	end

	tempHairBonus = temppcs_hairbsh * 10

	tempBreathBonus = temppcs_breath * 5

	tempGroomingBonus = tempmakeupkoef + tempHairBonus + lipbalmKoef + tempBreathBonus

	!!Small bonus for wearing deodorant, if pcs_sweat is low enough
	if tempdeodorant_on = 1 and temppcs_sweat < 20: tempGroomingBonus += 5

	tempGroomingBonus = FUNC('body_shape', 'bmiadjust', tempGroomingBonus, tempbmi_calc)

	result = tempGroomingBonus

	killvar 'temppcs_lipbalm'
	killvar 'tempmakeupkoef'
	killvar 'temppcs_hairbsh'
	killvar 'temppcs_breath'
	killvar 'tempdeodorant_on'
	killvar 'tempHairBonus'
	killvar 'tempBreathBonus'
	killvar 'tempGroomingBonus'

!! commented out as not used in the original calc
!!Removed as primary appearance factors; will be eventually used in an NPC preference system instead; tanKoef = suntan
!!	if temppcs_tan > 0:
!!		tanKoef = 1
!!	else
!!		tanKoef = 0
!!	end

end

if $ARGS[0] = 'calcGroomingPenalty':
	temppcs_hairlng = ARGS[1] & !! pcs_hairlng
	temppcs_sweat = ARGS[2] & !! pcs_sweat
	tempglassvnesh = ARGS[3] & !! glassvnesh
	tempdyevmod = ARGS[4] & !! dyevmod
	templegkoef = ARGS[5] & !! legkoef

	!! buzzcut penalty to pcs_apprnc
	if temppcs_hairlng < 10:
		hairkoef = 10
	else
		hairkoef = 0
	end

	if temppcs_sweat < 22:
		sweatKoef = 0
	elseif temppcs_sweat < 38:
		sweatKoef = (temppcs_sweat - 10) / 4
	elseif temppcs_sweat < 54:
		sweatKoef = (temppcs_sweat - 10) / 2
	else
		sweatKoef = 3 * (temppcs_sweat - 10) / 4
	end

	tempGroomingPenalty = sweatKoef + tempglassvnesh + tempdyevmod + hairkoef + templegkoef

	result = tempGroomingPenalty

	killvar 'temppcs_hairlng'
	killvar 'temppcs_sweat'
	killvar 'tempglassvnesh'
	killvar 'tempdyevmod'
	killvar 'templegkoef'
	killvar 'tempGroomingPenalty'
end


if $ARGS[0] = 'calcHotcat':
	!!Appearance factors; hairkoef = very short hair / mopkoef = makeup bonus / legkoef = shaved legs / CloAdjustedBeauty = clothes / pcs_hairbsh = Hair brushed / lipbalmKoef = lip balm / pcs_breath = freash breath / sweatKoef = pcs_sweat / glassvnesh = glasses / dyevmod = hair dye fading
	!!Moved to base calculation in "body_shape"; bimbobeauty = Bimbo bonus; pcs_lip = lip size; skin = skin condition; pcs_lashes = eyelashes; pcs_eyesize = eyes; bodykoef = hips:waist ratio; pcs_teeth = missing teeth; vidagebonus = youth
	temppcs_apprncbase = ARGS[1] & !! pcs_apprncbase
	tempsupnatvnesh = ARGS[2] & !! supnatvnesh
	tempCloAdjustedBeauty = ARGS[3] & !! result of calcClothesBonus
	tempAccessoriesBonus = ARGS[4] & !! result of calcAccessoriesBonus
	tempGroomingBonus = ARGS[5] & !! result of calcGroomingBonus
	tempGroomingPenalty = ARGS[6] & !! result of calcGroomingPenalty

	temppcs_apprnc = temppcs_apprncbase + tempCloAdjustedBeauty + tempAccessoriesBonus + tempGroomingBonus - tempGroomingPenalty
	!!adjustment to make max achievable
	!!temppcs_maxapprnc = temppcs_apprnc*200/170
	temppcs_maxapprnc = temppcs_apprnc

	if temppcs_maxapprnc >= 200: 
		temppcs_apprnc = 200
	else
		temppcs_apprnc = temppcs_maxapprnc
	end 

	temppcs_apprnc = temppcs_apprnc + tempsupnatvnesh

	if temppcs_apprnc < 0: temppcs_apprnc = 0

	!!This sets the "hotness catagory" varibale for use in checks
	if temppcs_apprnc <= 200:
		temphotcat = temppcs_apprnc / 20
	else
		temphotcat = 10
	end

	result = tempHotcat
	!! do not kill temppcs_apprnc this is used as a second returned variable set pcs_apprnc = temppcs_apprnc and kill temppcs_apprnc after calling it
	killvar 'tempsupnatvnesh'
	killvar 'temppcs_apprncbase'
	killvar 'tempCloAdjustedBeauty'
	killvar 'tempGroomingBonus'
	killvar 'tempGroomingBonus'
	killvar 'tempGroomingPenalty'
end 


--- body ---------------------------------
