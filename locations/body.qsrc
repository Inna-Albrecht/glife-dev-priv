# body

!! I have gone through and added a new category of string variables, standardised to be used for descriptive writing. You will notice some duplication here -- this is for purposes of backwards compatibility, as if I completely rename and discard some of these strings,
!! extant saved games will irreparably break, and this would be Bad. One day down the line, most of the duplicated variables here can be culled, when there is a new release that warrants new saves being used, but until then the duplication needs to stay. Use of
!! strings for descriptions that do not begin with $pcdesc_ should be avoided, as it would be best to have everything standardised when moving forward into the present and future.
!! Available strings follow this format: $pcdesc_type, $pcdesc_typewordy, and $pcdesc_typerandom. The last will give a new string each time, to allow for repeatedly referencing something accurately without it becoming repetitive.  Not EVERYTHING has a wordy 
!! or even random variation, but these should work regardless -- they just may use a non-wordy, or non-random version, if it is something that does not really warrant such. 
!! There is also a special string, $pcdesc_typeinsertive, that follows the format " , descriptive text" as in, " , invitingly-glossed". Please check the master string document to see if this exists for the string you need. - xerya, July 2017

$this = 'body'

if $ARGS[0] = '':
	if pcs_makeup = 6 and bimbolevel >= 2: extra_supnatvnesh = 2*bimbolevel - 2

	!! Calculate hotcat etc
	clothingBonus = FUNC('body', 'CalcClothingBonus')
	accessoriesBonus = FUNC('body', 'CalcAccessoriesBonus', bonusZ, PShoQuality)
	groomingBonus = FUNC('body', 'CalcGroomingBonus')
	groomingPenalty = FUNC('body', 'CalcGroomingPenalty')
	Hotcat = FUNC('body', 'calcHotcat', pcs_apprncbase, (supnatvnesh + extra_supnatvnesh), clothingBonus, accessoriesBonus, groomingBonus, groomingPenalty)
	pcs_apprnc = temppcs_apprnc & killvar 'temppcs_apprnc' & killvar 'temppcs_maxapprnc'


	if StrongNarkota <= 0:
		if SNarkTimes >= 3 and SLomka = 0:SLomka = 1
		if Gerpes >= 3 or Sifilis >= 21 or Triper > 2:pcs_apprnc = -10
	end

	if amphCount > 3 and amphHigh <= 0:
		addictChance = rand(1,10)
		if addictChance > 8:
			amphWithdrawl = 2
		end
	end

	if StrongNarkota > 0:
		if SLomka > 0:SLomka = 0
		pcs_mood = 100
	end

	if pcs_apprnc = -10:
		$pcs_apprnc = 'Your looks are even worse than terrible. It''s god-awful. No one wants to see or talk to you and people avoid you at any cost.'
	elseif pcs_apprnc < 20:
		$pcs_apprnc = 'Your looks are terrible. People try to avoid looking and talking to you.'
	elseif pcs_apprnc < 60:
		$pcs_apprnc = 'You''re a wallflower. Hardly anyone notices you.'
	elseif pcs_apprnc < 120:
		$pcs_apprnc = 'You have good looks. Men and women check you out when they think you aren''t looking.'
	elseif pcs_apprnc < 160:
		$pcs_apprnc = 'You''re unquestionably gorgeous. Men constantly get caught staring at you by their girlfriends.'
	elseif pcs_apprnc < 200:
		$pcs_apprnc = 'You have a simply stunning appearance. Other girls are a bit jealous and guys constantly check you out.'
	else
		$pcs_apprnc = 'Your appearance is divine! No one is able to take his or her eyes off of you.'
	end

	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

	!! kill temporary variables
	killvar 'tempclothingbonus'
	killvar 'tempAccessoriesBonus'
	killvar 'tempGroomingBonus'
	killvar 'tempGroomingPenalty'
end

if $args[0] = 'UpdateBaseAppearnce':
	!{
		Base Appearance is updated once a day at midnight and called from cikl
		Base Appearance is calculated from:
			vidage, skin, body shape (fat and strength)
	}

	!!	Setting vidagebonus
	if vidage < 20: 
		vidageBonus = 60 + (2 * vidage)
	elseif vidage > 30: 
		vidageBonus = 160 - (2 * vidage)
	else
		vinageBonus = 100
	end

	skinBonus = (pcs_skin * 2 / 5)
	bodyShapeBonus = func($this, 'SetBodyShapeBonus')

	! Calculte vnesh
	pcs_apprncbase = (vidageBonus * (skinBonus + bodyShapeBonus) / 100) + supnatvnesh

	killvar 'skinBonus'
	killvar 'bodyShapeBonus'
	killvar 'vidageBonus'
end 

if $ARGS[0] = 'SetBodyShapeBonus':
	! Magic uses a different calculation
	if dounspell = 1:
		bodytipe = pcs_hips - pcs_waist
		if bodytipe < 20:
			result = 0
		elseif bodytipe >= 20 and bodytipe < 25:
			result = 2
		elseif (bodytipe >= 25 and bodytipe < 30) or bodytipe >=35:
			result = 4
		elseif bodytipe >= 30 and bodytipe < 35:
			result = 8
		end

	!!Setting the pcs_apprnc bonus based on fat and strength
	else
		if pcs_bmi < 16:
			!! severely underweight
			tempbodykoef = 30
		elseif pcs_bmi < 19:
			!! underweight
			tempbodykoef = 65
		elseif pcs_bmi < 25:
			!! healthy weight
			tempbodykoef = 85
		elseif pcs_bmi < 30:
			!! overweight
			tempbodykoef = 75
		elseif pcs_bmi < 35:
			!! moderately obese
			tempbodykoef = 45
		elseif pcs_bmi < 40:
			!! severely obese
			tempbodykoef = 25
		elseif pcs_bmi < 45:
			!! very severely obese
			tempbodykoef = 5
		else
			!! morbidly obese
			tempbodykoef = 0
		end

		if (strenbuf >= 40 and strenbuf < 60) or strenbuf >= 80:
			tempbodykoef += 8
		elseif strenbuf >= 60:
			tempbodykoef += 10
		elseif strenbuf >= 20:
			tempbodykoef += 5
		end

		!!This modifies bodykoef for high or low salo values
		if salocatnow = 0 or salocatnow >= 7:
			tempbodykoef -= 8
		elseif salocatnow = 1 or salocatnow = 6:
			tempbodykoef -= 4
		end

		if vofat > 0: tempbodykoef -= vofat

		result = tempbodykoef

		killvar 'tempbodykoef'
	end
end

if $ARGS[0] = 'CalcClothingBonus':

	if $clothingworntype = 'nude':
		!heathy weight and overweight
		if pcs_bmi >= 19 and pcs_bmi < 30:
			tempRevealing = 405
			tempPCloQuality = 3
		else
			tempRevealing = 0
			tempPCloQuality = 1
		end
	else
		!heathy weight and overweight
		if pcs_bmi >= 19 and pcs_bmi < 30:
			tempRevealing = PXCloThinness + PXCloTopCut + PXCloBottomShortness
		else
			tempRevealing = (400 - PXCloThinness) + (500 - PXCloTopCut) + (400 - PXCloBottomShortness)
		end
		tempRevealing = tempRevealing / 2
	end
	tempRevealing = tempRevealing / 76

	result = tempRevealing * PCloQuality

	killvar 'tempRevealing'
end

if $ARGS[0] = 'CalcAccessoriesBonus':
	coatQualityBonus = ARGS[1] & !! bonusZ
	shoesQualityBonus = ARGS[2] & !! PShoQuality
	
	!!bonuses for certain underwear
	if $pantyworntype = 'boutique':
		pantyBonus = 4
	elseif $pantyworntype = 'fashionista':
		pantyBonus = 2
	end

	if $braworntype = 'boutique':
		braBonus = 4
	elseif $braworntype = 'fashionista':
		braBonus = 2
	end

	result = coatQualityBonus + shoesQualityBonus + pantyBonus + braBonus

	killvar 'coatQualityBonus'
	killvar 'shoesQualityBonus'
	killvar 'pantyBonus'
	killvar 'braBonus'
end

if $ARGS[0] = 'CalcGroomingBonus':
	if pcs_lipbalm > 0:
		lipBalmBonus = 5
	else
		lipBalmBonus = 0
	end

	makeupBonus = pcs_makupskl / 5
	if pcs_makeup = 0: makeupBonus = -5
	if pcs_makeup = 1: makeupBonus = 0
	if pcs_makeup = 5: makeupBonus = 30

	hairBonus = pcs_hairbsh * 10

	breathBonus = pcs_breath * 5

	tempGroomingBonus = makeupBonus + hairBonus + lipBalmBonus + breathBonus

	!!Small bonus for wearing deodorant, if pcs_sweat is low enough
	if deodorant_on = 1 and pcs_sweat < 20: tempGroomingBonus += 5

	result = FUNC($this, 'AdjustFromBMI', tempGroomingBonus, pcs_bmi)

	killvar 'lipBalmBonus'
	killvar 'makeupBonus'
	killvar 'hairBonus'
	killvar 'breathBonus'
	killvar 'tempGroomingBonus'
end

if $ARGS[0] = 'CalcGroomingPenalty':
	!! buzzcut penalty to pcs_apprnc
	if pcs_hairlng < 10:
		buzzCutPenalty = 10
	else
		buzzCutPenalty = 0
	end

	if pcs_sweat < 22:
		sweatPenalty = 0
	elseif temppcs_sweat < 38:
		sweatPenalty = (temppcs_sweat - 10) / 4
	elseif temppcs_sweat < 54:
		sweatPenalty = (temppcs_sweat - 10) / 2
	else
		sweatPenalty = 3 * (temppcs_sweat - 10) / 4
	end

	!Glasses Penalty
	if glass >= 2 or glass = 0:
		glassesPenalty = 0
	elseif glass = 1:
		glassesPenalty = 10
	end

	!hair color fade penalty
	if pcs_haircol ! nathcol:
		if dyefade > 0 and dyefade < 7: hairDyePenalty = 5
		if dyefade = 0: hairDyePenalty = 15
	end

	! Leg hair penalty
	if pcs_leghair <= 0:
		legPenalty = 0
	elseif pcs_leghair <= 3:
		legPenalty = 3
	elseif pcs_leghair <= 6:
		legPenalty = 6
	else
		legPenalty = 9 
	end

	result = sweatPenalty + glassesPenalty + hairDyePenalty + buzzCutPenalty + legPenalty


	killvar 'sweatPenalty'
	killvar 'glassesPenalty'
	killvar 'hairDyePenalty'
	killvar 'buzzCutPenalty'
	killvar 'legPenalty'
end


if $ARGS[0] = 'CalcHotcat':
	!!Appearance factors; hairkoef = very short hair / mopkoef = makeup bonus / legkoef = shaved legs / CloAdjustedBeauty = clothes / pcs_hairbsh = Hair brushed / lipbalmKoef = lip balm / pcs_breath = freash breath / sweatKoef = pcs_sweat / glassvnesh = glasses / dyevmod = hair dye fading
	!!Moved to base calculation in "body_shape"; bimbobeauty = Bimbo bonus; pcs_lip = lip size; skin = skin condition; pcs_lashes = eyelashes; pcs_eyesize = eyes; bodykoef = hips:waist ratio; pcs_teeth = missing teeth; vidagebonus = youth
	temppcs_apprncbase = ARGS[1] & !! pcs_apprncbase
	tempsupnatvnesh = ARGS[2] & !! supnatvnesh
	tempCloAdjustedBeauty = ARGS[3] & !! result of calcClothesBonus
	tempAccessoriesBonus = ARGS[4] & !! result of calcAccessoriesBonus
	tempGroomingBonus = ARGS[5] & !! result of calcGroomingBonus
	tempGroomingPenalty = ARGS[6] & !! result of calcGroomingPenalty

	temppcs_apprnc = temppcs_apprncbase + tempCloAdjustedBeauty + tempAccessoriesBonus + tempGroomingBonus - tempGroomingPenalty
	!!adjustment to make max achievable
	!!temppcs_maxapprnc = temppcs_apprnc*200/170
	temppcs_maxapprnc = temppcs_apprnc

	if temppcs_maxapprnc >= 200: 
		temppcs_apprnc = 200
	else
		temppcs_apprnc = temppcs_maxapprnc
	end 

	temppcs_apprnc = temppcs_apprnc + tempsupnatvnesh

	if temppcs_apprnc < 0: temppcs_apprnc = 0

	!!This sets the "hotness catagory" varibale for use in checks
	if temppcs_apprnc <= 200:
		temphotcat = temppcs_apprnc / 20
	else
		temphotcat = 10
	end

	result = tempHotcat
	!! do not kill temppcs_apprnc this is used as a second returned variable set pcs_apprnc = temppcs_apprnc and kill temppcs_apprnc after calling it
	killvar 'tempsupnatvnesh'
	killvar 'temppcs_apprncbase'
	killvar 'tempCloAdjustedBeauty'
	killvar 'tempGroomingBonus'
	killvar 'tempGroomingBonus'
	killvar 'tempGroomingPenalty'
end 

if $ARGS[0] = 'AdjustFromBMI':
	tempValue = ARGS[1] & !!value to be adjusted
	tempadjpcs_bmi = ARGS[2] & !!pcs_bmi

	if tempadjpcs_bmi < 16:
		!! severely underweight
		tempValue = tempValue * 50 / 100
    elseif tempadjpcs_bmi < 19:
		!! underweight
		tempValue = tempValue * 95 / 100
    elseif tempadjpcs_bmi < 25:
		!! healthy weight
		!! normal bonus
    elseif tempadjpcs_bmi < 30:
		!! overweight
		tempValue = tempValue * 95 / 100
    elseif tempadjpcs_bmi < 35:
		!! moderately obese
		tempValue = tempValue * 80 / 100
    elseif tempadjpcs_bmi < 40:
		!! severely obese
		tempValue = tempValue * 55 / 100
    elseif tempadjpcs_bmi < 45:
		!! very severely obese
		tempValue = tempValue * 50 / 100
    else
		!!morbidly obese
		tempValue = tempValue * 40 / 100
    end

	result = tempValue
	killvar 'tempValue'
	killvar 'tempadjpcs_bmi'
end
--- body ---------------------------------
