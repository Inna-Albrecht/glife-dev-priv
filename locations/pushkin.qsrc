# pushkin
!!2021/05/14
*clr & cla
gs 'shortgs', 'setloc', 'pushkin', $ARGS[0]
$region = 'puskin'
$menu_arg = ''
menu_off = 0
if sound = 0:PLAY 'sound/street.mp3',30
gs'stat'
'<center><B>Pushkin Town Center</B></center>'
if month >= 11 or month <= 3:
	if hour >= 8 and hour <= 21:
		'<center><img <<$set_imgh>> src="images/locations/pushkin/townwinter.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/pushkin/townwinternight.jpg"></center>'
	end
else
	if hour >= 8 and hour <= 21:
		'<center><img <<$set_imgh>> src="images/locations/pushkin/liam.jpg" ></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/pushkin/townnight.jpg"></center>'
	end
end

'This historic region, sometimes called Old Town was originally the village of Pushkin and that name is still used to this day.'
'Later, the rich bought the land off the poor and knocked down the wooden shacks and farm houses to build large estates. The town began to grow rapidly, and it became a desirable suburb.'
'Since then, even the old houses have been improved, extended, and modernized. The old buildings and beautiful park made it popular with tourists, and expansion has lead to the boarders between itself and Pavlovsk being merged.'

'Not far from here is the <a href="exec:minut += 5 & gt ''pushkin_sq''">Okhlopkov Square</a> with its boutique shops.'
'Close by is a narrow street leading to a <a href="exec:gt ''dvor'',''start''">vacant house</a>.'
'You can also see a <a href="exec:gt ''ugol'',''start''">passage</a> between the houses leading to a courtyard.'
if car > 0 and cardrive = 14:
	'<a href="exec:gs ''carF'',''start''">Your <<$car>></a> is parked at the curb.'
end

if hour >= 13 and hour <= 15 and bodimodel = 1 and arts = 0 and tusa = 1:
	if rand(1,100) >= 90:
		cls
		minut += 5
		gs'stat'
		gs'dibodi'

		'<center><img <<$set_imgh>> src="images/characters/pushkin/marinka/marinka.jpg"></center>'
		'You notice a girl from the local hangout, Marinka, standing in a doorway nearby.'

		act 'Ask Marinka about the art studio':gt 'dibodi', 'sptusa'
	end
end

!!this location does not exist
!!if arts = 1:'<a href="exec:gt ''Marin'',''start''">Marinka''s Apartment</a>'

act 'Walk to Okhlopkov Square (0:05)':minut += 5 & gt 'pushkin_sq'
act 'Walk to the highway (0:05)':minut += 5 & nroad = 19 & gt 'road'
act 'Walk to the park (0:10)':minut += 10 & gt 'pav_park', 'start'
act 'Walk to the train station (0:15)':minut += 15 & gt 'pav_train_hall'
act 'Walk to the Market (0:20)':minut += 20 & gt 'pav_market'
act 'Walk to the Commercial area (0:25)':minut += 25 & gt 'pav_commercial'
act 'Walk to the Residential area (0:30)':minut += 30 & gt 'pav_residential'
act 'Walk to the Industrial region (0:30)':minut += 30 & gt 'pav_industrial'
act 'Walk to the Five Eight estate (0:30)':minut += 30 & gt 'pav_complex', 'start'

if pushkin_walk_day ! daystart:
	act 'Take a walk (1:00)':
		cla & *nl
		minut += 60
		pcs_mood += 10
		pcs_willpwr += 1
		pushkin_walk_day = daystart
		gs 'stat'
		'You breathe in the fresh air and slowly walk along the narrow streets of the Old Town, admiring the beautiful architecture.'
		act 'Continue': gt'pushkin'
	end
end

act 'Wait':gs 'obj_din', 'wait'

!-- Random prostitution solicitation check ------------------------ 
if prostitute['blocked'] = 0: gs 'prostitution_functions', 'check_solicitation_event'


--- pushkin ---------------------------------

