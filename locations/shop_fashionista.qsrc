# shop_fashionista
!2021/04/29

$location_type = 'event'

if $ARGS[0] = 'start':
	$loc_arg = 'start'
	$loc = 'shop_fashionista'
	$menu_loc = 'shop_fashionista'
	$menu_arg = 'start'
	killvar '$locclass'
	menu_off = 0
	!!Variable for buying underwear for Natasha so it only procs once per changingroom event
	NatbelQW['underwearShop'] = 0
	
	gs 'stat'

	gs 'themes', 'indoors'
	'<center><b><font color="maroon">Fashionista</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/fashionista/fashionista.jpg"</center>'
	'This shop features many big name brands and designer clothing. Even their bags look cool.'
	'You can try on outfits via the changing room.'
	
	act 'Leave':
		minut += 3
		view
		gt 'torgcentr'
	end

	act 'View purses': minut += 5 & gt 'shop_fashionista', 'purses'
	
	if $pantyworntype ! 'none':
		act 'Enter changing room (strip to underwear)':
			*clr & cla
			'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/fashionista/underwear.jpg"</center>'
			gs 'clothing', 'strip'
			gs 'shop_fashionista', 'changingroom', 1
		end
	end

	act 'Enter changing room (strip naked)':
		*clr & cla
		'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/fashionista/nude.jpg"</center>'
		gs 'clothing', 'strip_all'
		gs 'shop_fashionista', 'changingroom', 1
	end
end

if $ARGS[0] = 'changingroom':
	$locclass = 'changingroom'
	$loc_arg = 'changingroom'
	$loc = 'shop_fashionista'
	$menu_loc = 'shop_fashionista'
	$menu_arg = 'changingroom'
	gs 'stat'
	menu_off = 0
	
	if $ARGS[1] = '':
		*clr & cla
		'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/fashionista/changingroom.jpg"</center>'
		'You are in the changing rooms in Fashionista with several outfits available to try on.'
	else
		'As you walk through the store you notice that there''s nobody in the changing rooms. Feeling a bit bored, you go look at some clothes and grab several items to try on before heading into one of the changing rooms.'
	end

	if $pantyworntype = 'none' and week = 7 and rimma['photo'] = 0:
		*clr & cla
		rimma['photo'] = 1
		rimma['day'] = daystart
		'<center><img <<$set_imgh>> src="images/characters/city/rimma/photo.jpg"</center>'
		'While in the changing room you get a strange feeling like you are being watched, thinking nothing of it you pose like you are a model. Wait was that the sound of a phone camera? You can''t see anyone and you are not going to run out and look while naked, it was probably just your imagination.'

		act 'Disregard':gt 'shop_fashionista', 'changingroom'
		exit
	end

	*nl
	gs 'shortgs', 'clothing_state'

!!	if week = 7 and rimma['photo'] = 1 and rimma['day'] ! daystart: Rimma event will start here
	
	act 'Try on dresses': gt 'shop_fashionista', 'dressingclothes'
	act 'Try on outfits': gt 'shop_fashionista', 'dressingoutfit'
	act 'Try on bra':
		if NatbelQW['seethrough'] = 0 and NatbelQW['QWstage'] > 5 and NatbelQW['underwearShop'] = 0:
			*clr & cla
			'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/fashionista/nude.jpg"</center>'
			'As you try on a particularly spicy set of see-through underwear, you notice that it doesn''t quite fit you.'
			'"This should fit Natasha perfectly though. And it only costs 750 <b>â‚½</b>." You think to yourself, imagining the look on her face if you make her wear it.'
			act 'Buy it':
				*clr & cla
				'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/fashionista/nude.jpg"</center>'
				'You put the underwear aside for now so that you can take it to the register with you.'
				money -= 750
				NatbelQW['seethrough'] = 1
				act 'Stay in the changing room': gt 'shop_fashionista', 'changingroom'
				act 'Get dressed and go back to the store': gt 'shop_fashionista', 'changingend'
			end			
			act 'Don''t buy it':
				*clr & cla
				'<center><img <<$set_imgh>> src="images/locations/city/citycenter/mall/fashionista/nude.jpg"</center>'
				'Deciding against it, you look through the rest of the underwear for something that would fit you better.'
				NatbelQW['underwearShop'] = 1
				act 'Stay in the changing room': gt 'shop_fashionista', 'changingroom'
				act 'Get dressed and go back to the store': gt 'shop_fashionista', 'changingend'
			end
		else
			gt 'shop_fashionista', 'dressingbra'
		end
	end
	act 'Try on panties': gt 'shop_fashionista', 'dressingpanties'
	if $pantyworntype = 'none':
		act 'Put on underwear':
			gs 'underwear', 'wear'
			gt 'shop_fashionista', 'changingroom'
		end
	end
	if $pantyworntype ! 'none':
		act 'Get naked':
			gs 'clothing', 'strip_all'
			gt 'shop_fashionista', 'changingroom'
		end
	end
	act 'Get dressed and go back to the store': gt 'shop_fashionista', 'changingend'
end

if $ARGS[0] = 'dressingclothes':
	*clr & cla
	$loc_arg = 'dressingclothes'
	$loc = 'shop_fashionista'
	!clorand = rand(1,ARRSIZE('fashionista'))
	!gt 'clothing', 'view_clothing_item', 'fashionista', clorand, 8000, 'shop'
	menu_off = 1
	*clr & cla
	gs'stat'
	'<center><b><font color="maroon">Viewing shop_fashionista clothing</font></b></center>'
	*nl

	gs 'clothing_QV', 'shop_filter_header'

	act 'Return':
		minut += 1
		gt 'shop_fashionista', 'changingroom'
	end

	!!To filter just this set
	clo_i = 0
	:loopfashionista_filter

		if Clothingstock >= 18:
			i = 1
		elseif Clothingstock >= 12:
			i = 2
		elseif Clothingstock >= 6:
			i = 3
		else
			i = 4
		end

		:loopfashionista2
			if fashionista[i] = 0:
				gs 'clothing_attributes', 'fashionista', i
				gs 'clothing_QV', 'shop_filter'

				!!sort by filter
				if (clothingfilter['quality'] = 0 and CloQuality = clo_i) or (clothingfilter['inhibition'] = 0 and CloInhibit = clo_i) or clothingfilter['number'] = 0:
					if clothingfilter['inc'] = 1 and clothingfilter['only'] = 1: *p '<a href="exec: i = <<i>> & gt ''shop_fashionista'', ''dressingclothes2''"><img src="images/pc/items/fashionista/dress/<<i>>.jpg" 	height="250" /></a>  '
				end
			end
			i += 4
		if i <= 200:jump 'loopfashionista2'

	clo_i += 1
	if clothingfilter['quality'] = 0 and clo_i <= 7:
		jump 'loopfashionista_filter'
	elseif clothingfilter['inhibition'] = 0 and clo_i <= 40:
		jump 'loopfashionista_filter'
	end
end

if $ARGS[0] = 'dressingclothes2':
	cla
	
	if fashionista[i] ! 0:
		msg 'You already own these clothes.'
		gt 'shop_fashionista', 'clothes'
	end

	gt 'clothing', 'view_clothing_item', 'fashionista',i, 8000, 'shop'
end

if $ARGS[0] = 'dressingoutfit':
	*clr & cla
	$loc_arg = 'dressingoutfit'
	$loc = 'shop_fashionista'
	!clorand = rand(1,ARRSIZE('fashionista'))
	!gt 'clothing', 'view_clothing_item', 'fashionista', clorand, 8000, 'shop'
	menu_off = 1
	*clr & cla
	gs'stat'
	'<center><b><font color="maroon">Viewing shop_fashionista clothing</font></b></center>'
	*nl

	gs 'clothing_QV', 'shop_filter_header'

	act 'Return':
		minut += 1
		gt 'shop_fashionista', 'changingroom'
	end

	!!To filter just this set
	clo_i = 0
	:loopfashionista_outfits_filter

	i = 1

	:loopfashionista_outfits
		if fashionista_outfits[i] = 0:
			gs 'clothing_attributes', 'fashionista_outfits', i
			gs 'clothing_QV', 'shop_filter'

			!!sort by filter
			if (clothingfilter['quality'] = 0 and CloQuality = clo_i) or (clothingfilter['inhibition'] = 0 and CloInhibit = clo_i) or clothingfilter['number'] = 0:
				if clothingfilter['inc'] = 1 and clothingfilter['only'] = 1: *p '<a href="exec: i = <<i>> & gt ''shop_fashionista'', ''dressingoutfit2''"><img src="images/pc/items/fashionista/outfits/<<i>>.jpg" 	height="250" /></a>  '
			end
		end
		i += 1
	if i <= 20:jump 'loopfashionista_outfits'

	clo_i += 1
	if clothingfilter['quality'] = 0 and clo_i <= 7:
		jump 'loopfashionista_outfits_filter'
	elseif clothingfilter['inhibition'] = 0 and clo_i <= 40:
		jump 'loopfashionista_outfits_filter'
	end
end

if $ARGS[0] = 'dressingoutfit2':
	cla
	
	if fashionista_outfits[i] ! 0:
		msg 'You already own these clothes.'
		gt 'shop_fashionista', 'dressingclothes'
	end

	gt 'clothing', 'view_clothing_item', 'fashionista_outfits',i, 8000, 'shop'
end

if $ARGS[0] = 'dressingbra':
	$loc_arg = 'dressingbra'
	$loc = 'shop_fashionista'
	menu_off = 1
	*clr & cla
	gs'stat'

	act 'Return':
		cla
		minut += 1
		gt 'shop_fashionista', 'changingroom'
	end

	i = 1

	:loopfashionista_bras2
		if fashionista_bras[i] = 0:*p '<a href="exec: i = <<i>> & gt ''shop_fashionista'', ''dressingbras2''"><img src="images/pc/bras/fashionista/<<i>>.jpg" 	height="150" /></a> '
		i += 1
	if i <= 42:jump 'loopfashionista_bras2'
end

if $ARGS[0] = 'dressingbras2':
	cla
	
	if fashionista_bras[i] ! 0:
		msg 'You already own this bra.'
		gt 'shop_fashionista', 'bras'
	end

	gt 'bras', 'view_bra_item', 'shop', 'fashionista',i, 750
end



if $ARGS[0] = 'changingend':
	!! Sveta dresses again
	gs 'underwear', 'wear'
	gs 'clothing', 'wear_last_worn'
	gt 'shop_fashionista', 'start'
end

if $ARGS[0] = 'dressingpanties':
	$loc_arg = 'dressingpanties'
	$loc = 'shop_fashionista'
	menu_off = 1
	*clr & cla
	gs'stat'

	act 'Return':
		cla
		minut += 1
		gt 'shop_fashionista', 'changingroom'
	end

	i = 1

	:loopfashionista_panties2
		if fashionista_panties[i] = 0:*p '<a href="exec: i = <<i>> & gt ''shop_fashionista'', ''dressingpanties2''"><img src="images/pc/panties/fashionista/<<i>>.jpg" 	height="150" /></a> '
		i += 1
	if i <= 79:jump 'loopfashionista_panties2'
end

if $ARGS[0] = 'dressingpanties2':
	cla
	
	if fashionista_panties[i] ! 0:
		msg 'You already own these panties.'
		gt 'shop_fashionista', 'dressingpanties'
	end

	gt 'panties', 'view_panty_item', 'shop', 'fashionista',i, 750
end

!!only items not in dressing room
if $ARGS[0] = 'purses':
	$loc_arg = 'purses'
	$loc = 'shop_fashionista'
	menu_off = 1
	*clr & cla
	gs'stat'

	act 'Return':
		cla
		minut += 1
		gt 'shop_fashionista', 'start'
	end

	i = 1

	:loopfashionista_purses
		if fashionista_purses[i] = 0:*p '<a href="exec: i = <<i>> & gt ''shop_fashionista'', ''purses2''"><img src="images/pc/items/fashionista/purses/<<i>>.jpg" 	height="150" /></a> '
		i += 1
	if i <= 20:jump 'loopfashionista_purses'
end

if $ARGS[0] = 'purses2': gt 'purses', 'view_purse_item', 'shop', 'fashionista',i, 3000

!!Not available yet. This part needs images for most scenes, second part (not triggered here) changed to candid photo from old dog sex blackmail to trigger
if $ARGS[0] = 'rima':
	*clr & cla
	menu_off = 1
	minut += 5
    $location_type = 'event'
	gs 'stat'
	'Turning your head, you think nobody noticed, but your actions have attracted the saleswoman. The seller is a young girl in a little-too-narrow shirt with the logo of the Boutique, it''s not enough that her shirt was so tight, but she had to unbutton the top button as well (You think if it was a woman''s shirt it would be okay, but since it''s a man you wonder what the administration must be thinking.) she came up to you a little bit uncomfortably and quickly asked:'
	'"Can I help you with anything?" the seller asks.'

	act 'No, thank you':gt 'moncheri', 'start'
	act 'Silently leave': minut += 8 & gt 'city_center'

	act 'Stare at her breasts':
		*clr & cla
		gs 'stat'
		'Seeing that the she is a novice saleswoman and that you are most likely her first client, you decided to go for it. Just as you opened your mouth to say some harsh sentence, your eyes came to rest on her chest. The bra together with the shirt successfully emphasized her breasts, and even though only the upper part of her chest was visible, your imagination filled in the rest. You wonder what her nipples are like The thought crossed your mind suprisingly'
		'"Yes, you can give me a few minutes of your time, "You said.'
		'Your words make an impression on the saleswoman, and she blushes slightly embarassed.'
		'"Yes, yes, yes - often we ask: what dress color do you like? shape of the neckline of the dress? do you want more freedom to move? or turn? do you want to highlight your perfect figure?"'
		'"Stop, stop, - you''re a person and not a stranger, first tell me what you would like me to call you" Your words make an impression on the girl, she is flattered by your words. '
		'"Irina" she says, before correcting herself "Saleswoman Rima."'
		'"May I call you Rimma?" You ask."I''m <<$pcs_nickname>>".'
		'"Yes, of course, if you''d like.", responds Rima, smiling shyly.'

		gs 'arousal', 'erotic', 5
		gs 'stat'

		act 'Choose a dress with Rimma':
			*clr & cla
			minut += 5
			gs 'stat'

			if pcs_hairbsh = 1 and pcs_makeup > 1 and pcs_leghair <= 0 and pcs_sweat < 20 and $clothingworntype = 'moncheri':
				'You choose some things  and evaluate them together with Rima while hanging out a bit.'
				'"Rima, where is the dressing room?" You ask suddenly.'
				'Come with me - Rima didn''t notice that the invisible barrier between you is lost: the seller and the buyer.'

				act 'Go inside the dressing room':
					*clr & cla
					minut += 5
					gs 'stat'
					'You and Rima went together into the dressing room.'

					act 'You do not like the dress':
						*clr & cla
						minut += 5
						gs 'stat'
						'As a result, you spent time together, while in your head you wonder about Rima, and her mysterious and curious body.'
						'As you go home you suddenly feel eyes on you'

						act 'Ignore': minut += 8 & gt 'city_center'

						act 'Look around':
							*clr & cla
							minut += 5
							gs 'stat'
							'You start to turn your head, looking around sharply.'
							'You notice that a girl is looking at you.'

							act 'Approach her':
								*clr & cla
								minut += 5
								gs 'stat'
								'You silently move towards her and give her a look'
								nl
								'"Good afternoon," you announce. "Do I know you?"'
								'"Good day," she says, "Do you not remember me?"'
								'The girl does not respond to your sharpness'
								'"In principle, there is no way out of the situation that you had?" the girl answers'
								'"What is the situation where when?" You will not hide anger.'
								'"Yes, in the vacant lot where you were having ''fun'' with a dog" girl replied with a grin.'

								act 'Yes you ohereli! (Go)': minut += 8 & gt 'city_center'

								act 'Extort taken in head? What do you want?':
									*clr & cla
									minut += 5
									QWdogReiq = 2
									QWdogreiqTimer = 7
									gs 'stat'
									'"So you decided to blackmail me? And how much do you want?" You spit viciously '
									'Again, your words fail to disturb her. She cocks her head to the side, and asks "Is your tantrum over?"'
									'You tell her irritably that you have not even begun to get angry.'
									'"No, you''re still upset let''s meet next week." The girl says suddenly.'
									'"I guess..." you say, shaking your head.'
									'"That''s fine, my name is Margaret" she says, looking at you expectantly.'
									'"<<$pcs_nickname>>" you reply.'
									'"I''m looking forward to our meeting." she says as she leaves.'

									act 'Leave': minut += 8 & gt 'city_center'
								end
							end
						end
					end

					act 'Take your panties off in front of Rimma':
						*clr & cla
						minut += 5
						gs 'stat'
						'You remove your panties in front Rimma and noticed how her cheeks glowed red, and her breathing quickened.'

						act 'Do you like me?':
							*clr & cla

							if RimmaSex = 0:RimmaSex = 1 & girl += 1

							rimmaday = daystart
							gs 'stat'
							'<center><img <<$set_imgh>> src="mod/bestmod/images/beast/zoo/primer.jpg"></center>'
							'"Rimma, do you like me?" you ask quietly'
							'"yes, very much" Rimma answers excitedly'
							'"Then lick me" you demand'
							'Rimma kneels down and begins to kiss your body, not very skillfully at first, starting by timidly kissing your belly then licking and covering all of your body as she grew more confident. Rimma successfully made your pussy very wet. She starts kissing and licking your crotch little by little, then bites at it. The unexpectedness makes the body flow into a sweet languor. '
							'You smell of sex, Rimma looks up at you from between your legs for a second'
							'After putting her right palm gently on the pubis, Rimma fingers spread your wet lips, poking out her tongue and running it inside.'
							'The soft reciprocating action of the tongue on the clitoris excits you and causes a mild spasm. Rimma suddenly begins to press her tongue on your clitoris. Your body is already weak and no longer obeys your brain, only trying not moan load enough to alert the whole store.'
							$orgasm_or = 'custom'
							$orgasm_txt = 'Suddenly, Your body begins to shake and is covered in a soft wave of spasms, with the last spasm you cum. You let out a loud moan while falling to your knees.'
							'You collapse to the floor, exhausted with pleasure.'

							gs 'arousal', 'cuni', 10, 'dom', 'lesbian'
							gs 'arousal', 'vaginal_finger', 10, 'dom', 'lesbian'
							minut -= 10
							gs 'arousal', 'end'
							gs 'stat'

							act 'leave the dressing room':
								*clr & cla
								minut += 5
								QWdogReiq = 2
								QWdogreiqTimer = 7
								gs 'stat'
								'Rimma walked you to the door.'
								'"<<$pcs_nickname>>, did you like it?" Rimma asked, blushing.'
								'"What do you think? I want more, Do you want to continue?"'
								'"No, I can''t, my parents are waiting at home" Rimma tells you, looking upset. '
								'"Then how about next week?" you offer'
								'"let''s do it" Rimma looks very happy.'
								'You head home, on the way you hear a woman''s voice behind you:'
								'"Goodnight" said the strange woman.'
								'Looking closely you recognize her as girl at the vacant lot.'
								'"Hi" you cheerfully answered, "was it you that was watching me the entire time?"'
								'"Yes," she smiled in response. "I saw you in the boutique, but I didn''t dare to approach you at that time, so I decided to wait outside for you."'
								'"But why did you wait?"'
								'"Now it''s too late, Meet me next week so we can talk" She looks at you, waiting for your response'
								'"Let''s meet then," You agree"By way I''m <<$pcs_nickname>>"'
								'"Margaret" She responds'

								act 'Leave': minut += 8 & gt 'city_center'
							end
						end
					end
				end
			else
				'You and Rimma search for a dress, but couldn''t find one. You leave and Rimma is a little sad in saying goodbye to you.'
				'You think it might help keep her attention if you make a real effort with your appearance and wear one of the nce outfits from here.'

				act 'Leave': minut += 8 & gt 'city_center'
			end
		end
	end
end

--- shop_fashionista ---------------------------------

