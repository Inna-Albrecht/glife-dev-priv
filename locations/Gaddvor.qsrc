# Gaddvor

if $ARGS[0]='':
	music_loop = 0
	$metka = ''
	$loc = 'Gaddvor'
	$location_type = 'public_outdoors'
	$menu_loc = 'Gaddvor'
	$menu_arg = ''
	menu_off = 0
	killvar '$locclass'
	gs 'stat'
	CLOSE ALL
	gs 'gadukino_event', 'sound'
	if hour >= 6 and hour < 22:
		if grandma_trouble = 10:
			*clr & cla
			menu_off = 1
			killvar 'grandma_trouble'
			'<center><h4>Your grandparents'' yard</h4></center>'
			'<center><img <<$set_imgh>> src="images/locations/gadukino/grandparents/grandma_trouble.jpg"></center>'
			'Suddenly your grandmother approaches you with disappointed look on her face. "I had higher expectations of you young lady! You should be ashamed of yourself running around outdoors without clothes. You have embarrassed me AND your grandfather! Unlike you, we have to live in this village, and with the people in it. If you keep this up, I''ll be cutting your time here short and sending you back to your mother, and she can deal with your sinful behaviour. Go and get dressed immediately!" She then walks off. You are too ashamed to say anything.'
			act 'Continue': gt 'Gaddvor'
			exit
		end
	end
	clothesAtLocation = FUNC('lost_clothes_here',$CURLOC)
	if clothesAtLocation = 1:
		if $clothingworntype = 'nude':
			act 'Get dressed':
				gs 'clothing', 'recover_lost_clothes', 'Gaddvor', 1
				gs 'underwear', 'wear'
				gt 'Gaddvor'
			end
		else
			act 'Pick up your clothes':
				gs 'clothing', 'recover_lost_clothes', 'Gaddvor'
				'You picked up your clothes.'
				gt 'Gaddvor'
			end
		end
	end

	if StoryLine = 1:
		'<center><h4>Your grandparents'' yard</h4></center>'
	else
		'<center><h4>Old Couple''s yard</h4></center>'
	end
	if month >= 4 and month <= 10:
		if hour >= 6 and hour < 22:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/dvor.jpg"></center>'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/dvor_night.jpg"></center>'
		end
	else
		if hour >= 8 and hour < 18:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/dvor_winter.jpg"></center>'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/dvor_winter_night.jpg"></center>'
		end
	end
	if StoryLine = 1:
		'A fenced yard behind your grandparents'' house. The yard is surrounded by a small barn packed with junk and a separate building that is used as a banya where you can take a bath. On the back porch are a few chairs to sit in.'
	else
		'A fenced yard behind the house. The yard is surrounded by a small barn packed with junk and a separate building that is used as a banya where people take a bath. On the back porch are a few chairs to sit in.'
	end


	if StoryLine = 1:
		act 'Enter your grandparents'' house':minut += 1 & gt 'Gadhouse'
		act 'Enter the banya':minut += 1 & gt 'Gadbana'
	end

	if $clothingworntype = 'nude':
		*nl
		'You are naked and can only go into your grandparent''s house to get dressed or go to the banya.'
	else
		if feed_chickens = 1 or (hour >= 6 and hour < 20 and month >= 4 and month <= 10):'In the yard there are <a href="exec:gt ''Gaddvor'',''chickens''">chickens</a> running around.'

		if mira_guest = 1: Miraloc = 7 & 'Your friend <a href="exec: gt ''Miroslava'', ''Start''">Mira</a> stands next to you.'

		act 'Go to the barn':minut += 1 & gt 'Gadsarai'
		act 'Go to the garden':minut += 1 & gt'Gadgarden','garden'
		act '<b>Go to Gadukino</b>':minut += 5 & gt 'gadukino'

		mira_temp = rand(1,5)
		if mira_temp = 3 and hour >= 9 and hour <= 19 and npc_rel['A60'] >= 15 and Mira_no = 0 and sunWeather = 1 and mira_guestday ! daystart and (npc_QW['A63'] < 13 or npc_QW['A60'] >= 10 or mirasex > 1):
			*clr & cla
			menu_off = 1
			gs 'Miroslava', 'miraclothes'
			'You went out into the yard and noticed Mira standing there.'
			'"Oh, <<$pcs_nickname>> hi, hope you don’’t mind me visiting. I was bored being home alone." she said smiling.'
			mira_guestday = daystart
			minut += 5
			mira_guest = 1
			gs 'stat'

			act 'Further':gt'Gaddvor'
		end
	end
end

if $ARGS[0] = 'chickens':
	*clr & cla
	'<center><img <<$set_imgh>> src="images/locations/gadukino/village/chickens1.jpg"></center>'
	'Ordinary chickens clucking about their pen.'
	minut += 1
	gs 'stat'

	!job Granny
	if feed_chickens = 1:
		act 'Feed the chickens':
			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/chickens2.jpg"></center>'
			'You decide to feed the chickens. You pick up a bucket full of bird''s seed and grain mix and toss it on the ground. They immediately rush over and start to greedily eat. You watch them for a bit before you go about your business.'
			*clr & cla
			minut += 30
			grandmahelp += 1
			feed_chickens = 0
			hndiwrk_exp += rand(0,3)
			gs 'stat'

			act 'Finish':gt'Gaddvor'
		end
	end
	act 'Leave': gt'Gaddvor'
end

--- Gaddvor ---------------------------------

