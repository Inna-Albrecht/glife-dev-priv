# city_redlight


if $ARGS[0] = 'start':
	$loc_arg = 'start'
	$loc = 'city_redlight'
	$region = 'city'
	$location_type = 'public_outdoors'
	$menu_loc = 'city_redlight'
	$menu_arg = 'start'
	menu_off = 0
	CLOSE ALL
	if sound = 0:
		if month >= 11 and month <= 12 or month >= 1 and month <= 3:
			if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3', 30 & PLAY 'sound/street7.mp3', 30 & PLAY 'sound/zima.mp3', 30
			if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3', 30 & PLAY 'sound/zima.mp3',30
		else
			if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3', 30 & PLAY 'sound/street7.mp3', 30 & PLAY 'sound/street_leto.mp3', 30
			if hour >= 0 and hour <= 7: PLAY 'sound/street_night_leto.mp3' & PLAY 'sound/street_night.mp3', 30
		end
	end

	gs 'stat'
	'<center><h2>St. Petersburg</h2></center>'
	'<center><b>Red Light District</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/redlight/redlight.jpg"></center>'
	
	'This small area inside the Industrial District is home to a number of strip clubs and sex shops.'
	if hour >= 18 or hour < 3:
		'The bright lights and booming music of the <a href="exec:gt ''stripclub'', ''start''">Fuzzy Dice</a> strip club attract men from all over the city.'
	else
		'The strip club is closed for the day and will reopen at 18:00.'
	end
	if pornstudio >= 1 and hour >= 10 and hour < 22:
		'A <a href="exec:gt ''pornstudio'', ''start''">porn studio</a> is housed within one of the few warehouses that hasn''t been abandoned.'
	elseif pornstudio >= 1 and (hour < 10 or hour >= 22):
		'The porn studio is closed and will reopen at 10:00.'
	end
	if hour < 3 or hour >= 11: 
		'A <a href="exec:minut += 1 & gt ''sexshop'', ''start''">seedy looking shop</a> that sells sex toys and other erotic merchandise is open for perverts from all around the city to browse its stock.'
	else
		'A seedy looking shop that sells sex toys and other erotic merchandise. It is currently closed and will reopen at 11:00.'
	end
	if bordel > 0 and func('bordel', 'is_open'): 'Inside the courtyard is the entrance to the <a href="exec:gt ''bordel''">brothel</a>.'
	if hour >= 10: 
		'You see a small <a href="exec:gt ''city_redlight'',''city_copy_shop''">printing shop</a> tucked away between two larger buildings.'
	else
		'You see a small printing shop tucked away between two larger buildings. It is currently closed and will open at 10:00.'
	end
		if hour >= 19 or hour < 2:
		'A <a href="exec:gt ''hookahlounge'', ''start''">Hookah Lounge</a> sits on a corner, with Indian music and the scent of exotic spices streaming from an open door.'
	else 
		'The Hookah Lounge is currently closed and will reopen at 19:00.'
	end
	
	act 'Walk to the Industrial Region (0:05)': minut += 5 & gt 'city_industrial'

	act 'Wait': gs 'obj_din', 'wait'
	
	gs 'taxi'

	if car > 0:
		if wrek = 0 and ((cardrive = 7 and func('autoservisF', 'is_open') = 0) or (cardrive = 9 and func('autotraidF', 'is_open') = 0)):
			'It looks like the ' + iif(cardrive = 7, 'repair shop', 'car dealership') + ' moved your car outside before they closed.'
			cardrive = 3
		end
		if cardrive = 3 or cardrive = 6 or (cardrive = 7 and func('autoservisF', 'is_open')) or (cardrive = 9 and func('autotraidF', 'is_open')) or cardrive = 27:
			'Your <a href="exec:' + iif(cardrive = 27, '', 'minut += 1 & ') + 'gs ''carF'', ''start''"><<$car>></a> sits in the parking lot.'
		end
	end

	if hour >= 20 or hour <= 4:
		*nl
		'There are prostitutes working near here.'
		if pcs_makeup > 2 and PCloStyle = 4 and pcs_inhib >= 40:
			act 'Prostitute yourself': prst_loc = 1 & gt 'street_walker', 'work'
		elseif pcs_inhib >= 40:  
			*nl
			'You could work as a prostitute, but you need to wear appropriate clothes and more than a little bit of makeup.'
		else
			*nl
			'You could work as a prostitute, but you aren''t comfortable with the idea.'
		end
	end
end

if $ARGS[0] = 'city_copy_shop':
	*clr & cla
	menu_off = 1
	minut += 1
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/redlight/bm_seller.jpg"></center>'
	'You enter the run-down looking copy shop and look around. Scattered across the room are small tables with various dusty looking copy machines that don''t strike you as being very reliable. Watching you from behind a counter is a bored looking man with greasy hair and a sleazy look.'
	if fakepassport = 2:
		!!only notify player 1 time after events triggered
		fakepassport = 0
		'He gives you a hard stare. "A fake passport will cost you 30,000 <b>₽</b>," he says quietly.'
	end
	if university['fakediplom'] = 2:
		!!only notify player 1 time after events triggered
		university['fakediplom'] = 0
		'"A diploma for a bachelor''s degree will cost you 100,000 <b>₽</b>," he says quietly.'
	end
	act 'Leave': gt 'city_redlight', 'start'
	if age < 18 and fakepassport ! 1:
		if money => 30000:
			act 'Buy fake passport (30,000 <b>₽</b>)':
				*clr & cla
				minut += 60
				money -= 30000
				fakepassport = 1
				'<center><img <<$set_imgh>> src="images/locations/city/redlight/bm_seller.jpg"></center>'
				'He tells you to come in the back and takes a quick photo of your face, then tells you to wait out front until he''s done. About an hour goes by before he steps back out and hands you the passport, which shows your face but has an older age printed on it.'
				act 'Leave': gt 'city_redlight', 'start'
			end
		else
			act 'Buy fake passport (<font color="red">30,000 <b>₽</b></font>)': '<br><font color="red">You don''t have enough money to buy the fake passport.</font>'
		end
	end
	if university['diploma'] = 0 and university['fakediplom'] = 0:
		if money => 100000:
			act 'Buy fake diploma (100,000 <b>₽</b>)':
				*clr & cla
				minut += 60
				money -= 100000
				university['fakediplom'] = 1
				'<center><img <<$set_imgh>> src="images/locations/city/redlight/bm_seller.jpg"></center>'
				'You buy a fake diploma.'
				act 'Leave': gt 'city_redlight', 'start'
			end
		else
			act 'Buy fake diploma (<font color="red">100,000 <b>₽</b></font>)': '<br><font color="red">You don''t have enough money to buy the fake diploma.</font>'
		end
	end
end

--- city_redlight ---------------------------------
