# city_redlight


if $ARGS[0] = 'start':
	$loc_arg = 'start'
	$loc = 'city_redlight'
	$region = 'city_redlight'
	$location_type = 'public_outdoors'
	$menu_loc = 'city_redlight'
	$menu_arg = 'start'
	menu_off = 0
	CLOSE ALL
	if sound = 0:
		if month >=11 and month <= 12 or month >=1 and month <=3:
			if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street7.mp3',30 & PLAY 'sound/zima.mp3',30
			if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
		else
			if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street7.mp3',30 & PLAY 'sound/street_leto.mp3',30
			if hour >= 0 and hour <= 7: PLAY 'sound/street_night_leto.mp3' & PLAY 'sound/street_night.mp3',30
		end
	end

	gs 'stat'
	'<center><h2>St. Petersburg</h2></center>'
	'<center><b>Red Light District</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/redlight/redlight.jpg"></center>'
	
	'This small area inside the Industrial District has lots of strip clubs and sex shops. The Porn studio also operates out of this district.'
	if hour >= 18 or hour < 2:
		'The <a href="exec:gt ''stripclub'', ''start''">Strip Clubs</a> red lights, attracting men from all over the city.'
	else
		'The <a href="exec:msg ''The Strip Club is closed for the day and will reopen at 18:00''">Strip Clubs</a> red lights, attracting men from all over the city.'
	end
	if pornstudio >= 1 and hour >= 10 and hour < 22:
		'At the <a href="exec:gt ''pornstudio'', ''start''">Porn Studio</a>, where a variety of adult movies are created.'
	elseif pornstudio >= 1 and (hour < 10 or hour >= 22):
		'At the <a href="exec:msg ''The Porn Studio is closed for the day and will reopen at 10:00''">Porn Studio</a>, where a variety of adult movies are created.'
	end
	if hour < 3 or hour >= 11: 
		'A shop for <a href="exec:minut += 5 & gt ''sexshop'', ''start''">Sex Toys</a> is open for perverts from all around the city to browse its stock.'
	else
		'A shop that sells sex toys. It is currently closed and will open at 11:00.'
	end
	if bordel > 0 and func('bordel', 'is_open'):'Inside the courtyard is the entrance to the <a href="exec:gt ''bordel''">brothel</a>.'
	if hour >= 10: 
		'You see a small <a href="exec:gt ''city_redlight'',''city_copy_shop''">printing shop</a> tucked away between two larger buildings.'
	end

	act 'Return to the Industrial Region (0:05)': minut += 5 & gt 'city_industrial'

	act 'Wait':gs 'obj_din', 'wait'
	
	gs 'taxi'

	if car > 0:
		if wrek = 0 and ((cardrive = 7 and func('autoservisF', 'is_open') = 0) or (cardrive = 9 and func('autotraidF', 'is_open') = 0)):
			'It looks like the ' + iif(cardrive = 7, 'repair shop', 'car dealership') + ' moved your car outside before they closed.'
			cardrive = 3
		end
		if cardrive = 3 or cardrive = 6 or (cardrive = 7 and func('autoservisF', 'is_open')) or (cardrive = 9 and func('autotraidF', 'is_open')) or cardrive = 27:
			'In the parking stands your <a href="exec:' + iif(cardrive = 27, '', 'minut += 5 & ') + 'gs ''carF'', ''start''"><<$car>></a>.'
		end
	end

	if hour >= 20 or hour <= 4:
		*nl
		'There are prostitutes working in places near here.'
		if pcs_makeup > 2 and PCloStyle = 4 and pcs_inhib >= 40:
			act 'Prostitute yourself': gt 'street_walker', 'work'
		elseif pcs_inhib >= 40:  
			*nl
			'You could work as a prostitute, but you need to wear appropriate clothes and more than a little bit of makeup.'
		else
			*nl
			'You could work as a prostitute, but you aren''t comfortable with the idea.'
		end
	end
end

if $ARGS[0] = 'city_copy_shop':
	*clr & cla
	menu_off = 1
	minut += 1
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/redlight/bm_seller.jpg"></center>'
	'You enter the run-down looking copy shop and look around. Scattered across the room are small tables with various dusty looking copy machines that don''t strike you as being very reliable. Watching you from behind a counter is a bored looking man with greasy hair and a sleazy look.'
	if fakepassport = 2:
		!!only notify player 1 time after events triggered
		fakepassport = 0
		'He looks at you hard before saying in a quiet voice, "A passport with a fake age will cost you 30,000 <b>₽</b>"'
	end
	if func('uniutil', 'fake_diploma', 'aware_needed'):
		!!only notify player 1 time after events triggered
		gs 'uniutil', 'fake_diploma', 'reset'
		'He tells you "A diploma for a bachelor''s degree will cost you 100,000 <b>₽</b>"'
	end
	act 'Leave': gt 'city_redlight', 'start'
	if age < 18 and fakepassport ! 1:
		if money => 30000:
			act 'Buy fake passport (30,000 <b>₽</b>)':
				*clr & cla
				minut += 60
				money -= 30000
				fakepassport = 1
				'<center><img <<$set_imgh>> src="images/locations/city/redlight/bm_seller.jpg"></center>'
				'He tells you to come in the back and takes a quick photo of your face, then tells you to wait out out front until he''s done. About an hour goes by before he steps back out and hands you the passport, which shows your face but has an older age printed out on it.'
				act 'Leave': gt 'city_redlight', 'start'
			end
		else
			act 'Buy fake passport (<font color="red">30,000 <b>₽</b></font>)': '<br><font color="red">You don''t have enough money to buy the fake passport.</font>'
		end
	end
	if func('uniutil', 'appear_to_have_graduated') = 0:
		if money => 100000:
			act 'Buy fake diploma (100,000 <b>₽</b>)':
				*clr & cla
				minut += 60
				money -= 100000
				gs 'uniutil', 'fake_diploma', 'set_obtained'
				'<center><img <<$set_imgh>> src="images/locations/city/redlight/bm_seller.jpg"></center>'
				'You bought a fake diploma.'
				act 'Leave': gt 'city_redlight', 'start'
			end
		else
			act 'Buy fake diploma (<font color="red">100,000 <b>₽</b></font>)': '<br><font color="red">You don''t have enough money to buy the fake diploma.</font>'
		end
	end
end

--- city_redlight ---------------------------------
