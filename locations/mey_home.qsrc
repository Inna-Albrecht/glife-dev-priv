# mey_home

close all
gs 'themes', 'indoors'
$location_type = 'indoors'

!! corporate only at the end of the month on weekends, check
if day <= 25 and mey_vika['mey_vika_qw'] > 40: mey_vika['mey_vika_qw'] = 40

if $args[0] = 'door':
	$loc_arg = 'door'
	$loc = 'mey_home'
	$menu_loc = 'mey_home'
	$menu_arg = 'door'
	menu_off = 0
	cls
	gs 'vicky_meynold_schedule'
	gs 'katja_meynold_schedule'
	gs'stat'
	'<center><h4><font color="maroon">Meynold''s house</font></h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/meynolds/mey_home.jpg"></center>'
	*nl

	if mey_vika['key'] = 1:
		'You open the door with your key and enter the house.'

		if frost > 0: frost = 0
		act 'Enter': gt 'mey_home', 'start'
	elseif hour < 20 and ((week < 6 and hour >= 15) or (week >= 6 and hour > 10)) and mey_vika['mey_vika_qw'] >= 27:
		if mey_vika['mey_vika_qw'] = 27: 
			gt 'mey_tamara_events', 'next_meet'
		else
			*clr
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/meynolds/tamara_tea1.jpg"></center>'
			*nl
			'You walk up to the Meynolds'' residence and ring the doorbell...'
			'After a short wait, you hear the lock click, and you see Katja''s mother, Tamara and sister, Vicky standing by the door' 
			'"Oh, hi, <<$pcs_nickname>>, welcome! Come in, make yourself at home."'

			if frost > 0: frost = 0
			act 'Enter': minut += 1 & gt 'mey_home', 'start'
		end
	elseif locat['katja'] = 7:
		cla
		'You walk up to the Meynolds residence and ring the doorbell...'
		'Katja opens the door and quickly drags you to her room.'

		act 'Go to Katjas room':
			if frost > 0: frost = 0
			gt 'KatjaHomeTalk'
		end
	else
		*clr
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/meynolds/tamara_tea0.jpg"></center>'
		'You walk up to the Meynolds'' residence and ring the doorbell...'
		'No one opens the door. It looks like nobody''s home, or they are all asleep.'
		'Maybe you should try again at a more sociable time?'

		act 'Leave': minut += 5& gt 'pav_residential'
	end
end

! hall
if $ARGS[0] = 'start':
	$loc_arg = 'start'
	$loc = 'mey_home'
	$menu_loc = 'mey_home'
	$menu_arg = 'start'
	menu_off = 0
	killvar '$locclass'
	cls
	gs 'vicky_meynold_schedule'
	gs 'katja_meynold_schedule'
	gs'stat'
	'<center><h4><font color="maroon">Hall</font></h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/meynolds/corridor.jpg"></center>'
	*nl
	'A large and bright hall, this "hallway" stretches for quite a bit. You''ve never seen anything like this before! There is a mirror covering half of one of the walls. You see the kitchen from the hallway. There''s a big oval dining table.'
	'There are stairs leading to the second floor. You can also see two doors where the signs say "Toilet" and "Bathroom".'

	act 'Leave the house': minut += 5 & gt 'pav_residential'

	if mey_vika['mey_vika_qw'] = 30 and mey_vika['qw_day'] ! daystart and locat['Vicky'] = 15: gs 'mey_vika_events', 'case_gift'
	if mey_vika['mey_vika_qw'] >= 32 and mey_vika['gym_day'] ! daystart and locat['Vicky'] = 12: gs 'mey_vika_events', 'gym0'
	if mey_vika['mey_vika_qw'] >= 32 and mey_vika['gym_day'] ! daystart and locat['Vicky'] = 13: gs 'mey_vika_events', 'gym1'
	if mey_vika['mey_vika_qw'] >= 32 and mey_vika['mey_vika_qw'] < 35 and mey_vika['qw_day'] ! daystart and locat['Vicky'] = 15: gs 'mey_vika_events', 'learning0'

	if mey_vika['mey_vika_qw'] >= 32:
		! welcome to the corporate
		if week = 7 and hour >= 12 and hour <= 16 and day > 25 and mey_vika['mey_vika_qw'] = 40 and mey_tamara['qw_end'] = 0 and fame['pav_slut'] < 150:
			gt 'mey_tamara_events', 'party0'
		end
		! check appearance before going to corporate
		if week = 7 and (hour = 17 or hour = 18) and mey_vika['mey_vika_qw'] = 41:
			'<br><a href="exec: gt''mey_tamara_events'',''check_look''">Tamara Meynold.</a> is waiting for you in the lobby.'

			exit
		end

		! Vic calls in the shower
		if mesec = 0 and stat['think_virgin'] = 0 and mey_vika['bath_qw'] = 1 and mey_vika['bath_qw_day'] ! daystart and rand(0,1) = 0 and locat['Vicky'] = 9:
			gs 'mey_vika_events', 'bath_qw1'
			exit
		end

		if ((week < 6 and hour >= 14 and hour < 20) or (week = 6 and hour >= 10 and hour < 20)) and mey_tamara['help_day'] ! daystart:
			act 'Clean the house':
				menu_off = 1
				cla
				mey_tamara['help_day'] = daystart
				mey_tamara['help_count'] += 1
				minut += rand(50,70)
				gs 'sweat', 'add', 3
				pcs_mood -= 5
				gs 'stat'
				if mey_vika['bath_qw'] = 0 and mey_vika['bath_qw_day'] ! daystart:
					gs 'mey_vika_events', 'bath_qw0'
				else
					'<br>You take out a hefty vacuum cleaner from the closet, and it takes you about an hour to vacuum the house.'

					act 'Finish': gt 'mey_home', 'start'
				end
			end
		end
	end
	act 'Kitchen': minut += 1 & gt 'mey_home', 'kitchen'
	act 'Bathroom': minut += 1 & gt 'mey_home', 'bathroom'
	act 'Guest room': minut += 1 & gt 'mey_home', 'guest_bedroom'
	act 'Lounge': minut += 1 & gt 'mey_home', 'lounge'
	act 'Katja''s room': minut += 1 & gt 'mey_home', 'katja_bedroom'
	act 'Vicky''s room': minut += 1 & gt 'mey_home', 'vicky_bedroom'
	act 'Tamara Meynold''s room': minut += 1 & gt 'mey_home', 'tamara_bedroom'
	act 'Garden': minut += 1 & gt 'mey_home', 'garden'
	if func('homes_properties', 'is_current_home') = 0 and func('homes_properties', 'can_live_here'):
		act 'Set this household as your main home':gs 'homes_properties', 'set_home' & gt 'mey_home', 'start'
	end

	!!if rand (0,2) ! 0:gs 'din_bad' & dynamic $mobile_check
end

! kitchen
if $ARGS[0] = 'kitchen':
	*clr & cla
	$loc = 'mey_home'
	$loc_arg = 'kitchen'
	$locclass = 'kitr'
	$menu_loc = 'mey_home'
	$menu_arg = 'kitchen'
	menu_off = 0
	gs 'kit_din'
	gs 'stat'
	'<center><h4><font color="maroon">Kitchen</font></h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/meynolds/kitchen.jpg"></center>'
	*nl
	'The kitchen is vast and luxurious. It is decorated in a minimalist high-tech style. An oval dining room table stands in the middle of the room. The appliances are all hidden. As a result, the kitchen is sparkling clean.'

	act 'Return to hallway': minut += 1 & gt 'mey_home', 'start'

	if mey_vika['mey_vika_qw'] >= 32 and ((week < 6 and hour >= 14 and hour < 20) or (week = 6 and hour >= 10 and hour < 20)) and mey_tamara['help_day'] ! daystart:
		act 'Cook for the whole family':
			menu_off = 1
			cla
			mey_tamara['help_day'] = daystart
			mey_tamara['help_count'] += 1
			minut += rand(50,70)
			gs 'stat'
			'<br>You take out the food from the refrigerator and start cooking for the whole family. It takes you about an hour to finish.'

			act 'Finish': gt 'mey_home', 'kitchen'
		end
	end

	act 'Eat a hearty meal (0:20)':gs 'food', 'l_meal'
	act 'Eat a normal meal (0:15)':gs 'food', 'm_meal'
	act 'Eat a light meal (0:10)':gs 'food', 's_meal'

	dynamic $edasnack
	dynamic $sandwich
	dynamic $driwater
	dynamic $dritea
	dynamic $fill_bottle
	dynamic $edaD
	dynamic $pranik
	dynamic $fatdel
	dynamic $lekarstvo
	dynamic $vitamin

	gs 'events', 'read'

end

if $ARGS[0] = 'bathroom':
	!bathroom
	$locM = 'mey_home'
	$locM_arg = 'bathroom'
	$loc_arg = 'bathroom'
	$menu_loc = 'mey_home'
	$menu_arg = 'bathroom'
	menu_off = 0
	$location_type = 'bathroom'
	$bathtype = 'glass shower and tub'
	cls
	gs'stat'
	'<center><b><font color = maroon>Bathroom</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/hotel/hotel.room.best1.jpg"></center>'
	*nl
	'A luxurious bathroom looks like the five-star hotel bathrooms you read about in magazines.'
	'Above the beautiful vanity and sink there is a huge <a href="exec:gt ''mirror'',''start''">mirror</a>.'
	!acts copy from [shulgabath]
	act 'Return to the hallway': minut += 1 & gt 'mey_home', 'start'
	gs 'selfplay', 'suction_dildo'
	gs 'din_van', 'private'
end

! guestroom
if $ARGS[0] = 'guest_bedroom':
	cls
	$locM = 'mey_home'
	$locM_arg = 'guest_bedroom'
	$loc = 'mey_home'
	$loc_arg = 'guest_bedroom'
	$menu_loc = 'mey_home'
	$menu_arg = 'guest_bedroom'
	menu_off = 0
	gs 'stat'
	'<center><h4><font color="maroon">Guest room</font></h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/meynolds/guestroom.jpg"></center>'
	*nl
	'It feels and looks like a standard room in a hotel.'
	'There is a <a href="exec:gt ''bed'', ''start''">bed</a>, a small <a href="exec:gt ''wardrobe'', ''start''">wardrobe</a> (where you can choose outfits and organize your clothing), and a <a href="exec:gt ''stol'' ,''stolPar''">desk</a>.'
	*nl
	'Your <a href="exec:gt ''budilnik'', ''start''">alarm clock</a> is set to <b>'+iif(timer < 10,'0<<timer>>','<<timer>>')+':'+iif(timerM < 10,'0<<timerM>>','<<timerM>>')+'</b> during the week and <b>'+iif(timerEnd < 10,'0<<timerEnd>>','<<timerEnd>>')+':'+iif(timerEndM < 10,'0<<timerEndM>>','<<timerEndM>>')+'</b> during the weekend. It is currently turned '+iif(budilnikOn = 0, '<a href="exec: budilnikOn = 1 & gt $loc, $loc_arg">OFF</a>.', '<a href="exec: budilnikOn = 0 & gt $loc, $loc_arg">ON</a>.')
	*nl
	'A <a href="exec:gt ''mirror'', ''start''">mirror</a> hangs on the wall near the front door.'
	if $ml_guitar['location'] = $loc: 'Your guitar rests on its stand next to your bed.'
	!! Computer and internet use.
	if komp = 1:
		gs 'internet_mobile', 'get_access', 'free'
		'Your <a href="exec:gt ''Komp'',''start''">computer</a> is on the desk.'
	end

	act 'Return to the hallway':
		if ($clothingworntype = 'nude' or PCloswimwear = 1):
			*clr
			'<center><img <<$set_imgh>> src="'+'images/pc/clothing/'+iif($pantyworntype = 'none','0','tanga')+'.jpg'+'"></center>'
			*nl
			'<center><font color=red><b>You can''t go out dressed like this!</b></font></center>'
			exit
		end
		minut += 1
		gt 'mey_home', 'start'
	end

	gs 'exercise', 'start'
	gs 'music_actions', 'music_icon'
	if pcs_horny >= 50:	act 'Masturbate':gt 'selfplay', 'start'
	if pornMagazine > 0:
		'You hide your porn magazines under the mattress of your bed.'
		gs 'events', 'read_porn'
	end

	gs 'events', 'read'

	if dur > 0 and narkday ! daystart:
		act 'Snort the White Lady (0:05)':
			menu_off = 1
			cla
			*clr
			minut += 5
			narkday = daystart
			gs 'drugs', 'cocaine'
			'<center><img <<$set_imgh>> src="images/shared/drugs/dur.jpg"></center>'
			*nl
			'You snort some White Lady, feeling dizzy for a moment. After that, you feel fantastic and incredibly horny.'

			act 'Continue':gt 'mey_home', 'guest_bedroom'
		end
	end
	act 'Relax on your bed': gt 'bed', 'start'

	act 'Wait':gs 'obj_din', 'wait'
	gs 'prostitution_functions', 'prostitute_outfit_at_home'	
	!!if rand (0,2) ! 0:gs 'din_bad' & dynamic $mobile_check
end

if $ARGS[0] = 'lounge':
	cls
	$loc = 'mey_home'
	$loc_arg = 'lounge'
	$menu_loc = 'mey_home'
	$menu_arg = 'lounge'
	menu_off = 0
	gs 'stat'
	'<center><h4><font color="maroon">Lounge</font></h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/meynolds/hall.jpg"></center>'
	*nl
	'Spacious room with a sofa and armchairs, on the wall, hangs a large <a href="exec:gt ''TV'',''meyhome''">Plasma TV</a>, located next to the bookcase, and on the opposite side is the fireplace. From the hall, there is access to a bright garden overgrown with grapes.'

	act 'Return to hallway': minut += 1 & gt 'mey_home', 'start'
	act 'Garden': minut += 1 & gt 'mey_home', 'garden'
end

if $ARGS[0] = 'katja_bedroom':
	cls
	$loc = 'mey_home'
	$loc_arg = 'katja_bedroom'
	$menu_loc = 'mey_home'
	$menu_arg = 'katja_bedroom'
	menu_off = 0
	$locclass = 'livingr'
	gs 'katja_meynold_schedule'
	gs 'stat'
	'<center><h4><font color="maroon">Katja''s room</font></h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/meynolds/katya_room.jpg"></center>'
	*nl
	'Katja''s room feels like someone is living there temporarily and will soon move out. However, the things in the room are in perfect order, depicting the feeling of a "non-residential premise".'
	'There are numerous flowers in the pots on the windowsill, pots hanging on the walls and even plants in pots on the floor, giving the room a greenhouse feel.'
	'There is a computer desk in one corner, opposite the bed. In another corner is a hidden wardrobe. Lastly, a well-hidden bookshelf can be seen in the room, but it is kinda hard to spot because of the wildly overgrown plants.'
	if locat['katja'] = 7:
		if katjaQW['QWstage'] > 1:
			act 'Chat with Katja': gt'KatjaHomeTalk'
		else
			'<font color=red>Your relationship with Katja is not good enough to just barge into her room, better leave as soon as possible.</font>'
		end
	else
		'Katja is not in her room'
	end
	act 'Return to the hallway': minut += 1 & gt 'mey_home', 'start'
end

if $ARGS[0] = 'vicky_bedroom':
	cls
	$loc = 'mey_home'
	$loc_arg = 'vicky_bedroom'
	$menu_loc = 'mey_home'
	$menu_arg = 'vicky_bedroom'
	menu_off = 0
	gs 'stat'
	'<center><h4><font color="maroon">Vicky''s room</font></h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/meynolds/vika_room.jpg"></center>'
	*nl
	'Vicky''s room is always a mess. It''s clean, but everything is scattered all over the place. Soft toys are lying around in enormous quantities, and all the walls are plastered with posters of pop stars and film actors.'
	'There''s a laptop on the table by the window. It accidentally brings a sense of orderliness. The stacks of magazines resemble music speakers. The room''s centrepiece is the ginormous bed and a wardrobe next to it. There''s a lone chair somewhere hidden under all the stuff.'

	act 'Return to hallway': minut += 1 & gt 'mey_home', 'start'
end

if $ARGS[0] = 'tamara_bedroom':
	cls
	$loc = 'mey_home'
	$loc_arg = 'tamara_bedroom'
	$menu_loc = 'mey_home'
	$menu_arg = 'tamara_bedroom'
	menu_off = 0
	gs 'stat'
	'<center><h4><font color="maroon">Tamara''s room</font></h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/meynolds/tamara_room.jpg"></center>'
	*nl
	'If it was not for the bed in the corner, you would say Tamara Meynold''s room resembles an office. There''s a computer on a desk, a wardrobe, and a big but pretty modest bed. The room is relatively small and cramped. There are no decorations but one, a sword hanging on the wall. It''s unclear what it''s doing there.'

	act 'Return to hallway': minut += 1 & gt 'mey_home', 'start'
end

! courtyard
if $ARGS[0] = 'garden':
	cls
	$loc = 'mey_home'
	$loc_arg = 'garden'
	$menu_loc = 'mey_home'
	$menu_arg = 'garden'
	menu_off = 0
	gs 'stat'
	'<center><h4><font color="maroon">Garden</font></h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/meynolds/courtyard.jpg"></center>'
	*nl
	'The garden is relatively small for the house and overgrown. Most of the space is taken up by a small pool.'
	'A car belonging to Tamara Meynold is parked on the drive next to the garden.'
	if hour = 7 and minut >= 30 and week < 6:
		'<a href="exec:gs''mey_tamara_events'',''tamara''">Aunt Tamara</a> is here talking on her phone.'
	end
	act 'Return to hallway':
		if PCloswimwear = 1 or $clothingworntype = 'nude':
			msg '<center><font color = red>You need to get dressed first</font></center>'
			gt 'mey_home', 'garden'
		else
			minut += 1
			gt 'mey_home', 'start'
		end
	end
	act 'Return to lounge':
		if PCloswimwear = 1 or $clothingworntype = 'nude':
			msg '<center><font color = red>You need to get dressed first</font></center>'
			gt 'mey_home', 'garden'
		else
			minut += 1
			gt 'mey_home', 'lounge'
		end
	end
	if PCloswimwear = 1 or $clothingworntype = 'nude':
		act 'Change back into your clothes':
			minut += 5
			gs 'clothing', 'wear', $lastwornclothingtype['swim'], lastwornclothingnumber['swim']
			gs 'panties', 'wear', $lastwornpantytype['swim'], lastwornpantynumber['swim']
			gs 'bras', 'wear', $lastwornbratype['swim'], lastwornbranumber['swim']
			gt 'mey_home', 'garden'
		end
	end
	if temper >= 15 and daystage < 5 and sunWeather = 1:
		if func('changingroom','count_swim_item') > 0 and PCloswimwear = 0:
			act 'Change into a bikini':gt 'changingroom', 'view_swim_list'
			act 'Strip off clothing':
				*clr & cla
				minut += 1
				gs 'clothing', 'strip_all'
				gs 'stat'
				'<center><video autoplay loop src="images/locations/pavlovsk/resident/meynolds/strippy.mp4"></video></center>'
				'You strip out of your clothes and feel the warmth on your naked skin.'
				act 'Continue':gt 'mey_home', 'garden'
			end
		end
		if pcs_inhib > 30:
			act 'Sunbathe naked':
				*clr & cla
				minut += 60
				inhib_exp += rand(3,5)
				if pcs_sweat < 35: gs 'sweat', 'add', 5
				menu_off = 1
				pcs_mood += 5
				if PCloswimwear = 0 and $clothingworntype ! 'nude':
					$lastwornclothingtype['swim'] = $clothingworntype
					lastwornclothingnumber['swim'] = clothingwornnumber
					$lastwornpantytype['swim'] = $pantyworntype
					lastwornpantynumber['swim'] = pantywornnumber
					$lastwornbratype['swim'] = $braworntype
					lastwornbranumber['swim'] = brawornnumber
					gs 'clothing', 'strip_all'
				else
					gs 'clothing', 'strip_all'
				end
				gs 'stat'
				'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/meynolds/sunbathe_naked<<rand(0,1)>>.jpg"></center>'
				if krem = 0:
					pcs_tan += 1
					'You strip out of your clothes until you are naked, then lie down next to the pool to sunbathe.'
				elseif krem > 0:
					krem -= 1
					pcs_tan += 3
					'You strip out of your clothes until you are naked, then put sunblock on your body and lie down next to the pool to sunbathe.'
				end
				act 'Get Up':gt 'mey_home', 'garden'
			end
		end
		
		if PCloswimwear = 1:
			act 'Sunbathe (1:00)':
				*clr & cla
				minut += 60
				inhib_exp += rand(1,3)
				if pcs_sweat < 35: gs 'sweat', 'add', 5
				menu_off = 1
				pcs_mood += 5
				gs 'stat'
				'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/meynolds/sunbathe<<rand(0,1)>>.jpg"></center>'
				if krem = 0:
					pcs_tan += 1
					'You lie down to sunbathe.'
				elseif krem > 0:
					krem -= 1
					pcs_tan += 3
					'You put sunblock on your body and lie down next to the pool to sunbathe.'
				end
				act 'Get Up':gt 'mey_home', 'garden'
			end
		end
	end
	if PCloswimwear = 1 or $clothingworntype = 'nude':
		act 'Swim (1:00)':
			menu_off = 1
			pcs_makeup = 1
			cumspclnt = 10
			gs 'cum_cleanup'
			minut += 60
			pcs_mood += 50
			if pcs_sweat > 10:pcs_sweat = 10 + rand(0,4)
			if pcs_stren < 40:gs 'exp_gain', 'stren', 2
			fat -= 1
			gs 'stat'

			*clr & cla
			if func('body_din','pregnancyVisibility') = 0:
				if $clothingworntype = 'nude':
					'<center><img <<$set_imgh>> src="images/pc/activities/swim/pool/swim_nude_<<rand(1,10)>>.jpg"></center>'
					inhib_exp += rand(2,4)
				else
					'<center><img <<$set_imgh>> src="images/pc/activities/swim/pool/swim_bikini_<<rand(1,6)>>.jpg"></center>'
					if pcs_inhib < 50: inhib_exp += rand(2,4)
				end
			else
				if $clothingworntype = 'nude':
					'<center><img <<$set_imgh>> src="images/pc/activities/swim/pool/swim_nude_preg_<<rand(1,2)>>.jpg"></center>'
					inhib_exp += rand(2,4)
				else
					'<center><img <<$set_imgh>> src="images/pc/activities/swim/pool/swim_bikini_preg_<<rand(1,4)>>.jpg"></center>'
					if pcs_inhib < 50: inhib_exp += rand(2,4)
				end
			end
			'You swim in the pool. The water refreshes and invigorates you.'
			if deodorant_on = 1: gs 'sweat', 'remove_deo' & '<br>Your deodorant gets washed away in the water.'

			act 'Get out of the water':gt 'mey_home', 'garden'
		end
	end
end	

if $ARGS[0] = 'mey_morning_vomit':
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/hotel/hotel.room.best1.jpg"></center>'
	'At that moment, a sharp knocking sounds on the door and Tamara''s muffled voice comes through the fine wood.'
	'"<<$pcs_nickname>>? Are you all right? I thought I heard you throwing up. Is everything okay?"'
	
	act'Everything is fine':
		cla & *nl
		'"Everything is fine!" you lie hurriedly and rush to flush the toilet, not wanting Tamara worrying about you. "Just, uhh, had a bad cramp."'
		'"Oh, dear! Well, I hope it''s gone now. Breakfast is on in a bit!"'
		'"Okay!" you holler back.'
		act'Continue':gt'home_activity','vomiting_exit'
	end
		
	if vomit['hangover'] = 1:
		act'Hungover':
			cla & *nl
			'"I''m a little hungover," you moan, gripping the toilet bowl as another wave of nausea overtakes you.'
			'"Oh, dear! There''s some pain medication in the cupboard if it would help you."'
			'"Thank you, Tamara..." you reply.'
			act'Continue':gt'home_activity','vomiting_exit'
		end
	else
		act'Feeling a little sick':
			cla & *nl
			'"I think I might''ve caught something," you moan, standing to flush the toilet.'
			'"Oh, dear! Should I take you to the doctor?"'
			'"No, thank you," you reply. "I think it''s just a stomach bug or something. I''ll be okay."'
			act'Continue':gt'home_activity','vomiting_exit'
		end
	end
end

if $ARGS[0] = 'mey_preg_scare':
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/hotel/hotel.room.best1.jpg"></center>'
	'At that moment, a sharp knocking sounds on the door, and you jump in place, shocked by the sudden intrusion. Tamara''s muffled voice comes through the fine wood.'
	'"<<$pcs_nickname>>? Are you all right? I thought I heard you throwing up. Is everything okay?"'
	act'Everything is fine!':
		cla & *nl
		'"Everything is fine!" you say in a hurry, rushing to flush the toilet and turn on the tap to wash your mouth. "I''m fine! Don''t worry, Tamara. Everything is fine!"'
		act'You hope':
			cla & *clr
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/hotel/hotel.room.best1.jpg"></center>'
			'<i>I hope,</i> you gulp, desperately hoping this is a fluke and you''re not pregnant.'
			act'Continue':gt'home_activity','vomiting_exit'
		end
		
		act'This is really bad':
			cla & *clr
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/hotel/hotel.room.best1.jpg"></center>'
			'<i>This is really, really bad!</i> You think, breaking out in a sweat.'
			*nl
			'You''re pregnant! How could anything be fine!'
			act'Continue':gt'home_activity','vomiting_exit'
		end
		
		act'This is wonderful':
			cla & *clr
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/hotel/hotel.room.best1.jpg"></center>'
			'<i>I''m much more than fine,</i> you smile to yourself.'
			*nl
			'You''re pregnant, after all! So you''re going to be a mother! This is terrific news!'
			act'Continue':gt'home_activity','vomiting_exit'
		end
	end
end
--- mey_home ---------------------------------

