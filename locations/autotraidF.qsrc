# autotraidF
if $ARGS[0] = 'start':
	$loc_arg = $ARGS[0]
	$loc = 'autotraidF'
    $location_type = 'event'
	$menu_arg = 'start'
	$menu_loc = 'autotraidF'
	menu_off = 0
	*clr & cla
	gs 'stat'
	'<center><b>Car Dealership</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/usedcar/autotraid.jpg"></center>'
	'Upon entering the yard, you see a laminated piece of paper pinned to a wall near the entrance:'

	'- We only sell to people with valid driver''s licenses.'
	'- New State law: No sale if you already own a car.'

	act 'Leave': minut += 5 & gt 'city_industrial'
	
	if hour <= 17:
		if car > 0:act 'Ask about selling your car':gt 'autotraidF', 'manager1'

		act 'Look at the new cars for sale': gt 'autosalonF', 'start'
		act 'Look at the used cars for sale':gt 'autotraidF', 'car'
	end
end

if $ARGS[0] = 'manager1':
	$loc_arg = $ARGS[0]
	$loc = 'autotraidF'
	$menu_arg = 'manager1'
	$menu_loc = 'autotraidF'
	menu_off = 0
	*clr & cla
	gs 'stat'
	'<center><b>Used Car Dealership</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/usedcar/autotraid.jpg"></center>'
	gs 'avb'

	act 'Leave': killvar 'bupay' & minut += 5 & gt 'city_industrial'
	
	if cardrive = 9 and car > 0:
		'The shifty-looking salesman circles around your car a couple of times, making exaggerated displeased noises all the while and pointing out practically every speck of dust as if it is major damage. When he turns to you, he states that the best he can do is <<bupay>> <b>₽</b> - while he claims that he''s taking a risk with that price, you''re pretty sure he''s ripping you off.'

		act 'No way, not at that price - Leave': killvar 'bupay' & minut += 5 & gt 'city_industrial'
		
		act 'Sell ​your car (<<bupay>> <b>₽</b>) (1:00)':
			cla
			menu_off = 1
			minut += 60
			money += bupay
			car = 0
			'While you are almost certain that he''s offering less than the car is actually worth, you doubt that you''re going to find somebody who''s going to offer you more for it, and your attempts at haggling with the fence fall flat - he''s just too experienced at this.'
			'So, as much as you hate it, you give your car one last look before you shake hands with him and follow him into the tiny shed he calls his "office" to sign the papers. He hands you the money in cash and, with a nasty grin, suggests that you look around the yard.' 
			'"Maybe you''ll see something you like?"'

			act 'Leave': killvar 'bupay' & minut += 5 & gt 'city_industrial'
		end
	elseif cardrive ! 9:
		'The salesman tells you, his smile sneering rather than friendly, that he needs to look at your car before he can tell you how much it''s worth. "If you want, I can have somebody tow it here. It only costs 3000 <b>₽</b>. Very cheap!"'

		if money >= 3000:
			act 'Pay for towing service to the car market (3,000 <b>₽</b>) (1:00)':
				cla
				menu_off = 1
				minut += 60
				cardrive = 9
				money -= 3000
				'You give him the money - certain that he''s ripping you off - and wait for an hour until the tow truck finally drives into the yard, your car hooked up to the rear of it.'

				act 'Talk to the car salesman again': killvar 'bupay' & gt 'autotraidF', 'manager1'
			end
		end
	end	
end

if $ARGS[0] = 'car':
	$loc_arg = $ARGS[0]
	$loc = 'autotraidF'
	$menu_arg = 'car'
	$menu_loc = 'autotraidF'
	menu_off = 0
	*clr & cla
	minut += 5
	carT = 0
	gs 'stat'
	'<center><b>Used Car Dealership</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/industrial/usedcar/autotraid.jpg"></center>'
	'There are so many cars on the parking lot that you could spend a lifetime looking around without ever seeing all of them. According to the salesman, several cars are currently being test-driven or maintained. You doubt that there''s actually any maintenance going on though: several of the cars are visibly damaged or make awful sounds when they are started.' 
	*nl
	'It''s obvious that most, if not all of them have sustained some visible or invisible damage. Buying a car here could be a fantastic bargain... or a complete waste of money. If you are to buy any of them, you know that the first place to visit should be a service center.'

	act 'Return': killvar 'carT' & killvar 'bupay' & gt 'autotraidF', 'start'
	
	if hour <= 17:
		act 'Inspect a 2107':gt 'autotraidF', 'inspect', '2107', 1, 200, 1, 3

		act 'Inspect another 2107 (Broken Engine)':
			cla
			*clr
			menu_off = 1
			'<center><b>2107 (Broken engine)</b></center>'
			'<center><img <<$set_imgh>> src="images/shared/accessories/car/car.jpg"></center>'
			bupay = rand(10000, 50000)
			'The car is basically a wreck. It doesn''t even start. When you ask the salesman how much it costs, he tells you "<<bupay>> <b>₽</b>" without hesitation.'

			act 'Look around some more':gt 'autotraidf', 'car'

			if car = 0 and prava = 1 and money >= bupay:
				act 'Buy it for <<bupay>><b>₽</b>':
					cla
					money -= bupay
					$car = '2107'
					car = 1
					benz = 3
					cardrive = 9
					tehcar = 200
					teh = rand(10, tehcar)
					wrek = 1
					'You agree and before you know it, the car salesman and you have signed all the necessary documents. You hand over the <<bupay>> <b>₽</b> in cash, and the car is yours.' 
					'Since it''s crammed in between about a million other vehicles, the car salesman moves it outside near the yard''s entrance for you... and that''s it.' 
					'If you want to get the car to a garage, you''ll have to go there and have it towed.'
					killvar 'bupay'
					act 'Leave': minut += 5 & gt 'city_industrial'
				end
			end
		end

		act 'Inspect a VAZ 2115':gt 'autotraidF', 'inspect', 'VAZ 2115', 2, 300, 1, 3
		act 'Inspect a VAZ 2113':gt 'autotraidF', 'inspect', 'VAZ 2113', 3, 290, 1, 3
		act 'Inspect a VAZ 2114':gt 'autotraidF', 'inspect', 'VAZ 2114', 4, 300, 1, 3
		act 'Inspect a VAZ 2110':gt 'autotraidF', 'inspect', 'VAZ 2110', 5, 350, 1, 3
		act 'Inspect a GAZ 3102':gt 'autotraidF', 'inspect', '3102 GAS', 11, 1000, 2, 5
		act 'Inspect a 3110':gt 'autotraidF', 'inspect', '3110', 12, 800, 2, 5
	end
end

if $ARGS[0] = 'inspect':
	cla
	*clr
	menu_off = 1
	$autotraidF_carname = $ARGS[1]
	autotraidF_carnum = ARGS[2]
	tehNT = ARGS[3]
	carT = ARGS[4]
	autotraidF_benz = ARGS[5]

	'<center><b><<$autotraidF_carname>></b></center>'
	'<center><img <<$set_imgh>> src="images/shared/accessories/car/car<<iif(autotraidF_carnum = 1, '''', autotraidF_carnum)>>.jpg"></center>'

	gs 'avt'
	'<<$tehT>> The car salesman wants <<bupay>> <b>₽</b> for it.'

	act 'Look around some more':
		killvar 'bupay'
		killvar 'tehT'
		killvar 'tehNT'
		killvar '$autotraidF_carname'
		killvar 'autotraidF_carnum'
		killvar 'autotraidF_benz'
		gt 'autotraidF', 'car'
	end

	if car = 0 and prava = 1 and money >= bupay:
		act 'Buy (<<bupay>> <b>₽</b>)':
			cla
			money -= bupay
			$car = $autotraidF_carname
			car = autotraidF_carnum
			benz = autotraidF_benz
			cardrive = 9
			teh = tehT
			tehcar = tehNT
			wrek = 0
			'You agree and before you know it, the car salesman and you have signed all the necessary documents. You hand over the <<bupay>> <b>₽</b> in cash, and the car is yours.' 
			'Since it''s crammed in between about a million other vehicles, the car salesman moves it outside near the yard''s entrance for you and warns you that there is very little gas in the tank.'
			killvar 'bupay'
			killvar 'tehT'
			killvar 'tehNT'
			killvar '$autotraidF_carname'
			killvar 'autotraidF_carnum'
			killvar 'autotraidF_benz'
			act 'Leave': minut += 5 & gt 'city_industrial'
		end
	end
end

--- autotraidF ---------------------------------

