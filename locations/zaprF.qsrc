# zaprF

if $ARGS[0] = 'start':

	$loc_arg = 'start'
	$loc = 'zaprF'
	$menu_loc = 'zaprF'
	$menu_arg = 'start'
	menu_off = 0
	$location_type = 'public_outdoors'
	cla & *clr
	gs 'stat'
	'<center><b>Gas Station</b></center>'
	if hour < 8 or hour > 21:
		'<center><img <<$set_imgh>> src="images/locations/shared/gas/gazprom_night.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/shared/gas/gazprom_day.jpg"></center>'
	end
	'The local gas station, the cheap pumps are limited in functionality and will always fill the tank up completely.'
	*nl
	'When the weather is nice, sometimes girls will offer a car washing service for some tips. There''s no structure to it, so you could always try it.'
	'The petrol price is 30 <b>₽</b> per liter.'
	*nl

	act 'Leave':
		if cardrive = 6:
			minut += 5
			gt 'city_industrial'
		elseif cardrive = 21:
			minut += 5
			gt 'pav_commercial'
		elseif litltown = 0:
			minut += 5
			gt 'city_industrial'
		elseif litltown = 1:
			minut += 5
			gt 'pav_commercial'
		end
	end

	if temper < 10:
		'It''s too cold outside to wash cars. You''re more likely to get ill than make money. Maybe try again when the weather is warmer?'
	elseif sunWeather = 0:
		'You can''t wash cars if it''s raining outside! Maybe try again on a sunny day?'
	elseif hour < 8:
		'It''s too early to wash cars.'
	elseif hour >= 21:
		'It''s too late to wash cars.'
	else
		act 'Wash car windows (1:00)':gt 'zaprFWash', 'start'
	end
	act 'Go to the toilet':gt 'zaprF', 'toilet'

	if car > 0:
		if cardrive = 6 or cardrive = 21: 'Your <a href="exec: gs ''carF'', ''start''"><<$car>></a> is parked here.'
	
		if kanistra < 5:
			act 'Buy a canister and fill it with 5 liters of gasoline for 150 <b>₽</b>':
				cla
				kanistra += 1
				money -= 150
				'You buy a canister of gasoline. (It will automatically be put in the trunk of your car)'

				act 'Disengage from the pump':gt 'zaprF', 'start'
			end
		end

		if benz < 50 and (cardrive = 6 or cardrive = 21):
			zprbenz = 50 - benz

			if zprbenz * 30 <= money:
				act 'Fill the tank with petrol':
					cla
					*clr
					benz = 50
					zprpay = zprbenz * 30
					money -= zprpay

					! WD: Uninitialised ~ 'pol'
					! if pol > 0:'<center><img <<$set_imgh>> src="images/locations/shared/gas/zapr1.jpg"></center>'

					'<center><img <<$set_imgh>> src="images/locations/shared/gas/zapr1.jpg"></center>'

					'You fill the tank and pay <<zprpay>> <b>₽</b>.'

					act 'Disengage from the pump':gt 'zaprF', 'start'
				end
			else
				'You do not have enough money to refuel your car.'
			end
		end
	end
end

if $ARGS[0] = 'toilet':
	CLOSE ALL
	gs 'themes', 'indoors'
	cla & *clr
	$loc_arg = 'toilet'
	$loc = 'zaprF'
	$location_type = 'bathroom'
	$locM = 'zaprF'
	$locM_arg = 'toilet'
	minut += 2
	$menu_loc = 'zaprF'
	$menu_arg = 'toilet'
	menu_off = 0
	gs 'stat'

	'<center><h4>Gas station toilet</h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/shared/bathroom/toilet.jpg"></center>'
	'You are in a dirty gas station toilet. At least it provides some privacy.'

	act 'Leave the restroom':gt 'zaprF', 'start'

	act 'Look in the mirror': gt 'mirror', 'start'
	if pcs_hairbsh = 0: act 'Brush your hair': gt 'mirror', 'brush'

	dynamic $tampon
	dynamic $quickwash
	dynamic $basin
	dynamic $publicpan
	gs 'din_van', 'pblc_pee'
end

--- zaprF ---------------------------------

