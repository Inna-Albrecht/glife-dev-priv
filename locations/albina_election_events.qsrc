# albina_election_events
!!This is intended to happen during the election run, so should fire around the time that the "Vote Boris!" posters start appearing.
!!Add trigger depending on whether or not Sveta gave a statement so Albina can talk about her choice later.


if $ARGS[0] = 'start':
	AlbinaQW['RideDaystart'] = daystart + rand(2,5)
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/girl/albina/chernov.jpg"></center>'
	'A car suddenly pulls up next to you and a man wearing a suit and hat steps out and introduces himself. "Captain Chernov, Federal Security Service," he says and pulls out his badge.' 
	'"Why are you here? What did I do? Am I under arrest?" you ask in panicked surprise, but he just ignores you and continues.' 
	'"You are <<$pcs_firstname>> <<$pcs_lastname>>, correct? Please come and sit in my car. We need to have a little chat."'
	act 'Speak with Chernov':
		*clr & cla
		menu_off = 1
		minut += 1
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/girl/albina/chernov.jpg"></center>'
		'He opens the back door and motions for you to step inside. You notice he is holstering a gun under his jacket and so decide to listen to him. You step inside and he slides in after you, closing the door behind him. A second man sits silently behind the wheel as Chernov turns to you.'
		'"Miss <<$pcs_lastname>>, I know that you are a member of Barlovskaya''s entourage. I also know that this is just a front. These criminal activities are about to be put on hold permenantly, with or without your collaboration."'
		'You freeze, your body stiff with worry as you wonder what he''s about to do.'
		'"Our little talk here is quite important. This investigation will soon be the talk of the town, but you can testify anonymously like the other girls and keep your name out of it."'
		act 'Protest':
			*clr & cla
			minut += 1
			gs 'stat'
			'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/girl/albina/chernov.jpg"></center>'
			'"Albina didn''t whore me out!" you protest, but Chernov just smiles.'
			'"I never said it was Albina, did I? All you have to do is give me your testimony stating that you were forced into an illegal prostitution ring. You don''t have to worry about Albina. We''ll keep her safe from any consequences of her actions."'
			*nl
			'He pauses and gives you time to think before continuing.'
			'"I already have enough evidence through an anonymous source and statements from your fellow dancers to take down these activities, but your testimony will still help. Your name will be kept secret, of course."'
			if pcs_intel >= 70:
				act 'Ask why the Security Service is involved':
					*clr & cla
					minut += 1
					gs 'stat'
					'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/girl/albina/chernov.jpg"></center>'
					'"What''s so important about the Starlets that the Security Service are involved?" you ask, causing Chernov to raise a brow.'
					'"You''re a smart girl, huh? You know that slimeball Boris Barlovskay, her father who served as the chief of the St. Petersburg police? He wishes to become the mayor, but I don''t want the corrupt scumbag running the town in the interests of the criminal syndicates he formed connections with while in office. I''ve spent years investigating him, but was never able to prove he was dirty - until now."' 
					'He places a hand gently on your shoulder. "All I need to do to put him behind bars where he belongs is prove that young Albina''s dance troupe is actually a front for illegal prostitution. I need your statement, Miss <<$pcs_lastname>>."'
					gs 'albina_election_events', 'din_zajava_albfather'
					gs 'albina_election_events', 'din_zajava_albfather_no'
				end
			end

			gs 'albina_election_events', 'din_zajava_albfather'
			gs 'albina_election_events', 'din_zajava_albfather_no'
		end
	end
end

if $ARGS[0] = 'din_zajava_albfather':
	act 'Write a statement':
		*clr & cla
		menu_off = 1
		minut += 1
		AlbinaQW['Chernov'] = 1
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/girl/albina/chernov.jpg"></center>'
		'You agree to write a statement and Chernov nods.'
		'"You''re a smart girl. Just write your statement here and sign it," he says and hands you a pen and paper.'
		act 'Continue':
			*clr & cla
			minut += 1
			gs 'stat'
			'<center><img <<$set_imgh>> src="images/shared/misc/signature.jpg"></center>'
			'You write your statement and he lets you go without a fuss.'
			'"You''ve really helped me here, Miss <<$pcs_lastname>>. Thank you." He motions for you to leave the car and it drives away as soon as you close the door.'
			act 'Call Albina':
				*clr & cla
				minut += 1
				AlbinaQW['Friends'] = 1
				AlbinaQW['StarletsShutDown'] = 1
				gs 'stat'
				'<center><img <<$set_imgh>> src="images/pc/activities/phone/calling_outdoor.jpg"></center>'
				'You quickly dial Albina''s number and, in a panic, tell her what happened. She calms you down and asks if you told them anything.'
				'"I... I did. I told them everything. But they told me that you''d be left out of it! I didn''t have a choice Albina! I''m sorry!"'
				'"Oh. Well, thanks for telling me."'
				*nl
				'<i>Click.</i> The line goes dead.'
				*nl
				'You look down at your phone and see that she hung up. Despite Chernov''s promise, it feels like you sold Albina out. Yet, just before she hung up, it sounded like she might have been... <i>happy?</i>'
				'Did she <i>want</i> you to tell Chernov everything? The whole encounter has left you feeling bewildered and confused.'
				act 'Leave':gt 'pav_residential'
			end
		end
	end
end

if $ARGS[0] = 'din_zajava_albfather_no':
	act 'Refuse to write a statement':
		*clr & cla
		menu_off = 1
		minut += 1
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/girl/albina/chernov.jpg"></center>'
		'You are reluctant to write a statement, but Chernov keeps trying. "Look, I know you''re scared right now, but do you really not want to help stop this illegal exploitation of you and your friends? I promise you, nothing will happen to Albina."'
		if pcs_intel >= 80:
			act 'Why should I give you information?':
				*clr & cla
				minut += 1
				gs 'stat'
				'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/girl/albina/chernov.jpg"></center>'
				'"If you already have everything you need, then why do you need my testimony?" you ask.'
				'"To be honest, I don''t. Like I said, I got more than enough from the other girls, but a statement from you would still help me out and hopefully guarantee my success."'
				'"What about Albina? Have you questioned her? I doubt she would act against her father."'
				'"We have other arrangements for her," is all he says in reply.'
				gs 'albina_election_events', 'din_zajava_albfather'
				gs 'albina_election_events', 'din_zajava_albfather_no2'
			end
		end

		gs 'albina_election_events', 'din_zajava_albfather'
		gs 'albina_election_events', 'din_zajava_albfather_no2'
	end
end

if $ARGS[0] = 'din_zajava_albfather_no2':
	act 'Refuse':
		*clr & cla
		menu_off = 1
		minut += 1
		AlbinaQW['Chernov'] = 2
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/girl/albina/chernov.jpg"></center>'
		'While a part of you wants to help, you don''t trust him and refuse.' 
		'He sighs in resignation. "I can''t force you, but just know that you could have really helped your friend out here." He motions for you to get out of the car and it drives away as soon as you close the door.'
		act 'Call Albina':
			*clr & cla
			minut += 1
			AlbinaQW['Friends'] = 1
			AlbinaQW['StarletsShutDown'] = 1
			gs 'stat'
			'<center><img <<$set_imgh>> src="images/pc/activities/phone/calling_outdoor.jpg"></center>'
			'You quickly dial Albina''s number and, in a panic, tell her what happened. She calms you down and asks if you told them anything.'
			'"No, I didn''t! I would never sell you out like that!"'
			'"Oh. Well, thanks for telling me."'
			*nl
			'<i>Click.</i> The line goes dead.'
			*nl
			'You look down at your phone and see that she hung up. Despite Chernov''s promise, it feels like you were about to sell Albina out. Yet, just before she hung up, it sounded like she might have been... disappointed?'
			'Did she <i>want</i> you to tell Chernov? The whole encounter has left you feeling bewildered and confused.'
			act 'Leave':gt 'pav_residential'
		end
	end
end

--- albina_election_events ---------------------------------