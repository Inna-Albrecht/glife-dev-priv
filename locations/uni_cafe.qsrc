# uni_cafe

if $ARGS[0] = '':
	CLOSE ALL
	$loc_arg = ''
	$loc = 'uni_cafe'
	$menu_loc = 'uni_cafe'
	$menu_arg = ''
	menu_off = 0
	$location_type = 'public_indoors'
	gs 'stat'
	gs 'katja_meynold_schedule'
	gs 'themes', 'indoors'
	'<center><b><font color="maroon">University cafe</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/island/university/uni_cafe.jpg"></center>'
	*nl
	'The University cafe, a well-known meeting point for the students attending the university. Students spend their days here, either preparing for their exams or just to unwind from their busy schedule.'
	'The cafe offers a rich variation of reasonably priced food and beverages. There is also free wi-fi, even if it''s wonky at best, and you can often overhear students complain about it.'
	if locat['katja'] = 28:
		'You see '+iif( katjaQW['know_katja_uni'] = 0 and ($start_type = 'city_tg' or $start_type = 'uni_tg'), 'a redheaded girl', '<a href="exec:gt ''katja_chat'', ''uni_cafe''">Katja</a>')+' sitting  at a table eating her lunch.'
	end
	
	gs 'uni_cafe', 'menu'
	
	
	act 'Leave': gt 'uni_grounds', 'main'
	if university['semester_week'] > 0 and university['enrolled_in_semester'] > university['semester_passed']: gs 'uni_lessons', 'schedule'
	if university['exam_week'] > 0 and university['enrolled_in_semester'] > university['semester_passed']: gs 'uni_lessons', 'exam_schedule'
end

if $ARGS[0] = 'menu':
	if week < 5 and (university['semester_week'] > 0 or university['exam_week'] > 0) and university['enrolled_in_semester'] > university['semester_passed'] and ((hour = 10 and minut > 30) or (hour = 12 and minut > 45) or (hour = 13 and minut < 15)):
		'You might not have time to eat before your next class.'
		act 'Order from the menu anyway': 
			menu_off = 1
			$loc = 'uni_cafe'
			gs 'food_menu'
		end	
	else
		act 'Order from the menu': 
			menu_off = 1
			$loc = 'uni_cafe'
			gs 'food_menu'
		end
	end
end

if $ARGS[0] = 'sitting_choice':
	if locat['katja'] = 28 and npc_rel['A14'] > 40 and katjaQW['know_katja_uni'] = 1: 
		act 'Eat your lunch with Katja': gt 'katja_chat', 'uni_cafe'
	end

	act 'Eat you lunch alone':
		!!this is a variable to use in npc''s chats to see if you have lunch to eat together with them. Should be set to zero after it is checked in their chat.
		*clr & cla
		uni_lunch_bought = 0
		'<center><b><font color="maroon">University cafe</font></b></center>'
		'<center><img <<$set_imgh>> src="images/locations/city/island/university/uni_cafe.jpg"></center>'
		'You sit down and eat your lunch alone and<<$mtxt>>'
		gs 'stat'
		
		act 'Leave': gt 'uni_grounds', 'main'

		if university['semester_week'] > 0 and university['enrolled_in_semester'] > university['semester_passed']: gs 'uni_lessons', 'schedule'
		if university['exam_week'] > 0 and university['enrolled_in_semester'] > university['semester_passed']: gs 'uni_lessons', 'exam_schedule'
	end
end

--- uni_cafe ---------------------------------

