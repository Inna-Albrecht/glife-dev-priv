# city_suburbs

$location_type = 'public_outdoors'

if $ARGS[0]='start':
	$loc = 'city_suburbs'
	$loc_arg = 'start'
	$menu_loc = 'city_suburbs'
	$menu_arg = 'start'
	menu_off = 0
	gs'stat'
	'<center><H4>Elite settlement<H4></center>'
	'<left><img <<$set_imgh>> src="images/locations/city/suburb/settlement.jpg" ></left>'

	if car > 0 and cardrive = 28:'Your <a href="exec:GS ''carF'', ''start''"><<$car>></a> stands on the street.'

    if hour >= 4 and hour <= 23:'The <a href="exec: minut += 20 & gt ''metro'', ''start''">Metro</a> station is a 20 minut walk from here.'

	gs 'taxi'

	if AlexandriaQW > 6:
		if hour > 7 and hour < 20:'You can visit <a href="exec: minut += 5 & gt ''AlexandriaHome'', ''intercom''">Aleksei''s home</a> if you want to see the cranky wizard.'
	end

	if BDSMClub = 1 or BDSMmeet > 0:
		act 'BDSM-club':gt'BDSM_Club','start'
	end

	if home_owned[5] = 0:
		'There is a vacant piece of land for sale where a large house could be constructed.'
		act 'Visit the land for sale': minut += 10 & gt 'youplace'
	elseif home_owned[5] < 3:
		'You mansion is partially built near here.'
		act 'Visit your mansion': minut += 10 & gt 'youplace'
	else
		'One of the most impressive buildings is your newly built mansion.'
		act 'Visit your mansion': gt 'youplace'
	end
	act 'View the insane asylum': minut += 2 & gt 'city_suburbs', 'ps1'
	act 'View the creepy old building': minut += 2 & gt 'city_suburbs', 'ps2'

	act 'Go to the main road':nroad = 1 & minut += 15 & gt 'road'

	if AlexandriaQW = 6:
		gs 'willpower', 'misc', 'self', 'easy'
		if will_cost <= pcs_willpwr:
			act 'Look for Alexandria''s direction (<<will_cost>> Willpower)':
				gs 'willpower', 'pay', 'easy'
				gt'alexandriaEv','interview1'
			end
		else
			act 'Look for Alexandria''s direction (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
		end
	end
end

if $ARGS[0]='ps1':
	cla
	*clr
	$menu_loc = 'city_suburbs'
	$menu_arg = 'ps1'
	menu_off = 0
	gs'stat'
	'<center><H4>Insane asylum<H4></center>'
	'The inscription on the sign by the road reads: "Closed to the public".'
	if psiklik = 0:'<center><img <<$set_imgh>> src="images/locations/city/suburb/asylum0.jpg" ></center>'
	if psiklik = 1:'<center><img <<$set_imgh>> src="images/locations/city/suburb/asylum1.jpg" ></center>'

	act 'Go': minut += 2 & gt'city_suburbs','start'
end

if $ARGS[0]='ps2':
	cla
	*clr
	$menu_loc = 'city_suburbs'
	$menu_arg = 'ps2'
	menu_off = 0
	gs'stat'
	'<center><H4>An old abandoned building<H4></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/suburb/abandoned.jpg" ></center>'
	'An abandoned building that spoils the local communities appearance and intimidates local residents.'

	act 'Go': minut += 2 & gt'city_suburbs','start'
end

--- city_suburbs ---------------------------------

