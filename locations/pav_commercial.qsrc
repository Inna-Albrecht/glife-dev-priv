# pav_commercial

CLOSE ALL

$loc_arg = ''
$loc = 'pav_commercial'
$region = 'pav_commercial'
$location_type = 'public_outdoors'
$menu_loc = 'pav_commercial'
$menu_arg = ''
menu_off = 0

if sound = 0:
	if month >=11 and month <= 12 or month >=1 and month <=3:
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/zima.mp3',30
		if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
	else
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street_leto.mp3',30
		if hour >= 0 and hour <= 7:PLAY 'sound/street_night.mp3',30
	end
end

cla
*clr
litltown = 1
alkoblock = 0
boynumBlock = 0
!!sets the return location for bank and post office
bankloc = 0
!!sets the return location for allure swimwear store
allure_pos = 1

!call random events from [street_events], and display them before show location.
if streetevent_hour + 2 < hour:gt'street_event','pav_commercial', 'main'

'<center><h2>Pavlovsk</h2></center>'
if month >= 11 or month <= 3:
	if daystage = 2 or daystage = 3:
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/gorodokw.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/pavbuswn.jpg"></center>'
	end
else
	if daystage = 2 or daystage = 3:
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/gorodok.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/pavbusn.jpg"></center>'
	end
end
*nl
'Pavlovsk can be split into four regions, the commercial hub here, the residential suburbs, the industrial region and the Five Eight estate of Soviet era apartment blocks.'
*nl
'While the commercial hub of Pavlovsk is better maintained and a bit cleaner looking than other areas, it is still very much rural like the rest of the town, and even from here, you can still easily access the old <a href="exec:view''images/locations/pavlovsk/palace/pav_palace.jpg''">Imperial Palace</a> built by Catherine the Great, and the remodeled <a href="exec:view''images/locations/pavlovsk/fortress/bip_hotel.png''">Bip fortress</a> which now operates as a luxury hotel and restaurant.'
*nl
'A short distance away you can clearly see the golden domes of the <a href="exec:minut += 10 & gt ''pav_church'',''start''">Church of St. Nikolas</a>. It''s only a 10 minute walk to get there from here.'
*nl
'The nearby residential area is next to the lake and the town surrounds the old palace grounds which are now a large <a href="exec:minut += 5 & gt ''pav_park'', ''start''">public park</a>.'

gs'street_event','pav_commercial', 'extras'

if ((month = 9 and day > 16) or month >= 10) and (AlbinaQW['StarletsShutDown'] = 0 or AlbinaQW['Chernov'] = 0): 'You can see election posters on the walls all over town. They say: "Vote for Boris Barlovsky!" Wait a minute... isn''t that Albina''s father?'
if AlbinaQW['StarletsJoined'] > 0 and AlbinaQW['StarletsShutDown'] = 0 and npc_pregtalk['A23'] = 0 and AlbinaQW['Chernov'] = 0 and ((month = 11 and day > 18) or month = 12) and hour >= 14:gt 'albina_election_events', 'start'

if StoryLine > 0 and hour < 4 and motherWorry = 0 and SchoolAtestat = 0 and age < 18 and home_owned[2] = 1:
	motherWorry = 1
end

if rand(1, 5) >= 4 and fame['pav_slut'] >= 100:
	if fame['pav_slut'] < 150:
		'<br>You catch people staring at you intently, as if they''re supposed to know who you are but can''t quite remember why. You sincerely hope they don''t realize your promiscuous adventures are the reason why. The occasional person chuckling and pointing at you crushes those hopes, and you quickly keep walking before they get a chance to say anything.'
	elseif fame['pav_slut'] < 200:
		'<br>People look at you with lewd grins on their faces, some of them even making vulgar gestures. Looks like they know what you''ve been up to.'
	else
		'<br>Everywhere you go, people recognize you as a whore. Some even come over and slap you on the ass, claiming they know you like it when they do, or claiming they want to sample your goods. An elderly woman sitting on a bench calls out to you, loudly calling you a slut and a whore. You hide your face and run away as fast as you can, before she can draw too much attention to you.'
	end
end

gs 'stat'

if car > 0 and cardrive = 24:
	cardrive = 24
	'Your <a href="exec:gs ''carF'', ''start''"><<$car>></a> is parked in the street.'
end

if hour >= 8 and hour <= 20:
	$Pharmname = 'For easy access, there''s even a <a href="exec: minut += 5 & gt ''pav_pharmacy'', ''start''">pharmacy</a> attached to it.'
else
	$Pharmname = 'For easy access, there''s even a pharmacy attached to it but it''s currently closed for the day.'
end

'<br>Centered along the main street is the <a href="exec:minut += 10 & gt ''pav_market''">Market square</a> with the <a href="exec:minut += 10 & gt ''pav_train_hall''">Train station</a> that is always open and running. For those with a car, the end of the street leads to a <a href="exec:minut += 5 & gt ''zaprF'', ''start''">gas station</a> which is usually the last stop people make before heading out onto the nearby highway.'

if week < 6 and hour >= 9 and hour <= 18:
	$desc_txt4 = '<a href="exec:minut += 3 & gt ''post_office'', ''start''">post office</a>'
elseif week = 6 and hour >= 10 and hour <= 18:
	$desc_txt4 = '<a href="exec:minut += 3 & gt ''post_office'', ''start''">post office</a>'
else
	$desc_txt4 = '<a href="exec:msg ''The post office, which is closed right now, will reopen again at 9:00 and at 10:00 on Saturdays.''">post office</a>'
end

if (week < 6 and hour >= 9 and hour <= 16) or (week = 6 and hour >= 9 and hour <= 13):
	$desc_txt5 = '<a href="exec:gt ''bank'', ''start''">VTB Bank</a>'
else
	$desc_txt5 = '<a href="exec:msg ''VTB Bank, which is closed right now. It will reopen again at 9:00''">VTB Bank</a>'
end

'<br>On the street across from the station is a small <a href="exec:minut += 5 & gt ''pav_clinic''">Avicenna clinic</a>. <<$Pharmname>> The building next door is shared by the local <<$desc_txt4>> and a <<$desc_txt5>>.'

if hour >= 8 and hour <= 20:
	$desc_txt3 = '<a href="exec:minut += 2 & gt ''shop'', ''start''">Pyaterochka</a>, a popular discount supermarket chain'
else
	$desc_txt3 = '<a href="exec:msg ''Pyaterochka, which is closed right now. It will reopen again at 8:00''">Pyaterochka</a>, a popular discount supermarket chain'
end
'<br>Of course, right after you visit the bank, you can spend your money at <<$desc_txt3>>.' 

if hour >= 8 and hour <= 17:
	$desc_txt1 = '<a href="exec: minut += 3 & gt ''barbershop'', ''start''">A Cut Above</a>, the only barber shop in the area, <a href="exec: minut += 3 & gt ''shop_coco_carmen'', ''start''">Coco Carmen</a> the trendy clothes shop and <a href="exec: minut += 3 & gt ''shop_allure'', ''start''">Allure</a> the swimwear chain.'
else
	$desc_txt1 = '<a href="exec:msg ''A Cut Above, which is closed right now. It will reopen again at 8:00''">A Cut Above</a>, the only barber shop in the area, <a href="exec:msg ''Coco Carmen, which is closed right now. It will reopen again at 8:00''">Coco Carmen</a> the trendy clothes shop and <a href="exec:msg ''Allure, which is closed right now. It will reopen again at 8:00''">Allure</a> the swimwear chain.'
end
'<br>The rest of the street is filled with smaller locally owned businesses, most notable being <<$desc_txt1>>.'

if (week = 7 and hour >= 14 and hour <= 17) or (week = 6 and hour >= 12 and hour <= 17) and lari_gm ! daystart:
	if rand(0, 4) = 0: 'You see <a href="exec: gt ''gschool_socialchg1'',''lariska_gm_1''">Lariska</a> going inside Coco Carmen.'
end

if leonidSlave = 1:
	'<br>Near the town hall there is an old <a href="exec:minut += 15 & gt ''pav_commercial_offices''">office building</a>.'
end

if hour >= 7 and hour <= 18:
	$desc_txt2 = '<a href="exec:minut += 15 & gt ''etoexhib'', ''pos20''">vocational school</a>, where young adults can learn a trade.'
else
	$desc_txt2 = '<a href="exec:msg ''vocational school, which is closed right now. It will reopen again at 7:00''">vocational school</a>.'
end

'<br>If you continue past the shops, you''ll eventually find a <<$desc_txt2>>'

if workPTU > 2:act '<b>Go to the hostel</b>':gt 'etoexhib', 'pos40'

act 'Walk to the Residential area (0:05)':minut += 5 & gt 'pav_residential'
act 'Walk to the Industrial region (0:05)':minut += 5 & gt 'pav_industrial'
act 'Walk to the Five Eight estate (0:05)':minut += 5 & gt 'pav_complex', 'start'
act 'Walk to the Market (0:05)':minut += 5 & gt 'pav_market'
act 'Walk to the train station (0:10)':minut += 10 & gt 'pav_train_hall'
if Trainpass_day > daystart:
	act 'Board train (0:15)':minut += 15 & gt 'pav_train_hall', 'platform'
end
act 'Walk to the highway (0:15)':minut += 15 & nroad = 20 & gt 'road'
act 'Walk to Pushkin (0:25)':minut += 25 & nroad = 19 & gt 'liam'

if hour >= 8 and hour <= 22 and sunWeather = 1 and temper >= 15 and ml_guitar['carried'] = 1 and ml_performance['performed_minutes'] < ml_performance['max_perform_minutes'] and ml_performance['max_perform_minutes'] >= 15:
	*nl
	'With the weather being pleasant and sunny, there are groups of people moving around the streets. You could play some music, practice performing songs and maybe make some money too.'
	ml_buskingtime =  min((ml_performance['max_perform_minutes']-ml_performance['performed_minutes']), 60)
	if pcs_inhib < 30: 
		if pcs_inhib < 10: 
			$diff = 'hard'
		elseif pcs_inhib < 20: 
			$diff = 'medium'
		else
			$diff = 'easy'
		end
		
		gs 'willpower', 'skill', 'self', $diff
		will_cost = (will_cost * 3) - (pcs_perform)
		if will_cost <= pcs_willpwr:
			act 'Start busking (<<ml_buskingtime>> minutes) (<<will_cost>> Willpower)': 
				inhib_exp += rand(1,3)
				gs 'willpower', 'pay', 'self'
				gs 'stat'
				gs 'music_buskingevents', 'busking', 'pav'
				
				if rand(0,100) > 80 and (pcs_instrmusic + pcs_vokal + pcs_perform) >= 120 and hotcat >= 5:
					gt 'music_gigstarts', 'delparco_start', 'pav_commercial'
				end
			end
		else	
			act 'Start busking (<<ml_buskingtime>> minutes) (<font color="red"><<will_cost>> Willpower</font>)': '<br><font color="red">You don''t have enough willpower to use this action.</font>'
		end
		
	else
		act 'Start busking (<<ml_buskingtime>> minutes)':
			gs 'music_buskingevents', 'busking', 'pav'
			if rand(0,100) > 80 and (pcs_instrmusic + pcs_vokal + pcs_perform) >= 120 and hotcat >= 5:
				gt 'music_gigstarts', 'delparco_start', 'pav_commercial'
			end		
		end
	end		
end

act 'Wait':gs 'obj_din', 'wait'

!!if rand (0,2) ! 0:gs 'din_bad' & dynamic $mobile_check

if npc_QW['A113'] = 1 and daybelisex ! daystart and hour >= 9 and hour <= 23 and week = 7:
	cla & *clr
	daybelisex = daystart
	minut += 5

	'<center><img <<$set_imgh>> src="images/characters/pavlovsk/vadim/belyjeep.jpg"></center>'
	'As you walk through Pavlovsk, Vadim Belys Mitsubishi Pajero stops near you.'

	act 'Go to the jeep': gt 'belgang', 'payday'
elseif npc_QW['A113'] = 1 and daybelisex ! daystart and hour >= 9 and hour <= 23 and week ! 7 and belgangPayWeek >= 300 and belgangProstitute = 1 and rand (0,2) = 0:
	cla & *clr
	daybelisex = daystart
	minut += 5

	'<center><img <<$set_imgh>> src="images/characters/pavlovsk/vadim/belyjeep.jpg"></center>'
	'You see the by now familiar old Mitsubishi Pajero driving through the streets with Vadim Bely and his gang. Your heart skips a beat as you notice one of them pointing at you, and soon they pull over.'

	act 'Go to the jeep': gt 'belgang', 'workofdebt'
end

--- pav_commercial ---------------------------------

